;-------------------------------------------------
;武装改良
;-------------------------------------------------
@RPG_WEAPON_MENU(L_I)
#DIM L_I
#DIM L_INPUT
CLEARLINE LINECOUNT
DRAWLINE
CALL C_PICTURE_RD(L_I)
DRAWLINE
PRINTFORML 【경험치:{CFlag:L_I:住人_経験値}】【자금:{MONEY}】
DRAWLINE
PRINTFORML [0] 무장 강화(경험치・자금 소비)
PRINTFORML [1] 타입 변경(초기화 필요)
PRINTFORML [2] 이름 변경
PRINTFORML 
PRINTFORML [999] 돌아온다
INPUT
L_INPUT = RESULT
SELECTCASE L_INPUT
CASE 0
	CALL RPG_WEAPON_CUSTOM(L_I)
	SIF RESULT
		RETURN
CASE 1
	CALL RPG_WEAPON_CHANGE(L_I)
	SIF RESULT
		RETURN
CASE 2
	CALL RPG_WEAPON_NAMING(L_I)
	SIF RESULT
		RETURN
CASE 999
	RETURN
CASEELSE
ENDSELECT
RESTART



;-------------------------------------------------
;武装強化
;-------------------------------------------------
;かなり変則的な設計になってしまったがコレは有りなのだろうか
@RPG_WEAPON_CUSTOM(L_I)
#DIM         L_I
#DIM         L_INPUT
#DIM DYNAMIC PAGE
#DIM DYNAMIC WEAPON_SLOT
#DIM DYNAMIC CUSTOM_MENU
#DIM DYNAMIC COST_MONEY
#DIM DYNAMIC COST_EXP
#DIM CONST   PAGE_武器スロット選択 = 0
#DIM CONST   PAGE_強化項目選択     = 1
#DIM CONST   PAGE_武器強化実行     = 2
CLEARLINE LINECOUNT
DRAWLINE
CALL C_PICTURE_RD(L_I)
DRAWLINE
PRINTFORML 【경험치:{CFlag:L_I:住人_経験値}】【자금:{MONEY}】
DRAWLINE
SELECTCASE PAGE
CASE PAGE_武器スロット選択
	PRINTFORML 강화하고 싶은 무기를 선택하세요。
	DRAWLINE
	PRINTFORML [0]  메인 무기
	PRINTFORML [10] 서브 무기
	PRINTFORML 
	PRINTFORMLC [888] 무장 개량 선택으로 돌아온다
	PRINTFORMLC [999] 캐릭터 선택으로 돌아온다
	INPUT
	L_INPUT = RESULT
	SELECTCASE L_INPUT
	CASE 0, 10
		WEAPON_SLOT = L_INPUT
		PAGE = PAGE_強化項目選択
	CASE 888
		RETURN
	CASE 999
		RETURN -1
	CASEELSE
	ENDSELECT
	RESTART
CASE PAGE_強化項目選択
	IF !WEAPON_SLOT
		PRINTFORML ＊주 무기 강화중＊
	ELSE
		PRINTFORML ＊부 무기 강화중＊
	ENDIF
	PRINTFORML 강화하고 싶은 것을 선택하세요。
	DRAWLINE
	PRINTFORML [3] 데미지 보너스		(현재 {CFlag:L_I:(6553 + WEAPON_SLOT)})
	PRINTFORML [2] 최대 데미지　　	(현재 {CFlag:L_I:(6552 + WEAPON_SLOT)})
	PRINTFORML [1] 공격 횟수		(현재 {CFlag:L_I:(6551 + WEAPON_SLOT)})
	PRINTFORML 
	PRINTFORMLC [777] 다른 무기를 강화
	PRINTFORMLC [888] 무장 개량 선택으로 돌아온다
	PRINTFORMLC [999] 캐릭터 선택으로 돌아온다
	INPUT
	CUSTOM_MENU = RESULT
	SELECTCASE CUSTOM_MENU
	CASE 1, 2, 3
		PAGE = PAGE_武器強化実行
	CASE 777
		IF !WEAPON_SLOT
			WEAPON_SLOT = 10
		ELSE
			WEAPON_SLOT = 0
		ENDIF
	CASE 888
		RETURN
	CASE 999
		RETURN -1
	CASEELSE
	ENDSELECT
	RESTART
CASE PAGE_武器強化実行
	IF !WEAPON_SLOT
		PRINTFORML ＊주 무기 강화중＊
	ELSE
		PRINTFORML ＊부 무기 강화중＊
	ENDIF
	CALLF CALC_COST_WEAPON_CUSTOM(L_I, WEAPON_SLOT, CUSTOM_MENU, COST_MONEY, COST_EXP)
	IF MONEY < COST_MONEY
		PRINTFORMW 자금이 부족합니다。(필요:{COST_MONEY})
		PAGE = PAGE_強化項目選択
		RESTART
	ELSEIF CFlag:L_I:住人_経験値 < COST_EXP
		PRINTFORMW 経験値が 부족합니다。(필요:{COST_EXP})
		PAGE = PAGE_強化項目選択
		RESTART
	ELSEIF COST_MONEY <= 0 && COST_EXP <= 0
		PRINTFORMW 에러 발생、무기 강화를 중단합니다
		RESTART
	ENDIF
	DRAWLINE
	PRINTFORML 경험치 {COST_EXP}와 자금 {COST_MONEY}을 소비하는데 괜찮을까요？。
	DRAWLINE
	PRINTFORML [2] 연속으로 강화
	PRINTFORML [1] 네
	PRINTFORML 
	PRINTFORML [0] 아니오
	INPUT
	L_INPUT = RESULT
	SELECTCASE L_INPUT
	CASE 2, 1
		MONEY -= COST_MONEY
		CFlag:L_I:住人_経験値 -= COST_EXP
		CFlag:L_I:(6550+ WEAPON_SLOT + CUSTOM_MENU) ++
		CFlag:L_I:(6558+ WEAPON_SLOT) ++
		PRINTFORML %CSTR:L_I:(4000 + WEAPON_SLOT)%を 개조했ㅅ브니다。
		PRINTFORML
		SIF L_INPUT == 1
			PAGE = PAGE_武器スロット選択
		RESTART
	CASEELSE
	ENDSELECT
	PAGE = PAGE_強化項目選択
	RESTART
ENDSELECT



;-------------------------------------------------
;強化コストの算出
;-------------------------------------------------
@CALC_COST_WEAPON_CUSTOM(L_I, WEAPON_SLOT, CUSTOM_MENU, COST_MONEY, COST_EXP)
#FUNCTION
#DIM       L_I
#DIM       WEAPON_SLOT
#DIM       WEAPON_LV
#DIM       BASIC_COST
#DIM       CUSTOM_MENU
#DIM REF   COST_MONEY
#DIM REF   COST_EXP
#DIM CONST CUSTOM_ダイス数強化 = 1
#DIM CONST CUSTOM_ダイス面強化 = 2
#DIM CONST CUSTOM_固定値強化   = 3
WEAPON_LV  = MAX(1, CFlag:L_I:(6558 + WEAPON_SLOT))
BASIC_COST = WEAPON_LV * WEAPON_LV
SELECTCASE CUSTOM_MENU
CASE CUSTOM_ダイス数強化
	COST_MONEY = BASIC_COST * 5000
	COST_EXP   = BASIC_COST * 3
CASE CUSTOM_ダイス面強化
	COST_MONEY = BASIC_COST * 500
	COST_EXP   = BASIC_COST * 2
CASE CUSTOM_固定値強化
	COST_MONEY = BASIC_COST * 3500
	COST_EXP   = BASIC_COST * 3
ENDSELECT



;-------------------------------------------------
;タイプ変更
;-------------------------------------------------
@RPG_WEAPON_CHANGE(L_I)
#DIM          L_I
#DIM          L_INPUT
#DIM  DYNAMIC PAGE
#DIM  DYNAMIC WEAPON_SLOT
#DIM  DYNAMIC WEAPON_TYPE
#DIM  CONST   PAGE_武器スロット選択 = 0
#DIM  CONST   PAGE_武器タイプ選択   = 1
#DIM  CONST   PAGE_武器変更実行     = 2
CLEARLINE LINECOUNT
DRAWLINE
CALL C_PICTURE_RD(L_I)
DRAWLINE
SELECTCASE PAGE
CASE PAGE_武器スロット選択
	PRINTFORML 타입을 변경하고 싶은 무장을 선택하세요。
	DRAWLINE
	PRINTFORML [0]  메인 무기
	PRINTFORML [10] 서브 무기
	PRINTFORML 
	PRINTFORMLC [888] 무장 개량 선택으로 돌아온다
	PRINTFORMLC [999] 캐릭터 선택으로 돌아온다
	INPUT
	L_INPUT = RESULT
	SELECTCASE L_INPUT
	CASE 0, 10
		WEAPON_SLOT = L_INPUT
		PAGE = PAGE_武器タイプ選択
	CASE 888
		RETURN
	CASE 999
		RETURN -1
	CASEELSE
	ENDSELECT
	RESTART
CASE PAGE_武器タイプ選択
	IF !WEAPON_SLOT
		PRINTFORML ＊주 무기 변경중＊
	ELSE
		PRINTFORML ＊부 무기 변경중＊
	ENDIF
	PRINTFORML 타입을 선택하세요。
	DRAWLINE
	FOR WEAPON_TYPE, 0, 5
		PRINTFORML [{WEAPON_TYPE}] - %INFO_STR_HEISOU(WEAPON_TYPE)%
	NEXT
	PRINTFORML 
	PRINTFORMLC [777] 다른 무기로 변경
	PRINTFORMLC [888] 무장 개량 선택으로 돌아온다
	PRINTFORMLC [999] 캐릭터 선택으로 돌아온다
	INPUT
	L_INPUT = RESULT
	SELECTCASE L_INPUT
	CASE 0 TO 4
		WEAPON_TYPE = L_INPUT
		IF CFlag:L_I:(6550 + WEAPON_SLOT) == WEAPON_TYPE
			PRINTFORMW 선택한 무기는「%INFO_STR_HEISOU(WEAPON_TYPE)%」입니다
		ELSE
			PAGE = PAGE_武器変更実行
		ENDIF
	CASE 777
		IF !WEAPON_SLOT
			WEAPON_SLOT = 10
		ELSE
			WEAPON_SLOT = 0
		ENDIF
	CASE 888
		RETURN
	CASE 999
		RETURN -1
	CASEELSE
	ENDSELECT
	RESTART
CASE PAGE_武器変更実行
	IF !WEAPON_SLOT
		PRINTFORML ＊주 무기를「%INFO_STR_HEISOU(WEAPON_TYPE)%」로 변경중＊
	ELSE
		PRINTFORML ＊부 무기를「%INFO_STR_HEISOU(WEAPON_TYPE)%」로 변경중＊
	ENDIF
	DRAWLINE
	PRINTFORML 타입을 변경하면 무장이 초기화됩니다만 괜찮을까요？。
	DRAWLINE
	PRINTFORML [1] 네
	PRINTFORML [0] 아니오
	INPUT
	L_INPUT = RESULT
	SELECTCASE L_INPUT
	CASE 1
		CFlag:L_I:(6550 + WEAPON_SLOT) = WEAPON_TYPE
		IF WEAPON_SLOT == 0
			CFlag:L_I:(6551 + WEAPON_SLOT) = 2
			CFlag:L_I:(6552 + WEAPON_SLOT) = 3
			CFlag:L_I:(6553 + WEAPON_SLOT) = 2
		ELSE
			CFlag:L_I:(6551 + WEAPON_SLOT) = 1
			CFlag:L_I:(6552 + WEAPON_SLOT) = 3
			CFlag:L_I:(6553 + WEAPON_SLOT) = 1
		ENDIF
		CFlag:L_I:(6558 + WEAPON_SLOT) = 1
		PRINTFORMW %CSTR:L_I:(4000 + WEAPON_SLOT)%의 타입을 %INFO_STR_HEISOU(WEAPON_TYPE)%로 변경했습니다。
		PAGE = PAGE_武器スロット選択
	CASEELSE
		PAGE = PAGE_武器タイプ選択
	ENDSELECT
	RESTART
ENDSELECT



;-------------------------------------------------
;武装名変更
;-------------------------------------------------
@RPG_WEAPON_NAMING(L_I)
#DIM          L_I
#DIM          L_INPUT
#DIM  DYNAMIC PAGE
#DIM  DYNAMIC WEAPON_SLOT
#DIM  CONST   PAGE_武器スロット選択 = 0
#DIM  CONST   PAGE_武器名入力       = 1
CLEARLINE LINECOUNT
DRAWLINE
CALL C_PICTURE_RD(L_I)
DRAWLINE
SELECTCASE PAGE
CASE PAGE_武器スロット選択
	PRINTFORML 이름을 변경하고 싶은 무장을 선택하세요。
	DRAWLINE
	PRINTFORML [0]  메인 무기
	PRINTFORML [10] 서브 무기
	PRINTFORML 
	PRINTFORMLC [888] 무장 개량 선택으로 돌아온다
	PRINTFORMLC [999] 캐릭터 선택으로 돌아온다
	INPUT
	L_INPUT = RESULT
	SELECTCASE L_INPUT
	CASE 0, 10
		WEAPON_SLOT = L_INPUT
		PAGE = PAGE_武器名入力
	CASE 888
		RETURN
	CASE 999
		RETURN -1
	CASEELSE
	ENDSELECT
	RESTART
CASE PAGE_武器名入力
	IF !WEAPON_SLOT
		PRINTFORML ＊주 무기 변경중＊
	ELSE
		PRINTFORML ＊부 무기 변경중＊
	ENDIF
	PRINTFORML 새로운 이름을 입력하세요
	DRAWLINE
	INPUTS
	SELECTCASE RESULTS
	CASE ""
		PRINTFORMW 변경하지 않았습니다。
	CASEELSE
		CSTR:L_I:(4000 + WEAPON_SLOT) = %RESULTS%
		PRINTFORMW 무장 이름을「%RESULTS%」に 변경했습니다。
	ENDSELECT
	RETURN
ENDSELECT
