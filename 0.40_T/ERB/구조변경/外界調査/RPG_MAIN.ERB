@RPG_CHARA_SELECT(MODE_NOW = "RPG_武器強化")
#DIMS         MODE_NOW
#DIM          MODE_ID
#DIM          L_INPUT
#DIM          NOW_GROUP = 1 ;ページ記憶用
#DIMS DYNAMIC OP_RELOAD     ;初回以外のページ更新とリスト更新を抑制する
{
#DIMS CONST   MODE_NAME = 
	"RPG_武器強化", 
	"RPG_技能成長", 
	"RPG_強化剤投与", 
	"RPG_超人薬投与", 
	"RPG_随伴配置", 
	"RPG_後方配置", 
	"RPG_回収配置", 
	"RPG_探索配置"
}
{
#DIMS CONST   MEMO_STR = 
	"무기 강화 대상자를 선택하세요。", 
	"기능 성장 대상자를 선택하세요。", 
	"강화제 투여 대상자를 선택하세요。", 
	"초인약 투여 대상자를 선택하세요。", 
	"수반 부대에 배치할 인물을 선택하세요。", 
	"후방 부대에 배치할 인물을 선택하세요。", 
	"회수 부대에 배치할 인물을 선택하세요。", 
	"탐색 부대에 배치할 인물을 선택하세요。"
}



MODE_ID = FINDELEMENT(MODE_NAME, MODE_NOW)
SELECTCASE MODE_ID
CASE -1
	MODE_NOW '= "RPG_武器強化"
	RESTART
ENDSELECT
CLEARLINE LINECOUNT

CALL LIST_CHARA_STATUS_MAIN(0, NOW_GROUP, OP_RELOAD, MODE_NOW, MEMO_STR:MODE_ID)

L_INPUT = RESULT
OP_RELOAD '= "「ページ更新無し」「リスト更新無し」"
SELECTCASE L_INPUT
CASE 0
	NOW_GROUP = 1
	RETURN
CASE 1 TO CHARANUM - 1
	SELECTCASE MODE_NOW
	CASE "RPG_武器強化"
		CALL RPG_WEAPON_MENU(L_INPUT)
	CASE "RPG_技能成長"
		CALL RPG_SKILLSET(L_INPUT)
	CASE "RPG_強化剤投与", "RPG_超人薬投与"
		CALL RPG_DOPING(L_INPUT, MODE_NOW)
	CASE "RPG_随伴配置", "RPG_後方配置", "RPG_回収配置", "RPG_探索配置"
		CALL RPG_SUPPORT_MENU(L_INPUT, MODE_NOW)
	ENDSELECT
CASE 1000 TO 1007
	MODE_NOW '= MODE_NAME:(L_INPUT % 1000)
CASEELSE
ENDSELECT
RESTART
