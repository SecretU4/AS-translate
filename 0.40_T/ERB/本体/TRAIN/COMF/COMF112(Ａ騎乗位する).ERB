;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Module		:COMFxxx.ERB
;	Facility	:TRAINコマンド(112:Ａ騎乗位する)。
;
;	Licence		:ライセンスフリー。
;
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#

;===============================================================================
;  ◇記載関数一覧(記載順)
;
;		Module Name						Explanation
;
;	・ COM_FUNC_GET_NAME_Xxxx			取得:コマンド名称
;	・ COM_FUNC_ABLE_Xxxx				CHK:コマンド実行条件
;	・ COM_FUNC_EXE_Xxxx				コマンド実行
;	・ COM_FUNC_SRC_Xxxx				コマンドソース
;	・ COM_FUNC_TRAIN_MSG_Xxxx			コマンドメッセージ
;	・ COM_FUNC_GET_TYPE_Xxxx			取得:コマンド種別
;
;===============================================================================

;=================================================
;   sub COM_FUNC_GET_NAME_Xxxx
;=================================================
;   取得:コマンド名称
;-------------------------------------------------
; Output:
;  RESULTS				コマンド名称
;-------------------------------------------------
@COM_FUNC_GET_NAME_X112
IF TFLAG:サブコマンド実行中フラグ
	RESULTS = 역 기승위 Ａ
ELSE
	RESULTS = 기승위 Ａ한다
ENDIF

;=================================================
;   sub COM_FUNC_ABLE_Xxxx
;=================================================
;   CHK:コマンド実行条件
;-------------------------------------------------
; Output:
;  RESULT				真偽値
;-------------------------------------------------
@COM_FUNC_ABLE_X112

;- TR:性交可能(Ａ) (※逆)
SIF !TR_CHK_ABLE_SEX_A(PLAYER, TARGET)
	RETURN 0

;- TR:性交可能(逆位置別)
SIF !TR_CHK_ABLE_REV_SEX_POINT("騎乗位")
	RETURN 0

RETURN 1

;=================================================
;   sub COM_FUNC_EXE_Xxxx
;=================================================
;   コマンド実行
;-------------------------------------------------
@COM_FUNC_EXE_X112
#DIM L_P
#DIM L_T

L_P = PLAYER
L_T = TARGET

;-----------------------------
;COM別FLAGの設定
;-----------------------------

;- POSE:リセット
CALL POSE_RESET("Ｖ")

TEQUIP:L_T:ポイント性交 = [[TRAIN:Ａ騎乗位する]]


;-----------------------------
;ソース追加処理
;-----------------------------

;-----------------------------
;その他の処理
;-----------------------------


;=================================================
;   sub COM_FUNC_SRC_Xxxx
;=================================================
;   コマンドソース
;-------------------------------------------------
@COM_FUNC_SRC_X112
#DIM L_P
#DIM L_T
#DIM L_TMP
#DIM L_I
VARSET LOCAL

L_P = PLAYER
L_T = TARGET

;-----------------------------
;COM別FLAGの設定
;-----------------------------

;- POSE:SET (※逆)
CALL POSE_SET(L_P, [[占有部位:Ａ]], 1, L_T)

;-----------------------------
;ソース追加処理
;-----------------------------

LOCAL:0 = 150
LOCAL:1 = 150
LOCAL:2 = 60

;-- 補正追加

;- 範囲:100%～300%
LOCAL:10 = ABL:L_T:기교 + ABL:L_P:기교
SELECTCASE LOCAL:10
	CASE 0 to 17
		L_TMP = 100 + (10 * LOCAL:10)
	CASEELSE
		L_TMP = 300
ENDSELECT
LOCAL:0 = LOCAL:0 * L_TMP / 100
LOCAL:1 = LOCAL:1 * L_TMP / 100
LOCAL:2 = LOCAL:2 * L_TMP / 100

;- ソース
SOURCE:L_P:快Ａ += LOCAL:0
SOURCE:L_T:快Ｃ += LOCAL:1
SOURCE:L_T:性行為 += LOCAL:2


;-----------------------------
;経験の処理
;-----------------------------

;- 各種経験
GTR_EXPUP:L_P:DB_EXPUP_POS("유혹경험") += 1

;-----------------------------
;その他の処理
;-----------------------------

;- TR:PALAM変動(Ａ性交時) (※逆)
CALL TR_COM_PALAMCHG_SEX_A(TARGET, PLAYER, 1)



;=================================================
;   sub COM_FUNC_TRAIN_MSG_Xxxx
;=================================================
;   コマンドメッセージ
;-------------------------------------------------
@COM_FUNC_TRAIN_MSG_X112

;前回の体位が「Ａ騎乗位する」
IF TEQUIP:ポイント性交前回 == [[TRAIN:Ａ騎乗位する]]
	PRINTFORML %CALLNAME:PLAYER%는 %CALLNAME%에게 올라탄 채로、
	PRINTFORML 밀려오는 충동에 몸을 맡기고 허리를 움직였다……

;前回の体位が逆Ａ性交系の場合
ELSEIF TR_PREV_SEX_REV_A()
	PRINTFORML 엉덩이에서 뽑지 않고 %CALLNAME%를 쓰러뜨린 다음、위에 올라타 허리를 움직였다……

;その他
ELSE
	;처녀＋작은체형
	IF TCVAR:PLAYER:履歴_EXP_Ａ処女喪失 && TALENT:PLAYER:작은체형
		PRINTFORML %CALLNAME:PLAYER%는 %CALLNAME%에게 올라타 천천히 허리를 내리면서、
		PRINT 남자를 모르는 좁은 애널로 자지를 삼켰다……
	;처녀
	ELSEIF TCVAR:PLAYER:履歴_EXP_処女喪失
		PRINTFORML %CALLNAME:PLAYER%는 %CALLNAME%의 올라타 천천히 허리를 내리면서、
		PRINT 남자를 모르는 애널로 자지를 삼켰다……
	ELSE
		PRINTFORM %CALLNAME:PLAYER%는 %CALLNAME%의 위에 올라타、천천히 허리를 내렸다……
	ENDIF
	PRINTL
ENDIF
;
IF 1
	PRINTFORM %CALLNAME:PLAYER%는 
	;작은체형
	SIF TALENT:PLAYER:작은체형
		PRINT 여린 몸으로 
	;남자
	IF TALENT:PLAYER:남자
		;無し
	;거유
	ELSEIF VAR_바스트사이즈(PLAYER) == 1
		PRINT 커다란 젖가슴을 흔들면서 
	;폭유
	ELSEIF VAR_바스트사이즈(PLAYER) == 2
		PRINT 이리저리 요동치는 가슴에 몸까지 따라 흔들리면서 
	;その他
	ELSE
		PRINT 양쪽 젖가슴을 흔들면서 
	ENDIF
	PRINT 유혹하듯이 허리를 움직이고 있다……
	PRINTL
ENDIF

WAIT


;=================================================
;   sub COM_FUNC_GET_TYPE_Xxxx
;=================================================
;   取得:コマンド種別
;-------------------------------------------------
; Output:
;  RESULT				コマンド種別
;-------------------------------------------------
@COM_FUNC_GET_TYPE_X112
RETURN [[COM種別:逆Ａ性交系]]

