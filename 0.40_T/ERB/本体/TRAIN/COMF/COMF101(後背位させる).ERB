;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Module		:COMFxxx.ERB
;	Facility	:TRAINコマンド(101:後背位させる)。
;
;	Licence		:ライセンスフリー。
;
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#

;===============================================================================
;  ◇記載関数一覧(記載順)
;
;		Module Name						Explanation
;
;	・ COM_FUNC_GET_NAME_Xxxx			取得:コマンド名称
;	・ COM_FUNC_ABLE_Xxxx				CHK:コマンド実行条件
;	・ COM_FUNC_EXE_Xxxx				コマンド実行
;	・ COM_FUNC_SRC_Xxxx				コマンドソース
;	・ COM_FUNC_TRAIN_MSG_Xxxx			コマンドメッセージ
;	・ COM_FUNC_GET_TYPE_Xxxx			取得:コマンド種別
;
;===============================================================================

;=================================================
;   sub COM_FUNC_GET_NAME_Xxxx
;=================================================
;   取得:コマンド名称
;-------------------------------------------------
; Output:
;  RESULTS				コマンド名称
;-------------------------------------------------
@COM_FUNC_GET_NAME_X101
IF TFLAG:サブコマンド実行中フラグ
	RESULTS = 역 후배위
ELSE
	RESULTS = 후배위 시킨다
ENDIF

;=================================================
;   sub COM_FUNC_ABLE_Xxxx
;=================================================
;   CHK:コマンド実行条件
;-------------------------------------------------
; Output:
;  RESULT				真偽値
;-------------------------------------------------
@COM_FUNC_ABLE_X101

;- TR:性交可能(Ｖ) (※逆)
SIF !TR_CHK_ABLE_SEX_V(PLAYER, TARGET)
	RETURN 0

;- TR:性交可能(逆位置別)
SIF !TR_CHK_ABLE_REV_SEX_POINT("後背位")
	RETURN 0

RETURN 1

;=================================================
;   sub COM_FUNC_EXE_Xxxx
;=================================================
;   コマンド実行
;-------------------------------------------------
@COM_FUNC_EXE_X101
#DIM L_P
#DIM L_T

L_P = PLAYER
L_T = TARGET

;-----------------------------
;COM別FLAGの設定
;-----------------------------

;- POSE:リセット
CALL POSE_RESET("Ｖ")

TEQUIP:L_T:ポイント性交 = [[TRAIN:後背位させる]]


;-----------------------------
;ソース追加処理
;-----------------------------

;-----------------------------
;その他の処理
;-----------------------------


;=================================================
;   sub COM_FUNC_SRC_Xxxx
;=================================================
;   コマンドソース
;-------------------------------------------------
@COM_FUNC_SRC_X101
#DIM L_P
#DIM L_T
#DIM L_TMP
#DIM L_I
VARSET LOCAL

L_P = PLAYER
L_T = TARGET

;-----------------------------
;COM別FLAGの設定
;-----------------------------

;- POSE:SET (※逆)
CALL POSE_SET(L_P, [[占有部位:Ｖ]], 1, L_T)

;-----------------------------
;ソース追加処理
;-----------------------------

LOCAL:0 = 150
LOCAL:1 = 150
LOCAL:2 = 60

;-- 補正追加

;- 範囲:100%～300%
LOCAL:10 = ABL:L_T:기교 + ABL:L_P:기교
SELECTCASE LOCAL:10
	CASE 0 to 17
		L_TMP = 100 + (10 * LOCAL:10)
	CASEELSE
		L_TMP = 300
ENDSELECT
LOCAL:0 = LOCAL:0 * L_TMP / 100
LOCAL:1 = LOCAL:1 * L_TMP / 100
LOCAL:2 = LOCAL:2 * L_TMP / 100

;- ソース
SOURCE:L_P:快Ｖ += LOCAL:0
SOURCE:L_T:快Ｃ += LOCAL:1
SOURCE:L_T:性行為 += LOCAL:2


;-----------------------------
;経験の処理
;-----------------------------

;- 各種経験
GTR_EXPUP:L_P:DB_EXPUP_POS("유혹경험") += 1
GTR_EXPUP:L_T:DB_EXPUP_POS("봉사쾌락경험") += 3

;-----------------------------
;その他の処理
;-----------------------------

;- TR:PALAM変動(Ｖ性交時) (※逆)
CALL TR_COM_PALAMCHG_SEX_V(TARGET, PLAYER, 1)



;=================================================
;   sub COM_FUNC_TRAIN_MSG_Xxxx
;=================================================
;   コマンドメッセージ
;-------------------------------------------------
@COM_FUNC_TRAIN_MSG_X101

;前回の体位が「後背位させる」
IF TEQUIP:ポイント性交前回 == [[TRAIN:後背位させる]]
	PRINTFORML %CALLNAME%의 자지를 질벽으로 문지르면서、다시 뒤에서 허리를 밀어치게 시켰다……

;前回の体位が逆Ｖ性交系の場合
ELSEIF TR_PREV_SEX_REV_V()
	PRINTFORML 보지에서 뽑지 않고 네발로 엎드린 채、%CALLNAME%에게 뒤에서 허리를 찔러넣게 시켰다……

;その他
ELSE
	IF TCVAR:PLAYER:履歴_EXP_処女喪失
		PRINTFORML %CALLNAME:PLAYER%는 네발로 엎드려 남자를 모르는 \@ TALENT:PLAYER:작은체형 ? 미성숙한 보지 # 보지 \@를 %CALLNAME%에게 쑤셔박게 했다…
	ELSE
		PRINTFORM 네발로 엎드린 %CALLNAME:PLAYER%의 
		PRINTFORM 뒤에서 %CALLNAME%에게 
		;濡れ
		SIF TCVAR:PLAYER:キャラ潤滑
			PRINT 흠뻑 젖은 
		;작은체형
		IF TALENT:PLAYER:작은체형
			PRINT 미성숙한 보지
		ELSE
			PRINT 보지
		ENDIF
		PRINTFORML 의 가장 깊숙한 곳까지 쳐넣게 시켰다…
	ENDIF
ENDIF
;陥落済み
IF MARK:함락각인
	PRINTFORML %CALLNAME%는 수컷의 본능으로 자궁에 자지를 꾹 누르고 있다……
ENDIF

WAIT


;=================================================
;   sub COM_FUNC_GET_TYPE_Xxxx
;=================================================
;   取得:コマンド種別
;-------------------------------------------------
; Output:
;  RESULT				コマンド種別
;-------------------------------------------------
@COM_FUNC_GET_TYPE_X101
RETURN [[COM種別:逆Ｖ性交系]]

