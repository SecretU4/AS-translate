;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Module		:COMFxxx.ERB
;	Facility	:TRAINコマンド(82:騎乗位)。
;
;	Licence		:ライセンスフリー。
;
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#

;===============================================================================
;  ◇記載関数一覧(記載順)
;
;		Module Name						Explanation
;
;	・ COM_FUNC_GET_NAME_Xxxx			取得:コマンド名称
;	・ COM_FUNC_ABLE_Xxxx				CHK:コマンド実行条件
;	・ COM_FUNC_EXE_Xxxx				コマンド実行
;	・ COM_FUNC_SRC_Xxxx				コマンドソース
;	・ COM_FUNC_TRAIN_MSG_Xxxx			コマンドメッセージ
;	・ COM_FUNC_GET_TYPE_Xxxx			取得:コマンド種別
;
;===============================================================================

;=================================================
;   sub COM_FUNC_GET_NAME_Xxxx
;=================================================
;   取得:コマンド名称
;-------------------------------------------------
; Output:
;  RESULTS				コマンド名称
;-------------------------------------------------
@COM_FUNC_GET_NAME_X82
RESULTS = 기승위

;=================================================
;   sub COM_FUNC_ABLE_Xxxx
;=================================================
;   CHK:コマンド実行条件
;-------------------------------------------------
; Output:
;  RESULT				真偽値
;-------------------------------------------------
@COM_FUNC_ABLE_X82

;- TR:性交可能(Ｖ)
SIF !TR_CHK_ABLE_SEX_V()
	RETURN 0

;- TR:性交可能(位置別)
SIF !TR_CHK_ABLE_SEX_POINT("騎乗位")
	RETURN 0

RETURN 1

;=================================================
;   sub COM_FUNC_EXE_Xxxx
;=================================================
;   コマンド実行
;-------------------------------------------------
@COM_FUNC_EXE_X82
#DIM L_P
#DIM L_T

L_P = PLAYER
L_T = TARGET

;-----------------------------
;COM別FLAGの設定
;-----------------------------

;- POSE:リセット
CALL POSE_RESET("Ｖ")

TEQUIP:L_T:ポイント性交 = [[TRAIN:騎乗位]]


;-----------------------------
;ソース追加処理
;-----------------------------

;-----------------------------
;その他の処理
;-----------------------------


;=================================================
;   sub COM_FUNC_SRC_Xxxx
;=================================================
;   コマンドソース
;-------------------------------------------------
@COM_FUNC_SRC_X82
#DIM L_P
#DIM L_T
#DIM L_TMP
#DIM L_I
VARSET LOCAL

L_P = PLAYER
L_T = TARGET

;-----------------------------
;COM別FLAGの設定
;-----------------------------

;- POSE:SET
CALL POSE_SET(L_T, [[占有部位:Ｖ]])

;-----------------------------
;ソース追加処理
;-----------------------------

LOCAL:0 = 150
LOCAL:1 = 150
LOCAL:2 = 60

;-- 補正追加

;- 範囲:100%～300%
LOCAL:10 = ABL:L_T:기교 + ABL:L_P:기교
SELECTCASE LOCAL:10
	CASE 0 to 17
		L_TMP = 100 + (10 * LOCAL:10)
	CASEELSE
		L_TMP = 300
ENDSELECT
LOCAL:0 = LOCAL:0 * L_TMP / 100
LOCAL:1 = LOCAL:1 * L_TMP / 100
LOCAL:2 = LOCAL:2 * L_TMP / 100

;- ソース
SOURCE:L_P:快Ｃ += LOCAL:0
SOURCE:L_T:快Ｖ += LOCAL:1
SOURCE:L_T:性行為 += LOCAL:2


;-----------------------------
;経験の処理
;-----------------------------

;- 各種経験
GTR_EXPUP:L_T:DB_EXPUP_POS("유혹경험") += 1
GTR_EXPUP:L_T:DB_EXPUP_POS("봉사쾌락경험") += 3

;-----------------------------
;その他の処理
;-----------------------------

;- TR:PALAM変動(Ｖ性交時)
CALL TR_COM_PALAMCHG_SEX_V



;=================================================
;   sub COM_FUNC_TRAIN_MSG_Xxxx
;=================================================
;   コマンドメッセージ
;-------------------------------------------------
@COM_FUNC_TRAIN_MSG_X82

;前回の体位が「騎乗位」
IF TEQUIP:ポイント性交前回 == [[TRAIN:騎乗位]]
	PRINTFORML %CALLNAME:PLAYER%는 %CALLNAME%를 자신의 위에 태우고、
	PRINTFORML 충동에 몸을 맡기면서 허리를 쳐올렸다……

;前回の体位がＶ性交系の場合
ELSEIF TR_PREV_SEX_V()
	PRINTFORML 보지에서 뽑지 않고 %CALLNAME%를 자신의 위에 태운 다음、아래에서 허리를 쳐올렸다…………

;その他
ELSE
	;처녀＋작은체형
	IF TCVAR:履歴_EXP_処女喪失 && TALENT:작은체형
		PRINTFORML %CALLNAME%는 %CALLNAME:PLAYER%に올라타 천천히 허리를 내리면서、
		PRINT 남자를 모르는 미성숙한 보지로 자지를 삼켰다……
	;처녀
	ELSEIF TCVAR:履歴_EXP_処女喪失
		PRINTFORML %CALLNAME%는 %CALLNAME:PLAYER%의 올라타 천천히 허리를 내리면서、
		PRINT 남자를 모르는 보지로 자지를 삼켰다……
	ELSE
		PRINTFORM %CALLNAME%는 %CALLNAME:PLAYER%의 위에 올라타、천천히 허리를 내렸다……
	ENDIF
	PRINTL
ENDIF
;陥落済み
IF MARK:함락각인
	PRINTFORM %CALLNAME%는 
	;작은체형
	SIF TALENT:작은체형
		PRINT 여린 몸으로 열심히 
	;거유
	IF VAR_바스트사이즈(TARGET) == 1
		PRINT 커다란 젖가슴을 흔들면서 
	;폭유
	ELSEIF VAR_바스트사이즈(TARGET) == 2
		PRINT 이리저리 요동치는 가슴에 몸까지 따라 흔들리면서 
	;その他
	ELSE
		PRINT 양쪽 젖가슴을 흔들면서 
	ENDIF
	PRINT 스스로 허리를 움직이고 있다……
	PRINTL
ENDIF

WAIT


;=================================================
;   sub COM_FUNC_GET_TYPE_Xxxx
;=================================================
;   取得:コマンド種別
;-------------------------------------------------
; Output:
;  RESULT				コマンド種別
;-------------------------------------------------
@COM_FUNC_GET_TYPE_X82
RETURN [[COM種別:Ｖ性交系]]

