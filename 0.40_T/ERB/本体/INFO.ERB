;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Module		:INFO.ERB
;	Facility	:정보 표시。
;
;	Licence		:ライセンスフリー。
;
;	Modification Data:
;
;	Edit	Date			Author					Reason
;	004		2018/11/14		morph					共通処理を関数化して別の箇所でも使えるように
;														INFO_TARGET_MARK
;														INFO_TARGET_RELATION
;														INFO_TARGET_GENDER
;														INFO_TARGET_FALLEN
;														INFO_TARGET_PREGNANCY
;														INFO_TARGET_NAYAMI
;	003		2017/03/02		morph					ソート機能追加用に関数追加（INFO_LABEL_LIST/INFO_SORT_BUTTON/INFO_CHARA_DETAIL_LABEL/INFO_LABEL_ARRAY）
;	002		2017/02/16		「」					AS用に表示改変
;	001		20XX/XX/XX		----					新規作成
;
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#

;===============================================================================
;  ◇記載関数一覧(記載順)
;
;		Module Name						Explanation
;
; *- XXXX
;
;	・ INFO_DATE						情報:日付
;	・ INFO_TIME						情報:時間帯
;	・ INFO_TARGET_MAIN					情報:ターゲット(主処理)
;	・ INFO_TARGET_DETAIL				情報:ターゲット(詳細)
;	・ INFO_WORK_NAME					情報:労役役職名
;	・ INFO_危険日						情報:危険日
;	・ INFO_LABEL_LIST					情報:ラベル一覧を使用したソート用ボタン作成関数
;	・ INFO_SORT_BUTTON					情報:ソート用ボタン作成関数
;	・ INFO_CHARA_DETAIL_LABEL			情報:ラベル使用一行정보 표시関数
;	・ INFO_LABEL_ARRAY					情報:ラベル一覧を使用したキャラステータス簡易表示関数
;
;===============================================================================

;-------------------------------------------------------------------------------
;   XXXX
;-------------------------------------------------------------------------------

;=================================================
;   sub INFO_DATE
;=================================================
;   情報:日付
;-------------------------------------------------
@INFO_DATE

PRINTFORM {DAY}일째 
PRINTFORM (%GET_DAY(DAY)%) - 

;- 情報:時間帯
PRINTFORM %INFO_TIME(), 6, LEFT%
PRINT  


;=================================================
;   sub INFO_TIME
;=================================================
;   情報:時間帯
;-------------------------------------------------
@INFO_TIME
#FUNCTIONS

SELECTCASE FLAG:時間帯
	CASE [[時間帯:夜]]
		RETURNF "밤"
	;- 0:昼
	CASEELSE
		RETURNF "낮"
ENDSELECT


;=================================================
;   sub INFO_TARGET_MAIN
;=================================================
;   表示:ターゲット(主処理)
;-------------------------------------------------
@INFO_TARGET_MAIN
#DIM L_I
#DIM L_T

FOR L_I, 0, DEFTR_PL_NUM

	L_T = GTR_PL:L_I

	;CHK:END
	SIF L_T < 0
		BREAK

	IF L_T == MASTER
		PRINT 【주인】
	ELSE
		PRINT 　　　　　
	ENDIF

	;- 表示:ターゲット(詳細)
	CALL INFO_TARGET_DETAIL(L_T)
	PRINTL
NEXT
;PRINTL 


;=================================================
;   sub INFO_TARGET_DETAIL
;=================================================
;   表示:ターゲット(詳細)
;-------------------------------------------------
; Input:
;  ARG:0				対象キャラ
;  ARG:1				短縮フラグ(0:オフ 1:オン)
;-------------------------------------------------
@INFO_TARGET_DETAIL(ARG:0, ARG:1=0, ARG:2 = 0)
#DIM L_I
#DIM L_TMP
#DIM L_SEX
#DIM L_COLOR
#DIMS L_STR

L_I=ARG
L_COLOR = ARG:1
SETCOLOR 0xc1c1c1
RESETCOLOR
;PRINTFORM \[No.%TOSTR(NO:L_I),3%\] 
IF ARG:2 > 0
	PRINTFORM %CSTR:L_I:(ARG:2)%
ENDIF
CALL VAR_PRINT_LIST_NAME(L_I)

;VAR:バー出力(基礎)
;行内で最初にカラー変更が起きると半角スペースが入る仕様？で後ろがズレないためのSETCOLOR
SETCOLOR 0xc1c1c1
CALL VAR_BAR_BASE(L_I, 0, 2)
RESETCOLOR
CALL INFO_TARGET_MARK(L_I)
;-----------------------------
;-- 労役情報
;-----------------------------
;未実装みたいなので一時的にコメントアウト
;PRINTFORM [%INFO_WORK_NAME(L_I)%] 
CALL INFO_TARGET_RELATION(L_I)
CALL INFO_TARGET_GENDER(L_I)
CALL INFO_TARGET_FALLEN(L_I)
CALL INFO_TARGET_PREGNANCY(L_I, ARG:1)
CALL INFO_TARGET_NAYAMI(L_I)



;-----------------------------
;-- 刻印情報
;-----------------------------
@INFO_TARGET_MARK(L_I)
#DIM L_I
#DIM L_TMP
PRINTFORM [
;- 刻印:隷属
L_TMP = MARK:L_I:예속각인
SIF !L_TMP
	SETCOLOR COLOR("伏字")
PRINTFORM 예속 LV{L_TMP} 
RESETCOLOR
;- 刻印:陥落
L_TMP = MARK:L_I:함락각인
SIF !L_TMP
	SETCOLOR COLOR("伏字")
PRINTFORM 함락 LV{L_TMP} 
RESETCOLOR
;- 刻印:淫乱
L_TMP = MARK:L_I:음란각인
SIF !L_TMP
	SETCOLOR COLOR("伏字")
PRINTFORM 음란 LV{L_TMP} 
RESETCOLOR
;- 刻印:反発
L_TMP = MARK:L_I:반발각인
SIF !L_TMP
	SETCOLOR COLOR("伏字")
PRINTFORM 반발 LV{L_TMP}
RESETCOLOR
PRINTFORM ] 



;-----------------------------
;-- 합의と陥落
;-----------------------------
@INFO_TARGET_RELATION(L_I)
#DIM L_I
IF L_I == MASTER
	PRINTFORM ─ 
ELSEIF TALENT:L_I:합의
;	SETCOLOR COLOR("ピンク")
	SETCOLOR 250,250,100
	PRINTFORM 합의 
	RESETCOLOR
ELSEIF TALENT:L_I:조교완료
;	SETCOLOR COLOR("ピンク")
	SETCOLOR 250,0,0
	PRINTFORM 끝 
	RESETCOLOR
ELSEIF TALENT:L_I:범해짐
	SETCOLOR COLOR("紫")
	PRINTFORM 범함 
	RESETCOLOR
ELSEIF TALENT:L_I:약점
	SETCOLOR COLOR("紫")
	PRINTFORM 약점 
	RESETCOLOR
ELSEIF CFLAG:L_I:関係段階 == 2
	PRINTFORM 베프 
ELSEIF CFLAG:L_I:関係段階 == 1
	SETCOLOR 0x999999
	PRINTFORM 친구 
	RESETCOLOR
ELSE
	SETCOLOR 0x444444
	PRINTFORM 지인 
	RESETCOLOR
ENDIF
;各種陥落
IF TALENT:L_I:심취
	SETCOLOR COLOR("紫")
	PRINTFORM 심취 
	RESETCOLOR
ELSEIF TALENT:L_I:애인
	SETCOLOR COLOR("紫")
	PRINTFORM 애인 
	RESETCOLOR
ELSEIF TALENT:L_I:정부
	SETCOLOR COLOR("紫")
	PRINTFORM 정부 
	RESETCOLOR
ELSE
	PRINTFORM ─ 
ENDIF



;-----------------------------
;-- 性差系の素質表示
;-----------------------------
@INFO_TARGET_GENDER(L_I)
#DIM  L_I
#DIMS L_STR
;후타나리
IF TALENT:L_I:후타나리
	PRINT 双
ELSE
	PRINTFORM %DB_TALENT_NAME(L_I, "남자")%
ENDIF
;女の場合
IF VAR_SEX(L_I)
	IF !TALENT:L_I:비처녀
		PRINT ,
		SETCOLOR 0xFF66CC
		PRINT 처녀
		RESETCOLOR
	ELSE
		PRINTFORM ,─
	ENDIF
	;후타나리で童貞の場合
	IF !TALENT:L_I:비동정 && TALENT:L_I:후타나리
		PRINT ,
		SETCOLOR 0xFF66CC
		PRINT 동정
		RESETCOLOR
	ELSE
		PRINTFORM ,─
	ENDIF
;男の場合
ELSE
	PRINTFORM ,─
	IF !TALENT:L_I:비동정
		PRINT ,
		SETCOLOR COLOR("群青")
		PRINT 동정
		RESETCOLOR
	ELSE
		PRINTFORM ,─
	ENDIF
ENDIF

;女の場合
IF VAR_SEX(L_I)
	PRINT ,
	L_STR = %DB_TALENT_NAME(L_I, "바스트사이즈")%
	PRINTFORM %SUBSTRING(L_STR, 0,2), 2%
ELSE
	PRINTFORM ,─
ENDIF


;-----------------------------
;-- 陥落進行度
;-----------------------------
@INFO_TARGET_FALLEN(L_I)
#DIM  L_I
#DIMS L_STR
;예속도
IF TALENT:L_I:예속도
	PRINT ,
	L_STR = %DB_TALENT_NAME(L_I, "예속도")%
;	SETCOLOR COLOR("ピンク")
	PRINTFORM %SUBSTRING(L_STR, 0,2), 2%
	RESETCOLOR
;함락도
ELSEIF TALENT:L_I:함락도
	PRINT ,
	L_STR = %DB_TALENT_NAME(L_I, "함락도")%
;	SETCOLOR COLOR("ピンク")
	PRINTFORM %SUBSTRING(L_STR, 0,2), 2%
	RESETCOLOR
ELSE
	PRINTFORM ,　
ENDIF
;ＮＴＲ도
IF TALENT:L_I:ＮＴＲ도
	PRINT ,
	L_STR = %DB_TALENT_NAME(L_I, "ＮＴＲ도")%
	SETCOLOR COLOR("紫")
	PRINTFORM %SUBSTRING(L_STR, 0,2), 2%
	RESETCOLOR
ELSE
	PRINTFORM ,　
ENDIF



;-----------------------------
;-- 妊娠情報
;-----------------------------
@INFO_TARGET_PREGNANCY(L_I, SHORT)
#DIM L_I
#DIM SHORT
;短縮表示
IF SHORT
	;妊娠
	IF TALENT:L_I:妊娠
		PRINT ,
		SETCOLOR COLOR("黄色")
		PRINTFORM 임신
		RESETCOLOR
	ELSEIF TALENT:L_I:수정
		PRINT ,
		SETCOLOR COLOR("黄色")
		PRINTFORM 수정
		RESETCOLOR
	;배란약
	ELSEIF TALENT:L_I:배란약
		PRINT ,
		SETCOLOR COLOR("紫")
		CALL CP_HEARTB()
		PRINTFORM 배란
		CALL CP_HEARTB()
		RESETCOLOR

	;위험일 且つ 女
	ELSEIF C_危険日(L_I) && VAR_SEX(L_I)
		PRINT ,
		SETCOLOR COLOR("紫")
		CALL CP_HEARTB()
		PRINTFORM 위험일
		CALL CP_HEARTB()
		RESETCOLOR
	ELSE
		PRINTFORM ,─
	ENDIF
ELSE
	PRINT ,
	;妊娠
	IF TALENT:L_I:妊娠
		SETCOLOR COLOR("黄色")
		CALL CP_HEARTB()
		PRINTFORM 임신중
		CALL CP_HEARTB()
		RESETCOLOR
	ELSEIF TALENT:L_I:수정
		SETCOLOR COLOR("黄色")
		CALL CP_HEARTB()
		PRINTFORM 수정약
		CALL CP_HEARTB()
		RESETCOLOR
	;배란약
	ELSEIF TALENT:L_I:배란약
		SETCOLOR COLOR("紫")
		CALL CP_HEARTB()
		PRINTFORM 배란약
		CALL CP_HEARTB()
		RESETCOLOR
	;위험일
	ELSEIF C_危険日(L_I) && VAR_SEX(L_I)
		;- 情報:危険日
		CALL INFO_危険日(L_I)
	ELSE
		PRINTFORM 　　　　　
	ENDIF
ENDIF



;-----------------------------
;-- 悩み解決情報
;-----------------------------
@INFO_TARGET_NAYAMI(L_I)
#DIM L_I
#DIM NAYAMI
IF CFlag:L_I:住人_悩み解決判定回数 != 0 || CFlag:L_I:住人_悩み解決中 != 0
	PRINTFORM , 해결까지 {(CFlag:L_I:住人_悩み解決判定回数)+1}일 
ELSE
	NAYAMI = SEARCH_ALL_NAYAMI(L_I)
	IF INRANGE(NAYAMI, 201, 205)
		PRINT , 상담 필요！
	ELSEIF INRANGE(NAYAMI, 211, 215)
		PRINT , 상담 상태
	ELSEIF NAYAMI
		PRINT , 고민중…
	ENDIF
ENDIF



;-----------------------------
;-- 武装情報
;-----------------------------
@INFO_TARGET_WEAPON(L_I, WEAPON_TYPE = "MAIN")
#DIM  L_I
#DIMS WEAPON_TYPE
SELECTCASE WEAPON_TYPE
CASE "MAIN"
	PRINTFORM [
	PRINTFORM 주 {INFO_STATUS_GET_BY_LABEL(L_I, "主練度"),2}
	PRINTFORM %CHARATU(INFO_STR_HEISOU(CFlag:L_I:住人_M兵装), 0)%
	PRINTFORM %INFO_STR_MAIN_WEAPON_DAMAGE(L_I)%
	PRINTFORM ]
CASE "SUB"
	PRINTFORM [
	PRINTFORM 부 {INFO_STATUS_GET_BY_LABEL(L_I, "副練度"),2}
	PRINTFORM %CHARATU(INFO_STR_HEISOU(CFlag:L_I:住人_S兵装), 0)%
	PRINTFORM %INFO_STR_SUB_WEAPON_DAMAGE(L_I)%
	PRINTFORM ]
ENDSELECT



;-----------------------------
;-- 登録抹消情報
;-----------------------------
@INFO_TARGET_DELETE(C_ID)
#DIM          C_ID
IF CFLAG:C_ID:登録抹消 == 1
	PRINTFORM ×
ELSE
	PRINTFORM 　
ENDIF

;-----------------------------
;-- 레귤러情報
;-----------------------------
@INFO_TARGET_REGULAR(C_ID, SHORT)
#DIM          C_ID
#DIM          SHORT
IF SHORT
	IF CFLAG:C_ID:레귤러 == 1
		PRINTFORM 레귤러
	ELSE
		PRINTFORM 　
	ENDIF
	RETURN
ENDIF
IF CFLAG:C_ID:레귤러 == 1
	PRINTFORM [레귤러:ON ]
ELSE
	PRINTFORM [레귤러:OFF]
ENDIF



;-----------------------------
;-- 특별보호情報
;-----------------------------
@INFO_TARGET_PROTECT(C_ID, SHORT)
#DIM          C_ID
#DIM          SHORT
IF SHORT
	IF CFLAG:C_ID:특별보호 == 1
		PRINTFORM 보호
	ELSE
		PRINTFORM 　
	ENDIF
	RETURN
ENDIF
IF CFLAG:C_ID:특별보호 == 1
	PRINTFORM [특별 보호:ON ]
ELSE
	PRINTFORM [특별 보호:OFF]
ENDIF



;-----------------------------
;-- 交流イベント情報
;-----------------------------
@INFO_TARGET_PARTNER(C_ID)
#DIM        C_ID
#DIM        L_NPC
#DIMS CONST STR_EVE_RATE  = "NG", "낮음", "보통", "많음"
#DIMS CONST STR_EVE_LIMIT = "NG", "친구", "베프", "연인", "없음"
#DIMS CONST NPC_LV        = "－", "지인", "친구", "베프", "연인", "결혼"
#DIMS CONST NPC_TYPE        = "교", "연", "범", "성"
PRINTFORM 상한：%STR_EVE_LIMIT:(LIMIT(CFlag:C_ID:住人交流設定, 0, 4))%
FOR L_NPC, 0, 4
	PRINTFORM  %NPC_TYPE:L_NPC%
	PRINTFORM ：%STR_EVE_RATE:(LIMIT(CFlag:C_ID:住人イベント傾向_交流 / 30, 0, 3))%/
	SELECTCASE CFLAG:C_ID:(3020+L_NPC)
	CASE 0 TO 5
		PRINTFORM %NPC_LV:(CFLAG:C_ID:(3020+L_NPC))%
	CASE -5
		PRINTFORM 애인
	ENDSELECT
NEXT








;-----------------------------
;-- 입주室情報
;-----------------------------
@INFO_TARGET_ROOM_NUMBER(C_ID)
#DIM          C_ID
#DIMS DYNAMIC ROOM_NO
IF !INRANGE(CFLAG:C_ID:マンションID, 1, 10)
	PRINTFORM 　
	RETURN
ENDIF
PRINTFORM %UNICODE(0x02460 - 1 + CFLAG:C_ID:マンションID)%
;FLAG:(5000+L_LS) = CFLAG:C_S:個別番号
;CFLAG:C_S:マンションID = L_LS




;-----------------------------
;-- 階級情報
;-----------------------------
@INFO_TARGET_RANK(C_ID)
#DIM        C_ID
#DIM        TEMP_RANK
#DIMS CONST RANKNAME    = "NONE-", "SevenR", "SixO", "FiveY", "FourG", "ThreeB", "TwoI", "OneV"
TEMP_RANK = INFO_STATUS_GET_BY_LABEL(C_ID, "階級")
SETCOLOR RANK_TO_COLOR(TEMP_RANK)
PRINTFORMD [
PRINTFORM ■
PRINTFORMD %RANKNAME:TEMP_RANK, 6%]
RESETCOLOR



;-----------------------------
;-- 遺伝子情報
;-----------------------------
@INFO_TARGET_GENOM(C_ID)
#DIM          C_ID
#DIMS CONST   G_NAME = "人", "獣", "魔", "機", "精", "闇", "外", "雑", "主", "虫", "病", "天", "魚", "鳥", "死", "強", "堕", "勇", "妖", "貴"
#DIMS DYNAMIC L_STR
#DIM  CONST   CFLAG_START = 1800
#DIM          GENOM_ID
FOR GENOM_ID, 0, 20
	IF CFLAG:C_ID:(CFLAG_START + GENOM_ID)
		L_STR += G_NAME:GENOM_ID
	ELSE
		L_STR += "　"
	ENDIF
NEXT
PRINTFORM %L_STR%


;-----------------------------
;-- 次世代継承関連情報
;-----------------------------
@INFO_TARGET_BREEDING(C_ID)
#DIM          C_ID
PRINTFORM [출산 횟수:{CFLAG:C_ID:住人_回収済み数, 3 ,RIGHT}][계승 횟수:{CFLAG:C_ID:住人_継承, 3 ,RIGHT}]



;-----------------------------
;-- 社会関連情報
;-----------------------------
@INFO_TARGET_SOCIALITY(C_ID)
#DIM          C_ID
PRINTFORM [인기:{MIN(CFLAG:C_ID:住人_人気, 999999), 6 ,RIGHT}][모랄:{CFLAG:C_ID:住人_モラル, 4 ,RIGHT}][카르마:{CFLAG:C_ID:住人_カルマ, 4 ,RIGHT}][품성:{CFLAG:C_ID:住人_品性, 4 ,RIGHT}]



;-----------------------------
;-- 所属部隊情報
;-----------------------------

@INFO_TARGET_SUPPORT(C_ID)
#DIM          C_ID
#DIM  DYNAMIC SUP_ID
#DIMS CONST   SUP_NAME = "－", "随", "後", "回", "探"
SUP_ID = INFO_STATUS_GET_BY_LABEL(C_ID, "部隊")
PRINTFORM [%SUP_NAME:SUP_ID%]



;-----------------------------
;-- 配属施設情報
;-----------------------------
@INFO_TARGET_FACILITY(C_ID, SHORT)
#DIM          C_ID
#DIM          SHORT
#DIMS DYNAMIC FAC_NAME
IF SHORT
	IF CFLAG:C_ID:学園生徒 == 1
		FAC_NAME '= "학"
	ELSEIF  CFLAG:C_ID:支援ボランティア == 1
		FAC_NAME '= "봉"
	ELSEIF  CFLAG:C_ID:ラブホテル店員 == 1
		FAC_NAME '= "러"
	ELSEIF  CFLAG:C_ID:カジノ店員 == 1
		FAC_NAME '= "카"
	ELSE
		FAC_NAME '= "－－"
	ENDIF
	PRINTFORM [%FAC_NAME%]
	RETURN
ENDIF
IF CFLAG:C_ID:学園生徒 == 1
	FAC_NAME '= "학원"
ELSEIF  CFLAG:C_ID:支援ボランティア == 1
	FAC_NAME '= "지원주택"
ELSEIF  CFLAG:C_ID:ラブホテル店員 == 1
	FAC_NAME '= "러브호텔"
ELSEIF  CFLAG:C_ID:カジノ店員 == 1
	FAC_NAME '= "카지노"
ELSE
	FAC_NAME '= "－－"
ENDIF
PRINTFORM [배속:%FAC_NAME, 10%]



;-----------------------------
;-- 身分情報
;-----------------------------
@INFO_TARGET_JOB(C_ID, SHORT)
#DIM          C_ID
#DIM          SHORT
#DIM          L_SEX
#DIMS DYNAMIC JOB_NAME
IF SHORT
	SELECTCASE TALENT:C_ID:학생
	CASE 0
		JOB_NAME '= "　"
	CASE 5
		JOB_NAME '= "초"
	CASE 4
		JOB_NAME '= "중"
	CASE 3
		JOB_NAME '= "고"
	CASE 2
		JOB_NAME '= "대"
	CASEELSE
		JOB_NAME '= "학"
	ENDSELECT
	
	IF TALENT:C_ID:성직자 != 0
		JOB_NAME += "성직자"
	ELSEIF TALENT:C_ID:메이드 != 0
		JOB_NAME += "메이드"
	ELSEIF TALENT:C_ID:아이돌 != 0
		JOB_NAME += "아이돌"
	ELSEIF TALENT:C_ID:사회인 != 0
		JOB_NAME += "사회인"
	ELSEIF TALENT:C_ID:치안유지국직원 != 0
		JOB_NAME += "치안국"
	ELSE
		JOB_NAME += "　"
	ENDIF
	IF TALENT:C_ID:창녀 != 0
		JOB_NAME += "창녀"
	ELSE
		JOB_NAME += "　"
	ENDIF


	PRINTFORM [%JOB_NAME, 6%]
	RETURN
ENDIF

L_SEX = VAR_SEX(C_ID)
IF TALENT:C_ID:학생 != 0
	SELECTCASE TALENT:C_ID:학생
	CASE 5
		JOB_NAME '= "초등학생"
	CASE 4
		JOB_NAME '= "중학생"
	CASE 3
		JOB_NAME '= "고등학생"
		SIF L_SEX
			JOB_NAME '= "여고생"
	CASE 2
		JOB_NAME '= "대학생"
		SIF L_SEX
			JOB_NAME '= "여대생"
	CASEELSE
		JOB_NAME '= "힉셍"
	ENDSELECT
	SIF TALENT:C_ID:창녀 != 0
		JOB_NAME '= "후원자대기" + JOB_NAME
ENDIF
IF TALENT:C_ID:성직자 != 0
	JOB_NAME += "성직자"
ELSEIF TALENT:C_ID:메이드 != 0
	JOB_NAME += "메이드"
ELSEIF TALENT:C_ID:아이돌 != 0
	JOB_NAME '= JOB_NAME + "아이돌"
ELSEIF TALENT:C_ID:사회인 != 0
	JOB_NAME '= "사회인"
ELSEIF TALENT:C_ID:창녀 != 0
	JOB_NAME '= "창녀"
ELSEIF TALENT:C_ID:치안유지국직원 != 0
	JOB_NAME '= "치안 유지국 직원"
ENDIF
PRINTFORM [%JOB_NAME, 24%]



;=================================================
;   sub INFO_WORK_NAME
;=================================================
;   情報:労役役職名
;-------------------------------------------------
; Input:
;  ARG:0				対象キャラ
; Output:
;  RETURNF				労役役職名
;-------------------------------------------------
@INFO_WORK_NAME(ARG)
#FUNCTIONS
#DIMS L_STR

SELECTCASE CFLAG:ARG:労役役職タイプ
	CASE [[労役役職:通常職]]
		L_STR = 통상직
	CASE [[労役役職:歌姫職]]
		L_STR = 가희직
	CASE [[労役役職:戦闘職]]
		L_STR = 전투직
	CASE [[労役役職:娼館職]]
		L_STR = 창관직
	CASEELSE
		L_STR = ―――
ENDSELECT

RETURNF L_STR

;=================================================
;   sub INFO_危険日
;=================================================
;   情報:危険日
;-------------------------------------------------
; Input:
;  ARG:0				対象キャラ
;  ARG:1				句点有無
;-------------------------------------------------
@INFO_危険日(ARG, ARG:1=0)

;위험일 且つ 女
IF C_危険日(ARG) && VAR_SEX(ARG)
	SIF ARG:1
		PRINT ,
	SETCOLOR COLOR("紫")
	CALL CP_HEARTB()
	PRINTFORM 위험일
	CALL CP_HEARTB()
	RESETCOLOR
ENDIF


;-------------------------------------------------
;ラベル一覧を使用したソート用ボタン生成関数
;-------------------------------------------------
;渡されたラベル一覧配列をボタンとして表示する
@INFO_LABEL_LIST(SORT_TYPE, LABEL_LIST, LABEL_SIZE)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS     SORT_TYPE  ;現在のSORT内容
#DIMS REF LABEL_LIST ;この行で使用するラベル一覧配列
#DIM      LABEL_SIZE ;ラベル一覧配列の要素数
FOR LOCAL, 0, LABEL_SIZE
	PRINT |
	CALL INFO_SORT_BUTTON(SORT_TYPE, LABEL_LIST:LOCAL)
NEXT



;-------------------------------------------------
;ソート用ボタン作成関数
;-------------------------------------------------
@INFO_SORT_BUTTON(SORT_TYPE, B_NAME)
#DIMS SORT_TYPE ;現在のソート順
#DIMS B_NAME    ;基本のボタン名
IF SORT_TYPE == @"%B_NAME%내림차순"
	PRINTBUTTON @"▼%B_NAME%", @"%B_NAME%오름차순"
ELSEIF SORT_TYPE == @"%B_NAME%오름차순"
	PRINTBUTTON @"▲%B_NAME%", @"%B_NAME%내림차순"
ELSE
	PRINTBUTTON @"　%B_NAME%", @"%B_NAME%내림차순"
ENDIF



;-------------------------------------------------
;ラベル使用一行정보 표시関数
;-------------------------------------------------
;キャラの情報を一行で表示しつつボタンとして扱う
;キャララベル（ボタンとしての番号）と登録番号を別々に指定する必要がある
@INFO_CHARA_DETAIL_LABEL(C_LABEL, C_ID, SORT_TYPE, LABEL_LIST, LABEL_SIZE)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM      C_LABEL    ;ボタンとして表示させる数値
#DIM      C_ID       ;キャラの登録番号
#DIMS     SORT_TYPE  ;現在のソート順
#DIMS REF LABEL_LIST ;この行で使用するラベル一覧配列
#DIM      LABEL_SIZE ;ラベル一覧配列の要素数
;C_LABELが負数はありえないので表示内容のアナウンスに流用してます
IF C_LABEL < 0
	PRINTFORM %" " * 26%
	CALL INFO_LABEL_LIST(SORT_TYPE, LABEL_LIST, LABEL_SIZE)
	PRINTFORML 
ELSE
	PRINTFORM [{C_LABEL, 3}]
	CALL VAR_PRINT_LIST_NAME(C_ID)
	CALL INFO_LABEL_ARRAY(C_ID, LABEL_LIST, LABEL_SIZE)
	PRINTFORML 
ENDIF



;-------------------------------------------------
;ラベル一覧を使用したキャラステータス簡易表示関数
;-------------------------------------------------
;渡されたLABEL_LISTに入っているLABEL_NAMEを順に表示していく
@INFO_LABEL_ARRAY(C_ID, LABEL_LIST, LABEL_SIZE)
#DIM      C_ID       ;参照する登録番号
#DIMS REF LABEL_LIST ;この行で使用するラベル一覧配列
#DIM      LABEL_SIZE ;ラベル一覧配列の要素数
DEBUGPRINTFORML INFO_LABEL_ARRAY LABEL_SIZE{LABEL_SIZE}
FOR LOCAL, 0, LABEL_SIZE
	PRINT |
	CALL CP_STATUS_BY_LABEL(C_ID, LABEL_LIST:LOCAL)
NEXT



;-------------------------------------------------
;都市の予算情報
;-------------------------------------------------
@INFO_CITY_BASE
PRINTFORML 【ＴＰ:{Flag:都市_現在TP}/{Flag:都市_最大TP}】【BP:{FLAG:都市_現在BP}】【AP:{FLAG:都市_現在AP}】【소지금:{MONEY}】


