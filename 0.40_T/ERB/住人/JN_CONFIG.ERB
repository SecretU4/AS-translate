
;-------------------------------------------------
; 住人設定
;-------------------------------------------------
@JN_CONFIG
#DIM L_INPUT

$INPUT_LOOP2
CLEARLINE LINECOUNT

DRAWLINE
PRINTFORML 변경할 항목을 선택하세요。
PRINTFORML [0] - てすと
PRINTFORML [1] - 레귤러 설정(일부 이벤트 발생)
PRINTFORML [2] - 특별 보호 설정(피해 보호)
PRINTFORML [3] - 각 거주자 이벤트 경향
PRINTFORML [4] - 교류 대상
PRINTFORML [5] - 의상 변경
PRINTFORML [6] - 직위 변경
PRINTFORML
PRINTFORML [50] - 이름 변경
PRINTFORML
PRINTFORML
PRINTFORML [100] - 주민 말소
PRINTFORML 
PRINTFORML [999] - 돌아온다

$INPUT_LOOP
INPUT
L_INPUT = RESULT

SELECTCASE L_INPUT
CASE 0
	CALL MANAGE_CHARA_SELECT("MNG_레귤러指定")
CASE 1
	CALL JN_CONFIG_REGULAR
CASE 2
	CALL JN_CONFIG_T_BLOCK
CASE 3
	CALL JN_CONFIG_EVENT
CASE 4
	CALL JN_CONFIG_COM
CASE 5
	CALL COSTUME_MAIN
CASE 6
	CALL POSITION_CHANGE
CASE 50
	CALL JN_CONFIG_N_C
CASE 100
	CALL JN_CONFIG_DELETE
CASE 999
	RETURN
CASEELSE
	GOTO INPUT_LOOP
ENDSELECT

RESTART




;-------------------------------------------------
; 레귤러設定
;-------------------------------------------------
@JN_CONFIG_REGULAR(ARG:0,ARG:1,ARGS:0)
#DIM L_INPUT
#DIM L_INPUT2
#DIM L_I
#DIM L_REGULAR
$DATA_LIST
L_INPUT = 0
L_INPUT2 = 0
L_REGULAR = 0
L_I = 0

DRAWLINE
PRINTFORML 레귤러 설정을 변경할 주민을 선택하세요(레귤러는 최대 20명까지)
DRAWLINE
SETCOLOR 0,250,250
PRINTFORM 【레귤러:
ARG:0 = 531
ARG:1 = 1
CALL MEMBER_LIST2(ARG:0,ARG:1)
L_REGULAR = FLAG:汎用変数
PRINTFORML 】
RESETCOLOR


FOR L_I,0,CHARANUM
	CSTR:L_I:4999 '= \@(CFlag:L_I:레귤러) ? o # x\@ + " "
NEXT
ARGS:0 = 레귤러
CALL LIST_CHARA(ARGS:0,,,4999)

L_INPUT = RESULT

IF L_INPUT != 1000
	IF TALENT:L_INPUT:NPC == 999 
		PRINTFORMW NPC는 선택할 수 없습니다
		GOTO DATA_LIST
	ELSEIF L_INPUT == 0
		PRINTFORMW 아나타는 선택할 수 없습니다
		GOTO DATA_LIST
	ELSEIF L_REGULAR >= 20 && CFlag:L_INPUT:레귤러 == 0
		PRINTFORMW 레귤러는 최대 20명 입니다
		GOTO DATA_LIST
	ELSEIF CFlag:L_INPUT:레귤러 == 0
		CFlag:L_INPUT:레귤러 = 1
		PRINTFORMW %CALLNAME:L_INPUT%を 레귤러에 넣었습니다
		GOTO DATA_LIST
	ELSEIF CFlag:L_INPUT:레귤러 == 1
		CFlag:L_INPUT:레귤러 = 0
		PRINTFORMW %CALLNAME:L_INPUT%を 레귤러에서 제외했습니다
		GOTO DATA_LIST
	ENDIF
ENDIF

; CALL JN_CONFIG



;-------------------------------------------------
; 특별보호設定
;-------------------------------------------------
@JN_CONFIG_T_BLOCK(ARG:0,ARG:1,ARGS:0)
#DIM L_INPUT
#DIM L_INPUT2
#DIM L_I
#DIM L_REGULAR
$DATA_LIST
L_INPUT = 0
L_INPUT2 = 0
L_REGULAR = 0
L_I = 0

DRAWLINE
PRINTFORML 특별 보호 설정을 변경할 거주자를 선택하세요(특별 보호는 최대 30명까지)
PRINTFORML 특별 보호된 거주자는 불이익・피해를 입히는 이벤트가 발생하지 않습니다。
DRAWLINE
SETCOLOR 0,250,250
PRINTFORM 【보호 처리:
ARG:0 = 532
ARG:1 = 1
CALL MEMBER_LIST2(ARG:0,ARG:1)
L_REGULAR = FLAG:汎用変数
PRINTFORML 】
RESETCOLOR
DRAWLINE




FOR L_I,0,CHARANUM
	CSTR:L_I:4999 '= \@(CFlag:L_I:특별보호) ? o # x\@ + " "
NEXT
ARGS:0 = 특별보호
CALL LIST_CHARA(ARGS:0,,,4999)

L_INPUT = RESULT

IF L_INPUT != 1000
	IF TALENT:L_INPUT:NPC == 999 
		PRINTFORMW NPC는 선택할 수 없습니다
		GOTO DATA_LIST
	ELSEIF L_INPUT == 0
		PRINTFORMW 아나타는 선택할 수 없습니다
		GOTO DATA_LIST
	ELSEIF L_REGULAR >= 30&& CFlag:L_INPUT:특별보호 == 0
		PRINTFORMW 특별 보호는 30명까지입니다
		GOTO DATA_LIST
	ELSEIF CFlag:L_INPUT:특별보호 == 0
		CFlag:L_INPUT:특별보호 = 1
		PRINTFORMW %CALLNAME:L_INPUT%を 특별 보호 대상에 넣었습니다
		GOTO DATA_LIST
	ELSEIF CFlag:L_INPUT:특별보호 == 1
		CFlag:L_INPUT:특별보호 = 0
		PRINTFORMW %CALLNAME:L_INPUT%を 특별 보호 대상에서 제외했습니다
		GOTO DATA_LIST
	ENDIF
ENDIF

; CALL JN_CONFIG




;-------------------------------------------------
; 各住人イベント傾向
;-------------------------------------------------
@JN_CONFIG_EVENT
#DIM L_I
#DIM L_INPUT
$DATA_LIST

L_I = 0

DRAWLINE
PRINTFORML 변경할 캐릭터를 선택하세요
DRAWLINE




CALL LIST_CHARA

L_INPUT = RESULT
IF L_INPUT != 1000
	IF TALENT:L_INPUT:NPC == 999 
		PRINTFORMW NPC는 선택할 수 없습니다
		GOTO DATA_LIST
	ELSEIF L_INPUT == 0
		PRINTFORMW 아나타는 선택할 수 없습니다
		GOTO DATA_LIST
	ENDIF
	L_I = L_INPUT
	$INPUT_LOOP2
	CLEARLINE LINECOUNT
	DRAWLINE
	PRINTFORML  {L_I} %CALLNAME:L_I% 변경하고 싶은 이벤트 경향을 선택하세요。
	DRAWLINE
	PRINTFORM [0] 교류 이벤트(
	IF  CFlag:L_I:住人イベント傾向_交流 == 90
		PRINTFORM 많음
	ELSEIF CFlag:L_I:住人イベント傾向_交流 == 60
		PRINTFORM 보통
	ELSEIF CFlag:L_I:住人イベント傾向_交流 == 30
		PRINTFORM 적음
	ELSE
		PRINTFORM 발생하지 않음
	ENDIF
	PRINTFORML )
	PRINTFORM [1] 연애 이벤트(
	IF  CFlag:L_I:住人イベント傾向_恋愛 == 90
		PRINTFORM 많음
	ELSEIF CFlag:L_I:住人イベント傾向_恋愛 == 60
		PRINTFORM 보통
	ELSEIF CFlag:L_I:住人イベント傾向_恋愛 == 30
		PRINTFORM 적음
	ELSE
		PRINTFORM 발생하지 않음
	ENDIF
	PRINTFORML )
	PRINTFORM [2] 범죄 이벤트(
	IF  CFlag:L_I:住人イベント傾向_犯罪 == 90
		PRINTFORM 많음
	ELSEIF CFlag:L_I:住人イベント傾向_犯罪 == 60
		PRINTFORM 보통
	ELSEIF CFlag:L_I:住人イベント傾向_犯罪 == 30
		PRINTFORM 적음
	ELSE
		PRINTFORM 발생하지 않음
	ENDIF
	PRINTFORML )
	PRINTFORM [3] 성적인 이벤트(
	IF  CFlag:L_I:住人イベント傾向_性 == 90
		PRINTFORM 많음
	ELSEIF CFlag:L_I:住人イベント傾向_性 == 60
		PRINTFORM 보통
	ELSEIF CFlag:L_I:住人イベント傾向_性 == 30
		PRINTFORM 적음
	ELSE
		PRINTFORM 발생하지 않음
	ENDIF
	PRINTFORML )
	PRINTFORM [10] 주민 교류 상한(
	IF  CFlag:L_I:住人交流設定 == 4
		PRINTFORM 상한 없음
	ELSEIF CFlag:L_I:住人交流設定 == 3
		PRINTFORM 연인
	ELSEIF CFlag:L_I:住人交流設定 == 2
		PRINTFORM 베프
	ELSEIF CFlag:L_I:住人交流設定 == 1
		PRINTFORM 친구
	ELSE
		PRINTFORM 발생하지 않음
	ENDIF
	PRINTFORML )
	PRINTFORML 
	PRINTFORML [999] 돌아온다
	PRINTFORMLC \@(L_I > 1) ? [1001]이전 캐릭터 # %" " * 16%\@
	PRINTFORMLC   %" " * 18%
	PRINTFORMLC \@(L_I < CHARANUM-1) ? [1003]다음 캐릭터 # %" " * 16%\@
	$INPUT_LOOP
	INPUT
	L_INPUT = RESULT


	SELECTCASE L_INPUT
	CASE 999
	CASE 10
		CFlag:L_I:(3950+L_INPUT) += 1
		SIF CFlag:L_I:(3950+L_INPUT) >= 5
			CFlag:L_I:(3950+L_INPUT) = 0
		GOTO INPUT_LOOP2
	CASE 1001
		IF (L_I > 1) 
			L_I --
			GOTO INPUT_LOOP2
		ENDIF
		GOTO INPUT_LOOP
	CASE 1003
		IF (L_I < CHARANUM-1) 
			L_I ++
			GOTO INPUT_LOOP2
		ENDIF
		GOTO INPUT_LOOP
	CASE L_INPUT
		CFlag:L_I:(3950+L_INPUT) += 30
		SIF CFlag:L_I:(3950+L_INPUT) >= 100
			CFlag:L_I:(3950+L_INPUT) = 0
		GOTO INPUT_LOOP2
	CASEELSE
		GOTO INPUT_LOOP
	ENDSELECT
ENDIF




;-------------------------------------------------
; デリート
;-------------------------------------------------
@JN_CONFIG_DELETE
#DIM L_I
#DIM L_INPUT
$DATA_LIST

L_I = 0

DRAWLINE
PRINTFORML 삭제할 캐릭터를 선택하세요
DRAWLINE




CALL LIST_CHARA

L_INPUT = RESULT
IF L_INPUT != 1000
	IF TALENT:L_INPUT:NPC == 999 
		PRINTFORMW NPC는 선택할 수 없습니다
		GOTO DATA_LIST
	ELSEIF L_INPUT == 0
		PRINTFORMW 아나타는 선택할 수 없습니다
		GOTO DATA_LIST
	ENDIF
	L_I = L_INPUT
	PRINTFORML %CALLNAME:L_I%を 삭제합니다、정말 괜찮습니까？
	PRINTFORML [0] 네
	PRINTFORML [999] 돌아온다
	$INPUT_LOOP
	INPUT
	L_INPUT = RESULT
	SELECTCASE L_INPUT
	CASE 999
	CASE 0
	PRINTFORML 
	PRINTFORMW %CALLNAME:L_I%を 삭제했습니다
	DELCHARA  L_I
	CASEELSE
		GOTO INPUT_LOOP
	ENDSELECT
ENDIF









; CALL JN_CONFIG

;-------------------------------------------------
; 各交流住人
;-------------------------------------------------
@JN_CONFIG_COM
#DIM L_NPC
#DIM L_INPUT
#DIM L_T
#DIM L_NAME

L_T = TARGET
$COM_LIST

DRAWLINE
PRINTFORML 참조할 캐릭터를 선택하세요
DRAWLINE
CALL LIST_CHARA
TARGET = RESULT

SIF RESULT == 1000
	RETURN

IF TALENT:RESULT:NPC == 999 
	PRINTFORMW NPC는 선택할 수 없습니다
	GOTO COM_LIST
ELSEIF RESULT == 0
	PRINTFORMW 아나타는 선택할 수 없습니다
	GOTO COM_LIST
ENDIF


$INPUT_LOOP0
CLEARLINE LINECOUNT
DRAWLINE
PRINTFORML  {TARGET} %CALLNAME:TARGET%
PRINTFORML 교류 대상의 즐겨찾기、이름 변경을 할 수 있습니다。
DRAWLINE
L_NPC = 0
PRINTFORML [-1] 즐겨찾기 해제
$INPUT_LOOP
IF CSTR:(3000+L_NPC) == ""
	PRINTFORML 【미등록】
ELSE
SIF CFLAG:3600 == L_NPC
	SETCOLOR 50,250,250
	PRINTFORM [{L_NPC}] %CSTR:(3000+L_NPC), 18 ,LEFT%
	PRINTFORM 【
	IF CFLAG:(3030+L_NPC) == 1
		PRINTFORM 남
	ELSE
		PRINTFORM 녀
	ENDIF
	PRINTFORM 】【관계:
	IF CFLAG:(3020+L_NPC) == 5
		PRINTFORM 부부
	ELSEIF CFLAG:(3020+L_NPC) == 4
		PRINTFORM 연인
	ELSEIF CFLAG:(3020+L_NPC) == 3
		PRINTFORM 베프
	ELSEIF CFLAG:(3020+L_NPC) == 2
		PRINTFORM 친구
	ELSEIF CFLAG:(3020+L_NPC) == 1
		PRINTFORM 지인
	ELSEIF CFLAG:(3020+L_NPC) == -5
		PRINTFORM 애인
	ENDIF
	PRINTFORM 】【호감도:{CFLAG:(3010+L_NPC), 3 ,LEFT}】【상성:{CFLAG:(3050+L_NPC), 3 ,LEFT}】【성행위 횟수:{CFLAG:(3060+L_NPC)}】
	PRINTFORML
ENDIF
RESETCOLOR
L_NPC += 1
SIF L_NPC < 10
	GOTO INPUT_LOOP
DRAWLINE
PRINTFORML
PRINTFORML 
PRINTFORML [999] 돌아온다
PRINTFORMLC \@(TARGET > 1) ? [1001]이전 캐릭터 # %" " * 16%\@
PRINTFORMLC   %" " * 18%
PRINTFORMLC \@(TARGET < CHARANUM-1) ? [1003]다음 캐릭터 # %" " * 16%\@
$INPUT_LOOP2
INPUT
L_INPUT = RESULT
SELECTCASE L_INPUT
CASE -1
	CFLAG:3600 = -1
	PRINTFORMW 【즐겨찾기를 해제했습니다】
	GOTO INPUT_LOOP0
CASE 999
	TARGET = L_T
	; CALL JN_CONFIG
	RETURN
CASE 1001
	IF (TARGET > 1) 
		TARGET --
		GOTO INPUT_LOOP0
	ENDIF
	GOTO INPUT_LOOP2
CASE 1003
	IF (TARGET < CHARANUM-1) 
		TARGET ++
		GOTO INPUT_LOOP0
	ENDIF
	GOTO INPUT_LOOP2
CASEELSE
	CFLAG:3600 = RESULT
	PRINTFORML 【%CSTR:(3000+RESULT)%を 즐겨찾기에 등록했습니다】
	PRINTFORMW 즐겨찾기에 등록한 거주자와는 교류 발생이 많아지며、호감도・상성 증가에 보정이 붙습니다。
	DRAWLINE
	PRINTFORML 【이어서 이름을 변경합니다、입력하지 않으면 변경하지 않습니다。】
	DRAWLINE
	INPUTS
	IF RESULTS == ""
		;スルー
	ELSE
		SIF CFLAG:(3020+L_NPC) == 5
		CSTR:伴侶名前 = %RESULTS%
		CSTR:(3000+RESULT) = %RESULTS%
		PRINTFORML 【이름을 %CSTR:(3000+RESULT)%로 변경했습니다】
		SIF TALENT:아이같음 || TALENT:깜찍함
			CSTR:一人称 = %CALLNAME:TARGET%
	ENDIF
	GOTO INPUT_LOOP0
ENDSELECT









IF TALENT:결혼완료 == 1
	PRINTFORML [200] 남편의 이름 변경
ENDIF 
TARGET = L_T

;-------------------------------------------------
;役職変更
;-------------------------------------------------
@POSITION_CHANGE
#DIM L_INPUT
#DIM L_INPUT2
#DIM L_P

$INPUT_LOOP
CLEARLINE LINECOUNT

DRAWLINE
PRINTFORML 자금을 500000소비하여 주민의 직위를 변경할 수 있습니다。
PRINTFORML 다음 직위에서 선택하세요。
PRINTFORML 
PRINTFORML [1]  - 직위 없음
PRINTFORML [93] - 성직자
PRINTFORML [94] - 메이드
PRINTFORML [95] - 학생
PRINTFORML [96] - 아이돌
PRINTFORML [97] - 사회인
PRINTFORML [98] - 창녀
PRINTFORML [99] - 치안 유지국 직원
PRINTFORML
PRINTFORML 
PRINTFORML [999] - 돌아온다

INPUT
L_P = RESULT
L_INPUT = RESULT

SIF L_INPUT == 999
	RETURN
	
IF MONEY < 500000
	PRINTFORML 
	PRINTFORML 자금이 부족합니다。
	PRINTFORMW 
ENDIF

PRINTFORML 
PRINTFORML 직위를 변경할 주민을 선택하세요。
PRINTFORML 




CALL LIST_CHARA

L_INPUT = RESULT
SIF L_INPUT == 1000
	RETURN
IF L_INPUT != 1000
	IF TALENT:L_INPUT:NPC == 999 
		PRINTFORMW NPC는 선택할 수 없습니다
		GOTO INPUT_LOOP
	ELSEIF L_INPUT == 0
		PRINTFORMW 아나타는 선택할 수 없습니다
		GOTO INPUT_LOOP
	ENDIF
	
	IF TALENT:L_INPUT:학생 != 0 && L_P != 95
		PRINTFORML 학생의 신분을 유지합니까？
		PRINTFORML 
		PRINTFORML [1] - 네
		PRINTFORML [2] - 아니오
		INPUT
		L_INPUT2 = RESULT
		SIF L_INPUT2 >= 2
			TALENT:L_INPUT:학생 = 0
	ELSE
		TALENT:L_INPUT:학생 = 0
	ENDIF
	TALENT:L_INPUT:성직자 = 0
	TALENT:L_INPUT:메이드 = 0
	TALENT:L_INPUT:아이돌 = 0
	TALENT:L_INPUT:사회인 = 0
	TALENT:L_INPUT:창녀 = 0
	TALENT:L_INPUT:치안유지국직원 = 0


	IF L_P == 95
		PRINTFORML 학생의 신분을 선택하세요
		PRINTFORML 
		PRINTFORML [1] - 보통 학생
		PRINTFORML [2] - 대학생
		PRINTFORML [3] - 고등학생
		PRINTFORML [4] - 중학생
		PRINTFORML [5] - 초등학생
		INPUT
		L_INPUT2 = RESULT
		TALENT:L_INPUT:L_P = L_INPUT2
	ELSE
		TALENT:L_INPUT:L_P = 1
	ENDIF
	MONEY -= 500000
	PRINTFORML 
	PRINTFORML 변경했습니다。
	PRINTFORMW 
ENDIF









;-------------------------------------------------
;名前変更
;-------------------------------------------------
@JN_CONFIG_N_C
#DIM L_INPUT
#DIM L_NAME



$DATA_LIST

DRAWLINE
PRINTFORML 이름을 바꿀 거주자를 선택하세요。
DRAWLINE

CALL LIST_CHARA

L_INPUT = RESULT
L_NAME = RESULT
SIF L_INPUT == 1000
	RETURN
	; CALL JN_CONFIG
IF L_INPUT != 1000
	IF TALENT:L_INPUT:NPC == 999 
		PRINTFORMW NPC는 선택할 수 없습니다
		GOTO DATA_LIST
	ELSEIF L_INPUT == 0
		PRINTFORMW 아나타는 선택할 수 없습니다
		GOTO DATA_LIST
	ENDIF
	DRAWLINE
	PRINTL 새로운 CALLNAME을 선택하세요。
	PRINTL 입력하지 않으면、아무것도 선택하지 않고 돌아옵니다。
	DRAWLINE
	PRINTL
	INPUTS
	IF RESULTS == ""
		;スルー
	ELSE
		CALLNAME:L_NAME = %RESULTS%
		SIF TALENT:L_NAME:아이같음 || TALENT:L_NAME:깜찍함
			CSTR:L_NAME:一人称 = %CALLNAME:L_NAME%
	ENDIF
ENDIF







; CALL JN_CONFIG
;-------------------------------------------------
;キャラ管理用キャラセレクト関数
;-------------------------------------------------
@MANAGE_CHARA_SELECT(MODE_NOW = "MNG_레귤러指定")
#DIMS         MODE_NOW
#DIM          MODE_ID
#DIM          L_INPUT
#DIM          NOW_GROUP = 1 ;ページ記憶用
#DIM  DYNAMIC ROOM_ID       ;
#DIMS DYNAMIC OP_RELOAD     ;初回以外のページ更新とリスト更新を抑制する
{
#DIMS CONST   MODE_NAME = "MNG_登録_登録抹消", 
	
	"MNG_입주_一号室", 
	"MNG_입주_二号室", 
	"MNG_입주_三号室", 
	"MNG_입주_四号室", 
	"MNG_입주_五号室", 
	"MNG_입주_六号室", 
	"MNG_입주_七号室", 
	"MNG_입주_八号室", 
	"MNG_입주_九号室", 
	"MNG_입주_十号室", 
	
	"MNG_登録_레귤러", 
	"MNG_登録_특별보호", 
	"MNG_登録_名前変更", 
	
	"MNG_転職_성직자", 
	"MNG_転職_메이드", 
	"MNG_転職_아이돌", 
	"MNG_転職_사회인", 
	"MNG_転職_局員", 
	"MNG_転職_창녀", 
	
	"MNG_配属_学園生徒", 
	"MNG_配属_支援ボラ", 
	"MNG_配属_ラブホ店員", 
	"MNG_配属_カジノ店員", 
	
	"MNG_特殊_外見変更", 
	"MNG_特殊_治療設備", 
	"MNG_特殊_품종개량", 
	"MNG_特殊_신체강화", 
	"MNG_特殊_차세대창조", 
	
	"MNG_登録_교우제어", 
	"MNG_登録_衣装変更", 
	"MNG_登録_範囲消去", 
	"MNG_登録_抹消中止"
}

{
#DIMS CONST   MEMO_STR = "등록 말소 후보자를 선택하세요。", 
	
	"1호실에 입주할 인물을 선택하세요。", 
	"2호실에 입주할 인물을 선택하세요。", 
	"3호실에 입주할 인물을 선택하세요。", 
	"4호실에 입주할 인물을 선택하세요。", 
	"5호실에 입주할 인물을 선택하세요。", 
	"6호실에 입주할 인물을 선택하세요。", 
	"7호실에 입주할 인물을 선택하세요。", 
	"8호실에 입주할 인물을 선택하세요。", 
	"9호실에 입주할 인물을 선택하세요。", 
	"10호실에 입주할 인물을 선택하세요。", 
	
	"관찰 상황을 바꿀 인물을 선택하세요。", 
	"보호 지정을 바꿀 대상을 선택하세요。", 
	"이름을 바꿀 대상을 선택하세요。", 
	
	"전직 대상자를 선택하세요", 
	"전직 대상자를 선택하세요", 
	"전직 대상자를 선택하세요", 
	"전직 대상자를 선택하세요", 
	"전직 대상자를 선택하세요", 
	"전직 대상자를 선택하세요", 
	
	"학원 입학 대상자를 선택하세요", 
	"지원 주택 자원봉사 후보자를 선택하세요", 
	"러브호텔 점원 후보자를 선택하세요", 
	"고급 카지노 점원 후보자를 선택하세요", 
	
	"외관 변경 대상자를 선택하세요", 
	"치료 대상자를 선택하세요", 
	"품종 개량 대상자를 선택하세요", 
	"신체 강화 대상자를 선택하세요", 
	"차세대 창조의 모체를 선택하세요", 
	
	"이벤트 설정 대상자를 선택하세요", 
	"의상 변경 대상자를 선택하세요", 
	
	"등록 말소 범위의 시작값을 지정하세요", 
	""
}
#DIM CONST INPUT_MODE_外見変更   = 1024
#DIM CONST INPUT_MODE_治療設備   = 1025
#DIM CONST INPUT_MODE_품종개량   = 1026
#DIM CONST INPUT_MODE_신체강화   = 1027
#DIM CONST INPUT_MODE_차세대창조 = 1028
#DIM CONST INPUT_MODE_抹消中止   = 1032


MODE_ID = FINDELEMENT(MODE_NAME, MODE_NOW)
SELECTCASE MODE_ID
CASE -1
	MODE_NOW '= "MNG_登録_레귤러"
	MODE_ID = FINDELEMENT(MODE_NAME, MODE_NOW)
ENDSELECT
CLEARLINE LINECOUNT
CALL LIST_CHARA_STATUS_MAIN(0, NOW_GROUP, OP_RELOAD, MODE_NOW, MEMO_STR:MODE_ID)



L_INPUT = RESULT
OP_RELOAD '= "「ページ更新無し」「リスト更新無し」"
SELECTCASE L_INPUT
CASE 0
	NOW_GROUP = 1
	CALL MNG_DELETE_CHARA
	RETURN
CASE 1 TO CHARANUM - 1
	SELECTCASE MODE_NOW
	CASE "RPG_武器強化"
		CALL RPG_WEAPON_MENU(L_INPUT)
	CASE "MNG_登録_레귤러"
		CALL MNG_CHARA_REGULAR(L_INPUT)
	CASE "MNG_登録_특별보호"
		CALL MNG_CHARA_PROTECT(L_INPUT)
	CASE "MNG_登録_名前変更"
		CALL MNG_RENAME(L_INPUT)
	CASE "MNG_登録_登録抹消"
		IF TALENT:L_INPUT:NPC
			CFLAG:L_INPUT:登録抹消 = 0
		ELSEIF CFLAG:L_INPUT:레귤러 || CFLAG:L_INPUT:특별보호 || CFLAG:L_INPUT:マンションID
			CFLAG:L_INPUT:登録抹消 = 0
		ELSE
			CFLAG:L_INPUT:登録抹消 = !CFLAG:L_INPUT:登録抹消
		ENDIF
	CASE "MNG_登録_範囲消去"
	CASE "MNG_登録_抹消中止"
	CASE "MNG_登録_교우제어"
		CALL MNG_CONFIG_EVENT(L_INPUT)
	CASE "MNG_登録_衣装変更"
		CALL MNG_CHANGE_CLOTHES(L_INPUT)
	CASE "MNG_特殊_外見変更"
		CALL MNG_CUSTOM_LOOKS(L_INPUT)
	CASE "MNG_特殊_治療設備"
		CALL MNG_CURE_SICK(L_INPUT)
	CASE "MNG_特殊_품종개량"
		CALL CITY_F_54_GI2(L_INPUT)
	CASE "MNG_特殊_신체강화"
		CALL LABORATORY_BOOST_ST(L_INPUT)
	CASE "MNG_特殊_차세대창조"
		IF CFLAG:L_INPUT:住人_回収済み数  <= CFLAG:L_INPUT:住人_継承
			PRINTFORMW 조건을 만족하지 못했습니다
		ELSE
			CALL LABORATORY_BREEDING_BABY_DESIGNING(L_INPUT)
			OP_RELOAD '= "「ページ更新無し」"
		ENDIF
	CASE "MNG_転職_성직자", "MNG_転職_메이드", "MNG_転職_아이돌", "MNG_転職_사회인", "MNG_転職_局員", "MNG_転職_창녀"
		CALL MNG_JOB(L_INPUT, MODE_NOW)
	CASE "MNG_配属_学園生徒", "MNG_配属_支援ボラ", "MNG_配属_ラブホ店員", "MNG_配属_カジノ店員"
		CALL MNG_FACILITY(L_INPUT, MODE_NOW)
	CASEELSE
		IF STRCOUNT(MODE_NOW, "MNG_입주")
			;マンション입주処理
			ROOM_ID = MODE_ID
			CALL MNG_MOVING_ROOM(L_INPUT, ROOM_ID)
		ELSE

		ENDIF
	ENDSELECT
CASE INPUT_MODE_外見変更
	IF FLAG:エステ >= 1
		MODE_NOW '= MODE_NAME:(L_INPUT % 1000)
	ENDIF
CASE INPUT_MODE_治療設備
	IF FLAG:病院 >= 1
		MODE_NOW '= MODE_NAME:(L_INPUT % 1000)
	ENDIF
CASE INPUT_MODE_품종개량
	IF FLAG:품종개량施設 >= 1
		MODE_NOW '= MODE_NAME:(L_INPUT % 1000)
	ENDIF
CASE INPUT_MODE_신체강화, INPUT_MODE_차세대창조
	IF FLAG:研究施設 >= 1
		MODE_NOW '= MODE_NAME:(L_INPUT % 1000)
	ENDIF
CASE INPUT_MODE_抹消中止
	CVARSET CFLAG, GETNUM(CFLAG, "登録抹消"), 0
	MODE_NOW '= MODE_NAME:0
CASE 1000 TO 1031
	MODE_NOW '= MODE_NAME:(L_INPUT % 1000)
CASEELSE
ENDSELECT
RESTART


@MNG_DELETE_CHARA
SIF FINDCHARA(CFLAG:登録抹消, 1) == -1
	RETURN

PRINTFORMW 주민 등록 말소를 실시합니다
FOR LOCAL, CHARANUM - 1, 0, -1
	SIF !CFLAG:LOCAL:登録抹消
		CONTINUE
	PRINTFORML 　[%CALLNAME:LOCAL%]は 당신의 관리에서 벗어났습니다
	DELCHARA LOCAL
NEXT
WAIT


;-------------------------------------------------
;레귤러指定管理関数
;-------------------------------------------------
@MNG_CHARA_REGULAR(L_C_ID)
#DIM         L_C_ID               ;対象登録番号
#DIM DYNAMIC LIST_REGULAR, 300    ;레귤러リスト
#DIM DYNAMIC CNT_REGULAR          ;레귤러キャラのカウント
#DIM         L_C_ID_REMOVE        ;解任対象登録番号
IF L_C_ID == MASTER
	PRINTFORMW ≪「플레이어」、당신의 모니터링은 불필요합니다≫
	RETURN
ELSEIF CFLAG:L_C_ID:레귤러
	CFLAG:L_C_ID:레귤러 = 0
	RETURN
ENDIF
CLEARLINE LINECOUNT
CALL INFO_STATUS_HEADER(L_C_ID)
CALL C_PICTURE_S(L_C_ID)
DRAWLINE
PRINTFORML 　현재 모니터링 대상
DRAWLINE
CALL SHOW_ALL_REGULAR(LIST_REGULAR, CNT_REGULAR, "「ボタン無し」")
DRAWLINE

IF CNT_REGULAR >= 20
;上限
	PRINTFORML 관찰 대상 인원이 너무 많습니다。
	PRINTFORML 관찰을 중지할 캐릭터를 선택하세요。
	PRINTFORML
	FOR LOCAL, 0, 20
		IF TALENT:(LIST_REGULAR:LOCAL):부재
			SETCOLOR COLOR("灰色")
			PRINT ---
			PRINTFORM [%CALLNAME:(LIST_REGULAR:LOCAL), 18%]
		ELSE
			PRINTBUTTON @"{LIST_REGULAR:LOCAL, 3}[%CALLNAME:(LIST_REGULAR:LOCAL), 18%]", LIST_REGULAR:LOCAL
		ENDIF
		RESETCOLOR
		PRINTFORM 　
		SIF LOCAL % 5 == 4
			PRINTFORML
	NEXT
	PRINTFORML
	PRINTFORML [999] - 캐릭터 선택으로 돌아온다
	DO
		INPUT
		L_C_ID_REMOVE = RESULT
		SIF L_C_ID_REMOVE == 999
			RETURN
		IF !INRANGE(L_C_ID_REMOVE, 1, CHARANUM - 1)
		ELSEIF TALENT:L_C_ID_REMOVE:부재
		ELSEIF CFLAG:L_C_ID_REMOVE:레귤러 != 1
		ELSE
			BREAK
		ENDIF
	LOOP 1
	CALL REGULAR_CHARA_REMOVE(L_C_ID_REMOVE)
ENDIF
CFLAG:L_C_ID:레귤러 = 1



;-------------------------------------------------
;레귤러指定解除関数
;-------------------------------------------------
@REGULAR_CHARA_REMOVE(L_C_ID)
#DIM         L_C_ID               ;観察対象キャラの登録番号
SIF TALENT:L_C_ID:부재 == 0
	PRINTFORMW %CALLNAME:L_C_ID%의 관찰 지정을 해제합니다。
CFLAG:L_C_ID:레귤러 = 0



;-------------------------------------------------
;레귤러指定関数
;-------------------------------------------------
@REGULAR_CHARA_SET(L_C_ID)
#DIM         L_C_ID               ;観察対象キャラの登録番号
IF CFLAG:L_C_ID:레귤러
	PRINTFORMW %CALLNAME:L_C_ID%は 이미 관찰 대상으로 지정되어 있습니다。
	PRINTFORML 관찰을 중지할까요？
	PRINTFORML [1] - 중지한다
	PRINTFORML [0] - 속행한다
	DO
		INPUT
		SIF RESULT == 1
			BREAK
		SIF RESULT == 0
			RETURN
	LOOP 1
	CALL REGULAR_CHARA_REMOVE(L_C_ID)
	RETURN
ENDIF
PRINTFORMW %CALLNAME:L_C_ID%を 관찰 대상으로 지정했습니다。
CFLAG:L_C_ID:레귤러 = 1



@SHOW_ALL_REGULAR(LIST_REGULAR, CNT_REGULAR, OP)
#DIM  REF LIST_REGULAR
#DIM  REF CNT_REGULAR
#DIM      C_ID
#DIMS     OP
VARSET LIST_REGULAR
VARSET CNT_REGULAR
IF STRCOUNT(OP, "「ボタン無し」")
	FOR C_ID, 1, CHARANUM
		SIF CFLAG:C_ID:레귤러 != 1
			CONTINUE
		SIF TALENT:C_ID:부재
			SETCOLOR COLOR("灰色")
		PRINTFORM [%CALLNAME:C_ID, 18%]
		RESETCOLOR
		SIF CNT_REGULAR % 5 == 4
			PRINTFORML
		LIST_REGULAR:CNT_REGULAR = C_ID
		CNT_REGULAR ++
	NEXT
	SIF (CNT_REGULAR - 1) % 5 != 4
		PRINTFORML
ELSE
	FOR C_ID, 1, CHARANUM
		SIF CFLAG:C_ID:레귤러 != 1
			CONTINUE
		IF TALENT:C_ID:부재
			SETCOLOR COLOR("灰色")
			PRINT ---
			PRINTFORM [%CALLNAME:C_ID, 18%]
		ELSE
			PRINTBUTTON @"{C_ID, 3}[%CALLNAME:C_ID, 18%]", C_ID
		ENDIF
		RESETCOLOR
		PRINTFORM 　
		SIF CNT_REGULAR % 5 == 4
			PRINTFORML
		LIST_REGULAR:CNT_REGULAR = C_ID
		CNT_REGULAR ++
	NEXT
	SIF (CNT_REGULAR - 1) % 5 != 4
		PRINTFORML
ENDIF





;-------------------------------------------------
;특별보호管理関数
;-------------------------------------------------
@MNG_CHARA_PROTECT(L_C_ID)
#DIM         L_C_ID               ;対象登録番号
#DIM DYNAMIC LIST_PROTECT, 300    ;특별보호リスト
#DIM DYNAMIC CNT_PROTECT          ;특별보호キャラのカウント
#DIM         L_C_ID_REMOVE        ;解任対象登録番号
IF L_C_ID == 0
	PRINTFORMW ≪「플레이어」、당신은 이미 보호받고 있습니다≫
	RETURN
ELSEIF CFLAG:L_C_ID:특별보호
	;解任
	CFLAG:L_C_ID:특별보호 = 0
	RETURN
ENDIF
CLEARLINE LINECOUNT
CALL INFO_STATUS_HEADER(L_C_ID)
CALL C_PICTURE_S(L_C_ID)
DRAWLINE
PRINTFORML 　현재 특별 보호 대상
DRAWLINE
CALL SHOW_ALL_PROTECT(LIST_PROTECT, CNT_PROTECT, "「ボタン無し」")
DRAWLINE

IF CNT_PROTECT >= 30
	;上限
	PRINTFORML 보호 대상이 너무 많습니다。
	PRINTFORML 보호 대상에서 제외할 캐릭터를 선택하세요。
	PRINTFORML
	FOR LOCAL, 0, CNT_PROTECT
		IF TALENT:(LIST_PROTECT:LOCAL):부재
			SETCOLOR COLOR("灰色")
			PRINT ---
			PRINTFORM [%CALLNAME:(LIST_PROTECT:LOCAL), 18%]
		ELSE
			PRINTBUTTON @"{LIST_PROTECT:LOCAL, 3}[%CALLNAME:(LIST_PROTECT:LOCAL), 18%]", LIST_PROTECT:LOCAL
		ENDIF
		RESETCOLOR
		PRINTFORM 　
		SIF LOCAL % 5 == 4
			PRINTFORML
	NEXT
	SIF (CNT_PROTECT - 1) % 5 != 4
		PRINTFORML
	PRINTFORML
	PRINTFORML [999] - 캐릭터 선택으로 돌아온다
	DO
		INPUT
		L_C_ID_REMOVE = RESULT
		SIF L_C_ID_REMOVE == 999
			RETURN
		IF !INRANGE(L_C_ID_REMOVE, 1, CHARANUM - 1)
		ELSEIF TALENT:L_C_ID_REMOVE:부재
		ELSEIF CFLAG:L_C_ID_REMOVE:특별보호 != 1
		ELSE
			BREAK
		ENDIF
	LOOP 1
	CALL PROTECT_CHARA_REMOVE(L_C_ID_REMOVE)

ENDIF
CFLAG:L_C_ID:특별보호 = 1



;-------------------------------------------------
;특별보호解除関数
;-------------------------------------------------
@PROTECT_CHARA_REMOVE(L_C_ID)
#DIM         L_C_ID               ;保護対象キャラの登録番号
SIF TALENT:L_C_ID:부재 == 0
	PRINTFORMW %CALLNAME:L_C_ID%を 보호 대상에서 제외했습니다。
CFLAG:L_C_ID:특별보호 = 0


;-------------------------------------------------
;특별보호指定関数
;-------------------------------------------------
@PROTECT_CHARA_SET(L_C_ID)
#DIM         L_C_ID               ;保護対象キャラの登録番号
IF CFLAG:L_C_ID:특별보호
	PRINTFORMW %CALLNAME:L_C_ID%は 이미 보호 대상으로 지정되어 있습니다。
	PRINTFORML 보호를 중지할까요？
	PRINTFORML [1] - 중지한다
	PRINTFORML [0] - 속행한다
	DO
		INPUT
		SIF RESULT == 1
			BREAK
		SIF RESULT == 0
			RETURN
	LOOP 1
	CALL PROTECT_CHARA_REMOVE(L_C_ID)
	RETURN
ENDIF
PRINTFORMW %CALLNAME:L_C_ID%を 보호 대상으로 지정했습니다。
CFLAG:L_C_ID:특별보호 = 1



@SHOW_ALL_PROTECT(LIST_PROTECT, CNT_PROTECT, OP)
#DIM  REF LIST_PROTECT
#DIM  REF CNT_PROTECT
#DIM      C_ID
#DIMS     OP
VARSET LIST_PROTECT
VARSET CNT_PROTECT
IF STRCOUNT(OP, "「ボタン無し」")
	FOR C_ID, 1, CHARANUM
		SIF CFLAG:C_ID:특별보호 != 1
			CONTINUE
		SIF TALENT:C_ID:부재
			SETCOLOR COLOR("灰色")
		PRINTFORM [%CALLNAME:C_ID, 18%]
		RESETCOLOR
		SIF CNT_PROTECT % 5 == 4
			PRINTFORML
		LIST_PROTECT:CNT_PROTECT = C_ID
		CNT_PROTECT ++
	NEXT
	SIF (CNT_PROTECT - 1) % 5 != 4
		PRINTFORML
ELSE
	FOR C_ID, 1, CHARANUM
		SIF CFLAG:C_ID:특별보호 != 1
			CONTINUE
		IF TALENT:C_ID:부재
			SETCOLOR COLOR("灰色")
			PRINT ---
			PRINTFORM [%CALLNAME:C_ID, 18%]
		ELSE
			PRINTBUTTON @"{C_ID, 3}[%CALLNAME:C_ID, 18%]", C_ID
		ENDIF
		RESETCOLOR
		PRINTFORM 　
		SIF CNT_PROTECT % 5 == 4
			PRINTFORML
		LIST_PROTECT:CNT_PROTECT = C_ID
		CNT_PROTECT ++
	NEXT
	SIF (CNT_PROTECT - 1) % 5 != 4
		PRINTFORML
ENDIF





@MNG_FACILITY(L_C_ID, MODE_NOW)
#DIM          L_C_ID               ;対象キャラの登録番号
#DIMS         MODE_NOW
#DIM  DYNAMIC TEMP_ID
#DIM  DYNAMIC FACI_ID
{
#DIMS CONST   MODE_NAME     = 
	"MNG_配属_学園生徒", 
	"MNG_配属_支援ボラ", 
	"MNG_配属_ラブホ店員", 
	"MNG_配属_カジノ店員"
}
{
#DIM          L_FACI_LIST     = 
	1, 
	7,
	50,
	53
}
#DIM  DYNAMIC L_CNT_WOKER, 4        ;各施設に配属済みのキャラ数
#DIM  CONST   L_CFLAG_START = 10000 ;施設用CFLAGの開始値
#DIM  CONST   L_CFLAG_END   = 10100 ;施設用CFLAGの終了値（このID自体は含まれない）
#DIM L_CHARA_ORDER                  ;対象キャラがすでに配属されている施設

TEMP_ID = FINDELEMENT(MODE_NAME, MODE_NOW)
SIF TEMP_ID == -1
	RETURN
FACI_ID = L_FACI_LIST:TEMP_ID
L_CHARA_ORDER = FINDELEMENT(CFLAG:L_C_ID:0, 1, L_CFLAG_START, L_CFLAG_END)
IF L_CHARA_ORDER == L_CFLAG_START + FACI_ID
	CFLAG:L_C_ID:(L_CFLAG_START + FACI_ID) = 0
	RETURN
ENDIF

CLEARLINE LINECOUNT
CALL INFO_STATUS_HEADER(L_C_ID)
CALL C_PICTURE_S(L_C_ID)
DRAWLINE
PRINTFORML 　현재 배속된 캐릭터
DRAWLINE
CALL SHOW_ALL_WORKERS(L_FACI_LIST, L_CNT_WOKER, "「ボタン無し」")
DRAWLINE
IF L_CNT_WOKER:TEMP_ID >= 5
	PRINTFORML %CFLAGNAME:(L_CFLAG_START + FACI_ID)%의 인원이 너무 많습니다
	PRINTFORML 해임할 캐릭터를 선택하세요。
	FOR LOCAL, 1, CHARANUM
		SIF TALENT:LOCAL:부재
			CONTINUE
		SIF CFLAG:LOCAL:(L_CFLAG_START + FACI_ID) == 0
			CONTINUE
		PRINTFORML [{LOCAL, 3}] - %CALLNAME:LOCAL%
	NEXT
	PRINTFORML [999] - 그만둔다
	DO
		INPUT
		LOCAL = RESULT
		SELECTCASE LOCAL
		CASE 999
			RETURN
		CASE 1 TO CHARANUM -1
			SIF TALENT:LOCAL:부재
				CONTINUE
			SIF CFLAG:LOCAL:(L_CFLAG_START + FACI_ID) == 0
				CONTINUE
			BREAK
		CASEELSE
			CONTINUE
		ENDSELECT
	LOOP 1
	CFLAG:LOCAL:(L_CFLAG_START + FACI_ID) = 0
ENDIF
SIF L_CHARA_ORDER > 0
	CFLAG:L_C_ID:L_CHARA_ORDER = 0
CFLAG:L_C_ID:(L_CFLAG_START + FACI_ID) = 1



@MNG_JOB(L_C_ID, MODE_NOW)
#DIM          L_C_ID               ;対象キャラの登録番号
#DIMS         MODE_NOW
{
#DIMS CONST   MODE_NAME = 
	"MNG_転職_성직자", 
	"MNG_転職_메이드", 
	"MNG_転職_아이돌", 
	"MNG_転職_사회인", 
	"MNG_転職_局員", 
	"MNG_転職_창녀"
}
{
#DIM  CONST   JOB_ID = 
	93, 
	94, 
	96, 
	97, 
	99, 
	98
}
#DIM DYNAMIC TEMP_ID
TEMP_ID = FINDELEMENT(MODE_NAME, MODE_NOW)
SIF TEMP_ID < 0
	RETURN
TEMP_ID = JOB_ID:TEMP_ID
IF MONEY < 500000
	PRINTFORMW 자금 500000이 팔요합니다
	RETURN
ENDIF

CLEARLINE LINECOUNT
CALL INFO_STATUS_HEADER(L_C_ID)
DRAWLINE
CALL C_PICTURE_S(L_C_ID)


IF MODE_NOW == "MNG_転職_창녀"
	DRAWLINE
	PRINTFORML 자금 500000을 소비하여 
	IF TALENT:L_C_ID:창녀
		PRINTFORML %CALLNAME:L_C_ID%의 업무 내용을 재검토합니다
		PRINTFORML 괜찮을까요？
		PRINTFORML [1] - 매춘을 멈추게 한다
		PRINTFORML [0] - 그대로
	ELSE
		PRINTFORML %CALLNAME:L_C_ID%에게 성처리 업무를 맡깁니다
		PRINTFORML 괜찮을까요？
		PRINTFORML [1] - 매춘시킨다
		PRINTFORML [0] - 그대로
	ENDIF
	DO
		INPUT
		SIF RESULT == 0
			RETURN
		SIF RESULT == 1
			BREAK
	LOOP 1
	IF TALENT:L_C_ID:창녀
		PRINTFORMW %CALLNAME:L_C_ID%は 매춘에서 손을 떼었다
	ELSE
		PRINTFORMW %CALLNAME:L_C_ID%は 매춘하기 시작했다
	ENDIF
	TALENT:L_C_ID:창녀 = !TALENT:L_C_ID:창녀
	MONEY -= 500000
	RETURN
ELSEIF TALENT:L_C_ID:TEMP_ID == 1
	RETURN
ENDIF
DRAWLINE
PRINTFORML 자금 500000을 소비하여 %CALLNAME:L_C_ID%에게 %TALENTNAME:TEMP_ID%의 업무를 맡깁니다
PRINTFORML 괜찮을까요？
PRINTFORML [1] - 전직 실행
PRINTFORML [0] - 그만둔다
DO
	INPUT
	SIF RESULT == 0
		RETURN
	SIF RESULT == 1
		BREAK
LOOP 1
TALENT:L_C_ID:성직자 = 0
TALENT:L_C_ID:메이드 = 0
TALENT:L_C_ID:아이돌 = 0
TALENT:L_C_ID:사회인 = 0
TALENT:L_C_ID:치안유지국직원 = 0
TALENT:L_C_ID:TEMP_ID = 1
PRINTFORMW %CALLNAME:L_C_ID%は %TALENTNAME:TEMP_ID%に 되었다
MONEY -= 500000



@MNG_MOVING_ROOM(C_ID, ROOM_ID)
#DIM C_ID
#DIM ROOM_ID
#DIM SWAP_ID
IF !INRANGE(ROOM_ID, 1, 10)
	RETURN
ELSEIF C_ID == MASTER
	RETURN
ELSEIF TALENT:C_ID:NPC
	RETURN
ELSEIF CFLAG:C_ID:住人_体格 == 1
	PRINTFORMW 아이는 입주할 수 없습니다。
	RETURN
ENDIF

;同じ部屋番号ならリセット
IF FLAG:(5000 + ROOM_ID) == CFLAG:C_ID:個別番号
	FLAG:(5000 + ROOM_ID) = 0
	CFLAG:C_ID:マンションID = 0
	RETURN
ELSEIF CFLAG:C_ID:マンションID == ROOM_ID
	FLAG:(5000 + CFLAG:C_ID:マンションID) = 0
	CFLAG:C_ID:マンションID = 0
	RETURN
ENDIF

;他のキャラが입주済みのとき
IF FLAG:(5000 + ROOM_ID)
	;該当キャラの検索
	SWAP_ID = FINDCHARA(CFLAG:個別番号, FLAG:(5000 + ROOM_ID), 1, CHARANUM)
	IF SWAP_ID >= 1
		CFLAG:SWAP_ID:マンションID = CFLAG:C_ID:マンションID
		;입주予定者が既に部屋に入っているなら入れ替え処理
		IF CFLAG:C_ID:マンションID
			FLAG:(5000 + CFLAG:C_ID:マンションID) = CFLAG:SWAP_ID:個別番号
			CFLAG:SWAP_ID:マンションID = CFLAG:C_ID:マンションID
		;そうでないなら部屋から出るだけ
		ELSE
			CFLAG:SWAP_ID:マンションID = 0
		ENDIF
	ENDIF
	FLAG:(5000 + ROOM_ID) = CFLAG:C_ID:個別番号
	CFLAG:C_ID:マンションID = ROOM_ID
ELSE
	FLAG:(5000 + CFLAG:C_ID:マンションID) = 0
	FLAG:(5000 + ROOM_ID) = CFLAG:C_ID:個別番号
	CFLAG:C_ID:マンションID = ROOM_ID
ENDIF



@MNG_CUSTOM_LOOKS(L_I)
#DIM         L_I
#DIM DYNAMIC L_CHANGE_H_COLOR = -1 ;
#DIM DYNAMIC L_CHANGE_H_STYLE = -1 ;えっちすたいるではなくヘアスタイルだ
#DIM DYNAMIC L_CHANGE_E_COLOR = -1 ;
#DIM         L_NOW_H_COLOR         ;画像表示用に一時変更する
#DIM         L_NOW_H_STYLE         ;
#DIM         L_NOW_E_COLOR         ;
#DIM         L_PARTS_TYPE
IF !INRANGE(L_I, 0, CHARANUM - 1)
	RETURN
ELSEIF L_I == MASTER
	RETURN
ELSEIF TALENT:L_I:NPC
	RETURN
ELSEIF MONEY < 10000
	PRINTFORMW 자금이 부족합니다。
	RETURN
ENDIF
;変更前の状態を保存
L_NOW_H_COLOR = CFLAG:L_I:髪色
L_NOW_H_STYLE = CFLAG:L_I:髪型
L_NOW_E_COLOR = CFLAG:L_I:瞳色

DO
	CLEARLINE LINECOUNT
	DRAWLINE
	PRINTFORML %CALLNAME:L_I%의 외모를 변경합니다
	PRINTFORM 【머리 색：%INFO_STR_HAIRCOLOR(L_NOW_H_COLOR), 20%→
	IF L_CHANGE_H_COLOR == -1
		PRINTFORML 변경 없음】
	ELSE
		PRINTFORML %INFO_STR_HAIRCOLOR(L_CHANGE_H_COLOR)%】
	ENDIF
	PRINTFORM 【머리 모양：%INFO_STR_HAIR(L_NOW_H_STYLE), 20%→
	IF L_CHANGE_H_STYLE == -1
		PRINTFORML 변경 없음】
	ELSE
		PRINTFORML %INFO_STR_HAIR(L_CHANGE_H_STYLE)%】
	ENDIF
	PRINTFORM 【눈동자 색：%INFO_STR_EYECOLOR(L_NOW_E_COLOR), 20%→
	IF L_CHANGE_E_COLOR == -1
		PRINTFORML 변경 없음】
	ELSE
		PRINTFORML %INFO_STR_EYECOLOR(L_CHANGE_E_COLOR)%】
	ENDIF
	DRAWLINE
	PRINTFORML 　　　　　
	PRINTFORML 　　　　　
	PRINTFORML 　　　　　
	PRINTFORML 　　　　　　→
	PRINTFORML 　　　　　
	PRINTFORML 　　　　　
	PRINTFORML 　　　　　
	CFLAG:L_I:髪色 = L_NOW_H_COLOR
	CFLAG:L_I:髪型 = L_NOW_H_STYLE
	CFLAG:L_I:瞳色 = L_NOW_E_COLOR
	
	HTML_PRINT C_PICTURE_HTML(L_I, 0, -702, 0, 850)
	SIF L_CHANGE_H_COLOR > -1
		CFLAG:L_I:髪色 = L_CHANGE_H_COLOR
	SIF L_CHANGE_H_STYLE > -1
		CFLAG:L_I:髪型 = L_CHANGE_H_STYLE
	SIF L_CHANGE_E_COLOR > -1
		CFLAG:L_I:瞳色 = L_CHANGE_E_COLOR
	HTML_PRINT C_PICTURE_HTML(L_I, 800, -800, 0, 850)
	DRAWLINE
	PRINTFORML [0] 변경을 리셋
	DRAWLINE
	PRINTFORML [1] 머리카락 색 변경
	IF FLAG:エステ >= 3
		PRINTFORML [2] 머리 모양을 변경
	ELSE
		PRINTFORML  머리 모양을 변경(레벨 3이상 필요)
	ENDIF
	IF FLAG:エステ >= 6
		PRINTFORML [3] 눈동자 색 변경
	ELSE
		PRINTFORML  눈동자 색 변경(레벨 6이상 필요)
	ENDIF
	PRINTFORML 
	PRINTFORML [100] 적용　(￥10000)
	PRINTFORML [999] 돌아온다
	INPUT
	SELECTCASE RESULT
	CASE 0
		L_CHANGE_H_COLOR = -1
		L_CHANGE_H_STYLE = -1
		L_CHANGE_E_COLOR = -1
	CASE 1
		FOR L_PARTS_TYPE, 10, -1, -1
			PRINTFORML [{L_PARTS_TYPE, 2}] %INFO_STR_HAIRCOLOR(L_PARTS_TYPE)%
		NEXT
		PRINTFORML 
		PRINTFORML [-1] 변경 없음
		DO
			INPUT
			L_PARTS_TYPE = RESULT
			SIF INRANGE(L_PARTS_TYPE, -1, 10)
				BREAK
		LOOP 1
		L_CHANGE_H_COLOR = L_PARTS_TYPE
	CASE 2
		SIF FLAG:エステ < 3
			CONTINUE
		FOR L_PARTS_TYPE, 24, -1, -1
			PRINTFORML [{L_PARTS_TYPE, 2}] %INFO_STR_HAIR(L_PARTS_TYPE)%
		NEXT
		PRINTFORML 
		PRINTFORML [-1] 변경 없음
		DO
			INPUT
			L_PARTS_TYPE = RESULT
			SIF INRANGE(L_PARTS_TYPE, -1, 24)
				BREAK
		LOOP 1
		L_CHANGE_H_STYLE = L_PARTS_TYPE
	CASE 3
		SIF FLAG:エステ < 6
			CONTINUE
		FOR L_PARTS_TYPE, 5, -1, -1
			PRINTFORML [{L_PARTS_TYPE, 2}] %INFO_STR_EYECOLOR(L_PARTS_TYPE)%
		NEXT
		PRINTFORML 
		PRINTFORML [-1] 변경 없음
		DO
			INPUT
			L_PARTS_TYPE = RESULT
			SIF INRANGE(L_PARTS_TYPE, -1, 5)
				BREAK
		LOOP 1
		L_CHANGE_E_COLOR = L_PARTS_TYPE
	CASE 100
		IF ALLSAMES(-1, L_CHANGE_H_COLOR, L_CHANGE_H_STYLE, L_CHANGE_E_COLOR)
			PRINTFORMW 변경 내용이 설정되지 않았습니다。
			CONTINUE
		;何らかの処理が適用される場合は個別に抜けるよ
		ELSEIF L_CHANGE_H_COLOR >= 0 && L_CHANGE_H_COLOR != L_NOW_H_COLOR
		ELSEIF L_CHANGE_H_STYLE >= 0 && L_CHANGE_H_STYLE != L_NOW_H_STYLE
		ELSEIF L_CHANGE_E_COLOR >= 0 && L_CHANGE_E_COLOR != L_NOW_E_COLOR
		ELSE
			PRINTFORMW %CALLNAME:L_I%는 이미 지정된 외모와 같습니다。
			CONTINUE
		ENDIF
		
		PRINTFORMW %CALLNAME:L_I%의 외모를 변경했습니다。
		MONEY -= 10000
		RETURN
	CASE 999
		CFLAG:L_I:髪色 = L_NOW_H_COLOR
		CFLAG:L_I:髪型 = L_NOW_H_STYLE
		CFLAG:L_I:瞳色 = L_NOW_E_COLOR
		RETURN
	ENDSELECT
LOOP 1



@MNG_CURE_SICK(L_I)
#DIM         L_I
#DIM         L_MONEY
#DIM         L_INPUT
IF !INRANGE(L_I, 0, CHARANUM - 1)
	RETURN
ELSEIF L_I == MASTER
	RETURN
ELSEIF TALENT:L_I:NPC
	RETURN
ELSEIF TALENT:L_I:쾌락중독 <= 0
	RETURN
ENDIF
L_MONEY = MIN(100000, 2000 + (EXP:L_I:매춘경험 * 10))
CLEARLINE LINECOUNT
CALL INFO_STATUS_HEADER(L_I)
DRAWLINE
CALL C_PICTURE_S(L_I)
PRINTFORML %CALLNAME:L_I%의 쾌락 중독 치료엔 ￥{L_MONEY}이 필요합니다
PRINTFORML 치료하시겠습니까？
PRINTFORML 
PRINTFORML [0] 네
PRINTFORML [1] 아니오
INPUT
L_INPUT = RESULT
SELECTCASE L_INPUT
CASE 0
	IF MONEY <= L_MONEY
		PRINTFORML 치료비가 부족합니다。
		RETURN
	ENDIF
	TALENT:L_I:쾌락중독 = 0
	PRINTFORMW %CALLNAME:L_I%を 치료했습니다。
ENDSELECT



@MNG_CONFIG_EVENT(L_I)
#DIM        L_I
#DIM        L_INPUT
#DIMS CONST STR_EVE_RATE = "NG", "적음", "보통", "많음"
#DIMS CONST STR_EVE_LIMIT = "NG", "친구", "베프", "연인", "무제한"
IF !INRANGE(L_I, 0, CHARANUM - 1)
	RETURN
ELSEIF L_I == MASTER
	RETURN
ELSEIF TALENT:L_I:NPC
	RETURN
ENDIF
CLEARLINE LINECOUNT
CALL INFO_STATUS_HEADER(L_I)
DRAWLINE
CALL C_PICTURE_S(L_I)
DRAWLINE
PRINTFORML 변경하고 싶은 이벤트 경향을 선택하세요。
DRAWLINE
PRINTFORML  [0] 교류 이벤트(빈도：%STR_EVE_RATE:(LIMIT(CFlag:L_I:住人イベント傾向_交流 / 30, 0, 3))%)
PRINTFORML  [1] 연애 이벤트(빈도：%STR_EVE_RATE:(LIMIT(CFlag:L_I:住人イベント傾向_恋愛 / 30, 0, 3))%)
PRINTFORML  [2] 범죄 이벤트(빈도：%STR_EVE_RATE:(LIMIT(CFlag:L_I:住人イベント傾向_犯罪 / 30, 0, 3))%)
PRINTFORML  [3] 성적인 이벤트(빈도：%STR_EVE_RATE:(LIMIT(CFlag:L_I:住人イベント傾向_性 / 30, 0, 3))%)
PRINTFORML [10] 주민 교류 상한(상한：%STR_EVE_LIMIT:(LIMIT(CFlag:L_I:住人交流設定, 0, 4))%)
PRINTFORML [20] 교류 대상 즐겨찾기 지정
PRINTFORML 
PRINTFORML [999] 돌아온다
INPUT
L_INPUT = RESULT
SELECTCASE L_INPUT
CASE 999
	RETURN
CASE 20
	CALL MNG_CONFIG_PARTNER(L_I)
CASE 10
	CFlag:L_I:(3950+L_INPUT) += 1
	SIF CFlag:L_I:(3950+L_INPUT) >= 5
		CFlag:L_I:(3950+L_INPUT) = 0
CASE 0, 1, 2, 3
	CFlag:L_I:(3950+L_INPUT) += 30
	SIF CFlag:L_I:(3950+L_INPUT) >= 100
		CFlag:L_I:(3950+L_INPUT) = 0
CASEELSE
ENDSELECT
RESTART



;-------------------------------------------------
; 各交流住人
;-------------------------------------------------
@MNG_CONFIG_PARTNER(L_I)
#DIM L_NPC
#DIM L_INPUT
#DIM L_I
#DIMS CONST NPC_LV = "애인", "지인", "친구", "베프", "연인", "남편"
CLEARLINE LINECOUNT
CALL INFO_STATUS_HEADER(L_I)
DRAWLINE
CALL C_PICTURE_S(L_I)
DRAWLINE
PRINTFORML 교류 대상의 즐겨찾기 지정、이름을 변경할 수 있습니다。
DRAWLINE
PRINTFORML [-1] 즐겨찾기 해제
FOR L_NPC, 0, 10
	IF CSTR:L_I:(3000+L_NPC) == ""
		PRINTFORML 【미등록】
	ELSE
		SIF CFLAG:L_I:3600 == L_NPC
			SETCOLOR 50,250,250
		PRINTBUTTON @"[{10 + L_NPC}] 이름 변경", 10 + L_NPC
		PRINTFORM 　
		PRINTBUTTON @"[{L_NPC}] %CSTR:L_I:(3000+L_NPC), 18 ,LEFT%", L_NPC
		PRINTFORM 【
		IF CFLAG:L_I:(3030+L_NPC) == 1
			PRINTFORM 남
		ELSE
			PRINTFORM 녀
		ENDIF
		PRINTFORM 】【관계:
		SELECTCASE CFLAG:L_I:(3020+L_NPC)
		CASE 1 TO 5, -5
			PRINTFORM %NPC_LV:(MAX(0, CFLAG:L_I:(3020+L_NPC)))%
		ENDSELECT
		PRINTFORM 】【호감도:{CFLAG:L_I:(3010+L_NPC), 3 ,LEFT}】【상성:{CFLAG:L_I:(3050+L_NPC), 3 ,LEFT}】【성행위 횟수:{CFLAG:L_I:(3060+L_NPC)}】
		PRINTFORML
	ENDIF
	RESETCOLOR
NEXT
DRAWLINE
PRINTFORML [999] 돌아온다
DO
	INPUT
	L_INPUT = RESULT
	SELECTCASE L_INPUT
	CASE -1
		CFLAG:L_I:3600 = -1
		PRINTFORMW 【즐겨찾기를 해제했습니다】
	CASE 999
		; CALL JN_CONFIG
		RETURN
	CASE 10 TO 19
		PRINTFORML 【교류 대상의 이름을 변경합니다、입력하지 않으면 변경하지 않습니다。】
		DRAWLINE
		INPUTS
		IF RESULTS == ""
			;スルー
		ELSE
			L_NPC = L_INPUT % 10
			SIF CFLAG:L_I:(3020 + L_NPC) == 5
				CSTR:L_I:伴侶名前 = %RESULTS%
			CSTR:L_I:(3000 + L_NPC) = %RESULTS%
			PRINTFORML 【이름을 %CSTR:L_I:(3000 + L_NPC)%に 변경했습니다】
			SIF TALENT:L_I:아이같음 || TALENT:L_I:깜찍함
				CSTR:L_I:一人称 = %CALLNAME:L_I%
		ENDIF
	CASE 0 TO 9
		SIF CSTR:L_I:(3000 + L_INPUT) == ""
			RESTART
		CFLAG:L_I:3600 = L_INPUT
		PRINTFORML 【%CSTR:L_I:(3000 + L_INPUT)%を 즐겨찾기 대상으로 지정했습니다】
		PRINTFORMW 즐겨찾기에 등록한 주민은 교류 발생이 많아지며、호감도・상성 증가에 보정이 붙습니다。
	CASEELSE
		CONTINUE
	ENDSELECT
	BREAK
LOOP 1
RESTART




@MNG_RENAME(C_ID)
#DIM          C_ID
#DIM          NAME_ID
#DIM          NAME_CNT
#DIMS DYNAMIC NAME_LIST, 30
#DIMS DYNAMIC HURIGANA_LIST, 30
IF !INRANGE(C_ID, 0, CHARANUM - 1)
	RETURN
ELSEIF C_ID == MASTER
	RETURN
ELSEIF TALENT:C_ID:NPC
	RETURN
ENDIF
CLEARLINE LINECOUNT
CALL INFO_STATUS_HEADER(C_ID)
DRAWLINE
CALL C_PICTURE_S(C_ID)
DRAWLINE
PRINTFORML 새로운 이름을 선택하세요（よみがなは記録されません）
DRAWLINE
CALL MAKE_NAMELIST(NAME_LIST, HURIGANA_LIST)
FOR NAME_ID, 0, 30
	PRINTBUTTON @"[{NAME_ID, 2}] %NAME_LIST:NAME_ID, 8% %HURIGANA_LIST:NAME_ID, 10% 同名{CNT_SAME_NAME(C_ID, NAME_LIST:NAME_ID), 3}", NAME_ID
	PRINTFORM 　
	SIF NAME_ID % 3 == 2
		PRINTFORML 
NEXT
PRINTFORML 
PRINTFORML [100] 스스로 입력한다
PRINTFORML [900] 이름 후보 재생성
PRINTFORML [999] 취소
DO
	INPUT
	SELECTCASE RESULT
	CASE 999
		RETURN
	CASE 900
		RESTART
	CASE 100
		PRINTFORML 새로운 이름을 입력하세요
		INPUTS
		IF RESULTS == ""
			;スルー
			RESTART
		ELSE
			PRINTFORMW 이름 변경을 마쳤습니다
			CALLNAME:C_ID '= RESULTS
			SIF TALENT:C_ID:아이같음 || TALENT:C_ID:깜찍함
				CSTR:C_ID:一人称 '= CALLNAME:C_ID
			RETURN
		ENDIF
	CASE 0 TO 29
		NAME_ID  = RESULT
		NAME_CNT = CNT_SAME_NAME(C_ID, NAME_LIST:NAME_ID)
		PRINTFORML %CALLNAME:C_ID%의 새로운 이름은 %NAME_LIST:NAME_ID%（%HURIGANA_LIST:NAME_ID%）입니다
		SIF NAME_CNT
			PRINTFORML 같은 이름의 캐릭터는 {NAME_CNT}명 존재합니다
		PRINTFORML 이런 이름으로 괜찮을까요？
		PRINTFORML [1] - %NAME_LIST:NAME_ID%으로 결정한다
		PRINTFORML [0] - 다시 생각한다
		DO
			INPUT
			SELECTCASE RESULT
			CASE 1
				PRINTFORMW 이름 변경을 마쳤습니다
				CALLNAME:C_ID '= NAME_LIST:NAME_ID
				SIF TALENT:C_ID:아이같음 || TALENT:C_ID:깜찍함
					CSTR:C_ID:一人称 '= CALLNAME:C_ID
				RETURN
			CASE 0
				RESTART
			ENDSELECT
		LOOP 1
	CASEELSE
		CONTINUE
	ENDSELECT
LOOP 1





@CULC_COST_COS_CHANGE(MEMO_COS, NEW_COS, COS_PRICE)
#FUNCTION
#DIM REF     MEMO_COS
#DIM REF     NEW_COS
#DIM REF     COS_PRICE
#DIM DYNAMIC SUM_PRICE
FOR LOCAL, 0, 7
	SIF NEW_COS:LOCAL < 0
		CONTINUE
	SIF MEMO_COS:LOCAL == NEW_COS:LOCAL
		CONTINUE
	SUM_PRICE += COS_PRICE:LOCAL
NEXT
RETURNF SUM_PRICE



@MNG_CHANGE_CLOTHES(C_ID)
#DIM          C_ID
#DIMS CONST   COS_TYPE_ALL  = "服", "下着トップス", "下着ボトムス", "靴下", "靴", "帽子＆アクセ", "淫紋"
#DIM  CONST   COS_CFLAG_ID  = 420, 423, 424, 421, 422, 419, 418
#DIMS CONST   STR_BODY_SIZE   = "NORMAL", "MINI"
#DIM  DYNAMIC LOAD_CHECK = 1
#DIM  DYNAMIC MEMO_COS, 7
#DIM  DYNAMIC NEW_COS, 7
#DIM  DYNAMIC COS_PRICE, 7
IF LOAD_CHECK
	LOAD_CHECK = 0
	FOR LOCAL, 0, 7
		MEMO_COS:LOCAL = CFLAG:C_ID:(COS_CFLAG_ID:LOCAL)
		NEW_COS:LOCAL  = MEMO_COS:LOCAL
	NEXT
ENDIF
CLEARLINE LINECOUNT
CALL INFO_STATUS_HEADER(C_ID)
CALL C_PICTURE_FITTING(C_ID, MEMO_COS, NEW_COS)
PRINTFORML 갈아입을 옷의 종류를 선택하세요(소지금:{MONEY})
DRAWLINE
FOR LOCAL, 0, VARSIZE("COS_TYPE_ALL")
	PRINTBUTTON @"[{LOCAL}] %COS_TYPE_ALL:LOCAL, 14%", LOCAL
	PRINT 　　｜
	IF MEMO_COS:LOCAL < 0
		PRINTFORM %"－－", 24, LEFT%　→
	ELSE
		PRINTFORM %COSTUME_DATA(STR_BODY_SIZE:(CFLAG:C_ID:住人_体格), COS_TYPE_ALL:LOCAL, MEMO_COS:LOCAL, "NAME"), 24, LEFT%　→
	ENDIF
	IF NEW_COS:LOCAL < 0
		PRINTFORM －－
	ELSEIF NEW_COS:LOCAL == MEMO_COS:LOCAL
		PRINTFORM 그대로
	ELSE
		PRINTFORM %COSTUME_DATA(STR_BODY_SIZE:(CFLAG:C_ID:住人_体格), COS_TYPE_ALL:LOCAL, NEW_COS:LOCAL, "NAME"), 24, LEFT%
	ENDIF
	PRINTL
NEXT
SIF !LINEISEMPTY()
	PRINTL
PRINTFORML
PRINTFORML [100] - 의상 구입 (비용 {CULC_COST_COS_CHANGE(MEMO_COS, NEW_COS, COS_PRICE)})
PRINTFORML [200] - 리셋
PRINTFORML [999] - 취소
DO
	INPUT
	SELECTCASE RESULT
	CASE 999
		FOR LOCAL, 0, 7
			CFLAG:C_ID:(COS_CFLAG_ID:LOCAL) = MEMO_COS:LOCAL
		NEXT
		RETURN
	CASE 100
		IF MONEY < CULC_COST_COS_CHANGE(MEMO_COS, NEW_COS, COS_PRICE)
			PRINTFORMW 소지금이 부족합니다
		ELSE
			MONEY -= CULC_COST_COS_CHANGE(MEMO_COS, NEW_COS, COS_PRICE)
			FOR LOCAL, 0, 7
				MEMO_COS:LOCAL = CFLAG:C_ID:(COS_CFLAG_ID:LOCAL)
				NEW_COS:LOCAL  = MEMO_COS:LOCAL
			NEXT
			VARSET COS_PRICE
		ENDIF
		BREAK
	CASE 200
		FOR LOCAL, 0, 7
			NEW_COS:LOCAL  = MEMO_COS:LOCAL
		NEXT
		VARSET COS_PRICE
		BREAK
	CASE 0 TO 6
		CALL MNG_CLOTHES_FITTING(C_ID, COS_TYPE_ALL:RESULT, MEMO_COS, NEW_COS, COS_PRICE)
		BREAK
	ENDSELECT
LOOP 1
RESTART


@MNG_CLOTHES_FITTING(C_ID, COS_TYPE, MEMO_COS, NEW_COS, COS_PRICE)
#DIM          C_ID
#DIMS         COS_TYPE
#DIM  REF     MEMO_COS
#DIM  REF     NEW_COS
#DIM  REF     COS_PRICE
#DIMS CONST   COS_TYPE_ALL  = "服", "下着トップス", "下着ボトムス", "靴下", "靴", "帽子＆アクセ", "淫紋"
#DIM  CONST   COS_CFLAG_ID  = 420, 423, 424, 421, 422, 419, 418
#DIM  CONST   服_NORMAL_INDEX = 0, 1, 2, 3, 4, -1, 5, 6, 7, 8, 17, 18, 9, 10, 11, 12, 13, 14, 15, 16, 19, 20, 21, 22, 23, 24, -1, 25, 26, 27
#DIMS CONST   STR_BODY_SIZE   = "NORMAL", "MINI"
#DIMS         TEMP_LABEL
#DIM  DYNAMIC PRICE_LIST, 40
#DIM  DYNAMIC NOW_TYPE

NOW_TYPE = FINDELEMENT(COS_TYPE_ALL, COS_TYPE, , , 1)
SIF NOW_TYPE < 0
	RETURN

PRINTFORML
;先に例外処理
IF CFLAG:C_ID:住人_体格 == 0 && COS_TYPE == "服"
	FOR LOCAL, 0, VARSIZE("服_NORMAL_INDEX")
		IF 服_NORMAL_INDEX:LOCAL < 0
			PRINTFORM %" " * 39%
		ELSE
			PRICE_LIST:(服_NORMAL_INDEX:LOCAL) = TOINT(COSTUME_DATA(STR_BODY_SIZE:(CFLAG:C_ID:住人_体格), COS_TYPE, 服_NORMAL_INDEX:LOCAL, "PRICE"))
			TEMP_LABEL '= @"[{服_NORMAL_INDEX:LOCAL, 2}] "
			TEMP_LABEL += @"%COSTUME_DATA(STR_BODY_SIZE:(CFLAG:C_ID:住人_体格), COS_TYPE, 服_NORMAL_INDEX:LOCAL, "NAME"), 24, LEFT%"
			TEMP_LABEL += "　\\" + @"{PRICE_LIST:(服_NORMAL_INDEX:LOCAL), 7, RIGHT}"
			PRINTBUTTON TEMP_LABEL, 服_NORMAL_INDEX:LOCAL
		ENDIF
		PRINT 　
		SIF LOCAL % 3 == 2
			PRINTL 
	NEXT
ELSEIF GROUPMATCH(COS_TYPE, "帽子＆アクセ", "淫紋")
	LOCAL = 1
	DO
		TEMP_LABEL '= @"%COSTUME_DATA(STR_BODY_SIZE:(CFLAG:C_ID:住人_体格), COS_TYPE, LOCAL, "NAME"), 24, LEFT%"
		SIF TEMP_LABEL == " " * 24
			BREAK
		PRICE_LIST:LOCAL = TOINT(COSTUME_DATA(STR_BODY_SIZE:(CFLAG:C_ID:住人_体格), COS_TYPE, LOCAL, "PRICE"))
		TEMP_LABEL '= TEMP_LABEL + "　\\" + @"{PRICE_LIST:LOCAL, 7, RIGHT}"
		TEMP_LABEL '= @"[{LOCAL, 2}] " + TEMP_LABEL
		PRINTBUTTON TEMP_LABEL, LOCAL
		PRINT 　
		SIF LOCAL % 4 == 3
			PRINTL 
		LOCAL ++
	LOOP 1
ELSE
	LOCAL = 0
	DO
		TEMP_LABEL '= @"%COSTUME_DATA(STR_BODY_SIZE:(CFLAG:C_ID:住人_体格), COS_TYPE, LOCAL, "NAME"), 24, LEFT%"
		SIF TEMP_LABEL == " " * 24
			BREAK
		PRICE_LIST:LOCAL = TOINT(COSTUME_DATA(STR_BODY_SIZE:(CFLAG:C_ID:住人_体格), COS_TYPE, LOCAL, "PRICE"))
		TEMP_LABEL '= TEMP_LABEL + "　\\" + @"{PRICE_LIST:LOCAL, 7, RIGHT}"
		TEMP_LABEL '= @"[{LOCAL, 2}] " + TEMP_LABEL
		PRINTBUTTON TEMP_LABEL, LOCAL
		PRINT 　
		SIF LOCAL % 3 == 2
			PRINTL 
		LOCAL ++
	LOOP 1
ENDIF
PRINTFORML
SIF !LINEISEMPTY()
	PRINTL
IF COS_TYPE == "淫紋"
	PRINTFORML [-1] 해제
ELSE
	PRINTFORML [-1] 벗긴다
ENDIF
PRINTFORML [100] 돌아온다
DO
	INPUT
	SELECTCASE RESULT
	CASE 100
		RETURN
	CASE -1
		CFLAG:C_ID:(COS_CFLAG_ID:NOW_TYPE) = -1
		NEW_COS:NOW_TYPE   = -1
		COS_PRICE:NOW_TYPE = 0
		RETURN
		[SKIPSTART]
		IF COS_TYPE == "淫紋"
			PRINTFORML 음문을 해제합니다
		ELSE
			PRINTFORML 옷을 벗깁니다
		ENDIF
		PRINTFORML 괜찮을까요？
		PRINTFORML [1] 네
		PRINTFORML [0] 아니오
		INPUT
		IF RESULT == 1
			CFLAG:C_ID:(COS_CFLAG_ID:NOW_TYPE) = -1
			NEW_COS:NOW_TYPE   = -1
			COS_PRICE:NOW_TYPE = 0
			RETURN
		ENDIF
		[SKIPEND]
	CASE 0 TO 99
		LOCAL = RESULT
		SIF !STRLENS(COSTUME_DATA(STR_BODY_SIZE:(CFLAG:C_ID:住人_体格), COS_TYPE, LOCAL, "NAME"))
			CONTINUE
		
		CFLAG:C_ID:(COS_CFLAG_ID:NOW_TYPE) = LOCAL
		NEW_COS:NOW_TYPE   = LOCAL
		COS_PRICE:NOW_TYPE = PRICE_LIST:LOCAL
		RETURN
	ENDSELECT
LOOP 1




