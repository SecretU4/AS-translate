;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Module		:LIB_PRINT.ERB
;	Facility	:共通関数ライブラリ(PRINT)。
;				 表示機能の汎用関数を纏める。
;
;				 尚、記載する関数は全て先頭に"CP_"を付ける。
;				 (C=Common、共通) (P=PRINT)
;
;	Licence		:ライセンスフリー。
;
;	Modification Data:
;
;	Edit	Date			Author					Reason
;	001		20XX/XX/XX		----					新規作成
;	002		2017/02/28		morph					ラベル使用キャラステータス表示関数（CP_STATUS_BY_LABEL）の追加
;	003		2019/04/17		M						CP_HEARTB_PINKで、現在のフォント色を保存し戻すように。
;	004		2019/08/21		morph					SHORTEN_NAME()、名前を半角10文字以下にして返す短縮関数
;
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#

;===============================================================================
;  ◇記載関数一覧(記載順)
;
;		Module Name						Explanation
;
;	・ CP_HEARTB_PINK					表示:ハートマーク(黒塗りピンク)
;	・ CP_HEARTB						表示:ハートマーク(黒塗り)
;	・ CP_HEARTBW						表示:口上用ハートマーク書式Ｗ(黒塗り)
;	・ CP_HEARTBL						表示:口上用ハートマーク書式Ｌ(黒塗り)
;	・ CP_PRINTFORM_HEART				表示:口上用ハートマーク挿入PRINT
;	・ CP_PRINTFORM_COLORED				表示:着色済み文字
;	・ CP_PRINTFORMW_COLORED			表示:着色済み文字(W)
;	・ CP_CUSTOM_BAR					表示:カスタムBAR
;	・ CP_CUSTOM_MAP					表示:カスタムMAP
;	・ CP_STATUS_BY_LABEL				表示:ラベル使用キャラステータス表示関数
;
;===============================================================================

;=================================================
;   sub CP_HEARTB_PINK
;=================================================
;   表示:ハートマーク(黒塗りピンク)
;-------------------------------------------------
;   塗りつぶしのハートマークを表示(ピンク)
;-------------------------------------------------
; Input:
;  ARG					ハートの数(デフォルト:1個)
;  ARG:1				出力桁数(デフォルト:ハートの数に比例)
;-------------------------------------------------
@CP_HEARTB_PINK(ARG:0=1, ARG:1=0)
#DIM DYNAMIC L_CURRENTCOLOR
L_CURRENTCOLOR = GETCOLOR()
;- ピンク
SETCOLOR 0xFF33CC

CALL CP_HEARTB(ARG, ARG:1)

SETCOLOR L_CURRENTCOLOR
RETURNFORM RESULT


;=================================================
;   sub CP_HEARTB
;=================================================
;   表示:ハートマーク(黒塗り)
;
;   [候補]
;   GungsuhChe			… (0x2665)ハート大きい
;   Times New Roman		… (0x2665)綺麗なハート。採用バリアント多し。人気
;   Webdings			… (0xF059)アート系のハート
;-------------------------------------------------
; Input:
;  ARG:0				ハートの数（省略すると1つ）
;  ARG:1				出力桁数(デフォルト:ハートの数に比例)
;-------------------------------------------------
@CP_HEARTB(ARG:0=1, ARG:1=0)
#DIM L_RES
#DIMS L_FONT
L_RES=RESULT
;-----------------------------
;L_FONT=GungsuhChe
;L_FONT=Webdings
L_FONT=Times New Roman
;-----------------------------

;- CHK:出力桁数
SIF ARG:1 < 1
	ARG:1 = ARG

GETFONT
CHKFONT L_FONT
SIF RESULT
	SETFONT L_FONT
PRINTFORM %UNICODE(0x2665) * ARG%

;- 桁数分を詰め(空白)
IF ARG:1 > ARG
	PRINTFORM %"　" * (ARG:1 - ARG),(ARG:1 - ARG)%
ENDIF

SETFONT RESULTS
RETURNFORM L_RES


;=================================================
;   sub CP_HEARTBW
;=================================================
;   表示:口上用ハートマーク書式Ｗ(黒塗り)
;-------------------------------------------------
; Input:
;  ARG:0				ハートの数（省略すると1つ）
;  ARG:1				出力桁数(デフォルト:ハートの数に比例)
;  ARGS					末端文字（省略すると"」"）
;-------------------------------------------------
@CP_HEARTBW(ARG:0=1, ARG:1=0, ARGS="")

CALL CP_HEARTB(ARG, ARG:1)

PRINTSW \@ !STRLENS(ARGS) ? 」 # %ARGS% \@
RETURNFORM RESULT


;=================================================
;   sub CP_HEARTBL
;=================================================
;   表示:口上用ハートマーク書式Ｌ(黒塗り)
;-------------------------------------------------
; Input:
;  ARG:0				ハートの数（省略すると1つ）
;  ARG:1				出力桁数(デフォルト:ハートの数に比例)
;  ARGS					末端文字（省略すると"」"）
;-------------------------------------------------
@CP_HEARTBL(ARG:0=1, ARG:1=0, ARGS="")

CALL CP_HEARTB(ARG, ARG:1)

PRINTSL \@ !STRLENS(ARGS) ? 」 # %ARGS% \@
RETURNFORM RESULT



;=================================================
;   sub CP_PRINTFORM_HEART
;=================================================
;   表示:口上用ハートマーク挿入PRINT
;-------------------------------------------------
;   「ARGS:2〜」にセットした文章を、ハートマークを挟みながら出力する
;   表示は「%ARGS:2%ハート%ARGS:3%ハート…」となる
;   先頭文字と末端文字は、""をセットすれば省略可能
;-------------------------------------------------
; Input:
;  ARGS:0				先頭文字
;  ARGS:1				末端文字
;  ARGS:2〜9			出力文章(※ 間を空けず、詰めてセット)
;-------------------------------------------------
@CP_PRINTFORM_HEART(ARGS:0, ARGS:1, ARGS:2="", ARGS:3="", ARGS:4="", ARGS:5="", ARGS:6="", ARGS:7="", ARGS:8="", ARGS:9="")
#DIM L_CNT

SIF STRLENS(ARGS:0)
	PRINTS ARGS:0

l_CNT=2
WHILE 1
	SIF !STRLENS(ARGS:L_CNT)
		BREAK
	PRINTS ARGS:L_CNT
	CALL CP_HEARTB
	L_CNT ++
WEND

SIF STRLENS(ARGS:1)
	PRINTS ARGS:1

;=================================================
;   sub CP_PRINTFORM_COLORED
;=================================================
;   表示:着色済み文字
;-------------------------------------------------
; Input:
;  ARGS:0				出力文字
;  ARG					色
;  ARGS:1				出力文字2
;-------------------------------------------------
@CP_PRINTFORM_COLORED(ARGS:0,ARG,ARGS:1="")
#DIM L_COLOR

L_COLOR = GETCOLOR()
SETCOLOR ARG
PRINTFORM %ARGS:0%
RESETCOLOR
SIF ARGS:1 != ""
	PRINTFORM %ARGS:1%
SETCOLOR L_COLOR
RETURNFORM RESULT

;=================================================
;   sub CP_PRINTFORMW_COLORED
;=================================================
;   表示:着色済み文字(W)
;-------------------------------------------------
; Input:
;  ARGS:0				出力文字
;  ARG					色
;  ARGS:1				出力文字2
;-------------------------------------------------
@CP_PRINTFORMW_COLORED(ARGS:0,ARG,ARGS:1="")

CALL CP_PRINTFORM_COLORED(ARGS:0,ARG,ARGS:1)
WAIT
RETURNFORM RESULT


;=================================================
;   sub CP_CUSTOM_BAR
;=================================================
;   表示:カスタムBAR
;-------------------------------------------------
;   カスタマイズ可能なBARを表示する
;   単色及びグラデーションで表示可能
;-------------------------------------------------
;   [書式サンプル]
;   入力が必須なのは、ARG:1〜3の３つ
;
;   ;グラデ - 簡略記述
;   CP_CUSTOM_BAR(60, 100, 30, 0xFFFFFF, 0xFF00FF)
;   ;グラデ - 背景色追加
;   CP_CUSTOM_BAR(60, 100, 30, 0xFFFFFF, 0xFF00FF, 0x666699)
;   ;単色 - 簡略記述
;   CP_CUSTOM_BAR(60, 100, 30, 0xFFFFFF)
;   ;現在色 - 簡略記述
;   CP_CUSTOM_BAR(60, 100, 30)
;-------------------------------------------------
; Input:
;  ARG:0				バーの現在値
;  ARG:1				バーの最大値（-1で現在値=最大値として処理）
;  ARG:2				バーの長さ
;  ARG:3				バー始端の色（16進。省略すると現在の文字色）
;  ARG:4				バー終端の色（同上。省略すると単色バーに）
;  ARG:8				バー背景の色（同上。省略するとシステム設定の背景色に）
;  ARG:9				バーの文字種類（省略すると関数規定文字で表示）
;-------------------------------------------------
@CP_CUSTOM_BAR(ARG, ARG:1 = -1, ARG:2, ARG:3 = -1, ARG:4 = -1, ARG:8 = -1, ARG:9=0x2583)
#DIM L_I
#DIM L_COLOR
#DIM L_濃度比率
#DIM L_グラデフラグ
#DIM L_R, 3
#DIM L_G, 3
#DIM L_B, 3
#DIM L_現在値
#DIM L_最大値
#DIM L_長さ
#DIM L_始色
#DIM L_終色
#DIM L_バー長さ
#FUNCTION

L_現在値 = ARG
L_最大値 = ARG:1
L_長さ = ARG:2
L_始色 = ARG:3
L_終色 = ARG:4
L_バー長さ = L_長さ * L_現在値 / L_最大値
L_COLOR = GETCOLOR()
L_グラデフラグ=0

;- 始色・終色がセットされている場合
IF L_始色  >= 0 && L_終色 >= 0
	;- グラデーションで表示
	L_グラデフラグ=1

	;16進数をRGBに分解
	L_R:1 = L_始色 / 256 / 256
	L_G:1 = L_始色 / 256 % 256
	L_B:1 = L_始色 % 256
	L_R:2 = L_終色 / 256 / 256
	L_G:2 = L_終色 / 256 % 256
	L_B:2 = L_終色 % 256
ELSEIF L_始色 >= 0
	;- 単色で表示
	SETCOLOR L_始色
ELSE
	;- 現在色で表示
ENDIF

FOR L_I, 0, L_長さ
	IF L_グラデフラグ
		;出力毎の色彩濃度を計算し、反映
		L_濃度比率 = L_長さ - L_I
		L_R = (L_R:1 * L_濃度比率 + L_R:2 * L_I ) / (L_濃度比率 + L_I)
		L_G = (L_G:1 * L_濃度比率 + L_G:2 * L_I ) / (L_濃度比率 + L_I)
		L_B = (L_B:1 * L_濃度比率 + L_B:2 * L_I ) / (L_濃度比率 + L_I)
		SETCOLOR L_R, L_G, L_B
	ENDIF

	IF (L_I <= L_バー長さ) && L_現在値
;		ARG:9 = 0x2588
;		ARG:9 = 0x258B
;		ARG:9 = 0x2585
		PRINTFORM %UNICODE(ARG:9)%
	ELSE
		IF ARG:8 > -1
			SETCOLOR ARG:8
			PRINTFORM %UNICODE(ARG:9)%
		ELSE
			PRINTFORM  
		ENDIF
	ENDIF
NEXT
SETCOLOR L_COLOR



;=================================================
;   sub CP_CUSTOM_MAP
;=================================================
;   表示:カスタムMAP
;-------------------------------------------------
;   マップ文字列を着色して出力する
;-------------------------------------------------
; Input:
;  ARGS:0				文字列
;-------------------------------------------------
@CP_CUSTOM_MAP(ARGS)
#DIMS L_STR
#DIMS L_BAK
#DIM  L_COLOR
#DIM L_I
L_BAK = %ARGS%
L_COLOR = GETCOLOR()

;- 2バイトずつ使用
FOR L_I, 0, STRLENS(L_BAK), 2

	L_STR = %SUBSTRING(ARGS, L_I, 2)%

	SELECTCASE L_STR
		CASE "山"
			;茶
			SETCOLOR 0xCC9933
		CASE "森"
			;深緑
			SETCOLOR 0x009900
		CASE "林"
			;茶
			SETCOLOR 0x996600
		CASE "砂"
			;薄茶
			SETCOLOR 0xffcc33
		CASE "木"
			;茶
			SETCOLOR 0xCC9900
		CASE "川"
			;水色
			SETCOLOR 0x66CCCC
		CASE "海"
			;水色
			SETCOLOR 0x6699cc
		CASE "湖"
			;青
			SETCOLOR 0x3333CC
		CASE "竹"
			;濃緑
			SETCOLOR 0x006600
		CASE "道"
;			SETCOLOR 0x996633
			;灰
			SETCOLOR 0x999999
		CASE "農"
			;緑
			SETCOLOR 0x66CC66
		CASE "里"
			;青
			SETCOLOR 0x336699
		CASEELSE
			SETCOLOR L_COLOR
	ENDSELECT

	PRINTS L_STR
NEXT
PRINTL

;- クリア
L_STR=
L_BAK=

SETCOLOR L_COLOR



;-------------------------------------------------
;ラベル使用キャラステータス表示関数
;-------------------------------------------------
;ラベルからステータスを表示する
;表示の文字数はラベルに合わせる＆全角一文字分空ける
@CP_STATUS_BY_LABEL(C_ID, LABEL_NAME)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM  C_ID           ;表示対象登録番号
#DIMS LABEL_NAME     ;表示対象ラベル
PRINTFORM {INFO_STATUS_GET_BY_LABEL(C_ID, LABEL_NAME), STRLENS(LABEL_NAME) + 2}



;-------------------------------------------------
;名前短縮関数
;-------------------------------------------------
;名前を半角10文字以下にして返す
@SHORTEN_NAME(C_ID)
#FUNCTIONS
#DIM C_ID
#DIMS DYNAMIC HALF_NAME
SIF STRLENS(CALLNAME:C_ID) <= 10
	RETURNF CALLNAME:C_ID
HALF_NAME '= TOHALF(CALLNAME:C_ID)
SIF STRLENS(HALF_NAME) <= 10
	RETURNF HALF_NAME
RETURNF SUBSTRING(CALLNAME:C_ID, 0, 10)
