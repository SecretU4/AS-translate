;-------------------------------------------------
;キャラ変数参照関数
;-------------------------------------------------
;特定キャラ、特定変数、特定要素を参照する汎用関数
@C_GET_CVAR_VALUE(C_ID, VAR_NAME, VAR_ID)
#FUNCTION
#DIM  DYNAMIC C_ID     ;
#DIMS DYNAMIC VAR_NAME ;
#DIM  DYNAMIC VAR_ID   ;
#DIM  DYNAMIC TEMP_INT ;
#DIM  CONST EXTRA_随伴     = 0;VAR_IDの分岐用定数
#DIM  CONST EXTRA_後方     = 1
#DIM  CONST EXTRA_回収     = 2
#DIM  CONST EXTRA_所属部隊 = 3
#DIM  CONST EXTRA_配属施設 = 4
#DIM  CONST EXTRA_親交     = 5
#DIM  CONST EXTRA_探索     = 6
#DIM  CONST EXTRA_出自     = 7
#DIM  CONST EXTRA_職業     = 8
#DIM  CONST EXTRA_管理指数 = 9
#DIM  CONST EXTRA_機密     = 10
#DIM  CONST EXTRA_合意     = 11
#DIM  CONST EXTRA_好感度LV = 12
#DIM  CONST EXTRA_攻略     = 13
#DIM  CONST EXTRA_階級     = 14
SELECTCASE VAR_NAME
CASE "ABL"
	RETURNF ABL:C_ID:VAR_ID
CASE "BASE"
	RETURNF BASE:C_ID:VAR_ID
CASE "MAXBASE"
	RETURNF MAXBASE:C_ID:VAR_ID
CASE "EXP"
	RETURNF EXP:C_ID:VAR_ID
CASE "CFLAG"
	RETURNF CFLAG:C_ID:VAR_ID
CASE "PALAM"
	RETURNF PALAM:C_ID:VAR_ID
CASE "TALENT"
	RETURNF TALENT:C_ID:VAR_ID
CASE "TCVAR"
	RETURNF TCVAR:C_ID:VAR_ID
CASE "MARK"
	RETURNF MARK:C_ID:VAR_ID
CASE "EXTRA"
	SELECTCASE VAR_ID
	CASE EXTRA_随伴
		RETURNF CFLAG:C_ID:住人_ATK + CFLAG:C_ID:住人_DEF + CFLAG:C_ID:住人_SPD + CFLAG:C_ID:住人_HIT
	CASE EXTRA_後方
		RETURNF CFLAG:C_ID:住人_SPD + CFLAG:C_ID:住人_HIT
	CASE EXTRA_回収
		RETURNF CFLAG:C_ID:住人_SPD + CFLAG:C_ID:住人_LUK
	CASE EXTRA_探索
		;複雑な評価値になっているが
		;これは扱いづらいイスが最上部に来るのを避けるために試行錯誤したせい
		{
		TEMP_INT = 
		CFLAG:C_ID:住人_ATK * 3
		 + CFLAG:C_ID:住人_DEF * 2
		 + CFLAG:C_ID:住人_SPD * 3
		 + CFLAG:C_ID:住人_HIT * 4
		 + CFLAG:C_ID:住人_現在体力 * 5
		 + CFLAG:C_ID:住人_現在気力 * 8
		 + CFLAG:C_ID:住人_現在BRE
		 + CFLAG:C_ID:住人_BRAVE * 5
		}
		RETURNF TEMP_INT / 10
	CASE EXTRA_所属部隊
		;所属なし＝0、住人_支援_随伴＝1、CFLAG側のIDは住人_支援_随伴＝6800なので注意
		TEMP_INT = MAX(0, 1 + FINDELEMENT(CFLAG:C_ID:0, 1, 6800, 6804) - 6800)
		RETURNF TEMP_INT
	CASE EXTRA_配属施設
		;注意点に関しては所属部隊と同様
		TEMP_INT = MAX(0, 1 + FINDELEMENT(CFLAG:C_ID:0, 1, 10000, 10100) - 10000)
		RETURNF TEMP_INT
	CASE EXTRA_親交
		;陥落と刻印だけ見る、反発は考慮しない
		IF TALENT:C_ID:心酔
			TEMP_INT = 30
		ELSEIF TALENT:C_ID:愛人
			TEMP_INT = 20
		ELSEIF TALENT:C_ID:情婦
			TEMP_INT = 10
		ELSE
			TEMP_INT = 0
		ENDIF
		RETURNF TEMP_INT + SUMARRAY(MARK:C_ID:0, 0, 3)
	CASE EXTRA_出自
		IF TALENT:C_ID:主人持ち == 1
			TEMP_INT = 6
		ELSEIF TALENT:C_ID:旧世代人 == 1
			TEMP_INT = 5
		ELSEIF TALENT:C_ID:元性奴隷 == 1
			TEMP_INT = 4
		ELSEIF TALENT:C_ID:人権損壊 == 1
			TEMP_INT = 3
		ELSEIF TALENT:C_ID:イレギュラー == 1
			TEMP_INT = 2
		ELSEIF TALENT:C_ID:青き血族 == 1
			TEMP_INT = 1
		ELSE
			TEMP_INT = 0
		ENDIF
		RETURNF TEMP_INT * 100 + CFLAG:C_ID:住人_種族
	CASE EXTRA_職業
		IF TALENT:C_ID:メイド == 1
			TEMP_INT = 7
		ELSEIF TALENT:C_ID:聖職者 == 1
			TEMP_INT = 6
		ELSEIF TALENT:C_ID:治安維持局員 == 1
			TEMP_INT = 5
		ELSEIF TALENT:C_ID:社会人 == 1
			TEMP_INT = 4
		ELSEIF TALENT:C_ID:アイドル == 1
			TEMP_INT = 3
		ELSEIF TALENT:C_ID:学生
			TEMP_INT = 2
		ELSE
			TEMP_INT = 1
		ENDIF
		
		RETURNF TEMP_INT * 100 + TALENT:C_ID:娼婦 * 10 + (TALENT:C_ID:学生 + 1)
	CASE EXTRA_管理指数
		TEMP_INT = CFLAG:C_ID:関係段階 + CFLAG:C_ID:レギュラー * 3 + CFLAG:C_ID:特別保護 * 2
		SIF CFLAG:C_ID:マンションID
			TEMP_INT += 2
		RETURNF TEMP_INT
	CASE EXTRA_機密
		RETURNF MAX(FINDLASTELEMENT(TALENT:C_ID:0, 1, 3000, 3010) - 3000, 0)
	CASE EXTRA_合意
		IF TALENT:C_ID:調教済み
			TEMP_INT = 4
		ELSEIF TALENT:C_ID:合意
			TEMP_INT = 3
		ELSEIF TALENT:C_ID:犯され
			TEMP_INT = 2
		ELSEIF TALENT:C_ID:弱み
			TEMP_INT = 1
		ELSE
			TEMP_INT = 0
		ENDIF
		RETURNF TEMP_INT
	CASE EXTRA_好感度LV
		SIF CFLAG:C_ID:好感度 <= 0
			RETURNF 0
		RETURNF LIMIT(SQRT(CFLAG:C_ID:好感度), 1, 999)
	CASE EXTRA_攻略
		IF CFLAG:C_ID:忠誠値 >= 900 && MARK:C_ID:隷属刻印 == 3 && TALENT:C_ID:心酔  == 0 && TALENT:C_ID:情婦  == 0 && TALENT:C_ID:愛人  == 0
			RETURNF 1003
		ELSEIF CFLAG:C_ID:愛情値 >= 900 && MARK:C_ID:陥落刻印 == 3 && TALENT:C_ID:心酔  == 0 && TALENT:C_ID:情婦  == 0 && TALENT:C_ID:愛人  == 0
			RETURNF 1002
		ELSEIF CFLAG:C_ID:淫乱値 >= 900 && MARK:C_ID:淫乱刻印 == 3 && TALENT:C_ID:心酔  == 0  && TALENT:C_ID:愛人  == 0 && TALENT:C_ID:情婦  == 0
			RETURNF 1001
		ELSEIF CFLAG:C_ID:忠誠値 >= 600 && MARK:C_ID:隷属刻印 <= 2
			RETURNF 103
		ELSEIF CFLAG:C_ID:愛情値 >= 600 && MARK:C_ID:陥落刻印 <= 2
			RETURNF 102
		ELSEIF CFLAG:C_ID:淫乱値 >= 600 && MARK:C_ID:淫乱刻印 <= 2
			RETURNF 101
		ELSEIF CFLAG:C_ID:忠誠値 >= 300 && MARK:C_ID:隷属刻印 <= 1
			RETURNF 13
		ELSEIF CFLAG:C_ID:愛情値 >= 300 && MARK:C_ID:陥落刻印 <= 1
			RETURNF 12
		ELSEIF CFLAG:C_ID:淫乱値 >= 300 && MARK:C_ID:淫乱刻印 <= 1
			RETURNF 11
		ELSEIF CFLAG:C_ID:忠誠値 >= 100 && MARK:C_ID:隷属刻印 <= 0
			RETURNF 3
		ELSEIF CFLAG:C_ID:愛情値 >= 100 && MARK:C_ID:陥落刻印 <= 0
			RETURNF 2
		ELSEIF CFLAG:C_ID:淫乱値 >= 100 && MARK:C_ID:淫乱刻印 <= 0
			RETURNF 1
		ENDIF
	CASE EXTRA_階級
		TEMP_INT = MAX(0, FINDELEMENT(TALENT:C_ID:0, 1, 3101, 3108) - 3100)
		RETURNF TEMP_INT
	ENDSELECT
	RETURNF 0
CASEELSE
	THROW 不明なVAR_NAME %VAR_NAME%
ENDSELECT


