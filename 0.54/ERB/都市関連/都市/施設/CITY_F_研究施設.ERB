
;-------------------------------------------------
;施設　研究施設
;-------------------------------------------------
@CITY_F_52
CALL MANAGE_CHARA_SELECT("MNG_特殊_身体強化")




@LABORATORY_BREEDING_SELECT_MOTHER
#DIM L_MOTHER
CLEARLINE LINECOUNT
DRAWLINE
CALL PRINT_LABEL("次世代創造")
PRINTL
PRINTFORML 最先端の技術を用いて、新たなる世代を創造します。
PRINTFORML 新しい世代は、以前の世代より＋補正値を得ます。
DRAWLINE
PRINTFORML 継承元の住民を選択して下さい。
CALL PRINT_LABEL("条件")
PRINTFORML 出産回数＞継承回数(住民登録以前の出産回数は含まれません)
CALL LIST_CHARA("次世代創造")
L_MOTHER = RESULT
SIF L_MOTHER == 1000
	RETURN
IF L_MOTHER == 0
	PRINTFORMW あなたは選択できません
ELSEIF !INRANGE(L_MOTHER, 0, CHARANUM - 1)
	PRINTFORMW 入力された番号はキャラの範囲外です
ELSEIF TALENT:L_MOTHER:NPC == 999
	PRINTFORMW NPCは選択できません
ELSEIF CFLAG:L_MOTHER:住人_回収済み数  <= CFLAG:L_MOTHER:住人_継承
	PRINTFORMW 条件を満たしていません
ELSE
	JUMP LABORATORY_BREEDING_BABY_DESIGNING(L_MOTHER)
ENDIF
RESTART



@LABORATORY_BREEDING_BABY_DESIGNING(L_MOTHER)
#DIM L_MOTHER
#DIM L_BABY
#DIM DYNAMIC L_CREATE_BABY = 1
#DIM DYNAMIC L_DESIGN_FLAG = 0
#DIM L_INPUT
#DIM L_I
IF L_CREATE_BABY == 1
	L_CREATE_BABY = 0
	CALL LABORATORY_BREEDING_CREATE_BABY(L_MOTHER, L_BABY, L_DESIGN_FLAG)
	CLEARLINE LINECOUNT
ENDIF
;リロードできるように？
SIF !TALENT:L_BABY:先祖返り
	CALL CHARACTER_SCOUT_COSTUME(L_BABY)
DRAWLINE
CALL C_PICTURE_S(L_BABY)
PRINTFORML ■------------------------------------ 　素質　 ------------------------------------■
CALL INFO_STATUS_TALENT(L_BABY)
DRAWLINE
PRINTFORML 創造する住民の詳細を設定してください。
DRAWLINE
PRINTFORML [0] 容姿の設定(ランダム選出)
PRINTFORML [1] 遺伝要素の選択
;PRINTLC [2] 
PRINTFORML 
;PRINTFORML [3] 種族の設定(要遺伝子)
PRINTFORML 
PRINTFORML [100] 次世代創造
PRINTFORML 
PRINTFORML [999] 戻る
INPUT

L_INPUT = RESULT

SELECTCASE L_INPUT
	CASE 0
		IF L_BABY > 0
			CALL CANCEL_SCOUT(L_BABY)
			L_CREATE_BABY = 1
		ENDIF
	CASE 1
		CLEARLINE LINECOUNT
		CALL LABORATORY_BREEDING_SELECT_DESIGN(L_BABY, L_DESIGN_FLAG)
	CASE 2
	CASE 3
	CASE 100
		TARGET = L_BABY
		;母体側の継承回数更新があるので決定時のこのタイミングでないとダメ
		CALL LABORATORY_BREEDING_BABY_HEREDITY_CFLAG(L_MOTHER, L_BABY)
		RETURN
	CASE 999
		SIF L_BABY > 0
			CALL CANCEL_SCOUT(L_BABY)
		FLAG:継承種族 = 0
		RETURN
	CASEELSE
ENDSELECT
RESTART



;-------------------------------------------------
;次世代の加入処理
;-------------------------------------------------
@LABORATORY_BREEDING_CREATE_BABY(L_MOTHER, L_BABY, L_DESIGN_FLAG)
#DIM     L_MOTHER
#DIM REF L_BABY
#DIM     L_DESIGN_FLAG
;ここだけはどうしても使用せざるをえず
FLAG:継承種族 = (CFLAG:L_MOTHER:住人_種族) + 1

FLAG:汎用変数 = 52
CALL CHARACTER_SCOUT1
L_BABY = CHARANUM - 1
;次世代素質遺伝
CALL LABORATORY_BREEDING_BABY_HEREDITY_TALENT(L_MOTHER, L_BABY)
CALL LABORATORY_BREEDING_FORMING_DESIGN(L_MOTHER, L_BABY, L_DESIGN_FLAG)
SIF TALENT:L_BABY:先祖返り
	CALL LABORATORY_BREEDING_REVERSION(L_MOTHER, L_BABY)
FLAG:汎用変数 = 0


;-------------------------------------------------
; 遺伝の継承　絞り込み適用
;-------------------------------------------------
@LABORATORY_BREEDING_FORMING_DESIGN(L_MOTHER, L_BABY, L_DESIGN_FLAG)
#DIM L_MOTHER
#DIM L_BABY
#DIM L_DESIGN_FLAG
#DIM  CONST L_BIT_髪色 = 0
#DIM  CONST L_BIT_瞳色 = 1
#DIM  CONST L_BIT_胸   = 2
#DIM  CONST L_BIT_尻   = 3
SIF GETBIT(L_DESIGN_FLAG, L_BIT_髪色)
	CFLAG:L_BABY:髪色 = CFLAG:L_MOTHER:髪色
SIF GETBIT(L_DESIGN_FLAG, L_BIT_瞳色)
	CFLAG:L_BABY:瞳色 = CFLAG:L_MOTHER:瞳色
IF GETBIT(L_DESIGN_FLAG, L_BIT_胸)
	TALENT:L_BABY:爆乳 = TALENT:L_MOTHER:爆乳
	TALENT:L_BABY:巨乳 = TALENT:L_MOTHER:巨乳
	TALENT:L_BABY:貧乳 = TALENT:L_MOTHER:貧乳
	TALENT:L_BABY:絶壁 = TALENT:L_MOTHER:絶壁
	CFLAG:L_BABY:バストサイズ = CHARACTER_BUST_RETURN(L_BABY)
ENDIF
IF GETBIT(L_DESIGN_FLAG, L_BIT_尻)
	TALENT:L_BABY:爆尻 = TALENT:L_MOTHER:爆尻
	TALENT:L_BABY:巨尻 = TALENT:L_MOTHER:巨尻
	CFLAG:L_BABY:ヒップサイズ = CHARACTER_HIP_RETURN(L_BABY)
ENDIF



;-------------------------------------------------
;継承素質の選択
;-------------------------------------------------
@LABORATORY_BREEDING_SELECT_DESIGN(L_BABY, L_DESIGN_FLAG)
#DIM        L_BABY
#DIM  REF   L_DESIGN_FLAG
#DIM        L_INPUT
#DIMS CONST L_LABEL = "髪色の遺伝", "瞳色の遺伝", "胸の遺伝", "尻の遺伝"
#DIM  CONST L_BIT_髪色 = 0
#DIM  CONST L_BIT_瞳色 = 1
#DIM  CONST L_BIT_胸   = 2
#DIM  CONST L_BIT_尻   = 3
#DIM        L_FLAG_BIT
DRAWLINE
FOR L_FLAG_BIT, 0, 4
	PRINTFORML [{L_FLAG_BIT}] %L_LABEL:L_FLAG_BIT, 10% %\@GETBIT(L_DESIGN_FLAG, L_FLAG_BIT) ? ○ # ×\@%
NEXT
PRINTFORML 
PRINTFORML 
PRINTFORML [999] 戻る
INPUT
L_INPUT = RESULT
SIF L_INPUT == 999
	RETURN
SIF INRANGE(L_INPUT, 0, 3)
	INVERTBIT L_DESIGN_FLAG, L_INPUT
RESTART



;-------------------------------------------------
;次世代創造
;主にCFLAG系のステータスの継承
;-------------------------------------------------
@LABORATORY_BREEDING_BABY_HEREDITY_CFLAG(L_MOTHER, L_BABY)
#DIM L_MOTHER
#DIM L_BABY
#DIM L_FIRST
#DIM L_CFLAG_ID
#DIM L_WOMB_GRADE
#DIM L_GENE_GRADE

L_WOMB_GRADE = 0
L_GENE_GRADE = 0

;遺伝因子継承
FOR L_CFLAG_ID, 1800, 1850
	SIF CFLAG:L_MOTHER:L_CFLAG_ID != 1
		CONTINUE
	CFLAG:L_BABY:L_CFLAG_ID = 1
	L_GENE_GRADE ++
NEXT
IF L_GENE_GRADE >= 19
	TALENT:L_BABY:3008 = 1
ELSE
	TALENT:L_BABY:(3000 + (L_GENE_GRADE / 2)) = 1
ENDIF

;d3+x/2+d(x/2) = > x + (2d4) - 5
CALL LABORATORY_BREEDING_BABY_HEREDITY_PHEROMONE(L_MOTHER, L_BABY)
;CFlag:母体レベル = 1+(CFlag:L_I:母体レベル/2)+RAND:(CFlag:L_I:母体レベル/2+1)+RAND:3
;CFlag:卵子レベル = 1+(CFlag:L_I:卵子レベル/2)+RAND:(CFlag:L_I:卵子レベル/2+1)+RAND:3

IF CSTR:L_MOTHER:始祖 == ""
	CSTR:L_BABY:始祖 = %CALLNAME:L_MOTHER%
	CFLAG:L_BABY:住人_始祖 = CFLAG:L_MOTHER:個別番号
	CFLAG:L_MOTHER:次世代創造子孫数 ++
ELSE
	CSTR:L_BABY:始祖 = %CSTR:L_MOTHER:始祖%
	CFLAG:L_BABY:住人_始祖 = CFLAG:L_MOTHER:住人_始祖
	;次善の策、記録されてないならわかるところから記録しはじめましょう
	SIF !CFLAG:L_BABY:住人_始祖
		CFLAG:L_BABY:住人_始祖 = CFLAG:L_MOTHER:個別番号
	L_FIRST = FINDCHARA(CFLAG:0:個別番号, CFLAG:L_BABY:住人_始祖)
	SIF L_FIRST >= 1
		CFLAG:L_FIRST:次世代創造子孫数 ++
ENDIF
CSTR:L_BABY:母親 = %CALLNAME:L_MOTHER%
CFLAG:L_BABY:住人_母親 = CFLAG:L_MOTHER:個別番号

CFLAG:L_BABY:住人_世代 = MAX(CFLAG:L_MOTHER:住人_世代 + 1, 2)
SIF CFLAG:L_BABY:住人_世代 >= 10
	TALENT:L_BABY:名家 = 1

SIF TALENT:L_BABY:名家 && !TALENT:L_MOTHER:名家
	CALL LABORATORY_BREEDING_NOBLE_FAMILY(L_MOTHER, L_BABY)


L_WOMB_GRADE = L_GENE_GRADE + CFLAG:L_MOTHER:母体レベル + CFLAG:L_MOTHER:卵子レベル + CFLAG:L_BABY:住人_世代

SIF TALENT:L_MOTHER:聖女の子宮 == 1
	L_WOMB_GRADE += 50

CFlag:L_BABY:住人_最大体力 = CFlag:L_MOTHER:住人_最大体力 + SQRT(MAX(1, CFlag:L_MOTHER:住人_最大体力 + L_WOMB_GRADE)) + (L_WOMB_GRADE / 5) + (L_GENE_GRADE * 2)
CFlag:L_BABY:住人_最大気力 = CFlag:L_MOTHER:住人_最大気力 + SQRT(MAX(1, CFlag:L_MOTHER:住人_最大気力 + L_WOMB_GRADE)) + (L_WOMB_GRADE / 5) + (L_GENE_GRADE * 2)
CFlag:L_BABY:住人_最大BRE  = CFlag:L_MOTHER:住人_最大BRE  + SQRT(MAX(1, CFlag:L_MOTHER:住人_最大BRE  + L_WOMB_GRADE)) + (L_WOMB_GRADE / 5) + (L_GENE_GRADE * 2)
CFlag:L_BABY:住人_現在体力 = CFlag:L_BABY:住人_最大体力
CFlag:L_BABY:住人_現在気力 = CFlag:L_BABY:住人_最大気力
CFlag:L_BABY:住人_現在BRE  = CFlag:L_BABY:住人_最大BRE

CFlag:L_BABY:住人_ATK = CFlag:L_MOTHER:住人_ATK + SQRT(MAX(1, CFlag:L_MOTHER:住人_ATK + L_WOMB_GRADE)) + (L_WOMB_GRADE / 5) + (L_GENE_GRADE * 2)
CFlag:L_BABY:住人_DEF = CFlag:L_MOTHER:住人_DEF + SQRT(MAX(1, CFlag:L_MOTHER:住人_DEF + L_WOMB_GRADE)) + (L_WOMB_GRADE / 5) + (L_GENE_GRADE * 2)
CFlag:L_BABY:住人_HIT = CFlag:L_MOTHER:住人_HIT + SQRT(MAX(1, CFlag:L_MOTHER:住人_HIT + L_WOMB_GRADE)) + (L_WOMB_GRADE / 5) + (L_GENE_GRADE * 2)
CFlag:L_BABY:住人_SPD = CFlag:L_MOTHER:住人_SPD + SQRT(MAX(1, CFlag:L_MOTHER:住人_SPD + L_WOMB_GRADE)) + (L_WOMB_GRADE / 5) + (L_GENE_GRADE * 2)

CFLAG:L_BABY:住人_技能ポイント += L_GENE_GRADE * 5

CSTR:L_BABY:キャラ解説03 = 【機密Lv2:採取】創造してから記録に至るまで性経験はなし。採取もされていません。
CSTR:L_BABY:キャラ解説04 = 【機密Lv3:権利(財産)】あなたの申請により、%CSTR:L_BABY:母親%の遺伝子を継承し創造・調整を施された住人。

CSTR:L_BABY:キャラ解説15 = 【重要機密:一族】始祖「%CSTR:L_BABY:始祖%」を始まりとし、母「%CSTR:L_BABY:母親%」から生まれた{CFLAG:L_BABY:住人_世代}世代目の住人です。

MONEY -= 200000

PRINTFORMW 要求された条件をもとに、素材を用いて次世代創造を行いました。
FLAG:継承種族 = 0
CFLAG:L_MOTHER:住人_継承 ++



;-------------------------------------------------
;次世代素質遺伝
;母親の素質を参照して確率で遺伝
;より母親に似る様に
;-------------------------------------------------
@LABORATORY_BREEDING_BABY_HEREDITY_TALENT(L_MOTHER, L_BABY)
#DIM L_MOTHER
#DIM L_BABY
;テンプレその１






;相反する素質が存在する場合
;母体の素質判定、母子で相反する素質を打消すか、持ってない素質を発露
;判定
;IF TALENT:L_I:OOOO == 1 || TALENT:L_I:XXXX == 1
;	
;	;相反する素質を打消し
;	IF TALENT:L_I2:OOOO == 1 && TALENT:L_I:XXXX == 1 
;		SIF !RAND:2
;			TALENT:L_I2:OOOO = 0
;	ELSEIF TALENT:L_I2:XXXX == 1 && TALENT:L_I:OOOO == 1
;		SIF !RAND:2
;			TALENT:L_I2:XXXX = 0
;	
;	;素質を発露
;	ELSEIF TALENT:L_I:OOOO == 1
;		SIF !RAND:2
;			TALENT:L_I2:OOOO = 1
;	ELSEIF TALENT:L_I:XXXX == 1
;		SIF !RAND:2
;			TALENT:L_I2:XXXX = 1
;	ENDIF
;ENDIF


;テンプレその２

;単独の素質の場合
;判定
;IF TALENT:L_I:OOOO == 1
;母の素質を発露
;	SIF !RAND:2
;		TALENT:L_I2:OOOO = 1
;ELSE
;ない場合子の素質を打消し
;	SIF !RAND:2
;		TALENT:L_I2:OOOO = 0
;ENDIF

SELECTCASE RAND:5
CASE 0
	Talent:L_BABY:学生 = 1
CASE 1
	Talent:L_BABY:アイドル = 1
CASE 2
	Talent:L_BABY:社会人 = 1
CASE 3
	Talent:L_BABY:娼婦 = 1
CASE 4
	Talent:L_BABY:治安維持局員 = 1
ENDSELECT

IF Talent:L_BABY:学生
	CALL CHARACTER_SCOUT_STUDENT(L_BABY)
	IF RAND:10 >= 2
		CFlag:L_BABY:住人_体格 = 1
		CFlag:L_BABY:服装 = (RAND:5)
		CFlag:L_BABY:靴 = RAND:3
	ENDIF
ENDIF


;感覚 [000-009]
;C~B感覚
CALL HEREDITY_CONFLICT_TALENT(0, 8, L_MOTHER, L_BABY)

;ステータス [010-029]
CALL HEREDITY_NORMAL_TALENT(10, 30, L_MOTHER, L_BABY)

;○○っ気 [030-034]
;何もなければスルー
IF ALLSAMES(0, TALENT:L_MOTHER:サド, TALENT:L_MOTHER:マゾ)
;1/2でスルー
ELSEIF !RAND:2
	IF ALLSAMES(1, TALENT:L_MOTHER:サド, TALENT:L_BABY:マゾ) || ALLSAMES(1, TALENT:L_MOTHER:マゾ, TALENT:L_BABY:サド)
		TALENT:L_BABY:サド = 0
		TALENT:L_BABY:マゾ = 0
	ELSE
		TALENT:L_BABY:サド = TALENT:L_MOTHER:サド
		TALENT:L_BABY:マゾ = TALENT:L_MOTHER:マゾ
	ENDIF
ENDIF

;中毒 [035-039]
CALL HEREDITY_NORMAL_TALENT(35, 40, L_MOTHER, L_BABY)
;技能 [040-049]
CALL HEREDITY_NORMAL_TALENT(40, 45, L_MOTHER, L_BABY)

;体質 [060-084]	 身長の書き換えが要るため体格の素質は保留
CALL HEREDITY_CONFLICT_TALENT(70, 78, L_MOTHER, L_BABY)

SIF !RAND:2
	TALENT:L_BABY:猫舌 = TALENT:L_MOTHER:猫舌

;性格 [100-129]
;臆病～楽観的
CALL HEREDITY_CONFLICT_TALENT(100, 114, L_MOTHER, L_BABY)

CALL HEREDITY_NORMAL_TALENT(114, 120, L_MOTHER, L_BABY)

;性格(社交面) [130-149]
;素直～目立ち
CALL HEREDITY_CONFLICT_TALENT(130, 136, L_MOTHER, L_BABY)

CALL HEREDITY_NORMAL_TALENT(136, 139, L_MOTHER, L_BABY)

;性格(性関連) [150-179]
SIF !RAND:2
	TALENT:L_BABY:自制心 = TALENT:L_MOTHER:自制心
;習得～貞操観念
CALL HEREDITY_CONFLICT_TALENT(151, 157, L_MOTHER, L_BABY)

SIF !RAND:2
	TALENT:L_BABY:一線越えない = TALENT:L_MOTHER:一線越えない

;快感の否定～開放
CALL HEREDITY_CONFLICT_TALENT(158, 162, L_MOTHER, L_BABY)
CALL HEREDITY_NORMAL_TALENT(165, 167, L_MOTHER, L_BABY)


;血統はかならず継承、記憶処理済は不自然なので消去
TALENT:L_BABY:名家 = TALENT:L_MOTHER:名家
TALENT:L_BABY:青き血族 = TALENT:L_MOTHER:青き血族
TALENT:L_BABY:記憶処理済 = 0

IF TALENT:L_MOTHER:即堕ち == 1
	IF TALENT:L_BABY:惚れっぽい
		IF !RAND:4
			TALENT:L_BABY:惚れっぽい = 0
			TALENT:L_BABY:即堕ち = 1
		ENDIF
	ELSEIF TALENT:L_BABY:警戒心
		SIF !RAND:3
			TALENT:L_BABY:警戒心 = 0
	ELSEIF TALENT:L_BABY:人間不信
		IF !RAND:2
			TALENT:L_BABY:人間不信 = 0
			TALENT:L_BABY:警戒心 = 1
		ENDIF
	ELSE
		SIF !RAND:4
			TALENT:L_BABY:即堕ち = 1
	ENDIF
ELSEIF TALENT:L_MOTHER:惚れっぽい
	IF TALENT:L_BABY:即堕ち
		IF !RAND:4
			TALENT:L_BABY:即堕ち = 0
			TALENT:L_BABY:惚れっぽい = 1
		ENDIF
	ELSEIF TALENT:L_BABY:警戒心
		SIF !RAND:3
			TALENT:L_BABY:警戒心 = 0
	ELSEIF TALENT:L_BABY:人間不信
		IF !RAND:2
			TALENT:L_BABY:人間不信 = 0
			TALENT:L_BABY:警戒心 = 1
		ENDIF
	ELSE
		SIF !RAND:3
			TALENT:L_BABY:惚れっぽい = 1
	ENDIF
ELSEIF TALENT:L_MOTHER:警戒心
	IF TALENT:L_BABY:即堕ち
		IF !RAND:2
			TALENT:L_BABY:即堕ち = 0
			TALENT:L_BABY:惚れっぽい = 1
		ENDIF
	ELSEIF TALENT:L_BABY:惚れっぽい
		SIF !RAND:3
			TALENT:L_BABY:惚れっぽい = 0
	ELSEIF TALENT:L_BABY:人間不信
		IF !RAND:4
			TALENT:L_BABY:人間不信 = 0
			TALENT:L_BABY:警戒心 = 1
		ENDIF
	ELSE
		SIF !RAND:3
			TALENT:L_BABY:警戒心 = 1
	ENDIF
ELSEIF TALENT:L_MOTHER:人間不信
	IF TALENT:L_BABY:即堕ち
		IF !RAND:2
			TALENT:L_BABY:即堕ち = 0
			TALENT:L_BABY:惚れっぽい = 1
		ENDIF
	ELSEIF TALENT:L_BABY:惚れっぽい
		SIF !RAND:3
			TALENT:L_BABY:惚れっぽい = 0
	ELSEIF TALENT:L_BABY:警戒心
		IF !RAND:4
			TALENT:L_BABY:警戒心 = 0
			TALENT:L_BABY:人間不信 = 1
		ENDIF
	ELSE
		SIF !RAND:4
			TALENT:L_BABY:人間不信 = 1
	ENDIF
ENDIF
;その他 [180-199]
;習得～天才
CALL HEREDITY_CONFLICT_TALENT(180, 184, L_MOTHER, L_BABY)

SIF !RAND:2
	TALENT:L_BABY:依存 = TALENT:L_MOTHER:依存

RETURN



;通常TALENTの範囲指定遺伝処理関数
@HEREDITY_NORMAL_TALENT(L_TALENT_START, L_TALENT_END, L_MOTHER, L_BABY)
#DIM L_TALENT_ID    ;継承対象のTALENT番号
#DIM L_TALENT_START ;継承対象のTALENT範囲
#DIM L_TALENT_END   ;継承対象のTALENT範囲、この番号自体は含まれない
#DIM L_MOTHER       ;母親
#DIM L_BABY         ;娘
FOR L_TALENT_ID, L_TALENT_START, L_TALENT_END
	;1/2でスルー
	SIF !RAND:2
		CONTINUE
	TALENT:L_BABY:L_TALENT_ID = TALENT:L_MOTHER:L_TALENT_ID
NEXT



;相反TALENTの範囲指定遺伝処理関数
;読んでもらうとわかりますが相反する素質同士のTALENT番号が隣接してる場合にのみうまく動きます
@HEREDITY_CONFLICT_TALENT(L_TALENT_START, L_TALENT_END, L_MOTHER, L_BABY)
#DIM L_TALENT_ID    ;継承対象のTALENT番号
#DIM L_TALENT_START ;継承対象のTALENT範囲
#DIM L_TALENT_END   ;継承対象のTALENT範囲、この番号自体は含まれない
#DIM L_MOTHER       ;母親
#DIM L_BABY         ;娘
FOR L_TALENT_ID, L_TALENT_START, L_TALENT_END, 2
	;何もなければスルー
	SIF ALLSAMES(0, TALENT:L_MOTHER:L_TALENT_ID, TALENT:L_MOTHER:(L_TALENT_ID + 1))
		CONTINUE
	;1/2でスルー
	SIF !RAND:2
		CONTINUE
	;相殺
	;母：鈍感＋娘：敏感、もしくは母：敏感＋娘：鈍感など
	IF ALLSAMES(1, TALENT:L_MOTHER:(L_TALENT_ID + 1), TALENT:L_BABY:L_TALENT_ID) || ALLSAMES(1, TALENT:L_MOTHER:L_TALENT_ID, TALENT:L_BABY:(L_TALENT_ID + 1))
		TALENT:L_BABY:L_TALENT_ID       = 0
		TALENT:L_BABY:(L_TALENT_ID + 1) = 0
	;遺伝
	ELSE
		TALENT:L_BABY:L_TALENT_ID       = TALENT:L_MOTHER:L_TALENT_ID
		TALENT:L_BABY:(L_TALENT_ID + 1) = TALENT:L_MOTHER:(L_TALENT_ID + 1)
	ENDIF
NEXT


;母体継承成長用ダイス
;成長期待値2、-1～5
@HEREDITY_PHEROMONE_DICE(BASE_VALUE)
#FUNCTION
#DIM BASE_VALUE
RETURNF BASE_VALUE + RAND:3 + RAND:3 + RAND:3 - 1



;フェロモン用の継承処理
@LABORATORY_BREEDING_BABY_HEREDITY_PHEROMONE(L_MOTHER, L_BABY)
#DIM  L_MOTHER
#DIM  L_BABY
#DIM  L_DICE
#DIMS L_RACE
#DIM  L_DICE_NUM

L_RACE = INFO_STR_SYUZOKU(CFLAG:L_BABY:住人_種族)
CFLAG:L_BABY:無自覚な雌 = HEREDITY_PHEROMONE_DICE(CFLAG:L_MOTHER:無自覚な雌)
L_DICE_NUM = 1
;素質によるボーナス成長
SIF TALENT:L_BABY:魅惑
	L_DICE_NUM ++
SIF TALENT:L_BABY:人気
	L_DICE_NUM ++
SIF TALENT:L_BABY:カリスマ
	L_DICE_NUM ++
SIF TALENT:L_BABY:セックス狂
	L_DICE_NUM ++
SIF TALENT:L_BABY:小悪魔
	L_DICE_NUM ++
SIF TALENT:L_BABY:貞操無頓着
	L_DICE_NUM ++
SIF TALENT:L_BABY:快感に素直
	L_DICE_NUM ++
SIF TALENT:L_BABY:解放
	L_DICE_NUM ++
SIF TALENT:L_BABY:男好き
	L_DICE_NUM ++
SIF TALENT:L_BABY:惚れっぽい
	L_DICE_NUM ++
SIF TALENT:L_BABY:即堕ち
	L_DICE_NUM ++
SIF TALENT:L_BABY:依存
	L_DICE_NUM ++

FOR LOCAL, 0, L_DICE_NUM
	CFLAG:L_BABY:無自覚な雌 += RAND:3
NEXT

;他の数値に対するボーナスの種
L_DICE = LIMIT(CFLAG:L_BABY:無自覚な雌/ 5, 2, 10)
SIF TALENT:L_BABY:極上の牝
	L_DICE += 3



CFLAG:L_BABY:フェロモン = HEREDITY_PHEROMONE_DICE(CFLAG:L_MOTHER:フェロモン)
L_DICE_NUM = L_DICE
SIF TALENT:L_BABY:マドンナ
	L_DICE_NUM += 3

SIF TALENT:L_BABY:魅惑
	L_DICE_NUM ++
SIF TALENT:L_BABY:魅力
	L_DICE_NUM ++
SIF TALENT:L_BABY:美貌
	L_DICE_NUM ++
SIF TALENT:L_BABY:人気
	L_DICE_NUM ++
SIF TALENT:L_BABY:小悪魔
	L_DICE_NUM ++
SIF TALENT:L_BABY:カリスマ
	L_DICE_NUM ++
FOR LOCAL, 0, L_DICE_NUM
	CFLAG:L_BABY:フェロモン += RAND:3
NEXT



CFLAG:L_BABY:雌の本能 = HEREDITY_PHEROMONE_DICE(CFLAG:L_MOTHER:雌の本能)
L_DICE_NUM = L_DICE
SIF GROUPMATCH(L_RACE, "亜人", "鬼", "鳥人", "夜魔")
	L_DICE_NUM += 3
SIF TALENT:L_BABY:セックス狂
	L_DICE_NUM ++
SIF TALENT:L_BABY:中毒しやすい
	L_DICE_NUM ++
SIF TALENT:L_BABY:貞操無頓着
	L_DICE_NUM ++
SIF TALENT:L_BABY:快感に素直
	L_DICE_NUM ++
SIF TALENT:L_BABY:解放
	L_DICE_NUM ++
SIF TALENT:L_BABY:依存
	L_DICE_NUM ++
FOR LOCAL, 0, L_DICE_NUM
	CFLAG:L_BABY:雌の本能 += RAND:3
NEXT



CFLAG:L_BABY:性への興味 = HEREDITY_PHEROMONE_DICE(CFLAG:L_MOTHER:性への興味)
L_DICE_NUM = L_DICE
;好奇心旺盛そうな種族
SIF GROUPMATCH(L_RACE, "人間", "イス")
	L_DICE_NUM += 4

SIF TALENT:L_BABY:中毒しやすい
	L_DICE_NUM ++
SIF TALENT:L_BABY:好奇心
	L_DICE_NUM ++
SIF TALENT:L_BABY:貞操無頓着
	L_DICE_NUM ++
SIF TALENT:L_BABY:自慰しやすい
	L_DICE_NUM ++
FOR LOCAL, 0, L_DICE_NUM
	CFLAG:L_BABY:性への興味 += RAND:3
NEXT



CFLAG:L_BABY:雄への関心 = HEREDITY_PHEROMONE_DICE(CFLAG:L_MOTHER:雄への関心)
L_DICE_NUM = L_DICE
SIF TALENT:L_BABY:即堕ち
	L_DICE_NUM += 5
SIF TALENT:L_BABY:魅惑
	L_DICE_NUM ++
SIF TALENT:L_BABY:中毒しやすい
	L_DICE_NUM ++
SIF TALENT:L_BABY:貞操無頓着
	L_DICE_NUM ++
SIF TALENT:L_BABY:快感に素直
	L_DICE_NUM ++
SIF TALENT:L_BABY:解放
	L_DICE_NUM ++
SIF TALENT:L_BABY:男好き
	L_DICE_NUM ++
SIF TALENT:L_BABY:惚れっぽい
	L_DICE_NUM ++
FOR LOCAL, 0, L_DICE_NUM
	CFLAG:L_BABY:雄への関心 += RAND:3
NEXT



CFLAG:L_BABY:性交願望 = HEREDITY_PHEROMONE_DICE(CFLAG:L_MOTHER:性交願望)
L_DICE_NUM = L_DICE
SIF TALENT:L_BABY:レイプ願望
	L_DICE_NUM += 5
SIF TALENT:L_BABY:セックス狂
	L_DICE_NUM ++
SIF TALENT:L_BABY:中毒しやすい
	L_DICE_NUM ++
SIF TALENT:L_BABY:貞操無頓着
	L_DICE_NUM ++
SIF TALENT:L_BABY:快感に素直
	L_DICE_NUM ++
SIF TALENT:L_BABY:解放
	L_DICE_NUM ++
FOR LOCAL, 0, L_DICE_NUM
	CFLAG:L_BABY:性交願望 += RAND:3
NEXT



CFLAG:L_BABY:妊娠願望 = HEREDITY_PHEROMONE_DICE(CFLAG:L_MOTHER:妊娠願望)
L_DICE_NUM = L_DICE
;繁殖力弱い連中は切実
SIF GROUPMATCH(L_RACE, "天使", "エルフ", "精霊", "オートマタ", "海人")
	L_DICE_NUM += 5
SIF TALENT:L_BABY:聖女の子宮
	L_DICE += 3

SIF TALENT:L_BABY:セックス狂
	L_DICE_NUM ++
SIF TALENT:L_BABY:中毒しやすい
	L_DICE_NUM ++
SIF TALENT:L_BABY:貞操無頓着
	L_DICE_NUM ++
SIF TALENT:L_BABY:ヤンデレ
	L_DICE_NUM ++
SIF TALENT:L_BABY:解放
	L_DICE_NUM ++
FOR LOCAL, 0, L_DICE_NUM
	CFLAG:L_BABY:妊娠願望 += RAND:3
NEXT



CFLAG:L_BABY:無自覚な雌 = LIMIT(CFLAG:L_BABY:無自覚な雌, 0, 99)
CFLAG:L_BABY:フェロモン = LIMIT(CFLAG:L_BABY:フェロモン, 0, 99)
CFLAG:L_BABY:雌の本能   = LIMIT(CFLAG:L_BABY:雌の本能,   0, 99)
CFLAG:L_BABY:性への興味 = LIMIT(CFLAG:L_BABY:性への興味, 0, 99)
CFLAG:L_BABY:雄への関心 = LIMIT(CFLAG:L_BABY:雄への関心, 0, 99)
CFLAG:L_BABY:性交願望   = LIMIT(CFLAG:L_BABY:性交願望,   0, 99)
CFLAG:L_BABY:妊娠願望   = LIMIT(CFLAG:L_BABY:妊娠願望,   0, 99)

CFLAG:L_BABY:母体レベル = LIMIT(HEREDITY_PHEROMONE_DICE(CFLAG:L_MOTHER:母体レベル), 0, 99)
CFLAG:L_BABY:卵子レベル = LIMIT(HEREDITY_PHEROMONE_DICE(CFLAG:L_MOTHER:卵子レベル), 0, 99)
CFLAG:L_BABY:機関評価   = CFLAG:L_BABY:母体レベル + CFLAG:L_BABY:卵子レベル



;始祖を共有するキャラを全て名家にする
;そして直近の母親と始祖だけ青き血族を付与（BLOOD_LINEをたどるのはめんどくさいので勘弁して）
@LABORATORY_BREEDING_NOBLE_FAMILY(L_MOTHER, L_BABY)
#DIM L_MOTHER
#DIM L_BABY
#DIM L_FIRST
#DIM L_C_ID
TALENT:L_BABY:青き血族 = 1
TALENT:L_MOTHER:青き血族 = 1
TALENT:L_MOTHER:名家 = 1
SIF CFLAG:L_BABY:住人_始祖 <= 0
	RETURN
;始祖に付与
L_FIRST = FINDCHARA(CFLAG:0:個別番号, CFLAG:L_BABY:住人_始祖)
IF L_FIRST >= 1
	TALENT:L_FIRST:青き血族 = 1
	TALENT:L_FIRST:名家 = 1
ENDIF
FOR L_C_ID, 1, CHARANUM
	SIF CFLAG:L_C_ID:住人_始祖 == CFLAG:L_BABY:住人_始祖
		TALENT:L_C_ID:名家 = 1
NEXT



;始祖の面影をコピーする
@LABORATORY_BREEDING_REVERSION(L_MOTHER, L_BABY)
#DIM L_BABY
#DIM L_MOTHER
#DIM L_FIRST
IF CFLAG:L_BABY:住人_始祖 <= 0
	L_FIRST = 0
ELSE
	L_FIRST = FINDCHARA(CFLAG:0:個別番号, CFLAG:L_BABY:住人_始祖)
ENDIF
;見つからないなら無理やり母親を真似る
SIF L_FIRST <= 0
	L_FIRST = L_MOTHER
FOR LOCAL, 0, 53
	TALENT:L_BABY:LOCAL = TALENT:L_FIRST:LOCAL
NEXT
FOR LOCAL, 58, 85
	TALENT:L_BABY:LOCAL = TALENT:L_FIRST:LOCAL
NEXT
FOR LOCAL, 100, 197
	TALENT:L_BABY:LOCAL = TALENT:L_FIRST:LOCAL
NEXT
FOR LOCAL,1400, 1499
	TALENT:L_BABY:LOCAL = TALENT:L_FIRST:LOCAL
NEXT
;たぶんNPC番号は必要ないはず
FOR LOCAL,300, 490
	CFLAG:L_BABY:LOCAL = CFLAG:L_FIRST:LOCAL
NEXT
CFLAG:L_BABY:住人_種族      = CFLAG:L_FIRST:住人_種族
CFLAG:L_BABY:住人_種族特徴1 = CFLAG:L_FIRST:住人_種族特徴1
CFLAG:L_BABY:住人_種族特徴2 = CFLAG:L_FIRST:住人_種族特徴2
CFLAG:L_BABY:住人_種族特徴3 = CFLAG:L_FIRST:住人_種族特徴3
CFLAG:L_BABY:住人_体格      = CFLAG:L_FIRST:住人_体格





;-------------------------------------------------
;身体強化
;-------------------------------------------------
@LABORATORY_BOOST_ST_SELECT
#DIM L_CHARA
CLEARLINE LINECOUNT
DRAWLINE
CALL PRINT_LABEL("身体強化")
PRINTL
PRINTFORML 最先端の技術を用いて、住民を強化します。
PRINTFORML 強化値は一定。
DRAWLINE
PRINTFORML 強化する住民を選択して下さい。
CALL LIST_CHARA
L_CHARA = RESULT
SIF L_CHARA == 1000
	RETURN
CALL LABORATORY_BOOST_ST(L_CHARA)
SIF RESULT < 0
	RESTART



@LABORATORY_BOOST_ST(L_CHARA)
#DIM L_CHARA
#DIM L_COST
#DIM L_BONUS
#DIM L_INPUT
L_COST = 1000 * (CFLAG:L_CHARA:住人_身体強化回数 + 1)
IF L_CHARA == MASTER
	PRINTFORMW あなたは選択できません
	RETURN -1
ELSEIF !INRANGE(L_CHARA, 1, CHARANUM - 1)
	PRINTFORMW 存在しないキャラです
	RETURN -1
ELSEIF TALENT:L_CHARA:NPC
	PRINTFORMW NPCは選択できません
	RETURN -1
ELSEIF FLAG:都市_功績 < L_COST
	PRINTFORMW 功績ポイントが不足しています。
	RETURN
ELSEIF MONEY < 200000
	PRINTFORMW 資金が不足しています。
	RETURN
ENDIF

DO
	CLEARLINE LINECOUNT
	CALL INFO_STATUS_HEADER(L_CHARA)
	CALL C_PICTURE_S(L_CHARA)
	DRAWLINE
	PRINTFORML 強化するのに資金200000と功績ポイント{L_COST}必要です。
	PRINTFORML 強化しますか？。
	CALL PRINT_LABEL("資金",,,TOSTR(MONEY),,,,,,,, 1)
	CALL PRINT_LABEL("功績ポイント",,,TOSTR(FLAG:都市_功績),,,,,,,, 1)
	PRINTL
	PRINTFORML
	PRINTFORML [0] 強化する
	PRINTFORML
	PRINTFORML [999] 戻る
	INPUT
	L_INPUT = RESULT
	SELECTCASE L_INPUT
		CASE 0
			BREAK
		CASE 999
			RETURN
		CASEELSE
			CONTINUE
	ENDSELECT
LOOP 1

MONEY -= 200000
FLAG:都市_功績 -= L_COST

IF GROUPMATCH(CFLAG:L_CHARA:住人_種族, 6, 8, 10)
	L_BONUS = 1
ELSE
	L_BONUS = 0
ENDIF

CFLAG:L_CHARA:住人_最大体力 += 5 + L_BONUS
CFLAG:L_CHARA:住人_最大気力 += 5 + L_BONUS
CFLAG:L_CHARA:住人_最大BRE += 10 + L_BONUS
CFLAG:L_CHARA:住人_ATK += 2 + L_BONUS
CFLAG:L_CHARA:住人_DEF += 2 + L_BONUS
CFLAG:L_CHARA:住人_HIT += 2 + L_BONUS
CFLAG:L_CHARA:住人_SPD += 2 + L_BONUS

CFLAG:L_CHARA:住人_現在体力 = CFLAG:L_CHARA:住人_最大体力
CFLAG:L_CHARA:住人_現在気力 = CFLAG:L_CHARA:住人_最大気力
CFLAG:L_CHARA:住人_現在BRE  = CFLAG:L_CHARA:住人_最大BRE

CFLAG:L_CHARA:住人_技能ポイント += 5 + L_BONUS

CLEARLINE LINECOUNT
CALL INFO_STATUS_HEADER(L_CHARA)
CALL C_PICTURE_S(L_CHARA)
DRAWLINE
PRINTFORML HP :{CFLAG:L_CHARA:住人_最大体力 - (5 + L_BONUS)}→{CFLAG:L_CHARA:住人_最大体力}
PRINTFORML SP :{CFLAG:L_CHARA:住人_最大気力 - (5 + L_BONUS)}→{CFLAG:L_CHARA:住人_最大気力}
PRINTFORML ATK:{CFLAG:L_CHARA:住人_ATK - (2 + L_BONUS)}→{CFLAG:L_CHARA:住人_ATK}
PRINTFORML DEF:{CFLAG:L_CHARA:住人_DEF - (2 + L_BONUS)}→{CFLAG:L_CHARA:住人_DEF}
PRINTFORML HIT:{CFLAG:L_CHARA:住人_HIT - (2 + L_BONUS)}→{CFLAG:L_CHARA:住人_HIT}
PRINTFORML SPD:{CFLAG:L_CHARA:住人_SPD - (2 + L_BONUS)}→{CFLAG:L_CHARA:住人_SPD}
SIF CFLAG:L_CHARA:住人_最大BRE && FLAG:STATUS伏字モード
	SETCOLOR COLOR("伏字")
PRINTFORML BRE:{CFLAG:L_CHARA:住人_最大BRE - (10 + L_BONUS)}→{CFLAG:L_CHARA:住人_最大BRE}
RESETCOLOR
SIF CFLAG:L_CHARA:住人_技能ポイント && FLAG:STATUS伏字モード
	SETCOLOR COLOR("伏字")
PRINTFORML 技能ポイント:{CFLAG:L_CHARA:住人_技能ポイント-(5 + L_BONUS)}→{CFLAG:L_CHARA:住人_技能ポイント}
RESETCOLOR
PRINTFORML 
PRINTFORML 
PRINTFORML 
PRINTFORMW 強化完了しました
