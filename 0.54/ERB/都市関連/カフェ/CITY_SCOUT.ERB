
;-------------------------------------------------
; 喫茶店スカウト
;-------------------------------------------------
;戻り値：加入した人数
@CITY_SCOUT
#DIM L_I
#DIM L_INPUT
#DIM DYNAMIC ADD_CNT
#DIM DYNAMIC NOT_CLEAR
Flag:喫茶店スカウト = 1
IF NOT_CLEAR
	NOT_CLEAR = 0
ELSE
	CLEARLINE LINECOUNT
ENDIF
CALL CITY_SCOUT_MAKE_CHARA("SOLO")
REDRAW 0
PRINTFORM [1] 決定　　　
PRINTFORM [2] 別の住人　
PRINTFORM [3] 絞り込み　
PRINTFORML
SIF CHARANUM < 200 && FLAG:遺研_研究中 == 0
		PRINTFORML [4] 10人加入　
PRINTFORML 
PRINTFORML [0] やめる
DO
	INPUT
	L_INPUT = RESULT
	SELECTCASE L_INPUT
	CASE 1
		PRINTFORML
		PRINTFORMW %CALLNAME:(CHARANUM - 1)%が新しい対象としてピックアップされました。
		ADD_CNT ++
		IF CHARANUM >= 200
			PRINTFORMW キャラ数が上限に達しています。
			RETURN ADD_CNT
		ENDIF
	CASE 2
		CALL CANCEL_SCOUT(CHARANUM - 1)
	CASE 3
		CALL CANCEL_SCOUT(CHARANUM - 1)
		CALL CAFE_SCOUT_OPTION
	CASE 4
		SIF CHARANUM >= 200 || FLAG:遺研_研究中 != 0
			GOTO RETRY
		FOR LOCAL, 0, 9
			IF CHARANUM >= 200
				PRINTFORMW キャラ数が上限に達しています。
				RETURN ADD_CNT
			ENDIF
			CALL CITY_SCOUT_MAKE_CHARA
			PRINTFORML
			PRINTFORMW %CALLNAME:(CHARANUM - 1)%が新しい対象としてピックアップされました。
			ADD_CNT ++
		NEXT
		NOT_CLEAR = 1
	CASE 0
		Flag:喫茶店スカウト = 0
		CALL CANCEL_SCOUT(CHARANUM - 1)
		RETURN ADD_CNT
	CASEELSE
		$RETRY
		CALL C_INPUT_ERROR("無効な入力です")
		CONTINUE
	ENDSELECT
	BREAK
LOOP 1
RESTART



;-------------------------------------------------
;生成処理
;-------------------------------------------------
@CITY_SCOUT_MAKE_CHARA(SCENE)
#DIMS SCENE
#DIM  L_I
CALL CHARACTER_SCOUT1
L_I = CHARANUM -1
CALL CAFE_SCOUT_OPTION2(L_I)
IF SCENE == "SOLO"
	DRAWLINE
	SIF FLAG:遺研_研究中 == 0
		CALL CITY_CAFE_NPC_TALK("SCOUT")
	DRAWLINE
	PRINTFORML 現在の住人の数{CHARANUM - 2, 3}名
	DRAWLINE
	PRINTFORML 名前:%CALLNAME:L_I%
ENDIF
DRAWLINE
CALL C_PICTURE_S(L_I)
PRINTFORML ■------------------------------------ 　素質　 ------------------------------------■
CALL INFO_STATUS_TALENT(L_I)
CALL CHARACTER_SCOUT_PROFILE(L_I)
;CALL INFO_PERSONAL(ARG)
DRAWLINE



;-------------------------------------------------
; 喫茶店スカウト　絞り込み
;-------------------------------------------------
@CAFE_SCOUT_OPTION
#DIM L_INPUT
SIF FLAG:カフェ絞り込み_身分 == 0
	FLAG:カフェ絞り込み_身分 =  94
SIF FLAG:カフェ絞り込み_胸 == 0
	FLAG:カフェ絞り込み_胸 = 60

SIF Flag:カフェ絞り込み_性格 >= 30
	Flag:カフェ絞り込み_性格 = 0

SIF GETBIT(FLAG:カフェ絞り込み_処女, 0)
	CLEARBIT FLAG:カフェ絞り込み_処女, 1
SIF GETBIT(FLAG:カフェ絞り込み_処女, 10)
	CLEARBIT FLAG:カフェ絞り込み_処女, 11

CLEARLINE LINECOUNT
DRAWLINE
PRINTFORM [1] 身分:
SELECTCASE FLAG:カフェ絞り込み_身分
CASE 94
	PRINTFORML 自由
CASE 95
	PRINTFORML 学生
CASE 96
	PRINTFORML アイドル
CASE 97
	PRINTFORML 社会人
CASE 98
	PRINTFORML 娼婦
CASE 99
	PRINTFORML 治安維持局員
ENDSELECT

PRINTFORM [2] 胸の大きさ:
SELECTCASE FLAG:カフェ絞り込み_胸
CASE 60
	PRINTFORML 自由
CASE 61
	PRINTFORML 絶壁
CASE 62
	PRINTFORML 貧乳
CASE 63
	PRINTFORML 巨乳
CASE 64
	PRINTFORML 爆乳
CASE 65
	PRINTFORML 普乳
ENDSELECT


PRINTFORM [3] Ｖ経験:
IF GETBIT(FLAG:カフェ絞り込み_処女, 0)
	PRINTFORML 処女指定
ELSEIF GETBIT(FLAG:カフェ絞り込み_処女, 1)
	PRINTFORML 非処女指定
ELSE
	PRINTFORML 指定なし
ENDIF

PRINTFORM [4] Ａ経験:
IF GETBIT(FLAG:カフェ絞り込み_処女, 10)
	PRINTFORML Ａ処女指定
ELSEIF GETBIT(FLAG:カフェ絞り込み_処女, 11)
	PRINTFORML Ａ非処女指定
ELSE
	PRINTFORML 指定なし
ENDIF

PRINTFORM [5] 他の人との交際状態:
SELECTCASE FLAG:カフェ絞り込み_交際
CASE 0
	PRINTFORML 自由
CASE 299
	PRINTFORML していない
CASE 300
	PRINTFORML 交際中
CASE 301
	PRINTFORML セフレ持ち
CASE 302
	PRINTFORML 結婚済み
ENDSELECT

PRINTFORM [6] 尻の大きさ:
SELECTCASE FLAG:カフェ絞り込み_尻
CASE 0
	PRINTL 自由
CASE 80
	PRINTL 巨尻
CASE 81
	PRINTL 爆尻
CASE 82
	PRINTL 普尻
ENDSELECT

PRINTFORM [7] 性格:
SELECTCASE FLAG:カフェ絞り込み_性格
CASE 0
	PRINTL 自由
CASE 1
	PRINTL 礼儀正しい
CASE 2
	PRINTL クール
CASE 3
	PRINTL ツンツン
CASE 4
	PRINTL マイペース
CASE 5
	PRINTL 高圧的
CASE 6
	PRINTL 荒々しい
CASE 7
	PRINTL 弱気
CASE 8
	PRINTL 気品高い
CASE 9
	PRINTL 男気
CASE 10
	PRINTL 色っぽい
CASE 11
	PRINTL フレンドリー
CASE 12
	PRINTL 古風
CASE 13
	PRINTL 子供っぽい
CASE 14
	PRINTL おしゃま
ENDSELECT

PRINTFORM [8] 耳髪一致:
SELECTCASE FLAG:カフェ絞り込み_耳髪一致
CASE 0
	PRINTL 自由
CASE 1
	PRINTL 一致させる
ENDSELECT
PRINTFORM [11] 体格:
SELECTCASE FLAG:カフェ絞り込み_体格
CASE 0
	PRINTL 自由
CASE 1
	PRINTL 大人体格
CASE 2
	PRINTL 子供体格
ENDSELECT
IF FLAG:都市_Lv >= 30
	PRINTFORM [10] 合意:
	SELECTCASE FLAG:カフェ絞り込み_合意
	CASE 0
		PRINTL 非合意
	CASE 1
		PRINTL 合意
	ENDSELECT
ELSE
	SETCOLOR 100,100,100
	PRINTFORML [ ] 合意:　都市Lv30必要
	RESETCOLOR
ENDIF
IF FLAG:都市_Lv >= 50
	PRINTFORM [9] 血族:
	SELECTCASE FLAG:カフェ絞り込み_血族
	CASE 0
		PRINTL 自由
	CASE 1
		PRINTL 青き血族
	ENDSELECT
ELSE
	SETCOLOR 100,100,100
	PRINTFORML [ ] 血族:　都市Lv50必要
	RESETCOLOR
ENDIF
IF FLAG:都市_Lv >= 80
	PRINTFORM [12] 種族:
	SELECTCASE FLAG:カフェ絞り込み_種族
	CASE 0
		PRINTL 自由
	CASE 1
		PRINTL 人間
	CASE 2
		PRINTL エルフ
	CASE 3
		PRINTL 夜魔
	CASE 4
		PRINTL 亜人
	CASE 5
		PRINTL オートマタ
	CASE 6
		PRINTL 天使
	CASE 7
		PRINTL 竜人
	ENDSELECT
ELSE
	SETCOLOR 100,100,100
	PRINTFORML [ ] 合意:　都市Lv80必要
	RESETCOLOR
ENDIF
PRINTFORML
PRINTFORML [999] 戻る
DO
	INPUT
	L_INPUT = RESULT
	SELECTCASE L_INPUT
	CASE 1
		SIF Flag:カフェ絞り込み_身分 <= 100
			Flag:カフェ絞り込み_身分 += 1
		SIF Flag:カフェ絞り込み_身分 >= 100
			Flag:カフェ絞り込み_身分 = 94
	CASE 2
		SIF Flag:カフェ絞り込み_胸 <= 66
			Flag:カフェ絞り込み_胸 += 1
		SIF Flag:カフェ絞り込み_胸 >= 66
			Flag:カフェ絞り込み_胸 = 60
	CASE 3
		IF GETBIT(FLAG:カフェ絞り込み_処女, 0)
			CLEARBIT FLAG:カフェ絞り込み_処女, 0
			SETBIT FLAG:カフェ絞り込み_処女, 1
		ELSEIF GETBIT(FLAG:カフェ絞り込み_処女, 1)
			CLEARBIT FLAG:カフェ絞り込み_処女, 1
		ELSE
			SETBIT FLAG:カフェ絞り込み_処女, 0
		ENDIF
	CASE 4
		IF GETBIT(FLAG:カフェ絞り込み_処女, 10)
			CLEARBIT FLAG:カフェ絞り込み_処女, 10
			SETBIT FLAG:カフェ絞り込み_処女, 11
		ELSEIF GETBIT(FLAG:カフェ絞り込み_処女, 11)
			CLEARBIT FLAG:カフェ絞り込み_処女, 11
		ELSE
			SETBIT FLAG:カフェ絞り込み_処女, 10
		ENDIF
	CASE 5
		SELECTCASE Flag:カフェ絞り込み_交際
		CASE 0
			FLAG:カフェ絞り込み_交際 = 299
		CASE 299 TO 301
			FLAG:カフェ絞り込み_交際 ++
		CASE 302
			Flag:カフェ絞り込み_交際 = 0
		ENDSELECT
	CASE 6
		Flag:カフェ絞り込み_尻++
		SIF Flag:カフェ絞り込み_尻 == 1
			Flag:カフェ絞り込み_尻 = 80
		SIF Flag:カフェ絞り込み_尻 == 83
			Flag:カフェ絞り込み_尻 = 0
	CASE 7
	;性格
		Flag:カフェ絞り込み_性格 += 1
		SIF Flag:カフェ絞り込み_性格 >= 15
			Flag:カフェ絞り込み_性格 = 0
	CASE 8
		IF (Flag:カフェ絞り込み_耳髪一致 == 0)
			Flag:カフェ絞り込み_耳髪一致 = 1
		ELSEIF (Flag:カフェ絞り込み_耳髪一致 == 1)
			Flag:カフェ絞り込み_耳髪一致 = 0
		ENDIF
	CASE 9
		IF (Flag:カフェ絞り込み_血族 == 0)
			Flag:カフェ絞り込み_血族 = 1
		ELSEIF (Flag:カフェ絞り込み_血族 == 1)
			Flag:カフェ絞り込み_血族 = 0
		ENDIF
	CASE 10
		IF (Flag:カフェ絞り込み_合意 == 0)
			Flag:カフェ絞り込み_合意 = 1
		ELSEIF (Flag:カフェ絞り込み_合意 == 1)
			Flag:カフェ絞り込み_合意 = 0
		ENDIF
	CASE 11
	;体格
		Flag:カフェ絞り込み_体格 += 1
		SIF Flag:カフェ絞り込み_体格 >= 3
			Flag:カフェ絞り込み_体格 = 0
	CASE 12
	;種族
		Flag:カフェ絞り込み_種族 += 1
		SIF Flag:カフェ絞り込み_種族 >= 8
			Flag:カフェ絞り込み_種族 = 0
	CASE 999
		RETURN
	CASEELSE
		CALL C_INPUT_ERROR("無効な入力です")
		CONTINUE
	ENDSELECT
	BREAK
LOOP 1
RESTART



;-------------------------------------------------
; 喫茶店スカウト　絞り込み適用
;-------------------------------------------------
@CAFE_SCOUT_OPTION2(L_I,ARG:0,ARG:1,ARG:2)
#DIM L_I
#DIM L_M
#DIM L_SEIKAKU
#DIMS L_NAME
#DIMS L_NAME2
#DIM V_VIRGIN
#DIM A_VIRGIN
ARG:0 = 0
ARG:1 = 0
ARG:2 = 0
L_NAME2 '= ""
L_NAME  '= ""
L_M = 0
L_SEIKAKU =-1

IF Flag:カフェ絞り込み_身分 >= 95
	Talent:L_I:(Flag:カフェ絞り込み_身分) = 1
ELSE
	SELECTCASE RAND:5
	CASE 0
		Talent:L_I:学生 = 1
	CASE 1
		Talent:L_I:アイドル = 1
	CASE 2
		Talent:L_I:社会人 = 1
	CASE 3
		Talent:L_I:娼婦 = 1
	CASE 4
		Talent:L_I:治安維持局員 = 1
	ENDSELECT
ENDIF
SIF Talent:L_I:学生
	CALL CHARACTER_SCOUT_STUDENT(L_I)

IF Flag:カフェ絞り込み_胸 >= 61
	Talent:L_I:絶壁 = 0
	Talent:L_I:貧乳 = 0
	Talent:L_I:巨乳 = 0
	Talent:L_I:爆乳 = 0
	SIF Flag:カフェ絞り込み_胸 != 65
		Talent:L_I:(Flag:カフェ絞り込み_胸) = 1
	CFLAG:L_I:バストサイズ = CHARACTER_BUST_RETURN(L_I)
ENDIF
IF Flag:カフェ絞り込み_尻
	Talent:L_I:巨尻 = 0
	Talent:L_I:爆尻 = 0
	SIF Flag:カフェ絞り込み_尻 != 82
		Talent:L_I:(Flag:カフェ絞り込み_尻) = 1
	CFLAG:L_I:ヒップサイズ = CHARACTER_HIP_RETURN(L_I)
ENDIF


;Ｖ経験
IF GETBIT(FLAG:カフェ絞り込み_処女, 0)
	V_VIRGIN = 1
ELSEIF GETBIT(FLAG:カフェ絞り込み_処女, 1)
	V_VIRGIN = 0
ELSEIF TALENT:L_I:娼婦 && !RAND:3
	V_VIRGIN = 0
ELSEIF RAND:3
	V_VIRGIN = 1
ELSE
	V_VIRGIN = 0
ENDIF

IF !V_VIRGIN
	TALENT:L_I:非処女 = 1
	TCVAR:L_I:履歴_EXP_処女喪失 = 1
	EXP:L_I:Ｖ経験 = (RAND:20) + 1
	EXP:L_I:性交経験 = RAND:(EXP:L_I:Ｖ経験) * 5 + RAND:(EXP:L_I:Ｖ経験) + 1
ENDIF

;Ａ経験
IF GETBIT(FLAG:カフェ絞り込み_処女, 10)
	A_VIRGIN = 1
ELSEIF GETBIT(FLAG:カフェ絞り込み_処女, 11)
	A_VIRGIN = 0
ELSEIF TALENT:L_I:娼婦
	IF V_VIRGIN && RAND:3
		A_VIRGIN = 0
	ELSE
		A_VIRGIN = 1
	ENDIF
ELSEIF RAND:10
	A_VIRGIN = 1
ELSE
	A_VIRGIN = 0
ENDIF


IF !A_VIRGIN
	TALENT:L_I:Ａ非処女 = 1
	TCVAR:L_I:履歴_EXP_Ａ処女喪失 = 1
	EXP:L_I:Ａ経験 = (RAND:20) + 1
	EXP:L_I:Ａ性交経験 = RAND:(EXP:L_I:Ａ経験) * 2 + RAND:(EXP:L_I:Ａ経験) + 1
ENDIF

IF TALENT:L_I:娼婦
	EXP:L_I:売春経験 += RAND:20 + 1
	SIF CFLAG:L_I:住人_モラル < 0
		EXP:L_I:売春経験 += RAND:(ABS(CFLAG:L_I:住人_モラル))
	EXP:L_I:精液経験 += RAND:(EXP:L_I:売春経験) / 2 + RAND:3
	
	IF !V_VIRGIN
		EXP:L_I:性交経験 += RAND:(EXP:L_I:売春経験) + RAND:3
	ELSEIF !A_VIRGIN
		EXP:L_I:Ａ性交経験 += RAND:(EXP:L_I:売春経験) + RAND:3
	ELSE
		EXP:L_I:フェラ経験 += RAND:(EXP:L_I:売春経験) + RAND:3
		EXP:L_I:精飲経験 += RAND:(MIN(EXP:L_I:フェラ経験, EXP:L_I:精液経験) + 1) / 2
	ENDIF
ENDIF

IF !FLAG:カフェ絞り込み_交際 && !RAND:3
	SELECTCASE RAND:4
	CASE 0
		TALENT:L_I:交際中 = 1
	CASE 1
		TALENT:L_I:セフレ持ち = 1
	CASE 2
		TALENT:L_I:結婚済み = 1
	ENDSELECT
	;結婚経験者以外の出産経験を抑制
	IF !TALENT:L_I:結婚済み && RAND:2
	ELSEIF RAND:10 > 5 && EXP:L_I:性交経験 >= 1
		EXP:L_I:出産経験 = (RAND:2) + 1
	ENDIF
ELSEIF FLAG:カフェ絞り込み_交際 >= 300
	TALENT:L_I:(Flag:カフェ絞り込み_交際) = 1
	IF !TALENT:L_I:結婚済み && RAND:2
	ELSEIF RAND:10 > 5 && EXP:L_I:性交経験 >= 1
		EXP:L_I:出産経験 = (RAND:2) + 1
	ENDIF
ENDIF




IF TALENT:L_I:結婚済み && !RAND:20
	TALENT:L_I:未亡人 = 1
	TALENT:L_I:結婚済み = 0
ELSEIF TALENT:L_I:結婚済み && TALENT:L_I:Ａ非処女 == 1 && !RAND:7
	TALENT:L_I:浮気 = (RAND:3)+1
	EXP:L_I:浮気経験 += (RAND:200)+50
	EXP:L_I:性交経験 += (RAND:100)+20
	EXP:L_I:Ａ性交経験 += (RAND:100)+20
	EXP:L_I:絶頂経験 += (RAND:100)+20
	EXP:L_I:キス経験 += (RAND:300)+50
	EXP:L_I:精飲経験 +=(RAND:100)+20
	EXP:L_I:精液経験 += (RAND:100)+20
	EXP:L_I:フェラ経験 += (RAND:200)+50
	EXP:L_I:絶頂経験 += (RAND:100)+20
	SIF Talent:L_I:巨乳 == 1 || Talent:L_I:爆乳 == 1
		EXP:L_I:パイズリ経験 += (RAND:200)+70
ENDIF


;経験つけるのが嫌な人は避けてな
IF FLAG:カフェ絞り込み_交際 == 299
ELSEIF TALENT:L_I:交際中
	EXP:L_I:性交経験 += RAND:3
	EXP:L_I:Ａ性交経験 += RAND:3
	EXP:L_I:絶頂経験 += RAND:3
	EXP:L_I:キス経験 += RAND:3
	EXP:L_I:精飲経験 += RAND:3
	EXP:L_I:精液経験 += RAND:3
	EXP:L_I:フェラ経験 += RAND:3
	EXP:L_I:絶頂経験 += RAND:3
ELSEIF TALENT:L_I:セフレ持ち
	EXP:L_I:性交経験 += RAND:4
	EXP:L_I:Ａ性交経験 += RAND:4
	EXP:L_I:絶頂経験 += RAND:4
	EXP:L_I:キス経験 += RAND:4
	EXP:L_I:精飲経験 += RAND:4
	EXP:L_I:精液経験 += RAND:4
	EXP:L_I:フェラ経験 += RAND:4
	EXP:L_I:絶頂経験 += RAND:4
ELSEIF TALENT:L_I:結婚済み
	EXP:L_I:性交経験 += RAND:5
	EXP:L_I:Ａ性交経験 += RAND:5
	EXP:L_I:絶頂経験 += RAND:5
	EXP:L_I:キス経験 += RAND:5
	EXP:L_I:精飲経験 += RAND:5
	EXP:L_I:精液経験 += RAND:5
	EXP:L_I:フェラ経験 += RAND:5
	EXP:L_I:絶頂経験 += RAND:5
ELSEIF RAND:5 == 0
	EXP:L_I:性交経験 += RAND:2
	EXP:L_I:Ａ性交経験 += RAND:2
	EXP:L_I:絶頂経験 += RAND:2
	EXP:L_I:キス経験 += RAND:2
	EXP:L_I:精飲経験 += RAND:2
	EXP:L_I:精液経験 += RAND:2
	EXP:L_I:フェラ経験 += RAND:2
	EXP:L_I:絶頂経験 += RAND:2
ENDIF



;つじつまあわせ
IF EXP:L_I:精飲経験
	SIF !EXP:L_I:精液経験
		EXP:L_I:精液経験 ++
	SIF !EXP:L_I:フェラ経験
		EXP:L_I:フェラ経験 ++
ENDIF
SIF V_VIRGIN
	EXP:L_I:性交経験 = 0
SIF A_VIRGIN
	EXP:L_I:Ａ性交経験 = 0


;かわいそうなんで先にキスをしたことにしちゃう
SIF EXP:L_I:フェラ経験 && !TALENT:L_I:娼婦 && !EXP:L_I:キス経験
	EXP:L_I:キス経験 ++



IF TALENT:L_I:同性愛 == 1 && TALENT:L_I:結婚済み == 1
	TALENT:L_I:結婚済み = 0
	TALENT:L_I:百合結婚 = 1
	EXP:L_I:性交経験 = 0
	EXP:L_I:Ａ性交経験 = 0
	EXP:L_I:出産経験 = 0
	EXP:L_I:絶頂経験 += RAND:3
	EXP:L_I:レズ経験 += RAND:3
	EXP:L_I:キス経験 += RAND:3
	EXP:L_I:精飲経験 = 0
	EXP:L_I:精液経験 = 0
	EXP:L_I:フェラ経験 = 0
	EXP:L_I:絶頂経験 += RAND:3
	ARG:0 = L_I
	ARG:1 = 0
	ARG:2 = 3
	CALL RANDOM_N_LADY(ARG:0,ARG:1,ARG:2)
	CFLAG:L_I:3000 = 1
	CFLAG:L_I:3010 = 200
	CFLAG:L_I:3020 = 5
	CFLAG:L_I:3030 = 2
	CFLAG:L_I:3040 = 10
	CFLAG:L_I:3050 = 40
	CFLAG:L_I:3060 = 5
	
ENDIF


{
IF  EXP:L_I:性交経験 >= 1 || 
	EXP:L_I:Ａ性交経験 >= 1 || 
	EXP:L_I:絶頂経験 >= 1 || 
	EXP:L_I:キス経験 >= 1 || 
	EXP:L_I:精飲経験 >= 1 || 
	EXP:L_I:精液経験 >= 1 || 
	EXP:L_I:フェラ経験 >= 1
}
	STRDATA L_NAME
		DATA 「プレイヤー」
		DATA 先輩
		DATA 後輩
		DATA 教師
		DATA 元カレ
		DATA 父親
		DATA 弟
		DATA 兄
	ENDDATA
	IF RAND:10
		IF TALENT:L_I:交際中
			L_NAME '= "恋人"
		ELSEIF TALENT:L_I:セフレ持ち
			L_NAME '= "セフレ"
		ELSEIF TALENT:L_I:結婚済み
			L_NAME '= "夫"
		ENDIF
	ENDIF
	SIF TALENT:L_I:娼婦 && !RAND:4
		L_NAME '= "客の男"
	SIF TALENT:L_I:百合結婚 
		L_NAME '= "夫(女性)"

ENDIF


IF EXP:L_I:出産経験 >= 1
	STRDATA L_NAME2
		DATA 「プレイヤー」
		DATA 先輩
		DATA 後輩
		DATA 教師
		DATA 元カレ
		DATA 父親
		DATA 弟
		DATA 兄
	ENDDATA
	IF RAND:20 > 7
		IF TALENT:L_I:交際中
			L_NAME '= "恋人"
		ELSEIF TALENT:L_I:セフレ持ち
			L_NAME '= "セフレ"
		ELSEIF TALENT:L_I:結婚済み
			L_NAME '= "夫"
		ENDIF
	ENDIF
ENDIF
IF TALENT:L_I:浮気 == 3
	L_NAME = "夫の上司"
ELSEIF TALENT:L_I:浮気 == 2
	L_NAME = "幼馴染"
ELSEIF TALENT:L_I:浮気 == 1
	L_NAME = "元カレ"
ENDIF

SIF CSTR:L_I:初えっち相手名 == "" && Talent:L_I:非処女 == 1
	CSTR:L_I:初えっち相手名 = %L_NAME%
SIF CSTR:L_I:初キス相手名 == "" && EXP:L_I:キス経験 >= 1
	CSTR:L_I:初キス相手名 = %L_NAME%
SIF CSTR:L_I:初精飲相手名 =="" && EXP:L_I:精飲経験 >= 1
	CSTR:L_I:初精飲相手名 = %L_NAME%
SIF CSTR:L_I:初フェラ相手名 =="" && EXP:L_I:フェラ経験 >= 1
	CSTR:L_I:初フェラ相手名 = %L_NAME%
SIF CSTR:L_I:初Ａえっち相手名 =="" && Talent:L_I:Ａ非処女 == 1
	CSTR:L_I:初Ａえっち相手名 = %L_NAME%
SIF CSTR:L_I:初絶頂相手名 =="" && EXP:L_I:絶頂経験 >= 1
	CSTR:L_I:初絶頂相手名 = %L_NAME%
SIF CSTR:L_I:初中出し相手名 =="" && Talent:L_I:非処女 == 1 
	CSTR:L_I:初中出し相手名 = %L_NAME%
SIF CSTR:L_I:初出産相手名 =="" && EXP:L_I:出産経験 >= 1
	CSTR:L_I:初出産相手名 = %L_NAME2%

L_M = 0

SIF !INRANGE(Flag:カフェ絞り込み_性格 - 1, 0, 13)
	Flag:カフェ絞り込み_性格 = 0

IF Flag:カフェ絞り込み_性格 != 0
	VARSET TALENT:L_I:0, 0, 1400, 1450
	TALENT:L_I:(1400+(Flag:カフェ絞り込み_性格-1)) = 1
	L_SEIKAKU = Flag:カフェ絞り込み_性格-1
	CALL CHARACTER_SCOUT_MIND_REPEAT(L_I, L_SEIKAKU)
ENDIF

;小さい体型の靴3は定義されてません...
SIF (TALENT:L_I:小人体型 || TALENT:L_I:小柄体型) && CFlag:L_I:住人_体格 == 0
	CFlag:L_I:住人_体格 = 1

CALL CHARACTER_SCOUT_COSTUME(L_I)

IF FLAG:カフェ絞り込み_耳髪一致 == 1 && CFLAG:L_I:住人_種族 == 3;亜人
	IF CFLAG:L_I:住人_種族特徴1 == 0 || CFLAG:L_I:住人_種族特徴1 == 3
		CFLAG:L_I:髪色 = 0
	ELSEIF CFLAG:L_I:住人_種族特徴1 == 1
		CFLAG:L_I:髪色 = 2
	ELSEIF CFLAG:L_I:住人_種族特徴1 == 2
		IF CFLAG:L_I:住人_種族特徴2 == 4
			CFLAG:L_I:髪色 = 4
		ELSE
			CFLAG:L_I:髪色 = 3 + RAND:2
		ENDIF
	ENDIF
ENDIF


IF FLAG:カフェ絞り込み_血族 == 1
	TALENT:L_I:青き血族 = 1
	CFLAG:L_I:住人_地位 = 3
ENDIF

IF FLAG:カフェ絞り込み_合意 == 1
	TALENT:L_I:合意 = 1
	TALENT:L_I:非合意 = 0
	CFLAG:L_I:関係段階 = 2
	CFLAG:L_I:好感度 += 1000
ENDIF

IF FLAG:カフェ絞り込み_体格 == 1
	CFlag:L_I:住人_体格 = 0
	CALL CHARACTER_SCOUT_COSTUME(L_I)
ELSEIF FLAG:カフェ絞り込み_体格 == 2
	CFlag:L_I:住人_体格 = 1
	CALL CHARACTER_SCOUT_COSTUME(L_I)
ENDIF


RETURN

