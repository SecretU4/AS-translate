
;-------------------------------------------------
; 喫茶店　会話
;-------------------------------------------------

@CAFE_TALK(L_I, ARG:1)
#DIM L_I
#DIM L_INPUT
#DIM L_color

CLEARLINE LINECOUNT

CALL NAYAMI_PAGE(L_I)
CALL C_PICTURE_T(L_I)
CALL INFO_CITY_BASE
DRAWLINE

REDRAW 0
CALL PRINT_LABEL("――――――――この場でのアクション――――――――")
PRINTL
L_color = GETCOLOR()
SIF Flag:都市_現在TP < 1 && FLAG:STATUS伏字モード
	SETCOLOR COLOR("伏字")
PRINTFORM [1] 話をする(TP-1)　　
PRINTFORM [2] 悩み解決(TP-1)　　
SETCOLOR L_color
IF CFlag:L_I:関係段階 >= 2 && TALENT:L_I:合意 == 0
	SIF Flag:都市_現在TP < 5 && FLAG:STATUS伏字モード
		SETCOLOR COLOR("伏字")
	PRINTFORM [5] 口説く(TP-5)　　
	SETCOLOR L_color
ENDIF
PRINTFORML
IF CFlag:L_I:関係段階 >= 2
	CALL PRINT_LABEL("――――――プライベートでのアクション――――――")
	PRINTL
	PRINTFORM [10] 傾向なし　
	SIF Flag:都市_現在TP < 3 && FLAG:STATUS伏字モード
		SETCOLOR COLOR("伏字")
	PRINTFORM [11] 忠誠傾向(TP-3)　
	PRINTFORM [12] 愛情傾向(TP-3)　
	PRINTFORM [13] 淫乱傾向(TP-3)　
	SETCOLOR L_color
	PRINTFORML 
	CALL PRINT_LABEL("――――――――――関係構築――――――――――")
	PRINTL
	SIF Flag:都市_現在TP < 5 && FLAG:STATUS伏字モード
		SETCOLOR COLOR("伏字")
	PRINTFORM [21] 従属√(TP-5)　
	PRINTFORM [22] 恋愛√(TP-5)　
	PRINTFORM [23] 淫乱√(TP-5)　
	SETCOLOR L_color
ENDIF
PRINTFORML
PRINTFORML 
SIF !FLAG:都市_現在BP && FLAG:STATUS伏字モード
	SETCOLOR COLOR("伏字")
PRINTFORML [50] BP摂取
SETCOLOR L_color
PRINTFORML 
PRINTFORML [999] やめる
DO
	$INPUT_LOOP
	INPUT
	L_INPUT = RESULT
	SELECTCASE L_INPUT
	CASE 1
		IF Flag:都市_現在TP <= 0
			$LESS_TP
			CALL C_INPUT_ERROR("TPが不足しています")
			CONTINUE
		ELSE
			Flag:都市_現在TP -= 1
			CALL CAFE_TALK2(L_I)
		ENDIF
	CASE 2
		SIF Flag:都市_現在TP <= 0
			GOTO LESS_TP
		IF CFlag:L_I:住人_悩み解決判定回数 != 0 || CFlag:L_I:住人_悩み解決中 != 0
			CALL C_INPUT_ERROR("現在悩みを解決中です、結果が出るまで再選択は出来ません。")
			CONTINUE
		ELSE
			CALL NAYAMI_CHALLENGE(L_I)
		ENDIF
	CASE 5
		SIF Flag:都市_現在TP < 5
			GOTO LESS_TP
		Flag:都市_現在TP -= 5
		CALL CAFE_TALK_KDK(L_I)
	CASE 10
		CFlag:L_I:値傾向 = 0
	CASE 11
		SIF Flag:都市_現在TP < 3
			GOTO LESS_TP
		Flag:都市_現在TP -= 3
		CFlag:L_I:値傾向 = 1
	CASE 12
		SIF Flag:都市_現在TP < 3
			GOTO LESS_TP
		Flag:都市_現在TP -= 3
		CFlag:L_I:値傾向 = 2
	CASE 13
		SIF Flag:都市_現在TP < 3
			GOTO LESS_TP
		Flag:都市_現在TP -= 3
		CFlag:L_I:値傾向 = 3
	CASE 21
		SIF Flag:都市_現在TP < 5
			GOTO LESS_TP
		Flag:都市_現在TP -= 5
		CALL CAFE_TALK_R_J(L_I)
	CASE 22
		SIF Flag:都市_現在TP < 5
			GOTO LESS_TP
		Flag:都市_現在TP -= 5
		CALL CAFE_TALK_R_R(L_I)
	CASE 23
		SIF Flag:都市_現在TP < 5
			GOTO LESS_TP
		Flag:都市_現在TP -= 5
		CALL CAFE_TALK_R_I(L_I)
	CASE 50
		CALL SYSTEM_BP
	CASE 999
		RETURN
	CASEELSE
		$RETRY
		CALL C_INPUT_ERROR("無効な入力です")
		CONTINUE
	ENDSELECT
	BREAK
LOOP 1
RESTART












;-------------------------------------------------
; 会話する
;-------------------------------------------------
@CAFE_TALK2(L_I)
#DIM L_I     ;対象登録番号
#DIM L_K_U   ;好感度上昇値
#DIM L_K_U2  ;好感度上昇値
#DIM L_N_K   ;悩み解明値
#DIM L_N_K2  ;悩み解明値
#DIM I_LOOP

L_K_U2 = (CFLAG:L_I:好感度 / 100) + 1
SIF L_K_U2 >= 100
	L_K_U2 = 100

L_K_U = (RAND:1) + 1
L_K_U +=(L_K_U2 / 2)
L_K_U += RAND:((L_K_U2 / 2) + 2)
IF TALENT:L_I:即堕ち == 1
	L_K_U = L_K_U * 5
ELSEIF TALENT:L_I:惚れっぽい == 1
	L_K_U = L_K_U * 2
ELSEIF TALENT:L_I:警戒心 == 1
	L_K_U -= L_K_U / 5
ELSEIF TALENT:L_I:人間不信 == 1
	L_K_U -= L_K_U / 2
ENDIF

SIF TALENT:L_I:高嶺の花 == 1
	L_K_U -= L_K_U /3






CLEARLINE LINECOUNT
DRAWLINE
PRINTFORML %CALLNAME:MASTER%は%CALLNAME:L_I%と軽く話をした…


PRINTFORM 「――
IF CFlag:L_I:関係段階 >= 2 && RAND:100 > 50
	PRINTDATA
		DATAFORM そう、以前の話はなんとかな%CSTR:L_I:語尾ました%
		DATAFORM %CSTR:L_I:語尾微笑%、%CSTR:L_I:二人称%は頼もしい%CSTR:L_I:語尾です2%
		DATAFORM %CSTR:L_I:一人称%も出来れば力に
		DATAFORM わか%CSTR:L_I:語尾ました%、いつも通りに
		DATAFORM そう%CSTR:L_I:語尾肯定%、これでや%CSTR:L_I:語尾促進%
	ENDDATA
ELSEIF CFlag:L_I:関係段階 >= 1 && RAND:100 > 50
	PRINTDATA
		DATAFORM %CSTR:L_I:語尾微笑%、そう%CSTR:L_I:語尾肯定%
		DATAFORM あれはそうすれば良い%CSTR:L_I:語尾行為%
		DATAFORM そう、あの時は%CSTR:L_I:語尾丁寧%世話にな%CSTR:L_I:語尾ました%
		DATAFORM そう%CSTR:L_I:語尾逆説%、あのままで
		DATAFORM %CSTR:L_I:語尾反応%、そういえばこの前の
	ENDDATA
ELSE
	PRINTDATA
		DATAFORM どう%CSTR:L_I:語尾疑問%？
		DATAFORM 実はこんなことが
		DATAFORM そう%CSTR:L_I:語尾同意求む%
		DATAFORM %CSTR:L_I:語尾逆説%、これは
		DATAFORM それはいい%CSTR:L_I:語尾褒める%っ
	ENDDATA
ENDIF
IF RAND:100 > 50
	PRINTDATA
		DATAFORM ―――それと
		DATAFORM ―――%CSTR:L_I:語尾逆説%
		DATAFORM ―――そうなる%CSTR:L_I:語尾ので%
		DATAFORM ―――これも
		DATAFORM ―――それに%CSTR:L_I:一人称%も
	ENDDATA
ENDIF
IF RAND:100 > 50
	PRINTDATA
		DATAFORM ――%CSTR:L_I:語尾YES%、お願い%CSTR:L_I:語尾お願い%
		DATAFORM ――%CSTR:L_I:語尾No%、大丈夫%CSTR:L_I:語尾です%
		DATAFORM ――%CSTR:L_I:語尾謝罪%、助か%CSTR:L_I:語尾ました%
		DATAFORM ――大丈夫そう%CSTR:L_I:語尾肯定%
		DATAFORM ――%CSTR:L_I:語尾相槌%、後は――
	ENDDATA
ENDIF

PRINTFORML 」

DRAWLINE
PRINTFORML %CALLNAME:L_I%との仲が少し深まった気がした…
PRINTFORML 好感度 {CFLAG:L_I:好感度}　→　{(CFLAG:L_I:好感度)+L_K_U}
PRINTFORML 
PRINTFORML 
PRINTFORMW 



L_N_K2 = (CFLAG:L_I:好感度 / 200) + 1
SIF L_N_K2 >= 20
	L_N_K2 = 20

L_N_K = (RAND:5) + 1
L_N_K += L_N_K2

I_LOOP = 1
FOR I_LOOP, 1, 5
	SIF CFLAG:L_I:(4000 + I_LOOP) <= 0
		CONTINUE
	CFLAG:L_I:(4100 + I_LOOP) += (RAND:L_N_K) + 1
	SIF CFLAG:L_I:(4100 + I_LOOP) >= 100
		CFLAG:L_I:(4100 + I_LOOP)  = 100
NEXT
FOR I_LOOP, 11, 15
	SIF CFLAG:L_I:(4000 + I_LOOP) <= 0
		CONTINUE
	CFLAG:L_I:(4100 + I_LOOP) += (RAND:L_N_K) + 2
	SIF CFLAG:L_I:(4100 + I_LOOP) >= 100
		CFLAG:L_I:(4100 + I_LOOP)  = 100
NEXT
CFLAG:L_I:好感度 += L_K_U


;-------------------------------------------------
; 悩み解決
;-------------------------------------------------
@NAYAMI_CHALLENGE(L_I)
#DIM L_I
#DIM I_LOOP
#DIM L_INPUT
CLEARLINE LINECOUNT
CALL INFO_CITY_BASE
PRINTFORML 
CALL INFO_STATUS_NAYAMI_LIST(L_I, "「ボタン」")
PRINTFORML
PRINTFORML [999] やめる
DO
	INPUT
	L_INPUT = RESULT
	SELECTCASE L_INPUT
	CASE 999
		RETURN
	CASE L_INPUT
		IF !INRANGE(L_INPUT, 1, 9) && !INRANGE(L_INPUT, 11, 19)
		;そんな悩みはない
			CALL C_INPUT_ERROR("無効な入力です")
			CONTINUE
		ENDIF
		IF CFLAG:L_I:(4000 + L_INPUT) <= 0
		;これまたそんな悩みはない
			CALL C_INPUT_ERROR("その悩みは存在しません")
			CONTINUE
		ENDIF
		IF CFLAG:L_I:(4100 + L_INPUT) < 100 
		;内容がわからない
			CALL C_INPUT_ERROR("まだ悩みが判明していません")
			CONTINUE
		ENDIF
		IF L_INPUT <= 10 && CFLAG:L_I:関係段階 == 0
			CALL C_INPUT_ERROR("解決できるほどの信頼を獲得していません")
			CONTINUE
		ENDIF
		CFLAG:L_I:住人_悩み解決選択 = L_INPUT
		CALL NAYAMI_CHALLENGE2(L_I, L_INPUT)
		SIF RESULT
			RETURN RESULT
		CFLAG:L_I:住人_悩み解決選択 = 0
		RESTART
	CASEELSE
		$RETRY
		CALL C_INPUT_ERROR("無効な入力です")
		CONTINUE
	ENDSELECT
LOOP 1



;-------------------------------------------------
; 悩み解決
;-------------------------------------------------
;戻り値：いずれかの解決法の提示に成功したら1
@NAYAMI_CHALLENGE2(C_ID, N_ID)
#DIM  C_ID           ;登録番号
#DIM  N_ID           ;悩みのID
#DIMS PLAN_NAME,  11 ;解決方法の名前
#DIM  SUC_PLUS,   11 ;解決方法による固定補正
#DIM  HAVE_PLAN      ;初回限定で更新する用フラグ
#DIM  COST_MONEY, 11 ;このキャラでかかる各プランの必要資金
#DIM  COST_AP,    11 ;このキャラでかかる各プランの必要AP
#DIM  L_S            ;成功率
#DIM  L_K            ;好感度補正値
#DIM  L_INPUT        ;入力の一時保持用

IF !HAVE_PLAN
	CALLF DIM_TABLE_NAYAMI_PLAN(PLAN_NAME, SUC_PLUS)
	HAVE_PLAN = 1
ENDIF
PRINTFORML 
PRINTFORML 解決方法を選択して下さい
PRINTFORML ※一度選択すると後日結果が出るまで再選択できません
;PRINTFORML {L_P}{ARG:0}
CALL NAYAMI_PLAN_BUTTON_LIST(C_ID, N_ID, PLAN_NAME, SUC_PLUS, COST_AP, COST_MONEY)
PRINTFORML 
PRINTFORML
PRINTFORML [0] - やめる
DO
	INPUT
	L_INPUT = RESULT
	SELECTCASE L_INPUT
	CASE 0
		RETURN
	CASE L_INPUT
		IF !INRANGE(L_INPUT, 1, 10)
			GOTO RETRY
		ENDIF
		IF MONEY < COST_MONEY:L_INPUT
			CALL C_INPUT_ERROR("資金が不足しています")
			CONTINUE
		ENDIF
		IF FLAG:都市_現在AP < COST_AP:L_INPUT
			CALL C_INPUT_ERROR("APが不足しています")
			CONTINUE
		ENDIF
		
		MONEY -= COST_MONEY:L_INPUT
		FLAG:都市_現在AP -= COST_AP:L_INPUT
		
		;成功確率の設定
		L_S = 30 + SUC_PLUS:L_INPUT
		;最案だった場合補正＋
		IF L_INPUT == CFLAG:C_ID:(4400 + N_ID)
			L_S += 50
		;愚案だった場合補正―
		ELSEIF L_INPUT == CFLAG:C_ID:(4500 + N_ID)
			L_S -= 50
		ENDIF
		
		ABL:MASTER:解決技能 += 1
		L_S += L_S * (1 + (ABL:MASTER:解決技能 / 100))
		L_K = (CFLAG:C_ID:好感度) / 50
		SIF L_K  > 200
			L_K  = 200
		
		CFLAG:C_ID:住人_悩み解決成功確立 = (L_K + L_S) - (CFLAG:C_ID:(4300 + N_ID) * 2)
		SIF CFLAG:C_ID:住人_悩み解決成功確立 >= 100
			CFLAG:C_ID:住人_悩み解決成功確立 = 100
		CFLAG:C_ID:住人_悩み解決成功確立 -= CFLAG:C_ID:(4300 + N_ID)

		CFLAG:C_ID:住人_悩み解決判定回数 = (RAND:1) + 1
		CFLAG:C_ID:住人_悩み解決合計加算 = 0

		;店に行くのは金が要る
		IF L_INPUT == 5 && (CFLAG:C_ID:住人_所持金 >= (CFLAG:C_ID:(4300 + N_ID) * 5000) * CFLAG:C_ID:住人_難度)
			CFLAG:C_ID:住人_悩み解決成功確立 += 20
		ELSE
			CFLAG:C_ID:住人_悩み解決成功確立 -= 20
		ENDIF
		
		IF CFLAG:C_ID:関係段階 >= 2 && RAND:100 > 50
			PRINTDATAL
				DATAFORM 「もう%CSTR:C_ID:一人称%が悩んでいること、全部知られているみたい%CSTR:C_ID:語尾肯定2%……%CSTR:C_ID:語尾微笑%」
				DATAFORM 「%CSTR:C_ID:語尾YES%、%CSTR:C_ID:二人称%がいてくれれば解決できそう%CSTR:C_ID:語尾です%」
				DATAFORM 「%CSTR:C_ID:語尾感謝%、本当に助かってい%CSTR:C_ID:語尾ます%」
			ENDDATA
		ELSEIF CFLAG:C_ID:関係段階 >= 1 && RAND:100 > 50
			PRINTDATAL
				DATAFORM 「いつも助けてもらって悪い%CSTR:C_ID:語尾応答2%、お願い%CSTR:C_ID:語尾問う2%」
				DATAFORM 「%CSTR:C_ID:語尾YES%、%CSTR:C_ID:二人称%の%CSTR:C_ID:語尾丁寧%力があれば解決できそう%CSTR:C_ID:語尾です%」
				DATAFORM 「丁度、%CSTR:C_ID:二人称%の%CSTR:C_ID:語尾丁寧%力を借りようと思ってた%CSTR:C_ID:語尾ので%…%CSTR:C_ID:語尾感謝%」
			ENDDATA
		ELSE
			PRINTDATAL
				DATAFORM 「%CSTR:C_ID:語尾感謝%、最近この問題で苦労してた%CSTR:C_ID:語尾繋ぎ%%CSTR:C_ID:語尾です%…」
				DATAFORM 「ずっと解決策が浮かばなった%CSTR:C_ID:語尾ので%……%CSTR:C_ID:語尾謝罪%、感謝%CSTR:C_ID:語尾問う2%」
				DATAFORM 「迷惑でないなら%CSTR:C_ID:二人称%の力を%CSTR:C_ID:語尾丁寧%借り%CSTR:C_ID:語尾ます%」
			ENDDATA
		ENDIF
		PRINTFORMW 解決結果は{(CFLAG:C_ID:住人_悩み解決判定回数) + 1}日後です
	CASEELSE
		$RETRY
		CALL C_INPUT_ERROR("無効な入力です")
		CONTINUE
	ENDSELECT
	CFlag:C_ID:住人_悩み前回協力 = RESULT	;前回結果表示用
	BREAK
LOOP 1
FLAG:都市_現在TP -= 1
RETURN 1



;-------------------------------------------------
;悩み解決ボタンのリスト表示関数
;-------------------------------------------------
;ボタンリストを表示するとともに各種コストを計算する
@NAYAMI_PLAN_BUTTON_LIST(C_ID, N_ID, PLAN_NAME, SUC_PLUS, COST_AP, COST_MONEY)
#DIM  C_ID
#DIM  N_ID
#DIMS REF PLAN_NAME
#DIM  REF SUC_PLUS
#DIM  REF COST_AP
#DIM  REF COST_MONEY
CALLF VAR_NAYAMI_PLAN_COST(C_ID, N_ID, COST_AP, COST_MONEY)
FOR LOCAL, 1, 11
	CALL NAYAMI_PLAN_BUTTON(C_ID, LOCAL, PLAN_NAME, SUC_PLUS, COST_AP, COST_MONEY)
NEXT



;-------------------------------------------------
;個々の悩み解決ボタン関数
;-------------------------------------------------
@NAYAMI_PLAN_BUTTON(C_ID, P_ID, PLAN_NAME, SUC_PLUS, COST_AP, COST_MONEY)
#DIM  C_ID
#DIM  P_ID
#DIMS REF PLAN_NAME
#DIM  REF SUC_PLUS
#DIM  REF COST_AP
#DIM  REF COST_MONEY
PRINTFORM [{P_ID, 2}] - %PLAN_NAME:P_ID%
IF SUC_PLUS:P_ID < 0
	PRINTFORM 【補正 -{ABS(SUC_PLUS:P_ID), 2}】
ELSE
	PRINTFORM 【補正 +{ABS(SUC_PLUS:P_ID), 2}】
ENDIF
IF COST_AP:P_ID == 0
	PRINTFORM %" " * 12%
ELSE
	PRINTFORM (必要AP:{COST_AP:P_ID, 3})
ENDIF
SIF COST_MONEY:P_ID
	PRINTFORM (必要資金:{COST_MONEY:P_ID})
PRINTFORML 







;-------------------------------------------------
; 口説く
;-------------------------------------------------
@CAFE_TALK_KDK(L_I)
#DIM L_I
CFlag:L_I:口説き値 += CAFE_TALK_KDK_CALC(L_I)
CFlag:L_I:口説き値 = LIMIT(CFlag:L_I:口説き値, 0, 100)

PRINTFORML 
DRAWLINE
PRINTFORML 会話の最中、%CALLNAME:MASTER%は%CALLNAME:L_I%をそっと抱き寄せ甘い言葉をかけた…
IF CFlag:L_I:口説き値 == 100
	PRINTFORML 
	PRINTDATAL
		DATAFORM 「………いい%CSTR:L_I:語尾許可%…%CSTR:L_I:二人称%になら身体を任せられ%CSTR:L_I:語尾ます%…」
		DATAFORM 「%CSTR:L_I:語尾YES%……%CSTR:L_I:一人称%も%CSTR:L_I:二人称%ともっと深い関係になりたい%CSTR:L_I:語尾です2%…」
		DATAFORM 「%CSTR:L_I:語尾微笑%……そんなに焦らなくても拒絶はしない%CSTR:L_I:語尾許可%……今度近いうちに…%UNICODE(0x2661)%」
		DATAFORM 「……%CSTR:L_I:語尾YES%%UNICODE(0x2661)%………%CSTR:L_I:一人称%も……もっと…」
		DATAFORM 「…今度、空いてる時間に呼んで%CSTR:L_I:語尾希望%…？………するなら長く愛し合いたい%CSTR:L_I:語尾です2%…%UNICODE(0x2661)%」
		DATAFORM 「…す、するなら別の場所が……%CSTR:L_I:二人称%と%CSTR:L_I:一人称%……二人きりで…%UNICODE(0x2661)%」
		DATAFORM 「……%UNICODE(0x2661)%、…%CSTR:L_I:二人称%が%CSTR:L_I:一人称%を本気にさせた%CSTR:L_I:語尾繋ぎ%%CSTR:L_I:語尾です%……責任取って%CSTR:L_I:語尾希望%…♪」
		DATAFORM 「%CSTR:L_I:二人称%にここまで求愛されたら、受け入れるしかない%CSTR:L_I:語尾応答2%…%UNICODE(0x2661)%」
		DATAFORM 「…携帯番号教える%CSTR:L_I:語尾ので%……時間があるとき呼んで%CSTR:L_I:語尾希望%…%UNICODE(0x2661)%」
		DATAFORM 「…もう、仕方ない%CSTR:L_I:語尾応答2%……優しくするって約束できるのなら…%UNICODE(0x2661)%」
	ENDDATA
	PRINTFORML ……アプローチの末、%CALLNAME:L_I%は深い関係になることを受け入れてくれたようだ。
	PRINTFORML %CALLNAME:L_I%から携帯番号を入手し【合意】を得た！
	CFlag:L_I:携帯番号入手 = 1
	Talent:L_I:合意 = 1
	SIF MARK:L_I:陥落刻印 == 0
		MARK:L_I:陥落刻印 = 1
	Talent:L_I:非合意 = 0
ELSE
	PRINTFORML
	PRINTDATAL
		DATAFORM 「ま、まだダメ%CSTR:L_I:語尾です%っ、……もっと親密になったら…」
		DATAFORM 「こんなに密着して……でも悪い気しない%CSTR:L_I:語尾応答2%…」
		DATAFORM 「女はムードに弱い……実際に経験すると反論の余地はない%CSTR:L_I:語尾応答2%…」
		DATAFORM 「……♪、いずれはそういう関係になるのも悪くない%CSTR:L_I:語尾応答2%…」
		DATAFORM 「こうして触れ合っていると…まるで恋人みたい%CSTR:L_I:語尾肯定2%…」
		DATAFORM 「いろいろ助けてもらった%CSTR:L_I:語尾繋ぎ%%CSTR:L_I:語尾逆説%…これ以上の関係になるならもっと深く%CSTR:L_I:二人称%を知りたい%CSTR:L_I:語尾応答2%」
		DATAFORM 「%CSTR:L_I:一人称%を抱きたいのなら%CSTR:L_I:二人称%をもっと教え%CSTR:L_I:語尾命令強%？…体を預けられる人かよく知りたい%CSTR:L_I:語尾繋ぎ%%CSTR:L_I:語尾です%」
		DATAFORM 「…もしかして、%CSTR:L_I:一人称%を…抱きたいの%CSTR:L_I:語尾問う%…？」
		DATAFORM 「…好意を持たれるのは嬉しい%CSTR:L_I:語尾逆説%、…心の準備が…」
		DATAFORM 「っ…！、…い、いきなり言われても…困\@STRFIND("らりるれろ",SUBSTRINGU(CSTR:L_I:語尾ます,0,1)) ?  # り\@%CSTR:L_I:語尾ます%…」
	ENDDATA
	PRINTFORML ……至ることは出来なかったが、%CALLNAME:L_I%は少しずつガードを緩めているようだ…
ENDIF
PRINTFORMW



@CAFE_TALK_KDK_CALC(L_I)
#FUNCTION
#DIM L_I
#DIM KDK_POINT
KDK_POINT = (CFLAG:L_I:好感度 / 200) + RAND(1, 4)
SIF TALENT:L_I:娼婦
	KDK_POINT += 5

KDK_POINT *= 100
SIF TALENT:L_I:即堕ち
	TIMES KDK_POINT, 5
SIF TALENT:L_I:惚れっぽい == 1
	TIMES KDK_POINT, 2
SIF TALENT:L_I:貞操無頓着
	TIMES KDK_POINT, 1.2

SIF TALENT:L_I:貞操観念
	TIMES KDK_POINT, 0.8
SIF TALENT:L_I:警戒心 == 1
	TIMES KDK_POINT, 0.2
SIF TALENT:L_I:人間不信 == 1
	TIMES KDK_POINT, 0.5
SIF TALENT:L_I:高嶺の花 == 1
	TIMES KDK_POINT, 0.5
SIF TALENT:L_I:白百合姫 == 1
	TIMES KDK_POINT, 0.1

RETURNF MAX(KDK_POINT / 100, 1)



;-------------------------------------------------
; 関係構築　従属
;-------------------------------------------------
@CAFE_TALK_R_J(L_I)
#DIM L_I
CLEARLINE LINECOUNT
DRAWLINE
	PRINTFORML %CALLNAME:MASTER%はこちらの指示を受け入れるように仕込んだ…
	PRINTFORML 
	PRINTFORMW 
	PRINTFORML ……
	PRINTFORML ………
	PRINTFORML …………
IF CFlag:L_I:忠誠値 >= 900 && MARK:L_I:隷属刻印 == 3 && TALENT:L_I:心酔  == 0 && TALENT:L_I:情婦  == 0 && TALENT:L_I:愛人  == 0
	CALL PRINT_LABEL("成功")
	PRINTL
	PRINTFORML 従属化が極り、%CALLNAME:L_I%は%CALLNAME:MASTER%の心酔する一人になったようだ…
	PRINTFORML %CALLNAME:L_I%は【心酔】を獲得した。
	TALENT:L_I:心酔  = 1
	TALENT:L_I:浮気 = 0
ELSEIF CFlag:L_I:忠誠値 >= 600 && MARK:L_I:隷属刻印 == 2
	CALL PRINT_LABEL("成功")
	PRINTL
	PRINTFORML 従属化の末、まるで仕えるような仕草をするようになった…、かなりの仕上がりになったようだ…
	PRINTFORML %CALLNAME:L_I%の隷属刻印が1上昇した。
	MARK:L_I:隷属刻印 += 1
ELSEIF CFlag:L_I:忠誠値 >= 300 && MARK:L_I:隷属刻印 == 1
	CALL PRINT_LABEL("成功")
	PRINTL
	PRINTFORML 従属化が進み、ある程度の指示なら難なく受け入れるようになったようだ…
	PRINTFORML %CALLNAME:L_I%の隷属刻印が1上昇した。
	MARK:L_I:隷属刻印 += 1
ELSEIF CFlag:L_I:忠誠値 >= 100 && MARK:L_I:隷属刻印 == 0
	CALL PRINT_LABEL("成功")
	PRINTL
	PRINTFORML ある程度の印象効果があったのか、%CALLNAME:L_I%はこちらを尊敬するような眼差しでこちらを見ている…
	PRINTFORML %CALLNAME:L_I%の隷属刻印が1上昇した。
	MARK:L_I:隷属刻印 += 1
ELSE
	CALL PRINT_LABEL("変化なし")
	PRINTL
	PRINTFORML 時間を費やし語ってみたものの、特に心境の変化はなかったようだ…
ENDIF

DRAWLINE
PRINTFORMW



;-------------------------------------------------
; 関係構築　恋愛
;-------------------------------------------------
@CAFE_TALK_R_R(L_I)
#DIM L_I
CLEARLINE LINECOUNT
DRAWLINE
	PRINTFORML %CALLNAME:MASTER%は過剰にならない程度に愛でることにした…
	PRINTFORML 
	PRINTFORMW 
	PRINTFORML ……
	PRINTFORML ………
	PRINTFORML …………
IF CFlag:L_I:愛情値 >= 900 && MARK:L_I:陥落刻印 == 3 && TALENT:L_I:心酔  == 0 && TALENT:L_I:情婦  == 0 && TALENT:L_I:愛人  == 0
	CALL PRINT_LABEL("成功")
	PRINTL
	PRINTFORML 少しでも%CALLNAME:MASTER%と長くいたいのか、身体で引き留めるのも辞さなくなったようだ…
	PRINTFORML %CALLNAME:L_I%は【愛人】を獲得した。
	TALENT:L_I:愛人  = 1
	TALENT:L_I:浮気 = 0
ELSEIF CFlag:L_I:愛情値 >= 600 && MARK:L_I:陥落刻印 == 2
	CALL PRINT_LABEL("成功")
	PRINTL
	PRINTFORML 一過性だったはずの恋は、確かな愛となって%CALLNAME:L_I%を一人の女に仕上げたようだ…
	PRINTFORML %CALLNAME:L_I%の陥落刻印が1上昇した。
	MARK:L_I:陥落刻印 += 1
ELSEIF CFlag:L_I:愛情値 >= 300 && MARK:L_I:陥落刻印 == 1
	CALL PRINT_LABEL("成功")
	PRINTL
	PRINTFORML %CALLNAME:L_I%は内にあった恋慕を自覚し、こちらに愛情を隠さずさらけ出している…
	PRINTFORML %CALLNAME:L_I%の陥落刻印が1上昇した。
	MARK:L_I:陥落刻印 += 1
ELSEIF CFlag:L_I:愛情値 >= 100 && MARK:L_I:陥落刻印 == 0
	CALL PRINT_LABEL("成功")
	PRINTL
	PRINTFORML 交流の成果か、%CALLNAME:L_I%はこちらに対して淡い恋慕を抱いているようだ…
	PRINTFORML %CALLNAME:L_I%の陥落刻印が1上昇した。
	MARK:L_I:陥落刻印 += 1
ELSE
	CALL PRINT_LABEL("変化なし")
	PRINTL
	PRINTFORML 時間を費やし語ってみたものの、特に心境の変化はなかったようだ…
ENDIF
DRAWLINE
PRINTFORMW



;-------------------------------------------------
; 関係構築　淫乱
;-------------------------------------------------
@CAFE_TALK_R_I(L_I)
#DIM L_I
CLEARLINE LINECOUNT
DRAWLINE
	PRINTFORML %CALLNAME:MASTER%は%CALLNAME:L_I%に眠っている女の性をさらけ出すように仕向けた…
	PRINTFORML 
	PRINTFORMW 
	PRINTFORML ……
	PRINTFORML ………
	PRINTFORML …………
IF CFlag:L_I:淫乱値 >= 900 && MARK:L_I:淫乱刻印 == 3 && TALENT:L_I:心酔  == 0  && TALENT:L_I:愛人  == 0 && TALENT:L_I:情婦  == 0
	CALL PRINT_LABEL("成功")
	PRINTL
	PRINTFORML 
	PRINTFORML %CALLNAME:L_I%は【情婦】を獲得した。
	TALENT:L_I:情婦  = 1
	TALENT:L_I:浮気 = 0
ELSEIF CFlag:L_I:淫乱値 >= 600 && MARK:L_I:淫乱刻印 == 2
	CALL PRINT_LABEL("成功")
	PRINTL
	PRINTFORML 
	PRINTFORML %CALLNAME:L_I%の淫乱刻印が1上昇した。
	MARK:L_I:淫乱刻印 += 1
ELSEIF CFlag:L_I:淫乱値 >= 300 && MARK:L_I:淫乱刻印 == 1
	CALL PRINT_LABEL("成功")
	PRINTL
	PRINTFORML 仕込みの成果か、%CALLNAME:L_I%の仕草が艶めかしくなってきた…
	PRINTFORML %CALLNAME:L_I%の淫乱刻印が1上昇した。
	MARK:L_I:淫乱刻印 += 1
ELSEIF CFlag:L_I:淫乱値 >= 100 && MARK:L_I:淫乱刻印 == 0
	CALL PRINT_LABEL("成功")
	PRINTL
	PRINTFORML 顔を赤らめつつも、秘めたる肉欲を制御することが出来なくなってきたようだ…
	PRINTFORML %CALLNAME:L_I%の淫乱刻印が1上昇した。
	MARK:L_I:淫乱刻印 += 1
ELSE
	CALL PRINT_LABEL("変化なし")
	PRINTL
	PRINTFORML 時間を費やし語ってみたものの、特に心境の変化はなかったようだ…
ENDIF
DRAWLINE
PRINTFORMW



;-------------------------------------------------
; 悩み一覧　簡易版
;-------------------------------------------------

@NAYAMI_PAGE(ARG)
#DIM L_I
#DIM I_LOOP
#DIM L_color

I_LOOP = 1
L_color = GETCOLOR()
L_I = ARG
DRAWLINE
PRINTFORML ◇----------------大きな悩み----------------◇
REPEAT 5
	IF CFlag:L_I:(4000+I_LOOP) != 0
		IF CFlag:L_I:(4100+I_LOOP) >= 100
			IF CFlag:L_I:(4000+I_LOOP) <= 199
				SETCOLOR COLOR("悩み_金銭")
				CALL PRINT_LABEL("金銭問題")
			ELSEIF CFlag:L_I:(4000+I_LOOP) <= 299
				SETCOLOR COLOR("悩み_恋愛")
				CALL PRINT_LABEL("恋愛問題")
			ELSEIF CFlag:L_I:(4000+I_LOOP) <= 399
				SETCOLOR COLOR("悩み_家庭")
				CALL PRINT_LABEL("家庭問題")
			ELSEIF CFlag:L_I:(4000+I_LOOP) <= 499
				SETCOLOR COLOR("悩み_生活")
				CALL PRINT_LABEL("生活問題")
			ELSEIF CFlag:L_I:(4000+I_LOOP) <= 599
				SETCOLOR COLOR("悩み_犯罪")
				CALL PRINT_LABEL("犯罪問題")
			ELSEIF CFlag:L_I:(4000+I_LOOP) <= 699
				SETCOLOR COLOR("悩み_性的")
				CALL PRINT_LABEL("性的問題")
			ENDIF
		ELSE
			SIF FLAG:STATUS伏字モード
				SETCOLOR COLOR("伏字")
			PRINTFORM ？？？(未判明)
			CALL PRINT_LABEL("解析",,,TOSTR(CFlag:L_I:(4100+I_LOOP)),3,,"/","100")
		ENDIF
		PRINTFORM 難度:{CFlag:L_I:(4300+I_LOOP),2}　
		PRINTFORML 達成率:({CFlag:L_I:(4200+I_LOOP),3}/100)
		SETCOLOR L_color
	ENDIF
	I_LOOP += 1
REND
PRINTFORML ◇----------------小さな悩み----------------◇
I_LOOP = 1
REPEAT 5
	IF CFlag:L_I:(4010+I_LOOP) != 0
		IF CFlag:L_I:(4110+I_LOOP) >= 100
			IF CFlag:L_I:(4010+I_LOOP) <= 199
				SETCOLOR COLOR("悩み_金銭")
				CALL PRINT_LABEL("金銭問題")
			ELSEIF CFlag:L_I:(4010+I_LOOP) <= 299
				SETCOLOR COLOR("悩み_恋愛")
				CALL PRINT_LABEL("恋愛問題")
			ELSEIF CFlag:L_I:(4010+I_LOOP) <= 399
				SETCOLOR COLOR("悩み_家庭")
				CALL PRINT_LABEL("家庭問題")
			ELSEIF CFlag:L_I:(4010+I_LOOP) <= 499
				SETCOLOR COLOR("悩み_生活")
				CALL PRINT_LABEL("生活問題")
			ELSEIF CFlag:L_I:(4010+I_LOOP) <= 599
				SETCOLOR COLOR("悩み_犯罪")
				CALL PRINT_LABEL("犯罪問題")
			ELSEIF CFlag:L_I:(4010+I_LOOP) <= 699
				SETCOLOR COLOR("悩み_性的")
				CALL PRINT_LABEL("性的問題")
			ENDIF
		ELSE
			SIF FLAG:STATUS伏字モード
				SETCOLOR COLOR("伏字")
			PRINTFORM ？？？(未判明)【解析:{CFlag:L_I:(4110+I_LOOP),3}/100】
		ENDIF
		PRINTFORM 難度:{CFlag:L_I:(4310+I_LOOP),2}　
		PRINTFORML 達成率:({CFlag:L_I:(4210+I_LOOP),3}/100)
		SETCOLOR L_color
	ENDIF
	I_LOOP += 1
REND
DRAWLINE



;-------------------------------------------------
;キャラ切替機能つき喫茶店会話関数
;-------------------------------------------------
@CAFE_TALK_GROUP(CHARA_LIST, CHARA_VIEW, CHARA_CNT, OP)
#DIM REF CHARA_LIST
#DIM     CHARA_VIEW
#DIM     CHARA_CNT
#DIMS    OP
#DIM     C_ID
#DIM     L_INPUT
#DIM     L_color
C_ID = CHARA_LIST:CHARA_VIEW
CALL INFO_STATUS_NAYAMI_LIST(C_ID)
CALL C_PICTURE_T(C_ID)
CALL INFO_CITY_BASE
L_color = GETCOLOR()
DRAWLINE
IF TALENT:C_ID:合意 || CFLAG:C_ID:関係段階 >= 2
	PRINTFORM 　
	PRINTBUTTON "[7] - 傾向変更　　　", 7
ELSE
	PRINTFORM %"　" * 11%
ENDIF
IF STRCOUNT(OP, "「キャラ固定」")
	PRINTFORM %"　" * 11%
ELSE
	PRINTFORM 　
	PRINTBUTTON "[8] - 前のキャラ　　", 8
ENDIF
IF TALENT:C_ID:合意 || CFLAG:C_ID:関係段階 >= 2
	PRINTFORM 　
	PRINTBUTTON "[9] - 関係構築　　　", 9
ENDIF
PRINTFORML 
PRINTFORM 　
SIF FLAG:都市_現在TP < 1 && FLAG:STATUS伏字モード
	SETCOLOR COLOR("伏字")
PRINTBUTTON "[4] - 話をする(TP:1)", 4
SETCOLOR L_color
IF VAR_NAYAMI_ASSIST_ABLE_LIST(C_ID) && CFLAG:C_ID:住人_悩み解決判定回数 == 0 ;!CFLAG:C_ID:住人_悩み協力状況
	PRINTFORM 　
	SIF FLAG:都市_現在TP < 1 && FLAG:STATUS伏字モード
		SETCOLOR COLOR("伏字")
	PRINTBUTTON "[5] - 悩み解決(TP:1)", 5
	SETCOLOR L_color
ELSE
	PRINTFORM %"　" * 11%
ENDIF
IF TALENT:C_ID:合意 == 0 && (CFLAG:C_ID:関係段階 >= 2 || TALENT:C_ID:娼婦)
	PRINTFORM 　
	SIF FLAG:都市_現在TP < 5 && FLAG:STATUS伏字モード
		SETCOLOR COLOR("伏字")
	PRINTBUTTON "[6] - 口説く　(TP:5)", 6
	SETCOLOR L_color
ENDIF
PRINTFORML 
PRINTFORM 　
PRINTBUTTON "[1] - 能力表示　　　", 1

IF STRCOUNT(OP, "「キャラ固定」")
	PRINTFORM %"　" * 11%
ELSE
	PRINTFORM 　
	PRINTBUTTON "[2] - 次のキャラ　　", 2
ENDIF
PRINTFORM 　
SIF !FLAG:都市_現在BP && FLAG:STATUS伏字モード
	SETCOLOR COLOR("伏字")
PRINTBUTTON "[3] - BP摂取　　　　", 3
SETCOLOR L_color
PRINTFORML 
PRINTFORML 
PRINTBUTTON "[0] - もどる　　　　", 0
DO
	ONEINPUT
	L_INPUT = RESULT
	SELECTCASE L_INPUT
	CASE 7
		SIF !(TALENT:C_ID:合意 || CFLAG:C_ID:関係段階 >= 2)
			GOTO RETRY
		PRINTFORML 接し方をどのように変更しますか？
		PRINTFORM 　
		SIF FLAG:都市_現在TP < 3 && FLAG:STATUS伏字モード
			SETCOLOR COLOR("伏字")
		PRINTBUTTON "[1] - 忠誠傾向(TP:3)", 1
		PRINTFORM 　
		PRINTBUTTON "[2] - 愛情傾向(TP:3)", 2
		PRINTFORM 　
		PRINTBUTTON "[3] - 淫乱傾向(TP:3)", 3
		SETCOLOR L_color
		PRINTFORML 
		PRINTFORML 
		PRINTFORM 　
		PRINTBUTTON "[0] - キャンセル　　", 0
		PRINTFORM %"　" * 11%
		PRINTFORM 　
		PRINTBUTTON "[9] - 傾向なし　　　", 9
		DO
			ONEINPUT
			SIF INRANGE(RESULT, 1, 3) && FLAG:都市_現在TP < 3
				GOTO LESS_TP
			SELECTCASE RESULT
			CASE 1
				FLAG:都市_現在TP -= 3
				CFLAG:C_ID:値傾向 = 1
			CASE 2
				FLAG:都市_現在TP -= 3
				CFLAG:C_ID:値傾向 = 2
			CASE 3
				FLAG:都市_現在TP -= 3
				CFLAG:C_ID:値傾向 = 3
			CASE 9
				CFLAG:C_ID:値傾向 = 0
			CASEELSE
			ENDSELECT
			BREAK
		LOOP 1
	CASE 9
		SIF !(TALENT:C_ID:合意 || CFLAG:C_ID:関係段階 >= 2)
			GOTO RETRY
		PRINTFORML どのような関係を構築しますか？
		PRINTFORM 　
		SIF FLAG:都市_現在TP < 5 && FLAG:STATUS伏字モード
			SETCOLOR COLOR("伏字")
		PRINTBUTTON "[1] - 従属√(TP:5)", 1
		PRINTFORM 　
		PRINTBUTTON "[2] - 恋愛√(TP:5)", 2
		PRINTFORM 　
		PRINTBUTTON "[3] - 淫乱√(TP:5)", 3
		SETCOLOR L_color
		PRINTFORML 
		PRINTFORML 
		PRINTFORM 　
		PRINTBUTTON "[0] - キャンセル　", 0
		DO
			ONEINPUT
			SIF INRANGE(RESULT, 1, 3) && FLAG:都市_現在TP < 5
				GOTO LESS_TP
			SELECTCASE RESULT
			CASE 1
				FLAG:都市_現在TP -= 5
				CALL CAFE_TALK_R_J(C_ID)
			CASE 2
				FLAG:都市_現在TP -= 5
				CALL CAFE_TALK_R_R(C_ID)
			CASE 3
				FLAG:都市_現在TP -= 5
				CALL CAFE_TALK_R_I(C_ID)
			CASEELSE
			ENDSELECT
			BREAK
		LOOP 1
	CASE 8
		SIF STRCOUNT(OP, "「キャラ固定」")
			GOTO RETRY
		CHARA_VIEW = (CHARA_VIEW + CHARA_CNT - 1) % CHARA_CNT
	CASE 2
		SIF STRCOUNT(OP, "「キャラ固定」")
			GOTO RETRY
		CHARA_VIEW = (CHARA_VIEW + CHARA_CNT + 1) % CHARA_CNT
	CASE 4
		IF FLAG:都市_現在TP < 1
			$LESS_TP
			CALL C_INPUT_ERROR("TPが不足しています")
			RESTART
		ENDIF
		FLAG:都市_現在TP --
		CALL CAFE_TALK2(C_ID)
	CASE 5
		SIF FLAG:都市_現在TP < 1
			GOTO LESS_TP
		;SIF CFLAG:C_ID:住人_悩み協力状況
			;GOTO RETRY
		SIF !VAR_NAYAMI_ASSIST_ABLE_LIST(C_ID)
			GOTO RETRY
		;FLAG:都市_現在TP --
		CALL CAFE_TALK_ASSIST_NAYAMI(C_ID)
		CALL NAYAMI_CHALLENGE(C_ID)
	CASE 6
		SIF FLAG:都市_現在TP < 5
			GOTO LESS_TP
		SIF TALENT:C_ID:合意 != 0
			GOTO RETRY
		SIF !(CFLAG:C_ID:関係段階 >= 2 || TALENT:C_ID:娼婦)
			GOTO RETRY
		FLAG:都市_現在TP -= 5
		CALL CAFE_TALK_KDK(C_ID)
	CASE 1
		CALL INFO_STATUS_MAIN(C_ID)
	CASE 3
		CALL SYSTEM_BP
	CASE 0
		RETURN
	CASEELSE
		$RETRY
		CALL C_INPUT_ERROR("無効な入力です")
		CONTINUE
	ENDSELECT
	BREAK
LOOP 1
RESTART



;-------------------------------------------------
;おなやみ協力関数
;-------------------------------------------------
;協力してあげるよ！っていうだけでここでは何もおこらない
@CAFE_TALK_ASSIST_NAYAMI(C_ID)
#DIM C_ID
CALL INFO_STATUS_NAYAMI_LIST(C_ID)
;PRINTFORML 
;PRINTFORML %CALLNAME:MASTER%は%CALLNAME:C_ID%の悩み解決への協力を申し出た
;PRINTFORMW 時間経過時に%CALLNAME:C_ID%が悩み相談にやってきます
CALL VAR_NAYAMI_SET_ASSIST(C_ID)



