;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Module		:COMFxxx.ERB
;	Facility	:TRAINコマンド(65:愛撫させる)。
;
;	Licence		:ライセンスフリー。
;
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#

;===============================================================================
;  ◇記載関数一覧(記載順)
;
;		Module Name						Explanation
;
;	・ COM_FUNC_GET_NAME_Xxxx			取得:コマンド名称
;	・ COM_FUNC_ABLE_Xxxx				CHK:コマンド実行条件
;	・ COM_FUNC_EXE_Xxxx				コマンド実行
;	・ COM_FUNC_SRC_Xxxx				コマンドソース
;	・ COM_FUNC_TRAIN_MSG_Xxxx			コマンドメッセージ
;	・ COM_FUNC_GET_TYPE_Xxxx			取得:コマンド種別
;
;===============================================================================

;=================================================
;   sub COM_FUNC_GET_NAME_Xxxx
;=================================================
;   取得:コマンド名称
;-------------------------------------------------
; Output:
;  RESULTS				コマンド名称
;-------------------------------------------------
@COM_FUNC_GET_NAME_X65
RESULTS = 愛撫させる

;=================================================
;   sub COM_FUNC_ABLE_Xxxx
;=================================================
;   CHK:コマンド実行条件
;-------------------------------------------------
; Output:
;  RESULT				真偽値
;-------------------------------------------------
@COM_FUNC_ABLE_X65

;- PLAYERが女以外だと不可
SIF !VAR_SEX(PLAYER)
	RETURN 0

;- TR:奉仕可能
SIF !TR_CHK_奉仕可能()
	RETURN 0

RETURN 1

;=================================================
;   sub COM_FUNC_EXE_Xxxx
;=================================================
;   コマンド実行
;-------------------------------------------------
@COM_FUNC_EXE_X65
#DIM L_P
#DIM L_T
#DIM L_TMP
;VARSET LOCAL

L_P = PLAYER
L_T = TARGET

;-----------------------------
;COM別FLAGの設定
;-----------------------------

TEQUIP:L_T:ポイント奉仕 = [[TRAIN:愛撫させる]]

;-----------------------------
;ソース追加処理
;-----------------------------

;-----------------------------
;その他の処理
;-----------------------------


;=================================================
;   sub COM_FUNC_SRC_Xxxx
;=================================================
;   コマンドソース
;-------------------------------------------------
@COM_FUNC_SRC_X65
#DIM L_P
#DIM L_T
#DIM L_TMP
#DIM L_I
VARSET LOCAL

L_P = PLAYER
L_T = TARGET

;-----------------------------
;COM別FLAGの設定
;-----------------------------

;-----------------------------
;ソース追加処理
;-----------------------------

LOCAL:0 = 70
LOCAL:1 = 40
LOCAL:2 = 120

;-- 補正追加

;- 範囲:100%〜200%
LOCAL:10 = ABL:L_T:技巧
SELECTCASE LOCAL:10
	CASE 0 to 8
		L_TMP = 100 + (10 * LOCAL:10)
	CASEELSE
		L_TMP = 200
ENDSELECT
LOCAL:0 = LOCAL:0 * L_TMP / 100
LOCAL:1 = LOCAL:1 * L_TMP / 100
LOCAL:2 = LOCAL:2 * L_TMP / 100

;- ソース
SOURCE:L_P:快Ｃ += LOCAL:0
SOURCE:L_P:快Ｂ += LOCAL:0
SOURCE:L_T:性行為 += LOCAL:1
SOURCE:L_T:性奉仕 += LOCAL:2


;-----------------------------
;経験の処理
;-----------------------------

;- TR:異性愛経験
CALL TR_EXP_ADD_HETEROSEX(L_T,L_P)

;- 各種経験
GTR_EXPUP:L_T:DB_EXPUP_POS("手淫経験") += 1
GTR_EXPUP:L_T:DB_EXPUP_POS("誘惑経験") += 1

;-----------------------------
;その他の処理
;-----------------------------



;=================================================
;   sub COM_FUNC_TRAIN_MSG_Xxxx
;=================================================
;   コマンドメッセージ
;-------------------------------------------------
@COM_FUNC_TRAIN_MSG_X65
SIF TEQUIP:ポイント性交
	PRINTFORM %CALLNAME%とつながったまま

PRINTFORM %CALLNAME%に%CALLNAME:PLAYER%の

;小柄体型
IF TALENT:PLAYER:小柄体型
	PRINTFORM 幼い体
	SIF VAR_バストサイズ(PLAYER) > 0
		PRINTFORM と不釣り合いな%DB_TALENT_NAME(PLAYER, "バストサイズ")%
ELSE
	PRINTFORM 体
ENDIF

PRINTL を丹念に愛撫させた……
IF MARK:陥落刻印
	PRINTFORM %CALLNAME%は%CALLNAME:PLAYER%にぴったりと寄り添いながら、
	PRINTFORM %CALLNAME:PLAYER%の唇を求めてきた…
	PRINTL
ENDIF

WAIT


;=================================================
;   sub COM_FUNC_GET_TYPE_Xxxx
;=================================================
;   取得:コマンド種別
;-------------------------------------------------
; Output:
;  RESULT				コマンド種別
;-------------------------------------------------
@COM_FUNC_GET_TYPE_X65
RETURN [[COM種別:奉仕系]]

