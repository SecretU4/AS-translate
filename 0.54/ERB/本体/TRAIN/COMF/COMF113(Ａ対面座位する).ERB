;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Module		:COMFxxx.ERB
;	Facility	:TRAINコマンド(113:Ａ対面座位する)。
;
;	Licence		:ライセンスフリー。
;
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#

;===============================================================================
;  ◇記載関数一覧(記載順)
;
;		Module Name						Explanation
;
;	・ COM_FUNC_GET_NAME_Xxxx			取得:コマンド名称
;	・ COM_FUNC_ABLE_Xxxx				CHK:コマンド実行条件
;	・ COM_FUNC_EXE_Xxxx				コマンド実行
;	・ COM_FUNC_SRC_Xxxx				コマンドソース
;	・ COM_FUNC_TRAIN_MSG_Xxxx			コマンドメッセージ
;	・ COM_FUNC_GET_TYPE_Xxxx			取得:コマンド種別
;
;===============================================================================

;=================================================
;   sub COM_FUNC_GET_NAME_Xxxx
;=================================================
;   取得:コマンド名称
;-------------------------------------------------
; Output:
;  RESULTS				コマンド名称
;-------------------------------------------------
@COM_FUNC_GET_NAME_X113
IF TFLAG:サブコマンド実行中フラグ
	RESULTS = 逆対面座位Ａ
ELSE
	RESULTS = 対面座位Ａする
ENDIF

;=================================================
;   sub COM_FUNC_ABLE_Xxxx
;=================================================
;   CHK:コマンド実行条件
;-------------------------------------------------
; Output:
;  RESULT				真偽値
;-------------------------------------------------
@COM_FUNC_ABLE_X113

;- TR:性交可能(Ａ) (※逆)
SIF !TR_CHK_ABLE_SEX_A(PLAYER, TARGET)
	RETURN 0

;- TR:性交可能(逆位置別)
SIF !TR_CHK_ABLE_REV_SEX_POINT("対面座位")
	RETURN 0

RETURN 1

;=================================================
;   sub COM_FUNC_EXE_Xxxx
;=================================================
;   コマンド実行
;-------------------------------------------------
@COM_FUNC_EXE_X113
#DIM L_P
#DIM L_T

L_P = PLAYER
L_T = TARGET

;-----------------------------
;COM別FLAGの設定
;-----------------------------

;- POSE:リセット
CALL POSE_RESET("Ａ")

TEQUIP:L_T:ポイント性交 = [[TRAIN:Ａ対面座位する]]


;-----------------------------
;ソース追加処理
;-----------------------------

;-----------------------------
;その他の処理
;-----------------------------


;=================================================
;   sub COM_FUNC_SRC_Xxxx
;=================================================
;   コマンドソース
;-------------------------------------------------
@COM_FUNC_SRC_X113
#DIM L_P
#DIM L_T
#DIM L_TMP
#DIM L_I
VARSET LOCAL

L_P = PLAYER
L_T = TARGET

;-----------------------------
;COM別FLAGの設定
;-----------------------------

;- POSE:SET (※逆)
CALL POSE_SET(L_P, [[占有部位:Ａ]], 1, L_T)

;-----------------------------
;ソース追加処理
;-----------------------------

LOCAL:0 = 150
LOCAL:1 = 150
LOCAL:2 = 60

;-- 補正追加

;- 範囲:100%〜300%
LOCAL:10 = ABL:L_T:技巧 + ABL:L_P:技巧
SELECTCASE LOCAL:10
	CASE 0 to 17
		L_TMP = 100 + (10 * LOCAL:10)
	CASEELSE
		L_TMP = 300
ENDSELECT
LOCAL:0 = LOCAL:0 * L_TMP / 100
LOCAL:1 = LOCAL:1 * L_TMP / 100
LOCAL:2 = LOCAL:2 * L_TMP / 100

;- ソース
SOURCE:L_P:快Ａ += LOCAL:0
SOURCE:L_T:快Ｃ += LOCAL:1
SOURCE:L_T:性行為 += LOCAL:2


;-----------------------------
;経験の処理
;-----------------------------

;- 各種経験
GTR_EXPUP:L_P:DB_EXPUP_POS("誘惑経験") += 1

;-----------------------------
;その他の処理
;-----------------------------

;- TR:PALAM変動(Ａ性交時) (※逆)
CALL TR_COM_PALAMCHG_SEX_A(TARGET, PLAYER, 1)



;=================================================
;   sub COM_FUNC_TRAIN_MSG_Xxxx
;=================================================
;   コマンドメッセージ
;-------------------------------------------------
@COM_FUNC_TRAIN_MSG_X113

;前回の体位が「対面座位する」
IF TEQUIP:ポイント性交前回 == [[TRAIN:Ａ対面座位する]]
	PRINTFORML %CALLNAME%と見つめ合いながらキスをし、舌を絡めながら再び腰を揺すり始めた…

;前回の体位が逆Ａ性交系の場合
ELSEIF TR_PREV_SEX_REV_V()
	PRINTFORML 腸内からペニスを抜き去ることなく%CALLNAME%の身体をぎゅっと抱き寄せ、
	PRINTFORML %CALLNAME%と見つめ合いながら再び腰を揺すり始めた…

;その他
ELSE
	PRINTFORML %CALLNAME%の身体を抱き寄せ、ゆっくりと降ろしながら
	IF TCVAR:PLAYER:履歴_EXP_処女喪失
		PRINTFORML 男を知らない\@ TALENT:PLAYER:小柄体型 ? 幼いアヌス # アヌス \@をペニスで貫いた…
	ELSE
		;潤滑
		SIF TCVAR:PLAYER:キャラ潤滑
			PRINT 濡れそぼった
		;小柄体型
		IF TALENT:PLAYER:小柄体型
			PRINT 幼いアヌス
		ELSE
			PRINT アヌス
		ENDIF
		PRINTFORML に挿入し、抱き締めながら腰を揺り動かした…
	ENDIF
ENDIF

WAIT


;=================================================
;   sub COM_FUNC_GET_TYPE_Xxxx
;=================================================
;   取得:コマンド種別
;-------------------------------------------------
; Output:
;  RESULT				コマンド種別
;-------------------------------------------------
@COM_FUNC_GET_TYPE_X113
RETURN [[COM種別:逆Ａ性交系]]

