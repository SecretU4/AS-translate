;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Module		:COMFxxx.ERB
;	Facility	:TRAINコマンド(72:バイブ)。
;
;	Licence		:ライセンスフリー。
;
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#

;===============================================================================
;  ◇記載関数一覧(記載順)
;
;		Module Name						Explanation
;
;	・ COM_FUNC_GET_NAME_Xxxx			取得:コマンド名称
;	・ COM_FUNC_ABLE_Xxxx				CHK:コマンド実行条件
;	・ COM_FUNC_INPUT_Xxxx				コマンド実行確認
;	・ COM_FUNC_EXE_Xxxx				コマンド実行
;	・ COM_FUNC_SRC_Xxxx				コマンドソース
;	・ COM_FUNC_TRAIN_MSG_Xxxx			コマンドメッセージ
;	・ COM_FUNC_GET_TYPE_Xxxx			取得:コマンド種別
;
;===============================================================================

;=================================================
;   sub COM_FUNC_GET_NAME_Xxxx
;=================================================
;   取得:コマンド名称
;-------------------------------------------------
; Output:
;  RESULTS				コマンド名称
;-------------------------------------------------
@COM_FUNC_GET_NAME_X72
RESULTS = バイブ

;=================================================
;   sub COM_FUNC_ABLE_Xxxx
;=================================================
;   CHK:コマンド実行条件
;-------------------------------------------------
; Output:
;  RESULT				真偽値
;-------------------------------------------------
@COM_FUNC_ABLE_X72

;特定アイテムを所有
SIF !ITEM:バイブ
	RETURN 0

;女性器
SIF !VAR_SEX()
	RETURN 0

RETURN 1

;=================================================
;   sub COM_FUNC_INPUT_Xxxx
;=================================================
;   コマンド実行確認
;-------------------------------------------------
; Output:
;  RETURN				真偽値
;-------------------------------------------------
@COM_FUNC_INPUT_X72
#DIM L_P
#DIM L_T

L_P = PLAYER
L_T = TARGET

;-----------------------------
;- 事前処理
;-----------------------------
;CHK
SIF TEQUIP:L_T:Ｖ器具 != [[TRAIN:バイブ]]
	RETURN 1

TEQUIP:L_T:Ｖ器具 = 0

;-----------------------------
;- 情景
;-----------------------------

PRINTFORML %CALLNAME:TARGET%から蠢くバイブをゆっくりと引き抜いた
IF 0
;Ｖ経験150以上＋陥落刻印
ELSEIF EXP:Ｖ経験 > 150 && MARK:陥落刻印
	PRINTFORM %CALLNAME%の膣内に溜まっていた粘液が掻き出されて滴り、水溜りを作っ
;Ｖ経験120以上＋陥落刻印
ELSEIF EXP:Ｖ経験 > 120 && MARK:陥落刻印
	PRINTFORM バイブが抜かれた%CALLNAME:TARGET%の肉襞からボタボタと愛液が滴っ
;Ｖ経験70以上＋陥落刻印
ELSEIF EXP:Ｖ経験 > 70 && MARK:陥落刻印
	PRINTFORM %CALLNAME%はもじもじと股を擦り合わせて%CALLNAME:PLAYER%の方を見
;Ｖ経験40以上
ELSEIF EXP:Ｖ経験 > 40 && MARK:陥落刻印
	PRINTFORM %CALLNAME%は熱のこもった吐息を吐い
;Ｖ経験20以上
ELSEIF EXP:Ｖ経験 > 20
	PRINTFORM %CALLNAME%はゆるんだ膣口をひくつかせ
;それ以外
ELSE
	PRINTFORM %CALLNAME%はホッとしたような表情をし
ENDIF
PRINTL ている…
WAIT


;解除だけの為、0返し
RETURN 0

;=================================================
;   sub COM_FUNC_EXE_Xxxx
;=================================================
;   コマンド実行
;-------------------------------------------------
@COM_FUNC_EXE_X72
#DIM L_P
#DIM L_T
#DIM L_TMP
#DIM L_I
VARSET LOCAL

L_P = PLAYER
L_T = TARGET

;-----------------------------
;COM別FLAGの設定
;-----------------------------

TEQUIP:L_T:Ｖ器具 = [[TRAIN:バイブ]]
TEQUIP:L_T:Ｖ器具経過ターン = 0

L_TMP = TEQUIP:L_T:ポイント性交
IF L_TMP
	CALLFORM COM_FUNC_GET_TYPE_X{L_TMP}
	SIF RESULT == [[COM種別:Ｖ性交系]]
		TEQUIP:L_T:ポイント性交 = [[TRAIN:無し]]
ENDIF

;-----------------------------
;ソース追加処理
;-----------------------------

;-----------------------------
;その他の処理
;-----------------------------


;=================================================
;   sub COM_FUNC_SRC_Xxxx
;=================================================
;   コマンドソース
;-------------------------------------------------
@COM_FUNC_SRC_X72
#DIM L_P
#DIM L_T
#DIM L_TMP
#DIM L_I
VARSET LOCAL

L_P = PLAYER
L_T = TARGET

;-----------------------------
;COM別FLAGの設定
;-----------------------------

;-----------------------------
;ソース追加処理
;-----------------------------

LOCAL:0 = 120
LOCAL:1 = 100
LOCAL:2 = 100

L_TMP=150
LOCAL:0 = LOCAL:0 * L_TMP / 100
LOCAL:1 = LOCAL:1 * L_TMP / 100
LOCAL:2 = LOCAL:2 * L_TMP / 100


;- ソース
SOURCE:L_T:快Ｖ += LOCAL:0
SOURCE:L_T:性行為 += LOCAL:1
SOURCE:L_T:露出 += LOCAL:2

;- TR:ソース(部位苦痛)
CALL TR_SOURCE_ADD_部位苦痛("Ｖ感覚", L_T)


;-----------------------------
;経験の処理
;-----------------------------

;- TR:処女喪失(Ｖ) (1:道具)
CALL TR_CHK_LOST_VIRGIN_V(L_T, 1)

;-----------------------------
;その他の処理
;-----------------------------



;=================================================
;   sub COM_FUNC_TRAIN_MSG_Xxxx
;=================================================
;   コマンドメッセージ
;-------------------------------------------------
@COM_FUNC_TRAIN_MSG_X72

;- 挿入時
IF !TEQUIP:Ｖ器具経過ターン

	SIF ABL:Ｖ感覚 >= 4 && TALENT:淫乱度
		PRINT 腰を浮かせておねだりしている
	PRINTFORM %CALLNAME%の
	;潤滑LV5以上でさらに追加
	SIF PALAM:潤滑 >= PALAMLV:5
		PRINT たっぷりと
	;潤滑LV3以上でさらに追加
	SIF PALAM:潤滑 >= PALAMLV:3
		PRINT 粘液をたたえた
	;小柄体型
	IF TALENT:小柄体型
		PRINT 幼いワレメに
	ELSE
		PRINT 秘裂に
	ENDIF
	PRINTL バイブの先端をあてがい馴染ませていく…
	PRINTFORM %CALLNAME%は
	;処女
	IF TCVAR:履歴_EXP_処女喪失
		;陥落刻印
		IF MARK:陥落刻印
			PRINTFORM 少し寂しそうな目で%CALLNAME:PLAYER%を見
		;淫乱
		ELSEIF TALENT:淫乱度
			PRINT 処女である事もお構いなしに、腰を揺らしておねだりをし
		;一線越えない
		ELSEIF TALENT:一線越えない
			PRINT 何かを諦めた表情で目にうっすら涙を浮かべ
		;それ以外
		ELSE
			PRINT ガタガタと震え、喪失の恐怖に怯え
		ENDIF

	;非処女
	ELSE
		;淫乱
		IF TALENT:淫乱度
			;Ｖ経験200以上
			IF EXP:Ｖ経験 > 200
				PRINT 挿入への期待だけでガクガクと腰を震わせてイきそうになっ
			;Ｖ経験150以上
			ELSEIF EXP:Ｖ経験 > 150
				PRINTFORM とろけきった目で%CALLNAME:PLAYER%を見つめ、自分から陰唇を擦りつけ
			;Ｖ経験120以上
			ELSEIF EXP:Ｖ経験 > 120
				PRINT 恍惚とした表情で待ちきれないように腰をくねらせ
			;それ以外
			ELSE
				PRINTFORM もの欲しそうに目を潤ませ%CALLNAME:PLAYER%の方を見
			ENDIF
		ELSE
			;Ｖ経験40以上
			IF EXP:Ｖ経験 > 40
				PRINT バイブの擦れる感触に陶酔しきった吐息をつい
			;Ｖ経験20以上
			ELSEIF EXP:Ｖ経験 > 20
				PRINT バイブの擦れる感触に身をよじらせ愛液を滲ませて
			;それ以外
			ELSE
				PRINT バイブの擦れる感触に肉襞をひくつかせ
			ENDIF
		ENDIF
	ENDIF
	PRINTL ている…
	PRINTFORM %CALLNAME%の
	;濡れ
	SIF TCVAR:キャラ潤滑
		PRINT 濡れそぼった
	PRINTFORML 性器にバイブを挿し込んだ

;- 継続中
ELSE
	PRINTFORML %CALLNAME%はバイブが与える快感に甘い声をあげている…
ENDIF

WAIT


;=================================================
;   sub COM_FUNC_GET_TYPE_Xxxx
;=================================================
;   取得:コマンド種別
;-------------------------------------------------
; Output:
;  RESULT				コマンド種別
;-------------------------------------------------
@COM_FUNC_GET_TYPE_X72
RETURN [[COM種別:道具系]]

