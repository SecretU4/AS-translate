;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Module		:SOURCE_CALC_ORGASM.ERB
;	Facility	:SOURCE関連処理(絶頂計算)。
;
;	Licence		:ライセンスフリー。
;
;	Modification Data:
;
;	Edit	Date			Author					Reason
;	001		20XX/XX/XX		----					新規作成
;
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#

;===============================================================================
;  ◇記載関数一覧(記載順)
;
;		Module Name						Explanation
;
; *- XXXX
;
;	・ SRC_CALC_ORGASM_MAIN				SRC計算:絶頂(主処理)
;	・ SRC_CALC_ORGASM_ADD				SRC計算:絶頂(パラメータ加算)
;	・ SRC_CALC_EJCT_POINT				SRC計算:射精ポイント
;	・ LOG_ORGASM_MAIN					LOG出力:絶頂(主処理)
;	・ LOG_ORGASM_SIGHT_MAIN			LOG出力:絶頂(情景 - 主処理)
;	・ LOG_ORGASM_SIGHT_SLAVE_MAIN		LOG出力:絶頂(奴隷情景-主処理)
;	・ LOG_ORGASM_SIGHT_MASTER_MAIN		LOG出力:絶頂(主人情景-主処理)
;	・ LOG_ORGASM_SIGHT_MASTER_DETAIL	LOG出力:絶頂(主人情景-詳細)
;
;===============================================================================

;-------------------------------------------------------------------------------
;   XXXX
;-------------------------------------------------------------------------------

;=================================================
;   sub SRC_CALC_ORGASM_MAIN
;=================================================
;   SRC計算:絶頂(主処理)
;-------------------------------------------------
@SRC_CALC_ORGASM_MAIN
#DIM L_I
#DIM L_T
#DIM L_TMP
#DIM L_MODE

;絶頂モード制御(0:イキそうで制御 1:無し) ※暫定で無しで調整
L_MODE = 1

FOR L_I, 0, DEFTR_PL_NUM
	L_T = GTR_PL:L_I

	;CHK
	SIF L_T < 0
		BREAK

	L_TMP = TCVAR:L_T:絶頂加算値

	IF L_MODE
		TCVAR:L_T:イきそう = 1
	ENDIF

	IF L_TMP
		;- 加算
		BASE:L_T:絶頂 += L_TMP

		;- CHK:MAX
		IF BASE:L_T:絶頂 >= MAXBASE:L_T:絶頂
			;- 既にイきそうな場合
			IF TCVAR:L_T:イきそう > 0
				;-----------------------------
				;-- 絶頂
				;-----------------------------
				TCVAR:L_T:イきそう = 0

				;- SRC計算:絶頂(パラメータ加算)
				CALL SRC_CALC_ORGASM_ADD(L_T)

				BASE:L_T:絶頂 %= MAXBASE:L_T:絶頂

			ELSE
				;- 1ターン継続
				TCVAR:L_T:イきそう = 1
			ENDIF
		ENDIF
	;絶頂加算無し
	ELSE
		SIF TCVAR:L_T:イきそう
			TCVAR:L_T:イきそう --

		;- イきそうで無い場合
		IF !TCVAR:L_T:イきそう
			BASE:L_T:絶頂 = MIN(BASE:L_T:絶頂, MAXBASE:L_T:絶頂 - 1)
		ENDIF
	ENDIF

	IF L_MODE
		TCVAR:L_T:イきそう = 0
	ENDIF

NEXT

;=================================================
;   sub SRC_CALC_ORGASM_ADD
;=================================================
;   SRC計算:絶頂(パラメータ加算)
;-------------------------------------------------
; Input:
;  ARG:0				対象者
;-------------------------------------------------
@SRC_CALC_ORGASM_ADD(ARG:0)
#DIM L_I
#DIM L_TMP
#DIM L_VAL
#DIM L_LV
#DIM L_T
#DIM L_P

L_T = ARG:0

L_TMP = BASE:L_T:絶頂

;- 計算:絶頂強度
SELECTCASE L_TMP
	;2.5倍以上
	CASE IS >= (FLAG:最大値_絶頂 * 250 / 100)
		L_LV = 3
	;1.5倍以上
	CASE IS >= (FLAG:最大値_絶頂 * 150 / 100)
		L_LV = 2
	CASEELSE
		L_LV = 1
ENDSELECT

TCVAR:L_T:絶頂強度 = L_LV

;-----------------------------
;-- 各種ソース加算
;-----------------------------

;- 絶頂
;SOURCE:L_T:絶頂 = (L_LV * 500)
SOURCE:L_T:絶頂 = 400 + (L_LV * 100)

;ペニス有り
IF VAR_PENIS(L_T)
;	L_VAL=(L_LV * 300)
	L_VAL= 200 + (L_LV * 100)

	L_TMP = TEQUIP:L_T:ペニス占有者１
	IF !GROUPMATCH(L_TMP, [[ペニス占有者:無し]], L_T)
		SOURCE:L_TMP:精液 += L_VAL
		;- SRC計算:射精ポイント
		CALL SRC_CALC_EJCT_POINT(L_TMP, TEQUIP:L_T:ペニス占有部位１, L_LV)
	ENDIF

	L_TMP = TEQUIP:L_T:ペニス占有者２
	IF !GROUPMATCH(L_TMP, [[ペニス占有者:無し]], L_T)
		SOURCE:L_TMP:精液 += L_VAL
		;- SRC計算:射精ポイント
		CALL SRC_CALC_EJCT_POINT(L_TMP, TEQUIP:L_T:ペニス占有部位２, L_LV)
	ENDIF

	;Ｖ射精時
	IF TEQUIP:L_T:ペニス占有部位１ == [[占有部位:Ｖ]]
		L_TMP = TEQUIP:L_T:ペニス占有者１
		CFLAG:MASTER:精子経験値 += 1
		IF CFLAG:MASTER:精子経験値 >= ((CFlag:MASTER:精子レベル)*5)+CFlag:MASTER:精子レベル
			CFLAG:MASTER:精子経験値 = 0
			CFlag:MASTER:精子レベル += 1
		ENDIF
		;妊娠処理
		;CALL NINSIN_CHK(L_TMP, L_T, L_LV)
	ENDIF

ENDIF

;-----------------------------
;- 絶頂タイプ判定
;-----------------------------
L_VAL=1000
IF 0
;ペニス有り
ELSEIF VAR_PENIS(L_T)
	TCVAR:L_T:絶頂タイプ = [[口上:Ｐ射精]]
;;ＶとＡが両方達成
;ELSEIF (CUP:L_T:快Ｖ >= L_VAL) && (CUP:L_T:快Ａ >= L_VAL)
;	TCVAR:L_T:絶頂タイプ = [[口上:ＶＡ絶頂]]
;Ｖが達成
ELSEIF (CUP:L_T:快Ｖ >= L_VAL)
	TCVAR:L_T:絶頂タイプ = [[口上:Ｖ絶頂]]
;Ａが達成
ELSEIF (CUP:L_T:快Ａ >= L_VAL)
	TCVAR:L_T:絶頂タイプ = [[口上:Ａ絶頂]]
;Ｃが達成
ELSEIF (CUP:L_T:快Ｃ >= L_VAL)
	TCVAR:L_T:絶頂タイプ = [[口上:Ｃ絶頂]]
;Ｂが達成
ELSEIF (CUP:L_T:快Ｂ >= L_VAL)
	TCVAR:L_T:絶頂タイプ = [[口上:Ｂ絶頂]]
;消去法
ELSE
	IF 0
;	;ＶとＡに値有り
;	ELSEIF (CUP:L_T:快Ｖ) && (CUP:L_T:快Ａ)
;		TCVAR:L_T:絶頂タイプ = [[口上:ＶＡ絶頂]]
	;Ｖに値有り
	ELSEIF (CUP:L_T:快Ｖ)
		TCVAR:L_T:絶頂タイプ = [[口上:Ｖ絶頂]]
	;Ａに値有り
	ELSEIF (CUP:L_T:快Ａ)
		TCVAR:L_T:絶頂タイプ = [[口上:Ａ絶頂]]
	;Ｂに値有り
	ELSEIF (CUP:L_T:快Ｂ)
		TCVAR:L_T:絶頂タイプ = [[口上:Ｂ絶頂]]
	;それ以外
	ELSE
		TCVAR:L_T:絶頂タイプ = [[口上:Ｃ絶頂]]
	ENDIF
ENDIF

;;- DOWNBASE
;DOWNBASE:L_T:体力 -= 10

;- PLAYERに習得
SOURCE:PLAYER:習得 += (L_LV * 100)

;- EXP関連
GTR_EXPUP:L_T:DB_EXPUP_POS("絶頂経験") += L_LV

TCVAR:L_T:絶頂回数 += L_LV

;=================================================
;   sub SRC_CALC_EJCT_POINT
;=================================================
;   SRC計算:射精ポイント
;-------------------------------------------------
; Input:
;  ARG:0				対象者
;  ARG:1				ペニス占有部位
;  ARG:2				絶頂強度
;-------------------------------------------------
@SRC_CALC_EJCT_POINT(ARG:0, ARG:1, ARG:2)
#DIM L_T
#DIM L_TYPE
#DIM L_RES

L_T = ARG:0
L_TYPE = ARG:1
L_RES = [[占有部位:無し]]

;- CHK:占有部位
SELECTCASE L_TYPE
	;膣奥へ射精
	CASE [[占有部位:Ｖ]]
		L_RES = [[口上:主人Ｖ射精]]
		GTR_EXPUP:L_T:DB_EXPUP_POS("膣射経験") += ARG:2
		TCVAR:L_T:子宮射精回数 += ARG:2

	;アナルへ射精
	CASE [[占有部位:Ａ]]
		L_RES = [[口上:主人Ａ射精]]
	;口へ射精
	CASE [[占有部位:口]], [[占有部位:６９]], [[占有部位:Ｐ６９]]
		L_RES = [[口上:主人口射精]]
		GTR_EXPUP:L_T:DB_EXPUP_POS("精飲経験") += ARG:2

	;胸へ射精
	CASE [[占有部位:Ｂ]]
		L_RES = [[口上:主人胸射精]]
	;手へ射精
	CASE [[占有部位:指]]
		L_RES = [[口上:主人手射精]]

	;-----------------------------
	;- 以下は拡張系
	;-----------------------------
	;秘裂へ射精
	CASE [[占有部位:Ｃ]]
		L_RES = [[口上:主人秘裂射精]]
	;足へ射精
	CASE [[占有部位:足]]
		L_RES = [[口上:主人足射精]]
	;-----------------------------
	;- 以下は逆性交系
	;-----------------------------
	CASEELSE
		;無し
ENDSELECT

TCVAR:L_T:被射精部位=L_RES

GTR_EXPUP:L_T:DB_EXPUP_POS("精液経験") += ARG:2


;=================================================
;   sub LOG_ORGASM_MAIN
;=================================================
;   LOG出力:絶頂(主処理)
;-------------------------------------------------
@LOG_ORGASM_MAIN
#DIM L_I
#DIM L_T
#DIM L_HIT

L_HIT=0

FOR L_I, 0, DEFTR_PL_NUM
	L_T = GTR_PL:L_I

	;CHK
	SIF L_T < 0
		BREAK

	;-----------------------------
	;- LOG出力
	;-----------------------------
	IF TCVAR:L_T:絶頂強度
		PRINTFORM %CALLNAME:L_T%

		SETCOLOR COLOR("ピンク")
		IF VAR_PENIS(L_T)
			SELECTCASE TCVAR:L_T:絶頂強度
				CASE IS >= 3
					PRINTFORM 超大量射精
				CASE 2
					PRINTFORM 大量射精
				CASEELSE
					PRINTFORM 射精
			ENDSELECT
		ELSE
			SELECTCASE TCVAR:L_T:絶頂強度
				CASE IS >= 3
					PRINTFORM 超強絶頂
				CASE 2
					PRINTFORM 強絶頂
				CASEELSE
					PRINTFORM 絶頂
			ENDSELECT
		ENDIF
		RESETCOLOR
		PRINTL
		L_HIT = 1

	ENDIF
NEXT

IF L_HIT
	WAIT
	PRINTL
ENDIF


;=================================================
;   sub LOG_ORGASM_SIGHT_MAIN
;=================================================
;   LOG出力:絶頂(情景 - 主処理)
;-------------------------------------------------
;   MASTER→メインTARGET→サブTARGETの順で
;   情景を出力
;-------------------------------------------------
@LOG_ORGASM_SIGHT_MAIN
#DIM L_I
#DIM L_T
#DIM L_TGT_BAK

;- ペニス有りなら主人専用情景
IF VAR_PENIS(PLAYER)
	;- LOG出力:絶頂(主人情景-主処理)
	CALL LOG_ORGASM_SIGHT_MASTER_MAIN
ELSE
	;- LOG出力:絶頂(奴隷情景-主処理)
	CALL LOG_ORGASM_SIGHT_SLAVE_MAIN(PLAYER)
ENDIF


;- LOG出力:絶頂(奴隷情景-主処理)
CALL LOG_ORGASM_SIGHT_SLAVE_MAIN(TARGET)

L_TGT_BAK = TARGET

FOR L_I, 0, DEFTR_PL_NUM
	L_T = GTR_PL:L_I

	;CHK
	SIF L_T < 0
		BREAK
	SIF TCVAR:L_T:ログ出力_絶頂
		CONTINUE

	TARGET = L_T

	;- LOG出力:絶頂(奴隷情景-主処理)
	CALL LOG_ORGASM_SIGHT_SLAVE_MAIN(TARGET)

NEXT

TARGET = L_TGT_BAK

;=================================================
;   sub LOG_ORGASM_SIGHT_SLAVE_MAIN
;=================================================
;   LOG出力:絶頂(奴隷情景-主処理)
;-------------------------------------------------
; Input:
;  ARG					対象キャラ
;-------------------------------------------------
@LOG_ORGASM_SIGHT_SLAVE_MAIN(ARG)
#DIM L_T

L_T = ARG

;- ログ出力済み
SIF TCVAR:L_T:ログ出力_絶頂
	RETURN

;- CHK
SIF !TCVAR:L_T:絶頂強度
	RETURN

TCVAR:L_T:ログ出力_絶頂	= 1


PRINTFORM %CALLNAME:L_T%は
IF VAR_PENIS(L_T)
	PRINT ペニスから精液を噴き出し
ELSE
	PRINT 陰唇から透明な潮を噴き出し
ENDIF
PRINTL ながら、全身を震わせて絶頂に達した……

PRINTFORM %CALLNAME:L_T%
IF TCVAR:L_T:絶頂強度 >= 2
	IF VAR_PENIS(L_T)
		PRINTL は射精の快楽に荒い息をつきながら、絶頂の余韻にひたっている……
	ELSE
		PRINTL は断続的に絶頂を繰り返しながら、絶頂の余韻にひたっている……
	ENDIF
ELSE
	PRINTL は荒い息をつきながら絶頂の余韻にひたっている……
ENDIF

WAIT

;- 口上:絶頂した時
CALL KOJO_ORGASM
PRINTL


;=================================================
;   sub LOG_ORGASM_SIGHT_MASTER_MAIN
;=================================================
;   LOG出力:絶頂(主人情景-主処理)
;-------------------------------------------------
@LOG_ORGASM_SIGHT_MASTER_MAIN
#DIM 部位１
#DIM 部位２
#DIM L_TMP

;- ログ出力済み
SIF TCVAR:PLAYER:ログ出力_絶頂
	RETURN

;- CHK
SIF !TCVAR:PLAYER:絶頂強度
	RETURN

TCVAR:PLAYER:ログ出力_絶頂	= 1


VARSET LOCALS
VARSET LOCAL

部位１ = TEQUIP:PLAYER:ペニス占有部位１
部位２ = TEQUIP:PLAYER:ペニス占有部位２


IF 0

;-----------------------------
;- ダブル系
;-----------------------------
ELSEIF (部位１ == [[占有部位:Ｖ]]) && (部位２ == [[占有部位:Ｖ]])
	LOCALS = 交互挿入
ELSEIF (部位１ == [[占有部位:口]]) && (部位２ == [[占有部位:口]])
	LOCALS = ダブルフェラ
ELSEIF (部位１ == [[占有部位:Ｂ]]) && (部位２ == [[占有部位:Ｂ]])
	LOCALS = ダブルパイズリ
ELSEIF (部位１ == [[占有部位:指]]) && (部位２ == [[占有部位:指]])
	LOCALS = ダブル手コキ
ELSEIF (部位１ == [[占有部位:足]]) && (部位２ == [[占有部位:足]])
	LOCALS = ダブル足コキ

;-----------------------------
;- シングル系
;-----------------------------
ELSEIF (部位１ == [[占有部位:Ｐ６９]])
	LOCALS = Ｐシックスナイン
ELSEIF (部位１ == [[占有部位:６９]])
	LOCALS = シックスナイン
ELSEIF (部位１ == [[占有部位:Ｖ]])
	LOCALS = Ｖ性交
ELSEIF (部位１ == [[占有部位:Ａ]])
	LOCALS = Ａ性交
ELSEIF (部位１ == [[占有部位:口]])
	LOCALS = フェラチオ
ELSEIF (部位１ == [[占有部位:Ｃ]])
	LOCALS = 素股
ELSEIF (部位１ == [[占有部位:Ｂ]])
	LOCALS = パイズリ
ELSEIF (部位１ == [[占有部位:足]])
	LOCALS = 足コキ
ELSEIF (部位１ == [[占有部位:指]])
	LOCALS = 手コキ
ELSE
	RETURN
ENDIF

;- LOG出力:絶頂(主人情景-詳細)
CALL LOG_ORGASM_SIGHT_MASTER_DETAIL(LOCALS)

;- 口上出力:絶頂させた時
CALL KOJO_ORGASM_M
PRINTL


;=================================================
;   sub LOG_ORGASM_SIGHT_MASTER_DETAIL
;=================================================
;   LOG出力:絶頂(主人情景-詳細)
;-------------------------------------------------
; Input:
;  ARGS					対象文字列
;-------------------------------------------------
@LOG_ORGASM_SIGHT_MASTER_DETAIL(ARGS,ARG,ARG:2)
#DIM 大量射精
#DIM 対象１
#DIM 対象２
大量射精 = TCVAR:PLAYER:絶頂強度 >= 2
IF TEQUIP:PLAYER:ペニス占有者２ == TARGET
	対象１ = TEQUIP:PLAYER:ペニス占有者２
	対象２ = TEQUIP:PLAYER:ペニス占有者１
ELSE
	対象１ = TEQUIP:PLAYER:ペニス占有者１
	対象２ = TEQUIP:PLAYER:ペニス占有者２
ENDIF
SELECTCASE ARGS
	CASE "交互挿入"
		PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:対象２%の膣内に精液を放つと一旦ペニスを抜いてから
		PRINTFORML 射精が止まらないうちに%CALLNAME:対象１%に挿入し、最後の一滴まで注ぎ込んだ…
	CASE "ダブルフェラ"
		IF 大量射精
			PRINTFORML 大量の精液が%CALLNAME:対象１%と%CALLNAME:対象２%の顔中に降り注いだ…
		ELSE
			PRINTFORML %CALLNAME:対象１%と%CALLNAME:対象２%は精液を口で受け止めている…
		ENDIF
		PRINTFORML その後、%CALLNAME:対象１%と%CALLNAME:対象２%は残った精液を吸い取りながら、ペニスの汚れを綺麗に舐めとっていった…
		PRINTFORML 二人はそれだけでは足りないかのように互いの口に溜まった精液を舐めあっている…
	CASE "ダブルパイズリ"
		IF 大量射精 && RAND:3
			PRINTFORML %CALLNAME:対象１%と%CALLNAME:対象２%の胸から顔、髪の毛に至るまでを精液が白く染め上げた…
			PRINTFORML その後、%CALLNAME:対象１%と%CALLNAME:対象２%は残った精液を吸い取りながら、ペニスの汚れを綺麗に舐めとっていった…
			PRINTFORML 二人はそれだけでは足りないかのように互いの口に溜まった精液を舐めあっている…
		ELSE
			IF VAR_バストサイズ(対象１) > 0 && VAR_バストサイズ(対象２) > 0
				PRINTFORML %CALLNAME:対象１%と%CALLNAME:対象２%の胸の谷間に精液が溜まっている…
			ELSEIF VAR_バストサイズ(対象１) > 0 && VAR_バストサイズ(対象２) <= 0
				PRINTFORML %CALLNAME:対象１%と%CALLNAME:対象２%の胸元から顔にまで精液が飛び散った…
				PRINTFORML %CALLNAME:対象１%の胸の谷間には精液が溜まっている…
			ELSE
				PRINTFORML %CALLNAME:対象１%と%CALLNAME:対象２%の胸元から顔にまで精液が飛び散った…
			ENDIF
		ENDIF
	CASE "ダブル手コキ"
		IF 大量射精
			PRINTFORML %CALLNAME:対象１%と%CALLNAME:対象２%の顔と手が大量の精液にまみれていった…
		ELSE
			PRINTFORML %CALLNAME:対象１%と%CALLNAME:対象２%に精液が降りかかっていった…
		ENDIF
	CASE "ダブル足コキ"
		IF TALENT:対象１:サド
			PRINTFORM %CALLNAME:対象１%と%CALLNAME:対象２%の冷ややかな視線を受けながら、その
		ELSE
			PRINTFORM %CALLNAME:対象１%と%CALLNAME:対象２%の
		ENDIF
		PRINTFORML 足に\@ 大量射精 ? 大量の # 熱い \@精液を放った…
		IF 大量射精
		ELSE
		ENDIF
	CASE "６９パイズリ"
		PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:対象１%の秘所に舌をはわせながら%CALLNAME:対象１%の口内で射精した…
		PRINTFORML %CALLNAME:対象１%は下半身への刺激にびくびくと反応しながら噴き上がる精液をすすっている…
	CASE "シックスナイン"
		PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:対象１%の秘所に舌をはわせながら%CALLNAME:対象１%の口内で射精した…
		PRINTFORML %CALLNAME:対象１%は下半身への刺激にびくびくと反応しながら噴き上がる精液をすすっている…
	CASE "Ｐシックスナイン"
		IF TCVAR:対象１:射精
			PRINTFORML %CALLNAME:対象１%と%CALLNAME:PLAYER%は同時に互いの口内へ射精した…
			PRINTFORML 二人は互いのペニスを根元まで咥え快楽を確かめあった…
		ELSE
			PRINTFORM %CALLNAME:PLAYER%は%CALLNAME:対象１%の男性器をしゃぶりながら
			PRINTFORML %CALLNAME:対象１%の口内で射精した…
			PRINTFORML %CALLNAME:対象１%は下半身への刺激にびくびくと反応しながら噴き上がる精液をすすっている…
		ENDIF
	CASE "Ｖ性交"

		PRINTFORM %CALLNAME:PLAYER%は%CALLNAME:対象１%の膣奥にペニスを固定し、
		IF 大量射精
			PRINTFORML 子宮内にたっぷりと精液を注ぎ込んだ…
		ELSE
			PRINTFORML 子宮内に精液を注ぎ込んだ…
		ENDIF
		SIF MARK:対象１:陥落刻印
			PRINTFORML %CALLNAME:対象１%は従順に子宮を捧げている…
			ARG = 対象１
			ARG:1 = PLAYER
			CALL NAKATDASI_MAIN(ARG,ARG:1)
	CASE "Ａ性交"
		PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:対象１%の腸内に射精した…
		PRINTFORML 精液で溢れている腸内は細かく震え、ペニスをきつく締めつけている…
	CASE "フェラチオ"
		PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:対象１%の口内に射精した…
		IF EXP:対象１:精飲経験 == 0
			PRINTFORML %CALLNAME:対象１%はびっくりしてペニスから口を離してしまった
			PRINTFORML 顔を精液で汚しながら初めての味に少し戸惑っているようだ…
			PRINTFORML %CALLNAME:対象１%は初めての精飲を経験した…
		ELSEIF EXP:対象１:精飲経験 < EXPLV:3
			PRINTFORML %CALLNAME:対象１%は精液を口内に受け止めると%CALLNAME:PLAYER%の命令を聞いて飲み込んだ…
			PRINTFORML まだ精液の味に慣れないようだ…
		ELSE
			IF 大量射精
				PRINTFORML %CALLNAME:対象１%は亀頭を咥えたまま口内に放たれる大量の精液を飲み込んでいった…
			ELSE
				PRINTFORML %CALLNAME:対象１%は唇で亀頭をしごいてすべての精液を口内に受け止めてから
				PRINTFORML 亀頭を咥えたまま口内の精液を喉を鳴らして飲み込んだ…
			ENDIF
			PRINTFORML %CALLNAME:対象１%は口内で亀頭をしゃぶりながら、さらなる射精を促している…
		ENDIF
	CASE "素股"
		PRINTFORML %CALLNAME:PLAYER%は密着した性器の間で射精した…
	CASE "パイズリ"
		PRINTFORML %CALLNAME:対象１%の胸に挟ませたペニスの先端から精液が噴き上がった…
		PRINTFORML %CALLNAME:対象１%は胸と顔で精液を受け止めながら、精液を舐め取り屈服した表情を見せている…
	CASE "足コキ"
		IF TALENT:対象１:サド
			PRINTFORM %CALLNAME:対象１%の冷ややかな視線を受けながら、その
		ELSE
			PRINTFORM %CALLNAME:対象１%の
		ENDIF
		PRINTFORML 足に\@ 大量射精 ? 大量の # 熱い \@精液を放った…
	CASE "手コキ"
		IF ABL:対象１:精液中毒 > 2 && ABL:対象１:性奴隷 > 2
			PRINTFORML %CALLNAME:対象１%は恍惚の表情で舌を突き出しながら%CALLNAME:PLAYER%の精液を浴びている…
		ELSEIF 大量射精
			PRINTFORML %CALLNAME:対象１%の顔と手が大量の精液にまみれていった…
		ELSE
			PRINTFORML %CALLNAME:対象１%に精液が降りかかっていった…
		ENDIF
ENDSELECT

WAIT
