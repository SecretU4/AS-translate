;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Module		:VAR_PROCESS.ERB
;	Facility	:本体系処理群。
;
;	Licence		:ライセンスフリー。
;
;	Modification Data:
;
;	Edit	Date			Author					Reason
;	001		20XX/XX/XX		----					新規作成
;
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#

;===============================================================================
;  ◇記載関数一覧(記載順)
;
;		Module Name						Explanation
;
; *- xxxx
;
;	・ VAR_TURNEND_MAIN					VAR:ターン終了処理
;	・ VAR_TURNEND_INIT					VAR:ターン終了処理(初期化)
;	・ VAR_DAYEND						VAR:一日終了処理
;	・ VAR_DAYEND_INIT					VAR:一日終了前処理
;
; *- その他
;
;	・ VAR_DEFAULT						VAR:開始処理(デフォルト)
;	・ VAR_INIT							VAR:初期化処理
;	・ VAR_RESTART						VAR:二週目処理
;
;===============================================================================

;-------------------------------------------------------------------------------
;   xxxx
;-------------------------------------------------------------------------------

;=================================================
;   sub VAR_TURNEND_MAIN
;=================================================
;   VAR:ターン終了処理
;-------------------------------------------------
@VAR_TURNEND_MAIN
#DIM L_I
#DIM L_TMP
#DIM L_CHG_DAY

;- CHK:更新
SIF !(FLAG:一日終了フラグ || FLAG:ターン終了フラグ)
	RETURN

L_I=0
L_TMP=0

L_CHG_DAY = ( (FLAG:時間帯 >= [[時間帯:限度時間]]) ? 1 # 0)

SIF FLAG:一日終了フラグ
	L_CHG_DAY = 1

;-----------------------------
;- ターン終了処理
;-----------------------------



;-------------------------------------------------

;-----------------------------
;- 一日終了処理
;-----------------------------
IF L_CHG_DAY
	;- VAR:一日終了前処理
	CALL VAR_DAYEND_INIT

	DRAWLINE
	;ターン経過イベント
	CALL TURN_EVENT


	;- 妊娠:出産処理
	;CALL NINSIN_CHILD_BIRTH

	;- VAR:一日終了処理
	CALL VAR_DAYEND

ENDIF


;-----------------------------
;- 時間区分更新処理
;-----------------------------

DRAWLINE
;表示:日付
CALL INFO_DATE
PRINTL が終わった・・・
WAIT

;- 日付が変わった場合は初期化
IF L_CHG_DAY
	FLAG:時間帯 = 0
	DAY ++

	;- EVE:エンディング主処理
	CALL EVENT_ED_MAIN
ELSE
	FLAG:時間帯 ++
ENDIF

CALL TURN_EVENT_1

;表示:日付
CALL INFO_DATE
PRINTL に移行します
WAIT

FLAG:一日終了フラグ		= 0
FLAG:ターン終了フラグ	= 0

;-----------------------------
;- 全キャラを処理
;-----------------------------
FOR L_I, 0, CHARANUM

	;- VAR:終了処理(初期化)
	CALL VAR_TURNEND_INIT(L_I)

NEXT

;-----------------------------
;- 追加処理
;-----------------------------

;- SYSTEM:オートセーブ処理
CALL SYS_AUTO_SAVE



;=================================================
;   sub VAR_TURNEND_INIT
;=================================================
;   VAR:ターン終了処理(初期化)
;-------------------------------------------------
;   次回の行動用に各ステータスを回復させる
;-------------------------------------------------
; Input:
;  ARG					キャラ位置
;-------------------------------------------------
@VAR_TURNEND_INIT(ARG)
#DIM L_TMP

L_TMP=0

;-----------------------------
;-- 基本処理
;-----------------------------

;- 時間帯で処理分岐(％を設定)
SELECTCASE FLAG:時間帯
	;- 0:最初の行動時間(=日付変更時)
	CASE 0
		L_TMP		= 100
	;- その他
	CASEELSE
		L_TMP		= 50
ENDSELECT

;- 計算
BASE:ARG:体力	+= C_TIMES(MAXBASE:ARG:体力, L_TMP)

;- 丸め
BASE:ARG:体力 = MIN(BASE:ARG:体力,MAXBASE:ARG:体力)


;-----------------------------
;-- 追加処理
;-----------------------------




;=================================================
;   sub VAR_DAYEND
;=================================================
;   VAR:一日終了処理
;-------------------------------------------------
@VAR_DAYEND
#DIM L_I
#DIM L_T

FOR L_I, 0, CHARANUM
	L_T = L_I


	;初期化
	TALENT:L_T:排卵薬 = 0
NEXT


;=================================================
;   sub VAR_DAYEND_INIT
;=================================================
;   VAR:一日終了前処理
;-------------------------------------------------
@VAR_DAYEND_INIT
#DIM L_I
#DIM L_T

FOR L_I, 0, CHARANUM
	L_T = L_I

	;初期化
	CALL MNG_ITEM_RENEW(L_T)
NEXT



;-------------------------------------------------------------------------------
;   その他
;-------------------------------------------------------------------------------

;=================================================
;   sub VAR_DEFAULT
;=================================================
;   VAR:開始処理(デフォルト)
;-------------------------------------------------
@VAR_DEFAULT

DAY = 1
MONEY = 0

;-----------------------------
;-- FLAG
;-----------------------------

FLAG:定数_キャラ表示数		= 15
FLAG:STATUS伏字モード		= 1
FLAG:バージンモード			= 1
FLAG:ＮＴＲモード			= 1


;=================================================
;   sub VAR_INIT
;=================================================
;   VAR:初期化処理
;-------------------------------------------------
@VAR_INIT

;-----------------------------
;-- FLAG
;-----------------------------

FLAG:最大数_キャラ			= DEFSMN_LIST_NUM
FLAG:最大数_JUEL			= 99999
FLAG:最大数_PALAM			= 99999
FLAG:最大数_PALAMLV			= 9
FLAG:最大数_ABLLV			= 9
FLAG:最大数_MARKLV			= 3
FLAG:最大数_EXP				= 999

FLAG:最大数_ムード			= 1000
FLAG:最大数_ムード桁数		= 5
FLAG:最大数_調教人数		= 2
FLAG:最大値_絶頂			= 10000
FLAG:最大値_体力			= 300
FLAG:最大値_子宮精液		= 10

;※ターンは0始まり
FLAG:最大数_行動限界ターン	= 100 - 1

FLAG:定数_体力消費			= -10
FLAG:労役最大値				= 9999


;-----------------------------
;- その他
;-----------------------------

GVAR_MONEY_NAME				= 資金


;-----------------------------
;- 追加処理
;-----------------------------
VARSET LOCAL

REPEAT CHARANUM
	LOCAL = NO:COUNT
	CFLAG:COUNT:使用口上 = LOCAL

;-------------------------------------------------
;	;存在チェック
;	IF EXISTCSV(LOCAL,0)
;		;使用口上を常にCSVに合わせる(暫定)
;		CFLAG:COUNT:使用口上 = CSVCFLAG(LOCAL, GETNUM(CFLAG, "使用口上"), 0)
;		SIF !CFLAG:COUNT:使用口上
;			CFLAG:COUNT:使用口上 = LOCAL
;	ENDIF
;-------------------------------------------------
REND





;=================================================
;   sub VAR_RESTART
;=================================================
;   VAR:二週目処理
;-------------------------------------------------
@VAR_RESTART
#DIM L_I
#DIM L_T

;-----------------------------
;- 変数類
;-----------------------------

;- 初期化
DAY = 1
TIME = 0
SIF MONEY < 500
	MONEY = 500

;奴隷ストック
VARSET GSMN_STOCK

;失踪キャラのデータ消去
CALL 中古処理初期化

;-----------------------------
;- キャラ引き継ぎ処理
;-----------------------------
FOR L_I, CHARANUM-1, -1, -1
	L_T = L_I

	SIF L_T == MASTER
		CONTINUE

	;引き継ぎ対象以外は削除
	IF (MARK:L_T:隷属刻印 < 3) || (MARK:L_T:陥落刻印 < 3)

		DELCHARA L_I
		CONTINUE

	;残す
	ELSE
		;初期化
		CFLAG:L_T:放置日数 = 0
	ENDIF

NEXT


