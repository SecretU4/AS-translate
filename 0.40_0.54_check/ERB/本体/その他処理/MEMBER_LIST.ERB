;-------------------------------------------------
;メンバーリスト
;ARG:0 参照する番号FLAG側
;ARG:1 参照する番号CFLAG側
;-------------------------------------------------
@MEMBER_LIST(L_FLAG_ID, L_CFLAG_ID, OPT_HIGHLIGHT=-1)
#DIM L_C_ID     ;登録番号
#DIM L_FLAG_ID  ;参照FLAG
#DIM L_CFLAG_ID ;参照CFLAG
#DIM OPT_HIGHLIGHT  ; ハイライト着色用
FOR L_C_ID, 0, CHARANUM
	SIF CFLAG:L_C_ID:L_CFLAG_ID != FLAG:L_FLAG_ID
		CONTINUE
	SIF L_C_ID == OPT_HIGHLIGHT
		SETCOLOR COLOR("HIGHLIGHT")
	PRINTFORM [%CALLNAME:L_C_ID%]
	RESETCOLOR
NEXT

;-------------------------------------------------
;メンバーリスト(ボランティア専用)
;ARG:0 参照する番号FLAG側
;ARG:1 参照する番号CFLAG側
;-------------------------------------------------
@MEMBER_LIST_B(L_FLAG_ID, L_CFLAG_ID, OPT_HIGHLIGHT=-1)
#DIM L_C_ID     ;登録番号
#DIM L_FLAG_ID  ;参照FLAG
#DIM L_CFLAG_ID ;参照CFLAG
#DIM OPT_HIGHLIGHT  ; ハイライト着色用
FOR L_C_ID, 0, CHARANUM
	SIF CFLAG:L_C_ID:L_CFLAG_ID != FLAG:L_FLAG_ID
		CONTINUE
	SIF L_C_ID == OPT_HIGHLIGHT
		SETCOLOR COLOR("HIGHLIGHT")
	IF CFLAG:L_C_ID:住人_肉体ダメージ >= 100 || CFLAG:L_C_ID:住人_精神ダメージ >= 100
		SETCOLOR 100,100,100
		PRINTFORM [{L_C_ID}]【%CALLNAME:L_C_ID%  死亡】
	ELSEIF TALENT:L_C_ID:ボランティア
		SIF TALENT:L_C_ID:ボランティア == 2
			SETCOLOR 150,250,200
		SIF TALENT:L_C_ID:ボランティア == 3
			SETCOLOR 150,200,250
		PRINTFORM [{L_C_ID}] %CALLNAME:L_C_ID% 【総経験:{CFLAG:L_C_ID:住人_遺研_総数}】
		RESETCOLOR
	ENDIF
	RESETCOLOR
NEXT
;メンバーリスト(ボランティア専用)
;ARG:0 参照する番号FLAG側
;ARG:1 参照する番号CFLAG側
;-------------------------------------------------
@MEMBER_LIST_B2(L_FLAG_ID, L_CFLAG_ID, OPT_HIGHLIGHT=-1)
#DIM L_C_ID     ;登録番号
#DIM L_FLAG_ID  ;参照FLAG
#DIM L_CFLAG_ID ;参照CFLAG
#DIM OPT_HIGHLIGHT  ; ハイライト着色用
FLAG:遺研_合計経験値 = 0
FOR L_C_ID, 0, CHARANUM
	SIF CFLAG:L_C_ID:L_CFLAG_ID != FLAG:L_FLAG_ID
		CONTINUE
	SIF L_C_ID == OPT_HIGHLIGHT
		SETCOLOR COLOR("HIGHLIGHT")
	IF TALENT:L_C_ID:ボランティア >= 2
		IF TALENT:L_C_ID:ボランティア == 2
			SETCOLOR 150,250,200
		ENDIF
		IF TALENT:L_C_ID:ボランティア == 3
			SETCOLOR 150,200,250
		ENDIF
		IF CFLAG:L_C_ID:住人_肉体ダメージ >= 100 || CFLAG:L_C_ID:住人_精神ダメージ >= 100
			SETCOLOR 100,100,100
			PRINTFORM 【%CALLNAME:L_C_ID%  死亡】
		ELSE
			PRINTFORM 【%CALLNAME:L_C_ID%  総験:{CFLAG:L_C_ID:住人_遺研_総数} 肉D:{CFLAG:L_C_ID:住人_肉体ダメージ} 精D:{CFLAG:L_C_ID:住人_精神ダメージ}】
		ENDIF
	ENDIF
	RESETCOLOR
NEXT

;-------------------------------------------------
;メンバーリスト
;ARG:0 参照したいの変数番号
;ARG:1 参照したいの値
;OPT_ARG:2 一致したIDをハイライト着色 (色はCOLOR.ERBで定義)
;-------------------------------------------------

@MEMBER_LIST2(L_KEY_ID, L_KEY_VALUE, OPT_HIGHLIGHT=-1)
#DIM L_C_ID      ;登録番号
#DIM L_KEY_ID    ;参照対象ID
#DIM L_KEY_VALUE ;一致検索値
#DIM L_CNT       ;カウント用
#DIM OPT_HIGHLIGHT  ; ハイライト着色用
L_CNT = 0
FOR L_C_ID, 0, CHARANUM
	SIF TALENT:L_C_ID:부재
		CONTINUE
	SIF CFLAG:L_C_ID:L_KEY_ID != L_KEY_VALUE
		CONTINUE
	SIF L_C_ID == OPT_HIGHLIGHT
		SETCOLOR COLOR("HIGHLIGHT")
	PRINTFORM [%CALLNAME:L_C_ID%]
	RESETCOLOR
	L_CNT ++
NEXT
FLAG:汎用変数 = L_CNT


;-------------------------------------------------
;メンバー合算
;ARG:0 参照したいの変数番号
;ARG:1 参照したいの値
;ARG:2 合算する変数番号
;-------------------------------------------------
@MEMBER_LIST3(L_MATCH, L_KEY, L_ADD)
#DIM L_C_ID  ;ループ用
#DIM L_MATCH ;参照CFLAG
#DIM L_KEY   ;一致検索値
#DIM L_ADD   ;加算対象ID
FLAG:汎用変数 = 0
FOR L_C_ID, 0, CHARANUM
	SIF CFLAG:L_C_ID:L_MATCH == L_KEY
		FLAG:汎用変数 += CFLAG:L_C_ID:L_ADD
NEXT

;-------------------------------------------------
;メンバーリスト(一致確認だけ)
;ARG:0 参照したいの変数番号
;ARG:1 参照したいの値
;ARG:2 0以上にすると != 0 で検索
;-------------------------------------------------
@MEMBER_LIST2_1(L_CFLAG, L_KEY, L_SWITCH)
#DIM L_CFLAG  ;参照CFLAG
#DIM L_KEY    ;一致検索値
#DIM L_SWITCH ;挙動変更用
#DIM L_C_ID   ;ループ用
FLAG:汎用変数 = 0
SELECTCASE L_SWITCH
CASE 0
	FLAG:汎用変数 = MAX(FINDLASTCHARA(CFLAG:L_CFLAG, L_KEY), 0)
	;最後尾の一致キャラを検索
CASE 1
	;最後尾の値が0でないキャラを検索
	FOR L_C_ID, CHARANUM - 1, -1, -1
		SIF CFLAG:L_C_ID:L_CFLAG == 0
			CONTINUE
		FLAG:汎用変数 = L_C_ID
		BREAK
	NEXT
ENDSELECT


;-------------------------------------------------
;メンバー呼び出し
;ARG:0 参照したいの変数番号
;ARG:1 参照したいの値
;-------------------------------------------------
@MEMBER_CALL(L_CFLAG, L_KEY)
#DIM L_CFLAG  ;参照CFLAG
#DIM L_KEY    ;一致検索値
#DIM L_C_ID   ;ループ用
FLAG:汎用変数 = 0
FLAG:支援_掛け合い = 0
FOR L_C_ID, 0, CHARANUM
	SIF  CFLAG:L_C_ID:L_CFLAG != L_KEY
		CONTINUE
	FLAG:汎用変数 ++
	;既に設定されている場合は30％で書き換え
	SIF !FLAG:支援_掛け合い || RAND:10 > 6
		FLAG:支援_掛け合い = L_C_ID
NEXT


;-------------------------------------------------
;メンバーリスト(随伴経験値増加)
;ARG:0 参照したいの変数番号
;ARG:1 参照したいの値
;-------------------------------------------------
@MEMBER_EXP(L_CFLAG, L_KEY)
#DIM L_CFLAG  ;参照CFLAG
#DIM L_KEY    ;一致検索値
#DIM L_C_ID   ;ループ用
FLAG:汎用変数 = 0
FOR L_C_ID, 0, CHARANUM
	SIF CFLAG:L_C_ID:L_CFLAG != L_KEY
		CONTINUE
	CFLAG:L_C_ID:住人_経験値 += FLAG:ターン経験値
	FLAG:汎用変数 ++ 
NEXT


;-------------------------------------------------
;全キャラリスト配列生成関数
;-------------------------------------------------
;元々はeraTW向けに実装した条件付きキャラリスト配列生成関数
;条件切替はばっさりオミットして簡易化
;登録番号を変えずにSORTする目的で導入
;渡された配列に現在存在する全てのキャラの番号を順にいれていく
;SORT項目が増えまくったのでこれでもいいかな、と
@MEMBER_LIST_ALL(SCENE = "DEFAULT", CHARA_LIST, CHARA_CNT)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS     SCENE      ;使用場面
#DIM  REF CHARA_LIST ;キャラ一覧
#DIM  REF CHARA_CNT  ;キャラカウンタ
VARSET CHARA_LIST
VARSET CHARA_CNT

;ループ中に分岐を仕込むのは一見簡潔にみえるが処理回数増えまくるのでやめとけ
SELECTCASE SCENE
CASE "EXPLORER"
	FOR LOCAL, 0, CHARANUM
		SIF LOCAL == MASTER
			CONTINUE
		SIF TALENT:LOCAL:부재 || TALENT:LOCAL:ボランティア
			CONTINUE
		SIF CFLAG:LOCAL:住人_探索者 == 0
			CONTINUE
		CHARA_LIST:CHARA_CNT = LOCAL
		CHARA_CNT ++
	NEXT
CASE "SUPPORT_随伴"
	FOR LOCAL, 0, CHARANUM
		SIF LOCAL == MASTER
			CONTINUE
		SIF TALENT:LOCAL:부재 || TALENT:LOCAL:ボランティア
			CONTINUE
		SIF CFLAG:LOCAL:住人_支援_随伴 == 0
			CONTINUE
		CHARA_LIST:CHARA_CNT = LOCAL
		CHARA_CNT ++
	NEXT
CASE "SUPPORT_後方"
	FOR LOCAL, 0, CHARANUM
		SIF LOCAL == MASTER
			CONTINUE
		SIF TALENT:LOCAL:부재 || TALENT:LOCAL:ボランティア
			CONTINUE
		SIF CFLAG:LOCAL:住人_支援_後方 == 0
			CONTINUE
		CHARA_LIST:CHARA_CNT = LOCAL
		CHARA_CNT ++
	NEXT
CASE "SUPPORT_回収"
	FOR LOCAL, 0, CHARANUM
		SIF LOCAL == MASTER
			CONTINUE
		SIF TALENT:LOCAL:부재 || TALENT:LOCAL:ボランティア
			CONTINUE
		SIF CFLAG:LOCAL:住人_支援_回収 == 0
			CONTINUE
		CHARA_LIST:CHARA_CNT = LOCAL
		CHARA_CNT ++
	NEXT

{
CASE "RPG", 
	"RPG_武器強化", 
	"RPG_技能成長", 
	"RPG_強化剤投与", 
	"RPG_超人薬投与", 
	"RPG_随伴配置", 
	"RPG_後方配置", 
	"RPG_回収配置", 
	"RPG_探索配置"
}
	FOR LOCAL, 0, CHARANUM
		SIF LOCAL == MASTER
			CONTINUE
		SIF TALENT:LOCAL:부재 || TALENT:LOCAL:ボランティア
			CONTINUE
		SIF TALENT:LOCAL:NPC == 999
			CONTINUE
		CHARA_LIST:CHARA_CNT = LOCAL
		CHARA_CNT ++
	NEXT
{
CASE 
	"MNG_登録_登録抹消", 
	"MNG_입주_一号室", "MNG_입주_二号室", "MNG_입주_三号室", "MNG_입주_四号室", "MNG_입주_五号室", 
	"MNG_입주_六号室", "MNG_입주_七号室", "MNG_입주_八号室", "MNG_입주_九号室", "MNG_입주_十号室", 
	
	"MNG_登録_레귤러", "MNG_登録_특별보호", "MNG_登録_名前変更", 
	"MNG_転職_성직자", "MNG_転職_메이드", "MNG_転職_아이돌", "MNG_転職_사회인", "MNG_転職_局員", "MNG_転職_창녀", 
	"MNG_配属_学園生徒", "MNG_配属_支援ボラ", "MNG_配属_ラブホ店員", "MNG_配属_カジノ店員", 
	"MNG_特殊_外見変更", "MNG_特殊_治療設備", "MNG_特殊_품종개량", "MNG_特殊_신체강화", "MNG_特殊_차세대창조", 
	"MNG_登録_교우제어", "MNG_登録_衣装変更", "MNG_登録_範囲消去", "MNG_登録_抹消中止"
}
	FOR LOCAL, 0, CHARANUM
		SIF LOCAL == MASTER
			CONTINUE
		SIF TALENT:LOCAL:부재 || TALENT:LOCAL:ボランティア
			CONTINUE
		SIF TALENT:LOCAL:NPC == 999
			CONTINUE
		CHARA_LIST:CHARA_CNT = LOCAL
		CHARA_CNT ++
	NEXT
CASE "NOT_VOLUNTEER"
	FOR LOCAL, 0, CHARANUM
		SIF TALENT:LOCAL:ボランティア
			CONTINUE
		CHARA_LIST:CHARA_CNT = LOCAL
		CHARA_CNT ++
	NEXT
{
CASE
	"AL_能力表示", 
	"AL_リーダー任命", 
	"AL_サポーター任命", 
	"AL_蘇生処置", 
	"AL_ボランティア終了"
}
	FOR LOCAL, 0, CHARANUM
		SIF TALENT:LOCAL:不在
			CONTINUE
		SIF TALENT:LOCAL:ボランティア == 0
			CONTINUE
		CHARA_LIST:CHARA_CNT = LOCAL
		CHARA_CNT ++
	NEXT
CASE "CAFE"
	FOR LOCAL, 0, CHARANUM
		SIF LOCAL == MASTER
			CONTINUE
		SIF TALENT:LOCAL:부재 || TALENT:LOCAL:ボランティア
			CONTINUE
		CHARA_LIST:CHARA_CNT = LOCAL
		CHARA_CNT ++
	NEXT
CASEELSE
	FOR LOCAL, 0, CHARANUM
		CHARA_LIST:CHARA_CNT = LOCAL
		CHARA_CNT ++
	NEXT
ENDSELECT

