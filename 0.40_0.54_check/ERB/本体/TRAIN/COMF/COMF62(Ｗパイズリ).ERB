;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Module		:COMFxxx.ERB
;	Facility	:TRAINコマンド(62:Ｗパイズリ)。
;
;	Licence		:ライセンスフリー。
;
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#

;===============================================================================
;  ◇記載関数一覧(記載順)
;
;		Module Name						Explanation
;
;	・ COM_FUNC_JUMP_Xxxx				取得:ジャンプ先
;	・ COM_FUNC_GET_NAME_Xxxx			取得:コマンド名称
;	・ COM_FUNC_ABLE_Xxxx				CHK:コマンド実行条件
;	・ COM_FUNC_EXE_Xxxx				コマンド実行
;	・ COM_FUNC_SRC_Xxxx				コマンドソース
;	・ COM_FUNC_TRAIN_MSG_Xxxx			コマンドメッセージ
;	・ COM_FUNC_GET_TYPE_Xxxx			取得:コマンド種別
;
;===============================================================================

;=================================================
;   sub COM_FUNC_GET_NAME_Xxxx
;=================================================
;   取得:コマンド名称
;-------------------------------------------------
; Output:
;  RESULTS				コマンド名称
;-------------------------------------------------
@COM_FUNC_GET_NAME_X62
RESULTS = Ｗ파이즈리

;=================================================
;   sub COM_FUNC_ABLE_Xxxx
;=================================================
;   CHK:コマンド実行条件
;-------------------------------------------------
; Output:
;  RESULT				真偽値
;-------------------------------------------------
@COM_FUNC_ABLE_X62

;- PLAYERにペニスが無いと不可
SIF !VAR_PENIS(PLAYER)
	RETURN 0

;- ASSIが居ないと不可
SIF ASSI < 0
	RETURN 0

;- 체력
SIF !BASE:체력 || !BASE:ASSI:체력
	RETURN 0

;- 両方女でないと不可
SIF !VAR_SEX() || !VAR_SEX(ASSI)
	RETURN 0

;- 隷属開始 + 기교
SIF (!MARK:예속각인) || (ABL:기교 < 3)
	RETURN 0

;- 隷属開始 + 기교
SIF (!MARK:ASSI:예속각인) || (ABL:ASSI:기교 < 3)
	RETURN 0

RETURN 1

;=================================================
;   sub COM_FUNC_EXE_Xxxx
;=================================================
;   コマンド実行
;-------------------------------------------------
@COM_FUNC_EXE_X62
#DIM L_P
#DIM L_T

L_P = PLAYER
L_T = TARGET

;-----------------------------
;COM別FLAGの設定
;-----------------------------

;- POSE:リセット
CALL POSE_RESET("Ｐ")

TEQUIP:L_T:ポイント奉仕 = [[TRAIN:Ｗパイズリ]]
TEQUIP:ASSI:ポイント奉仕 = [[TRAIN:Ｗパイズリ]]

;-----------------------------
;ソース追加処理
;-----------------------------

;-----------------------------
;その他の処理
;-----------------------------


;=================================================
;   sub COM_FUNC_SRC_Xxxx
;=================================================
;   コマンドソース
;-------------------------------------------------
@COM_FUNC_SRC_X62
#DIM L_P
#DIM L_T
#DIM L_TMP
#DIM L_I
VARSET LOCAL

L_P = PLAYER
L_T = TARGET

;-----------------------------
;COM別FLAGの設定
;-----------------------------

;- POSE:SET
CALL POSE_SET(L_T, [[占有部位:Ｂ]])
CALL POSE_SET(ASSI, [[占有部位:Ｂ]], 2)

;-----------------------------
;ソース追加処理
;-----------------------------

LOCAL:0 = 140
LOCAL:1 = 40
LOCAL:2 = 160

;-- 補正追加

;- 範囲:100%～200%
LOCAL:10 = (ABL:L_T:기교)
SELECTCASE LOCAL:10
	CASE 0 to 8
		L_TMP = 100 + (10 * LOCAL:10)
	CASEELSE
		L_TMP = 200
ENDSELECT
LOCAL:0 = LOCAL:0 * L_TMP / 100
LOCAL:1 = LOCAL:1 * L_TMP / 100
LOCAL:2 = LOCAL:2 * L_TMP / 100

;- ソース
SOURCE:L_P:快Ｃ += LOCAL:0
SOURCE:L_T:性行為 += LOCAL:1
SOURCE:L_T:性奉仕 += LOCAL:2


;-----------------------------
;経験の処理
;-----------------------------

;- TR:異性愛経験
CALL TR_EXP_ADD_HETEROSEX(L_T,L_P)
CALL TR_EXP_ADD_HETEROSEX(ASSI,L_P)

;- 各種経験
GTR_EXPUP:L_T:DB_EXPUP_POS("펠라경험") += 1
GTR_EXPUP:L_T:DB_EXPUP_POS("유혹경험") += 1
GTR_EXPUP:L_T:DB_EXPUP_POS("봉사쾌락경험") += 1

GTR_EXPUP:ASSI:DB_EXPUP_POS("펠라경험") += 1
GTR_EXPUP:ASSI:DB_EXPUP_POS("유혹경험") += 1
GTR_EXPUP:ASSI:DB_EXPUP_POS("봉사쾌락경험") += 1

;-----------------------------
;その他の処理
;-----------------------------



;=================================================
;   sub COM_FUNC_TRAIN_MSG_Xxxx
;=================================================
;   コマンドメッセージ
;-------------------------------------------------
@COM_FUNC_TRAIN_MSG_X62
;ASSIは出力済みに
TCVAR:ASSI:出力済みフラグ = 1

PRINTFORML %CALLNAME%와 %CALLNAME:ASSI%는 %CALLNAME:PLAYER%의 자지를 젖가슴으로 감싸고、
IF (VAR_바스트사이즈(TARGET) > 0) && (VAR_바스트사이즈(ASSI) > 0)
	PRINTFORM 그 터질듯한 젖가슴
ELSEIF (VAR_바스트사이즈(TARGET) < 0) && (VAR_바스트사이즈(ASSI) < 0)
	PRINTFORM 그 늘씬하고 아담한 가슴
ELSE
	PRINTFORM 모양 잡힌 아름다운 가슴
ENDIF
PRINTFORML 을 맞추고 %CALLNAME:PLAYER%의 자지를 훑으면서、열심히 빨아올리고 있다…

SIF (TALENT:반려 && TALENT:ASSI:반려)
	PRINTFORML 열심히 봉사하는 두 사람의 손가락엔、%CALLNAME:PLAYER%의 소유물이 된 증거인 반지가 반짝이고 있다…

WAIT

;=================================================
;   sub COM_FUNC_GET_TYPE_Xxxx
;=================================================
;   取得:コマンド種別
;-------------------------------------------------
; Output:
;  RESULT				コマンド種別
;-------------------------------------------------
@COM_FUNC_GET_TYPE_X62
RETURN [[COM種別:奉仕系]]

