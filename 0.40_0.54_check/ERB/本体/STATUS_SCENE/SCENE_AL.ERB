@AL_CHARA_SELECT(MODE_NOW = "AL_能力表示")
#DIMS         MODE_NOW
#DIM          MODE_ID
#DIM          L_INPUT
#DIM          NOW_GROUP = 1 ;ページ記憶用
#DIMS DYNAMIC OP_RELOAD     ;初回以外のページ更新とリスト更新を抑制する
{
#DIMS CONST   MODE_NAME = 
	"AL_能力表示", 
	"AL_リーダー任命", 
	"AL_サポーター任命", 
	"AL_蘇生処置", 
	"AL_ボランティア終了"
}

#DIM CONST    INPUT_研究対象設定 = 1011
#DIM CONST    INPUT_遺物詳細情報 = 1012
#DIM DYNAMIC  NOW_LEEDER

MODE_ID = FINDELEMENT(MODE_NAME, MODE_NOW)
SELECTCASE MODE_ID
CASE -1
	MODE_NOW '= "AL_能力表示"
	RESTART
ENDSELECT
CLEARLINE LINECOUNT

SELECTCASE MODE_NOW
CASE "AL_能力表示"
	SIF !STRCOUNT(OP_RELOAD, "「情報表示」")
		OP_RELOAD += "「情報表示」"
	OP_RELOAD '= REPLACE(OP_RELOAD, "「最終確認」", "")
	OP_RELOAD '= REPLACE(OP_RELOAD, "「キャラ固定」", "")
CASE "AL_ボランティア終了"
	SIF !STRCOUNT(OP_RELOAD, "「情報表示」")
		OP_RELOAD += "「情報表示」"
	SIF !STRCOUNT(OP_RELOAD, "「最終確認」")
		OP_RELOAD += "「最終確認」"
	SIF !STRCOUNT(OP_RELOAD, "「キャラ固定」")
		OP_RELOAD += "「キャラ固定」"
CASEELSE
	OP_RELOAD '= REPLACE(OP_RELOAD, "「情報表示」", "")
	OP_RELOAD '= REPLACE(OP_RELOAD, "「最終確認」", "")
	OP_RELOAD '= REPLACE(OP_RELOAD, "「キャラ固定」", "")
ENDSELECT

CALL LIST_CHARA_STATUS_MAIN(0, NOW_GROUP, OP_RELOAD, MODE_NOW, AL_WORDS())

L_INPUT = RESULT
OP_RELOAD '= "「ページ更新無し」「リスト更新無し」"
SELECTCASE L_INPUT
CASE 0
	NOW_GROUP = 1
	RETURN
CASE 1 TO CHARANUM - 1
	SELECTCASE MODE_NOW
	CASE "AL_リーダー任命"
		SIF FLAG:遺研_リーダー != 0
			NOW_LEEDER = FINDCHARA(CFLAG:個別番号, FLAG:遺研_リーダー, 1)
			
		IF NOW_LEEDER == L_INPUT
			TALENT:L_INPUT:ボランティア = 1
			FLAG:遺研_リーダー = 0
		ELSE
			SIF INRANGE(NOW_LEEDER, 1, CHARANUM - 1)
				TALENT:NOW_LEEDER:ボランティア = 1
			SIF TALENT:L_INPUT:ボランティア == 2
				FLAG:遺研_サポーター総数 --
			TALENT:L_INPUT:ボランティア = 3
			FLAG:遺研_リーダー = CFLAG:L_INPUT:個別番号
		ENDIF
	CASE "AL_サポーター任命"
		IF FLAG:遺研_リーダー == CFLAG:L_INPUT:個別番号
			SIF FLAG:遺研_サポーター総数 >= 3
				RESTART
			FLAG:遺研_リーダー = 0
			TALENT:L_INPUT:ボランティア = 2
			FLAG:遺研_サポーター総数 ++
		ELSEIF TALENT:L_INPUT:ボランティア == 2
			TALENT:L_INPUT:ボランティア = 1
			FLAG:遺研_サポーター総数 --
		ELSEIF FLAG:遺研_サポーター総数 >= 3
			RESTART
		ELSE
			TALENT:L_INPUT:ボランティア = 2
			FLAG:遺研_サポーター総数 ++
		ENDIF
	CASE "AL_蘇生処置"
		SIF MAX(CFLAG:L_INPUT:住人_肉体ダメージ, CFLAG:L_INPUT:住人_精神ダメージ) < 100
			RESTART
		IF MONEY < CFLAG:L_INPUT:住人_遺研_総数 * 1000
			PRINTFORMW 資金が不足しています。
			RESTART
		ENDIF
		
		CLEARLINE LINECOUNT
		PRINTFORML %CALLNAME:L_INPUT%を蘇生するには　資金{CFLAG:L_INPUT:住人_遺研_総数*1000}　かかります。
		PRINTFORML よろしいですか？(現資金{money})
		PRINTFORML 
		PRINTFORML [0] - はい
		PRINTFORML [1] - いいえ
		DO
			INPUT
			SELECTCASE RESULT
			CASE 0
				MONEY -= CFLAG:L_INPUT:住人_遺研_総数 * 1000
				CFLAG:L_INPUT:住人_肉体ダメージ = MIN(50, CFLAG:L_INPUT:住人_肉体ダメージ)
				CFLAG:L_INPUT:住人_精神ダメージ = MIN(50, CFLAG:L_INPUT:住人_精神ダメージ)
				PRINTFORMW %CALLNAME:L_INPUT%を蘇生しました。
			CASEELSE
			BREAK
			ENDSELECT
		LOOP 1
	CASE "AL_ボランティア終了"
		IF FLAG:遺研_リーダー == CFLAG:L_INPUT:個別番号
			FLAG:遺研_リーダー = 0
		ELSEIF TALENT:L_INPUT:ボランティア == 2
			FLAG:遺研_サポーター総数 --
		ENDIF
		DELCHARA L_INPUT
		OP_RELOAD '= REPLACE(OP_RELOAD, "「リスト更新無し」", "")
	ENDSELECT
CASE 1000 TO 1004
	MODE_NOW '= MODE_NAME:(L_INPUT % 1000)
CASE INPUT_研究対象設定
	CALL AL_ARTIFACT_LIST
CASE INPUT_遺物詳細情報
	SIF FLAG:遺研_研究対象 <= 0
		RESTART
	CLEARLINE LINECOUNT
	TRYCCALLFORM AL_Profile_{FLAG:遺研_研究対象}
	CATCH
		PRINTFORML エラー発生、スレッドに報告をお願いします
		PRINTFORMW 関数@AL_Profile_{FLAG:遺研_研究対象}が用意されていません
	ENDCATCH
CASEELSE
ENDSELECT
RESTART



@AL_LIST_CHARA_STATUS_HEADER(SCENE, LIST_NAME, CHARA_CNT, NOW_GROUP, LAST_GROUP)
#DIMS SCENE      ;現在の場面
#DIMS LIST_NAME  ;表示項目の分類＝PACK_NAME
#DIM  CHARA_CNT  ;キャラリスト配列のキャラ数
#DIM  NOW_GROUP  ;閲覧中のキャラグループ
#DIM  LAST_GROUP ;キャラグループの最大値
CALL AL_STATUS
DRAWLINE



;-------------------------------------------------
;AL用CALLNAME表示関数
;-------------------------------------------------
@AL_LIST_NAME(C_ID)
#DIM C_ID
SIF C_ID < 0
	RETURN
CALL INFO_TARGET_DELETE(C_ID)
PRINTFORM %SHORTEN_NAME(C_ID), 10, LEFT%
PRINT 　
CALL INFO_TARGET_VOLUNTEER(C_ID)
CALL INFO_TARGET_AL_EXP(C_ID)



@AL_INFO_TARGET_DETAIL(C_ID)
#DIM C_ID
CALL INFO_TARGET_GENDER(C_ID)
CALL INFO_TARGET_SOCIALITY(C_ID)


;-------------------------------------------------
;AL用ラベル使用一行情報表示関数
;-------------------------------------------------
@AL_INFO_CHARA_DETAIL_LABEL(C_LABEL, C_ID, SORT_TYPE, LABEL_LIST, LABEL_SIZE)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM      C_LABEL    ;ボタンとして表示させる数値
#DIM      C_ID       ;キャラの登録番号
#DIMS     SORT_TYPE  ;現在のソート順
#DIMS REF LABEL_LIST ;この行で使用するラベル一覧配列
#DIM      LABEL_SIZE ;ラベル一覧配列の要素数
;C_LABELが負数はありえないので表示内容のアナウンスに流用してます
IF C_LABEL < 0
	PRINTFORM %" " * 30%
	CALL INFO_LABEL_LIST(SORT_TYPE, LABEL_LIST, LABEL_SIZE)
	PRINTFORML 
ELSE
	CALL INFO_LABEL_ARRAY(C_ID, LABEL_LIST, LABEL_SIZE)
	PRINTFORML 
ENDIF


;-----------------------------
;-- 遺物調査用
;-----------------------------
@INFO_TARGET_VOLUNTEER(C_ID)
#DIM          C_ID
#DIMS CONST   JOB_NAME = "　", "　", "助", "主"

SELECTCASE TALENT:C_ID:ボランティア
CASE 3
	SETCOLOR 150,200,250
CASE 2
	SETCOLOR 150,250,200
CASE 1
ENDSELECT
PRINTFORM %JOB_NAME:(TALENT:C_ID:ボランティア)%
RESETCOLOR



;-----------------------------
;-- 遺物調査用
;-----------------------------
@INFO_TARGET_AL_EXP(C_ID)
#DIM          C_ID
CALL PRINT_LABEL("研", , (CFLAG:C_ID:住人_遺研_総数 <= 0 && FLAG:STATUS伏字モード ? COLOR("伏字") # -1), TOSTR(MIN(CFLAG:C_ID:住人_遺研_総数, 9999)), 4, , , , , , 1)



@AL_DIM_TABLE_LIST_PACK(SCENE, LIST_PACK, PACK_SIZE)
#FUNCTION
#DIMS SCENE
#DIMS REF LIST_PACK ;戻り値用、リスト配列
#DIM  REF PACK_SIZE ;
{
#DIMS CONST C_PACK = "基礎情報", 
	"性格ST", 
	"経済ST", 
	"身体ST", 
	"フェロモンST", 
	"性感覚ST", 
	"性技能ABL", 
	"性癖ABL", 
	"探索ABL", 
}
ARRAYCOPY "C_PACK", "LIST_PACK"
PACK_SIZE = VARSIZE("C_PACK")
RETURNF


;-------------------------------------------------
;AL用ラベル使用部分キャラリスト関数
;-------------------------------------------------
@AL_LIST_CHARA_STATUS_GROUP(SCENE, CHARA_LIST, SHOW_START, SHOW_END, INPUT_MIN, INPUT_MAX, SORT_TYPE, LIST_NAME)
#DIMS     SCENE         ;使用するシーン
#DIM  REF CHARA_LIST    ;キャラリスト配列
#DIM      SHOW_START    ;表示開始のキャララベルID
#DIM      SHOW_END      ;表示終了のキャララベルID
#DIM      INPUT_MIN     ;表示中キャララベルIDの最小値
#DIM      INPUT_MAX     ;表示中キャララベルIDの最大値
#DIMS     LABEL_LIST, 30;表示内容の取得用
#DIM      LABEL_SIZE    ;表示内容の数
#DIMS     SORT_TYPE     ;現在のソート順
#DIMS     LIST_NAME     ;現在の表示内容
#DIMS     PREV_LIST     ;前回表示した内容
#DIM      C_LABEL       ;キャララベルID
#DIM      DETAIL_OP     ;基礎情報の表示内容の切り替え用

IF LIST_NAME == "基礎情報"
	FOR C_LABEL, SHOW_START, SHOW_END
		IF INRANGE(C_LABEL, INPUT_MIN, INPUT_MAX)
			PRINTFORM [{C_LABEL, 3}]
			CALL AL_LIST_NAME(CHARA_LIST:C_LABEL)
			CALL AL_INFO_TARGET_DETAIL(CHARA_LIST:C_LABEL)
		ENDIF
		PRINTL
	NEXT
	RETURN
ENDIF

;ラベルリスト取得
IF LIST_NAME == PREV_LIST
ELSE
	CALLF DIM_TABLE_LABEL_LIST(LIST_NAME, LABEL_LIST, LABEL_SIZE)
	PREV_LIST '= LIST_NAME
ENDIF

;デバッグ確認用
;CALL VAR_SHOW_STR_ARRAY(LABEL_LIST, LABEL_SIZE)
CALL AL_INFO_CHARA_DETAIL_LABEL(-1, 0, SORT_TYPE, LABEL_LIST, LABEL_SIZE)
DRAWLINE
FOR C_LABEL, SHOW_START, SHOW_END
	IF INRANGE(C_LABEL, INPUT_MIN, INPUT_MAX)
		PRINTFORM [{C_LABEL, 3}]
		CALL AL_LIST_NAME(CHARA_LIST:C_LABEL)
		CALL AL_INFO_CHARA_DETAIL_LABEL(C_LABEL, CHARA_LIST:C_LABEL, SORT_TYPE, LABEL_LIST, LABEL_SIZE)
	ELSE
		PRINTL
	ENDIF
NEXT

;-------------------------------------------------
;AL用シーン切替可能入力関数
;-------------------------------------------------
@AL_LIST_CHARA_INPUT(SCENE, OP, CHARA_LIST, CHARA_CNT, NOW_GROUP, LAST_GROUP, LIST_PACK, PACK_PAGE, PACK_SIZE, INPUT_MIN, INPUT_MAX, SORT_TYPE, GET_LIST)
#DIMS     SCENE      ;使用場面
#DIMS     OP         ;オプション文字列
#DIM  REF CHARA_LIST ;キャラリスト一覧配列
#DIM      CHARA_CNT  ;リスト中のキャラ数
#DIM  REF NOW_GROUP  ;現在表示中のキャラグループ
#DIM      LAST_GROUP ;キャラグループの最後尾
#DIMS REF LIST_PACK  ;パックリスト配列
#DIM  REF PACK_PAGE  ;表示データのページ数
#DIM      PACK_SIZE  ;パックリスト配列のサイズ
#DIM      INPUT_MIN  ;表示中キャララベルIDの最小値
#DIM      INPUT_MAX  ;表示中キャララベルIDの最大値
#DIMS REF SORT_TYPE  ;現在のソート順
#DIM  REF GET_LIST   ;キャラリスト更新処理用フラグ
#DIMS     INPUT_STR  ;入力内容の一時保持
#DIM      INPUT_INT  ;入力内容の一時保持
#DIM      L_INPUT    ;入力内容の一時保持

#DIM DYNAMIC L_SUP_CNT, 4         ;各部隊に配属済みのキャラ数
VARSET INPUT_STR
VARSET INPUT_INT

IF CHARA_CNT > 0 && NOW_GROUP > 1
	PRINTFORM %" " * 29%
ELSE
	PRINTFORM 　
	PRINTBUTTON "[7777] - 　　　　一括任命解除", "7777"
ENDIF
PRINTFORM 　　
IF CHARA_CNT > 0 && NOW_GROUP > 1
	PRINTBUTTON "[8888] - 　前のキャラ　", "8888"
ELSE
	PRINTFORM %" " * 23%
ENDIF

PRINTFORML 

PRINTFORM 　
PRINTBUTTON @"[4444] - %LIST_PACK:((PACK_PAGE - 1 + PACK_SIZE) % PACK_SIZE), 20%", "4444"
PRINTFORM 　
PRINTFORM %" " * 23%
PRINTFORM 　
PRINTBUTTON @"[6666] - %LIST_PACK:((PACK_PAGE + 1 + PACK_SIZE) % PACK_SIZE), 20%", "6666"
PRINTFORML 
IF CHARANUM < 200
	PRINTFORM 　
	PRINTBUTTON "[1111] - 　　ボランティア募集", "1111"
	PRINTFORM 　
ELSE
	PRINTFORM %" " * 29%
	PRINTFORM 　　
ENDIF
IF CHARA_CNT > 0 && NOW_GROUP < LAST_GROUP
	PRINTBUTTON "[2222] - 　다음 캐릭터　", "2222"
ELSE
	PRINTFORM %" " * 23%
ENDIF
PRINTFORML 

DRAWLINE
PRINTFORM 　人員｜
SIF SCENE == "AL_能力表示"
	SETCOLOR COLOR("黄色")
PRINTBUTTON "[1000] - 　　　　能力表示", "1000"
PRINTFORM 　
RESETCOLOR
SIF SCENE == "AL_リーダー任命"
	SETCOLOR COLOR("黄色")
PRINTBUTTON "[1001] - 　　リーダー任命", "1001"
PRINTFORM 　
RESETCOLOR
SIF SCENE == "AL_サポーター任命"
	SETCOLOR COLOR("黄色")
PRINTBUTTON "[1002] - 　サポーター任命", "1002"
PRINTFORM 　
RESETCOLOR
PRINTFORML 

PRINTFORM 　　　｜
SIF SCENE == "AL_蘇生処置"
	SETCOLOR COLOR("黄色")
PRINTBUTTON "[1003] - 　　　　蘇生処置", "1003"
PRINTFORM 　
RESETCOLOR
SIF SCENE == "AL_ボランティア終了"
	SETCOLOR COLOR("黄色")
PRINTBUTTON "[1004] - ボランティア終了", "1004"
PRINTFORM 　
RESETCOLOR
PRINTFORML 

PRINTFORM 　遺物｜
PRINTBUTTON "[1011] - 　　研究対象設定", "1011"
PRINTFORM 　
PRINTBUTTON "[1012] - 　　遺物詳細情報", "1012"
PRINTFORM 　
PRINTBUTTON "[2000] - 　　　　施設紹介", "2000"
PRINTFORM 　
PRINTFORML 

DRAWLINE
PRINTFORM 　
PRINTBUTTON "[9999] - 　　　　もどる　　　　", "9999"
PRINTFORML 
PRINTFORML 

DO
	INPUTS
	INPUT_STR '= RESULTS
	SIF !STRLENS(INPUT_STR)
		GOTO AF_RETRY
	IF ISNUMERIC(INPUT_STR)
		INPUT_INT = TOINT(INPUT_STR)
		SELECTCASE INPUT_INT
		;リーダー任命/サポーター任命/ボランティア解任/研究対象設定/遺物詳細情報
		CASE 1000, 1001, 1002, 1003, 1004, 1011, 1012
			RETURN INPUT_INT
		CASE 2000
			CLEARLINE LINECOUNT
			PRINTFORML ボランティアに遺物を調査させることで研究し、有用なデータを獲得する施設。
			PRINTFORML 
			PRINTFORML 研究に成功した場合LPが上昇する、上昇したLPはアーティファクトを強化することができる。
			PRINTFORML 遺物研究する為の方法は以下の通り。
			PRINTFORML 1.ボランティア募集でキャラを加入させる
			PRINTFORML 2.研究対象を選ぶ
			PRINTFORML 3.ボランティアの中でリーダーを任命させる(サポーターは任意、一部＋補正が入る)
			PRINTFORML 4.ターン経過中に成功失敗のイベントが発生、成功した場合LPを獲得する。
			PRINTFORML 
			PRINTFORML 注意！　研究した場合リーダーorサポーターは肉体・精神ダメージを受ける。
			PRINTFORML 　　　　肉体or精神ダメージが100越えるとそのボランティアは死亡する。
			PRINTFORMW 
			RETURN -1
		;一括任命解除
		CASE 7777
			FOR LOCAL, 0, CHARA_CNT
				TALENT:(CHARA_LIST:LOCAL):ボランティア = 1
			NEXT
			FLAG:遺研_リーダー = 0
			FLAG:遺研_サポーター総数 = 0
			RETURN -1
		CASE 8888
			SIF !(CHARA_CNT > 0 && NOW_GROUP > 1)
				GOTO AF_RETRY
			NOW_GROUP --
			RETURN -1
		CASE 1111
			SIF CHARANUM >= 200
				GOTO AF_RETRY
			CALL CITY_SCOUT
			SIF RESULT
				GET_LIST = 1
			RETURN -1
		CASE 2222
			SIF !(CHARA_CNT > 0 && NOW_GROUP < LAST_GROUP)
				GOTO AF_RETRY
			NOW_GROUP ++
			RETURN -1
		CASE 4444
			PACK_PAGE = (PACK_PAGE - 1 + PACK_SIZE) % PACK_SIZE
			RETURN -1
		CASE 6666
			PACK_PAGE = (PACK_PAGE + 1 + PACK_SIZE) % PACK_SIZE
			RETURN -1
		CASE 9999
			NOW_GROUP = 1
			RETURN
		CASEELSE
		;キャラが選択された時
			SIF !INRANGE(INPUT_INT, INPUT_MIN, INPUT_MAX)
				GOTO AF_RETRY
			;ここだけDO-LOOPぬけます
			BREAK
			$AF_RETRY
			CALL C_INPUT_ERROR("無効な入力です")
			CONTINUE
		ENDSELECT
	ELSE
		;STRで受け取った時、SORT処理なので処理が可能な場合は全てRESTART
		;妙な文字列を受け取った場合にはCONTINUE
		;DO-LOOPを脱出できるのはINTかつ、リスト上のキャラを選択した場合のみ
		CALL CHARA_SORT_CTRL(CHARA_LIST, CHARA_CNT, INPUT_STR, SORT_TYPE)
		IF RESULT
			RETURN -1
		ELSE
			GOTO AF_RETRY
		ENDIF
	ENDIF
LOOP 1

IF STRCOUNT(OP, "「情報表示」")
	CALL INFO_STATUS_GROUP(CHARA_LIST, INPUT_INT, CHARA_CNT, OP)
	SIF RESULT < 0
		RETURN -1
	SIF STRCOUNT(OP, "「最終確認」")
		RETURN RESULT
	RETURN CHARA_LIST:INPUT_INT
ELSE
	RETURN CHARA_LIST:INPUT_INT
ENDIF

