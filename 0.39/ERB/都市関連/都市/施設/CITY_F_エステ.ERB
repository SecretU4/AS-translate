;-------------------------------------------------
;施設　エステ
;-------------------------------------------------
@CITY_F_5(ARG:0,ARG:1)
#DIM L_INPUT
#DIM L_M
#DIM L_I
#DIM L_MONEY
#DIM L_PARTS_TYPE
#DIM DYNAMIC L_CHANGE_H_COLOR = -1 ;
#DIM DYNAMIC L_CHANGE_H_STYLE = -1 ;えっちすたいるではなくヘアスタイルだ
#DIM DYNAMIC L_CHANGE_E_COLOR = -1 ;
#DIM         L_NOW_H_COLOR         ;画像表示用に一時変更する
#DIM         L_NOW_H_STYLE         ;
#DIM         L_NOW_E_COLOR         ;
L_MONEY = 0
CLEARLINE LINECOUNT
DRAWLINE
PRINTFORML 【エステ】　所持金:{MONEY}
DRAWLINE
PRINTFORML 住民の見た目を一部変更します。
DRAWLINE
PRINTFORML 【変更内容】

PRINTFORM 【髪色:
IF L_CHANGE_H_COLOR == -1
	PRINTFORML  変更なし】
ELSE
	PRINTFORML %INFO_STR_HAIRCOLOR(L_CHANGE_H_COLOR)%】
ENDIF

PRINTFORM 【髪型:
IF L_CHANGE_H_STYLE == -1
	PRINTFORML  変更なし】
ELSE
	PRINTFORML %INFO_STR_HAIR(L_CHANGE_H_STYLE)%】
ENDIF

PRINTFORM 【瞳色:
IF L_CHANGE_E_COLOR == -1
	PRINTFORML  変更なし】
ELSE
	PRINTFORML %INFO_STR_EYECOLOR(L_CHANGE_E_COLOR)%】
ENDIF

DRAWLINE
PRINTFORML [1] 髪色の変更
IF FLAG:エステ >= 3
	PRINTFORML [2] 髪型の変更
ELSE
	PRINTFORML  髪型の変更(レベル3以上必要)
ENDIF
IF FLAG:エステ >= 6
	PRINTFORML [3] 瞳色の変更
ELSE
	PRINTFORML  瞳色の変更(レベル6以上必要)
ENDIF
PRINTFORML 
PRINTFORML [100] 適用　(￥10000)
PRINTFORML 
PRINTFORML [999] 戻る
INPUT
L_INPUT = RESULT
L_M = RESULT
SELECTCASE L_INPUT
CASE 1
	FOR L_PARTS_TYPE, 10, -1, -1
		PRINTFORML [{L_PARTS_TYPE, 2}] %INFO_STR_HAIRCOLOR(L_PARTS_TYPE)%
	NEXT
	PRINTFORML 
	PRINTFORML [-1] 変更なし
	DO
		INPUT
		L_PARTS_TYPE = RESULT
		SIF INRANGE(L_PARTS_TYPE, -1, 10)
			BREAK
	LOOP 1
	L_CHANGE_H_COLOR = L_PARTS_TYPE
CASE 2
	SIF FLAG:エステ < 3
		RESTART
	FOR L_PARTS_TYPE, 24, -1, -1
		PRINTFORML [{L_PARTS_TYPE, 2}] %INFO_STR_HAIR(L_PARTS_TYPE)%
	NEXT
	PRINTFORML 
	PRINTFORML [-1] 変更なし
	DO
		INPUT
		L_PARTS_TYPE = RESULT
		SIF INRANGE(L_PARTS_TYPE, -1, 24)
			BREAK
	LOOP 1
	L_CHANGE_H_STYLE = L_PARTS_TYPE
CASE 3
	SIF FLAG:エステ < 6
		RESTART
	FOR L_PARTS_TYPE, 5, -1, -1
		PRINTFORML [{L_PARTS_TYPE, 2}] %INFO_STR_EYECOLOR(L_PARTS_TYPE)%
	NEXT
	PRINTFORML 
	PRINTFORML [-1] 変更なし
	DO
		INPUT
		L_PARTS_TYPE = RESULT
		SIF INRANGE(L_PARTS_TYPE, -1, 5)
			BREAK
	LOOP 1
	L_CHANGE_E_COLOR = L_PARTS_TYPE
CASE 100
	IF MONEY < 10000
		PRINTFORMW 資金が不足しています。
		RESTART
	ELSEIF ALLSAMES(-1, L_CHANGE_H_COLOR, L_CHANGE_H_STYLE, L_CHANGE_E_COLOR)
		PRINTFORMW 変更内容が設定されていません。
		RESTART
	ENDIF
	CLEARLINE LINECOUNT
	DRAWLINE
	PRINTFORML 適用させる住民を選択して下さい。
	DRAWLINE
	CALL RPG_LIST_CHARA
	L_I = RESULT
	IF L_I == 1000
		RESTART
	ELSEIF L_I == 0
		PRINTFORMW あなたは選択できません
		RESTART
	ELSEIF !INRANGE(L_I, 0, CHARANUM - 1)
		PRINTFORMW 入力された番号はキャラの範囲外です。
		RESTART
	ELSEIF TALENT:L_I:NPC == 999
		PRINTFORMW NPCは選択できません。
		RESTART
	;何らかの処理が適用される場合は個別に抜けるよ
	ELSEIF L_CHANGE_H_COLOR >= 0 && L_CHANGE_H_COLOR != CFLAG:L_I:髪色
	ELSEIF L_CHANGE_H_STYLE >= 0 && L_CHANGE_H_STYLE != CFLAG:L_I:髪型
	ELSEIF L_CHANGE_E_COLOR >= 0 && L_CHANGE_E_COLOR != CFLAG:L_I:瞳色
	ELSE
		PRINTFORMW %CALLNAME:L_I%は既に指定の容姿になっています。
		RESTART
	ENDIF
;-------------------------------------------------
;描画処理
;-------------------------------------------------
	DRAWLINE
	PRINTFORML %CALLNAME:L_I%の容姿を変更します
	DRAWLINE
	PRINTFORML 　　　　　
	PRINTFORML 　　　　　
	PRINTFORML 　　　　　
	PRINTFORML 　　　　　　→
	PRINTFORML 　　　　　
	PRINTFORML 　　　　　
	PRINTFORML 　　　　　
	HTML_PRINT C_PICTURE_HTML(L_I, 0, -702, 0, 850)
	L_NOW_H_COLOR = CFLAG:L_I:髪色
	L_NOW_H_STYLE = CFLAG:L_I:髪型
	L_NOW_E_COLOR = CFLAG:L_I:瞳色
	SIF L_CHANGE_H_COLOR > -1
		CFLAG:L_I:髪色 = L_CHANGE_H_COLOR
	SIF L_CHANGE_H_STYLE > -1
		CFLAG:L_I:髪型 = L_CHANGE_H_STYLE
	SIF L_CHANGE_E_COLOR > -1
		CFLAG:L_I:瞳色 = L_CHANGE_E_COLOR
	HTML_PRINT C_PICTURE_HTML(L_I, 800, -800, 0, 850)
	DRAWLINE
	PRINTFORML この変更を実行しますか？
	PRINTFORML [1] 実行する
	PRINTFORML [0] やめる
	DO
		INPUT
		IF !RESULT
			CFLAG:L_I:髪色 = L_NOW_H_COLOR
			CFLAG:L_I:髪型 = L_NOW_H_STYLE
			CFLAG:L_I:瞳色 = L_NOW_E_COLOR
			RESTART
		ENDIF
		SIF RESULT == 1
			BREAK
	LOOP 1
	PRINTFORMW %CALLNAME:L_I%の容姿を変更しました。
	MONEY -= 10000
CASE 999
	RETURN
CASEELSE
ENDSELECT
RESTART









