
;-------------------------------------------------
;施設　品種改良施設
;-------------------------------------------------
@CITY_F_54
#DIM L_INPUT
CLEARLINE LINECOUNT
DRAWLINE
PRINTFORML 【品種改良施設】　所持金:{MONEY}
DRAWLINE
PRINTFORML [0] ゲノム移植 ￥300000
PRINTFORML
PRINTFORML
PRINTFORML [999] 戻る
INPUT

L_INPUT = RESULT
SELECTCASE L_INPUT
	CASE 0
		IF MONEY < 300000
			PRINTFORMW 資金が不足しています。
		ELSE
			CALL CITY_F_54_GI
		ENDIF
	CASE 999
		RETURN
	CASEELSE
ENDSELECT
RESTART




;-------------------------------------------------
;ゲノム移植
;-------------------------------------------------
@CITY_F_54_GI
#DIM L_INPUT
#DIM L_C_ID
CLEARLINE LINECOUNT
DRAWLINE
PRINTFORML 【ゲノム移植】
PRINTFORML ゲノムアイテムを住民に移植します。
PRINTFORML 移植されるとステータスの底上げされます。
DRAWLINE
CALL LIST_CHARA
L_C_ID = RESULT
IF L_C_ID == 1000
	JUMP CITY_F_54
ELSEIF !INRANGE(L_C_ID, 0, CHARANUM - 1)
	PRINTFORMW 入力された番号はキャラの範囲外です
ELSEIF L_C_ID == 0
	PRINTFORMW あなたは選択できません
ELSEIF TALENT:L_C_ID:NPC == 999
	PRINTFORMW NPCは選択できません
ELSE
	JUMP CITY_F_54_GI2(L_C_ID)
ENDIF
RESTART



;-------------------------------------------------
;ゲノム移植2
;-------------------------------------------------
@CITY_F_54_GI2(L_C_ID)
#DIM L_C_ID  ;対象の登録番号
#DIM L_GENOM ;ループ処理用、参照しているゲノム≒FLAGやCFLAGのID
#DIM L_INPUT ;
CLEARLINE LINECOUNT
DRAWLINE
PRINTFORML 【%CALLNAME:L_C_ID%の遺伝子情報】
DRAWLINE
FOR L_GENOM, 1800, 1820
	IF CFLAG:L_C_ID:L_GENOM != 0
		SETCOLOR 50,250,250
		PRINTFORM 【%CFLAGNAME:L_GENOM%:済】
	ELSE
		SETCOLOR 250,50,50
		PRINTFORM 【%CFLAGNAME:L_GENOM%:未】
	ENDIF
	SIF L_GENOM > 1800 && (L_GENOM % 5 == 0)
		PRINTFORML
	RESETCOLOR
NEXT

DRAWLINE
PRINTFORML 移植するゲノムを選択して下さい。
DRAWLINE
;同じ変数を使いまわしてますが
;アレ↑CFLAG、コレ↓FLAGです
FOR L_GENOM, 2500, 2520
	PRINTFORM [{L_GENOM - 2500, 2}]【%FLAGNAME:L_GENOM%: {FLAG:L_GENOM, 3}個】
	SIF L_GENOM > 2500 && (L_GENOM % 5 == 0)
		PRINTFORML
NEXT


PRINTFORML
PRINTFORML [999] 戻る
INPUT
L_INPUT = RESULT
SELECTCASE L_INPUT
	CASE 999
		RETURN
	CASE 0 TO 19
		IF MONEY < 300000
			PRINTFORMW 資金が不足しています。
			RESTART
		ELSEIF FLAG:(2500 + L_INPUT) <= 0
			PRINTFORMW ゲノムがありません。
			RESTART
		ELSEIF CFLAG:L_C_ID:(1800 + L_INPUT) != 0
			PRINTFORMW 既に移植済みです
			RESTART
		ENDIF
		PRINTFORML %CALLNAME:L_C_ID%に%CFLAGNAME:(1800 + L_INPUT)%を移植しました。
		PRINTFORMW 一部ステータスが2上昇し、技能ポイントに5加算されました。
		CFLAG:L_C_ID:住人_最大体力 += 2
		CFLAG:L_C_ID:住人_現在体力 += 2
		CFLAG:L_C_ID:住人_最大気力 += 2
		CFLAG:L_C_ID:住人_現在気力 += 2
		CFLAG:L_C_ID:住人_ATK      += 2
		CFLAG:L_C_ID:住人_DEF      += 2
		CFLAG:L_C_ID:住人_HIT      += 2
		CFLAG:L_C_ID:住人_SPD      += 2
		CFLAG:L_C_ID:住人_最大BRE  += 2
		CFLAG:L_C_ID:住人_現在BRE  += 2
		CFLAG:L_C_ID:住人_技能ポイント += 5
		
		CFLAG:L_C_ID:(1800 + L_INPUT) = 1
		FLAG:(2500 + L_INPUT) -= 1
		MONEY -= 300000
ENDSELECT
RESTART
