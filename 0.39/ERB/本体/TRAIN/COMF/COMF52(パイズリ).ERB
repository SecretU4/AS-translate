;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Module		:COMFxxx.ERB
;	Facility	:TRAINコマンド(52:パイズリ)。
;
;	Licence		:ライセンスフリー。
;
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#

;===============================================================================
;  ◇記載関数一覧(記載順)
;
;		Module Name						Explanation
;
;	・ COM_FUNC_JUMP_Xxxx				取得:ジャンプ先
;	・ COM_FUNC_GET_NAME_Xxxx			取得:コマンド名称
;	・ COM_FUNC_ABLE_Xxxx				CHK:コマンド実行条件
;	・ COM_FUNC_EXE_Xxxx				コマンド実行
;	・ COM_FUNC_SRC_Xxxx				コマンドソース
;	・ COM_FUNC_TRAIN_MSG_Xxxx			コマンドメッセージ
;	・ COM_FUNC_GET_TYPE_Xxxx			取得:コマンド種別
;
;===============================================================================

;=================================================
;   sub COM_FUNC_GET_NAME_Xxxx
;=================================================
;   取得:コマンド名称
;-------------------------------------------------
; Output:
;  RESULTS				コマンド名称
;-------------------------------------------------
@COM_FUNC_GET_NAME_X52
RESULTS = パイズリ

;=================================================
;   sub COM_FUNC_ABLE_Xxxx
;=================================================
;   CHK:コマンド実行条件
;-------------------------------------------------
; Output:
;  RESULT				真偽値
;-------------------------------------------------
@COM_FUNC_ABLE_X52

;- PLAYERにペニスが無いと不可
SIF !VAR_PENIS(PLAYER)
	RETURN 0

;- TR:奉仕可能
SIF !TR_CHK_奉仕可能()
	RETURN 0

;- 技巧
SIF ABL:技巧 < 3
	RETURN 0

RETURN 1

;=================================================
;   sub COM_FUNC_EXE_Xxxx
;=================================================
;   コマンド実行
;-------------------------------------------------
@COM_FUNC_EXE_X52
#DIM L_P
#DIM L_T

L_P = PLAYER
L_T = TARGET

;-----------------------------
;COM別FLAGの設定
;-----------------------------

;- POSE:リセット
CALL POSE_RESET("Ｐ")

TEQUIP:L_T:ポイント奉仕 = [[TRAIN:パイズリ]]

;-----------------------------
;ソース追加処理
;-----------------------------

;-----------------------------
;その他の処理
;-----------------------------


;=================================================
;   sub COM_FUNC_SRC_Xxxx
;=================================================
;   コマンドソース
;-------------------------------------------------
@COM_FUNC_SRC_X52
#DIM L_P
#DIM L_T
#DIM L_TMP
#DIM L_I
VARSET LOCAL

L_P = PLAYER
L_T = TARGET

;-----------------------------
;COM別FLAGの設定
;-----------------------------

;- POSE:SET
CALL POSE_SET(L_T, [[占有部位:Ｂ]])

;-----------------------------
;ソース追加処理
;-----------------------------

LOCAL:0 = 140
LOCAL:1 = 40
LOCAL:2 = 160
LOCAL:3 = 40

;-- 補正追加

;- 範囲:100%～200%
LOCAL:10 = ABL:L_T:技巧
SELECTCASE LOCAL:10
	CASE 0 to 8
		L_TMP = 100 + (10 * LOCAL:10)
	CASEELSE
		L_TMP = 200
ENDSELECT
LOCAL:0 = LOCAL:0 * L_TMP / 100
LOCAL:1 = LOCAL:1 * L_TMP / 100
LOCAL:2 = LOCAL:2 * L_TMP / 100
LOCAL:3 = LOCAL:3 * L_TMP / 100

;- ソース
SOURCE:L_P:快Ｃ += LOCAL:0
SOURCE:L_T:性行為 += LOCAL:1
SOURCE:L_T:性奉仕 += LOCAL:2
SOURCE:L_T:快Ｂ += LOCAL:3


;-----------------------------
;経験の処理
;-----------------------------

;- TR:異性愛経験
CALL TR_EXP_ADD_HETEROSEX(L_T,L_P)

;- 各種経験
GTR_EXPUP:L_T:DB_EXPUP_POS("パイズリ経験") += 1
GTR_EXPUP:L_T:DB_EXPUP_POS("誘惑経験") += 2
GTR_EXPUP:L_T:DB_EXPUP_POS("奉仕快楽経験") += 1

;-----------------------------
;その他の処理
;-----------------------------



;=================================================
;   sub COM_FUNC_TRAIN_MSG_Xxxx
;=================================================
;   コマンドメッセージ
;-------------------------------------------------
@COM_FUNC_TRAIN_MSG_X52

PRINTFORM %CALLNAME%は
SIF MARK:陥落刻印
	PRINT うっとりとした表情をしながら、
;貧乳
IF VAR_バストサイズ(TARGET) < 0
	PRINTL 薄い胸だけでなく舌も使って懸命にペニス全体を刺激していた…
;巨乳
ELSEIF VAR_バストサイズ(TARGET) == 1
	PRINTL ペニスを豊満な胸で包み込み、余った亀頭を舐めて刺激している…
;爆乳
ELSEIF VAR_バストサイズ(TARGET) >= 2
	PRINTL 豊満すぎる程の胸でペニスを包み隠してしまい、時折舌を入れて、懸命にペニスを舐め上げている…
ELSE
	PRINTL 胸でペニスをはさみ、余った部分を舌で、刺激している…
ENDIF

WAIT


;=================================================
;   sub COM_FUNC_GET_TYPE_Xxxx
;=================================================
;   取得:コマンド種別
;-------------------------------------------------
; Output:
;  RESULT				コマンド種別
;-------------------------------------------------
@COM_FUNC_GET_TYPE_X52
RETURN [[COM種別:奉仕系]]

