;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Module		:COMFxxx.ERB
;	Facility	:TRAINコマンド(102:騎乗位する)。
;
;	Licence		:ライセンスフリー。
;
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#

;===============================================================================
;  ◇記載関数一覧(記載順)
;
;		Module Name						Explanation
;
;	・ COM_FUNC_GET_NAME_Xxxx			取得:コマンド名称
;	・ COM_FUNC_ABLE_Xxxx				CHK:コマンド実行条件
;	・ COM_FUNC_EXE_Xxxx				コマンド実行
;	・ COM_FUNC_SRC_Xxxx				コマンドソース
;	・ COM_FUNC_TRAIN_MSG_Xxxx			コマンドメッセージ
;	・ COM_FUNC_GET_TYPE_Xxxx			取得:コマンド種別
;
;===============================================================================

;=================================================
;   sub COM_FUNC_GET_NAME_Xxxx
;=================================================
;   取得:コマンド名称
;-------------------------------------------------
; Output:
;  RESULTS				コマンド名称
;-------------------------------------------------
@COM_FUNC_GET_NAME_X102
IF TFLAG:サブコマンド実行中フラグ
	RESULTS = 逆騎乗位
ELSE
	RESULTS = 騎乗位する
ENDIF

;=================================================
;   sub COM_FUNC_ABLE_Xxxx
;=================================================
;   CHK:コマンド実行条件
;-------------------------------------------------
; Output:
;  RESULT				真偽値
;-------------------------------------------------
@COM_FUNC_ABLE_X102

;- TR:性交可能(Ｖ) (※逆)
SIF !TR_CHK_ABLE_SEX_V(PLAYER, TARGET)
	RETURN 0

;- TR:性交可能(逆位置別)
SIF !TR_CHK_ABLE_REV_SEX_POINT("騎乗位")
	RETURN 0

RETURN 1

;=================================================
;   sub COM_FUNC_EXE_Xxxx
;=================================================
;   コマンド実行
;-------------------------------------------------
@COM_FUNC_EXE_X102
#DIM L_P
#DIM L_T

L_P = PLAYER
L_T = TARGET

;-----------------------------
;COM別FLAGの設定
;-----------------------------

;- POSE:リセット
CALL POSE_RESET("Ｖ")

TEQUIP:L_T:ポイント性交 = [[TRAIN:騎乗位する]]


;-----------------------------
;ソース追加処理
;-----------------------------

;-----------------------------
;その他の処理
;-----------------------------


;=================================================
;   sub COM_FUNC_SRC_Xxxx
;=================================================
;   コマンドソース
;-------------------------------------------------
@COM_FUNC_SRC_X102
#DIM L_P
#DIM L_T
#DIM L_TMP
#DIM L_I
VARSET LOCAL

L_P = PLAYER
L_T = TARGET

;-----------------------------
;COM別FLAGの設定
;-----------------------------

;- POSE:SET (※逆)
CALL POSE_SET(L_P, [[占有部位:Ｖ]], 1, L_T)

;-----------------------------
;ソース追加処理
;-----------------------------

LOCAL:0 = 150
LOCAL:1 = 150
LOCAL:2 = 60

;-- 補正追加

;- 範囲:100%〜300%
LOCAL:10 = ABL:L_T:技巧 + ABL:L_P:技巧
SELECTCASE LOCAL:10
	CASE 0 to 17
		L_TMP = 100 + (10 * LOCAL:10)
	CASEELSE
		L_TMP = 300
ENDSELECT
LOCAL:0 = LOCAL:0 * L_TMP / 100
LOCAL:1 = LOCAL:1 * L_TMP / 100
LOCAL:2 = LOCAL:2 * L_TMP / 100

;- ソース
SOURCE:L_P:快Ｖ += LOCAL:0
SOURCE:L_T:快Ｃ += LOCAL:1
SOURCE:L_T:性行為 += LOCAL:2


;-----------------------------
;経験の処理
;-----------------------------

;- 各種経験
GTR_EXPUP:L_P:DB_EXPUP_POS("誘惑経験") += 1

;-----------------------------
;その他の処理
;-----------------------------

;- TR:PALAM変動(Ｖ性交時) (※逆)
CALL TR_COM_PALAMCHG_SEX_V(TARGET, PLAYER, 1)



;=================================================
;   sub COM_FUNC_TRAIN_MSG_Xxxx
;=================================================
;   コマンドメッセージ
;-------------------------------------------------
@COM_FUNC_TRAIN_MSG_X102

;前回の体位が「騎乗位する」
IF TEQUIP:ポイント性交前回 == [[TRAIN:騎乗位する]]
	PRINTFORML %CALLNAME:PLAYER%は%CALLNAME%にまたがったまま、
	PRINTFORML こみ上げてくる衝動に任せて腰を揺り動かした……

;前回の体位が逆Ｖ性交系の場合
ELSEIF TR_PREV_SEX_REV_V()
	PRINTFORML 膣内から抜き去ることなく%CALLNAME%を押し倒し、上から腰を揺り動かした……

;その他
ELSE
	;処女＋小柄体型
	IF TCVAR:PLAYER:履歴_EXP_処女喪失 && TALENT:PLAYER:小柄体型
		PRINTFORML %CALLNAME:PLAYER%は%CALLNAME%にまたがりゆっくりと腰を下ろし、
		PRINT 男を知らない幼いワレメでペニスを飲み込んでいった……
	;処女
	ELSEIF TCVAR:PLAYER:履歴_EXP_処女喪失
		PRINTFORML %CALLNAME:PLAYER%は%CALLNAME%のまたがりゆっくりと腰を下ろし、
		PRINT 男を知らない秘裂でペニスを飲み込んでいった……
	ELSE
		PRINTFORM %CALLNAME:PLAYER%は%CALLNAME%の上にまたがり、ゆっくりと腰を下ろしていった……
	ENDIF
	PRINTL
ENDIF
;
IF 1
	PRINTFORM %CALLNAME:PLAYER%は
	;小柄体型
	SIF TALENT:PLAYER:小柄体型
		PRINT 小さな身体で
	;巨乳
	IF VAR_バストサイズ(PLAYER) == 1
		PRINT 大きく胸を揺らしながら
	;爆乳
	ELSEIF VAR_バストサイズ(PLAYER) == 2
		PRINT 跳ねる胸に身体ごと揺らされながら
	;その他
	ELSE
		PRINT その両胸を揺らしながら
	ENDIF
	PRINT 誘う様に腰を動かしている……
	PRINTL
ENDIF

WAIT


;=================================================
;   sub COM_FUNC_GET_TYPE_Xxxx
;=================================================
;   取得:コマンド種別
;-------------------------------------------------
; Output:
;  RESULT				コマンド種別
;-------------------------------------------------
@COM_FUNC_GET_TYPE_X102
RETURN [[COM種別:逆Ｖ性交系]]

