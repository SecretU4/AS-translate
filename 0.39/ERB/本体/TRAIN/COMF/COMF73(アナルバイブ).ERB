;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Module		:COMFxxx.ERB
;	Facility	:TRAINコマンド(73:アナルバイブ)。
;
;	Licence		:ライセンスフリー。
;
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#

;===============================================================================
;  ◇記載関数一覧(記載順)
;
;		Module Name						Explanation
;
;	・ COM_FUNC_GET_NAME_Xxxx			取得:コマンド名称
;	・ COM_FUNC_ABLE_Xxxx				CHK:コマンド実行条件
;	・ COM_FUNC_INPUT_Xxxx				コマンド実行確認
;	・ COM_FUNC_EXE_Xxxx				コマンド実行
;	・ COM_FUNC_SRC_Xxxx				コマンドソース
;	・ COM_FUNC_TRAIN_MSG_Xxxx			コマンドメッセージ
;	・ COM_FUNC_GET_TYPE_Xxxx			取得:コマンド種別
;
;===============================================================================

;=================================================
;   sub COM_FUNC_GET_NAME_Xxxx
;=================================================
;   取得:コマンド名称
;-------------------------------------------------
; Output:
;  RESULTS				コマンド名称
;-------------------------------------------------
@COM_FUNC_GET_NAME_X73
RESULTS = アナルバイブ

;=================================================
;   sub COM_FUNC_ABLE_Xxxx
;=================================================
;   CHK:コマンド実行条件
;-------------------------------------------------
; Output:
;  RESULT				真偽値
;-------------------------------------------------
@COM_FUNC_ABLE_X73

;特定アイテムを所有
SIF !ITEM:アナルバイブ
	RETURN 0

RETURN 1

;=================================================
;   sub COM_FUNC_INPUT_Xxxx
;=================================================
;   コマンド実行確認
;-------------------------------------------------
; Output:
;  RETURN				真偽値
;-------------------------------------------------
@COM_FUNC_INPUT_X73
#DIM L_P
#DIM L_T

L_P = PLAYER
L_T = TARGET

;-----------------------------
;- 事前処理
;-----------------------------
;CHK
SIF TEQUIP:L_T:Ａ器具 != [[TRAIN:アナルバイブ]]
	RETURN 1

TEQUIP:L_T:Ａ器具 = 0

;-----------------------------
;- 情景
;-----------------------------

PRINTFORML %CALLNAME%のアヌスからアナルバイブを抜いた
PRINTFORM %CALLNAME%は
IF TALENT:淫乱度
	;Ａ経験180以上
	IF EXP:Ａ経験 > 180
		PRINT 秘唇と口から涎をたらしながら、お尻を広げ続け
	;Ａ経験140以上
	ELSEIF EXP:Ａ経験 > 140
		PRINT 潤んだ瞳で懇願するようにお尻を振っ
	;Ａ経験100以上
	ELSEIF EXP:Ａ経験 > 100
		PRINT 物足りなさそうにお尻を突き出し
	;それ以外
	ELSE
		PRINT 少し名残惜しそうにお尻を揺らし
	ENDIF
ELSE
	;Ａ経験40以上
	IF EXP:Ａ経験 > 40
		PRINT お尻をもじもじさせつつも、どこかほっとした表情をうかべ
	;Ａ経験20以上
	ELSEIF EXP:Ａ経験 > 20
		PRINT 強制的な排泄感に頬を染め
	;それ以外
	ELSE
		PRINT 刺激に辛そうな表情で耐え
	ENDIF
ENDIF
PRINTFORML ている…
WAIT


;解除だけの為、0返し
RETURN 0

;=================================================
;   sub COM_FUNC_EXE_Xxxx
;=================================================
;   コマンド実行
;-------------------------------------------------
@COM_FUNC_EXE_X73
#DIM L_P
#DIM L_T
#DIM L_TMP
#DIM L_I
VARSET LOCAL

L_P = PLAYER
L_T = TARGET

;-----------------------------
;COM別FLAGの設定
;-----------------------------

TEQUIP:L_T:Ａ器具 = [[TRAIN:アナルバイブ]]
TEQUIP:L_T:Ａ器具経過ターン = 0

L_TMP = TEQUIP:L_T:ポイント性交
IF L_TMP
	CALLFORM COM_FUNC_GET_TYPE_X{L_TMP}
	SIF RESULT == [[COM種別:Ａ性交系]]
		TEQUIP:L_T:ポイント性交 = [[TRAIN:無し]]
ENDIF

;-----------------------------
;ソース追加処理
;-----------------------------

;-----------------------------
;その他の処理
;-----------------------------


;=================================================
;   sub COM_FUNC_SRC_Xxxx
;=================================================
;   コマンドソース
;-------------------------------------------------
@COM_FUNC_SRC_X73
#DIM L_P
#DIM L_T
#DIM L_TMP
#DIM L_I
VARSET LOCAL

L_P = PLAYER
L_T = TARGET

;-----------------------------
;COM別FLAGの設定
;-----------------------------

;-----------------------------
;ソース追加処理
;-----------------------------

LOCAL:0 = 120
LOCAL:1 = 100
LOCAL:2 = 100

L_TMP=150
LOCAL:0 = LOCAL:0 * L_TMP / 100
LOCAL:1 = LOCAL:1 * L_TMP / 100
LOCAL:2 = LOCAL:2 * L_TMP / 100


;- ソース
SOURCE:L_T:快Ａ += LOCAL:0
SOURCE:L_T:性行為 += LOCAL:1
SOURCE:L_T:露出 += LOCAL:2

;- TR:ソース(部位苦痛)
CALL TR_SOURCE_ADD_部位苦痛("Ａ感覚", L_T)


;-----------------------------
;経験の処理
;-----------------------------

;-----------------------------
;その他の処理
;-----------------------------



;=================================================
;   sub COM_FUNC_TRAIN_MSG_Xxxx
;=================================================
;   コマンドメッセージ
;-------------------------------------------------
@COM_FUNC_TRAIN_MSG_X73

;- 挿入時
IF !TEQUIP:Ｖ器具経過ターン

	PRINTFORM %CALLNAME%の
	PRINTFORML 菊穴にアナルバイブをあてがった…
	PRINTFORM %CALLNAME%は
	IF TALENT:淫乱度
		;Ａ経験180以上
		IF EXP:Ａ経験 > 180
			PRINT とろけきった表情で、自らのお尻を広げ
		;Ａ経験140以上
		ELSEIF EXP:Ａ経験 > 140
			PRINT 挿入を待ち焦がれるように腰をくねらせ
		;Ａ経験100以上
		ELSEIF EXP:Ａ経験 > 100
			PRINT 頬を染め、期待にお尻を揺らめかせ
		;それ以外
		ELSE
			PRINT すぼまりに与えられるバイブの刺激に身体を震わせ
		ENDIF
	ELSE
		;Ａ経験40以上
		IF EXP:Ａ経験 > 40
			PRINT 期待と否定の入り混じったような複雑な表情をし
		;Ａ経験20以上
		ELSEIF EXP:Ａ経験 > 20
			PRINT 恥じらいに染まった顔を伏せ
		;それ以外
		ELSE
			PRINT 不浄の穴を犯される恥辱と屈辱感にまみれた表情をし
		ENDIF
	ENDIF
	PRINTL ている…
	PRINTFORM %CALLNAME%の
	;濡れ
	SIF TCVAR:キャラ潤滑
		PRINT 粘液にまみれた
	PRINTFORML アヌスにバイブを挿し込んだ

;- 継続中
ELSE
	PRINTFORML %CALLNAME:TARGET%はアヌスに挿れたバイブが与える快感に甘い声をあげている…
ENDIF

WAIT


;=================================================
;   sub COM_FUNC_GET_TYPE_Xxxx
;=================================================
;   取得:コマンド種別
;-------------------------------------------------
; Output:
;  RESULT				コマンド種別
;-------------------------------------------------
@COM_FUNC_GET_TYPE_X73
RETURN [[COM種別:道具系]]

