;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Module		:COMFxxx.ERB
;	Facility	:TRAINコマンド(80:正常位)。
;
;	Licence		:ライセンスフリー。
;
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#

;===============================================================================
;  ◇記載関数一覧(記載順)
;
;		Module Name						Explanation
;
;	・ COM_FUNC_GET_NAME_Xxxx			取得:コマンド名称
;	・ COM_FUNC_ABLE_Xxxx				CHK:コマンド実行条件
;	・ COM_FUNC_EXE_Xxxx				コマンド実行
;	・ COM_FUNC_SRC_Xxxx				コマンドソース
;	・ COM_FUNC_TRAIN_MSG_Xxxx			コマンドメッセージ
;	・ COM_FUNC_GET_TYPE_Xxxx			取得:コマンド種別
;
;===============================================================================

;=================================================
;   sub COM_FUNC_GET_NAME_Xxxx
;=================================================
;   取得:コマンド名称
;-------------------------------------------------
; Output:
;  RESULTS				コマンド名称
;-------------------------------------------------
@COM_FUNC_GET_NAME_X80
RESULTS = 正常位

;=================================================
;   sub COM_FUNC_ABLE_Xxxx
;=================================================
;   CHK:コマンド実行条件
;-------------------------------------------------
; Output:
;  RESULT				真偽値
;-------------------------------------------------
@COM_FUNC_ABLE_X80

;- TR:性交可能(Ｖ)
SIF !TR_CHK_ABLE_SEX_V()
	RETURN 0

RETURN 1

;=================================================
;   sub COM_FUNC_EXE_Xxxx
;=================================================
;   コマンド実行
;-------------------------------------------------
@COM_FUNC_EXE_X80
#DIM L_P
#DIM L_T

L_P = PLAYER
L_T = TARGET

;-----------------------------
;COM別FLAGの設定
;-----------------------------

;- POSE:リセット
CALL POSE_RESET("Ｖ")

TEQUIP:L_T:ポイント性交 = [[TRAIN:正常位]]


;-----------------------------
;ソース追加処理
;-----------------------------

;-----------------------------
;その他の処理
;-----------------------------


;=================================================
;   sub COM_FUNC_SRC_Xxxx
;=================================================
;   コマンドソース
;-------------------------------------------------
@COM_FUNC_SRC_X80
#DIM L_P
#DIM L_T
#DIM L_TMP
#DIM L_I
VARSET LOCAL

L_P = PLAYER
L_T = TARGET

;-----------------------------
;COM別FLAGの設定
;-----------------------------

;- POSE:SET
CALL POSE_SET(L_T, [[占有部位:Ｖ]])

;-----------------------------
;ソース追加処理
;-----------------------------

LOCAL:0 = 150
LOCAL:1 = 150
LOCAL:2 = 60

;-- 補正追加

;- 範囲:100%〜300%
LOCAL:10 = ABL:L_T:技巧 + ABL:L_P:技巧
SELECTCASE LOCAL:10
	CASE 0 to 17
		L_TMP = 100 + (10 * LOCAL:10)
	CASEELSE
		L_TMP = 300
ENDSELECT
LOCAL:0 = LOCAL:0 * L_TMP / 100
LOCAL:1 = LOCAL:1 * L_TMP / 100
LOCAL:2 = LOCAL:2 * L_TMP / 100

;- ソース
SOURCE:L_P:快Ｃ += LOCAL:0
SOURCE:L_T:快Ｖ += LOCAL:1
SOURCE:L_T:性行為 += LOCAL:2


;-----------------------------
;経験の処理
;-----------------------------

;-----------------------------
;その他の処理
;-----------------------------

;- TR:PALAM変動(Ｖ性交時)
CALL TR_COM_PALAMCHG_SEX_V



;=================================================
;   sub COM_FUNC_TRAIN_MSG_Xxxx
;=================================================
;   コマンドメッセージ
;-------------------------------------------------
@COM_FUNC_TRAIN_MSG_X80

;前回の体位が「正常位」
IF TEQUIP:ポイント性交前回 == [[TRAIN:正常位]]
	PRINTFORML %CALLNAME%の膣内にペニスを擦り付けながら、再び腰を動かし始めた……

;前回の体位がＶ性交系の場合
ELSEIF TR_PREV_SEX_V()
	PRINTFORML 膣内から抜き去ることなく%CALLNAME%を仰向けに寝かせ、再び腰を動かし始めた……

;その他
ELSE
	PRINTFORM %CALLNAME%の
	;処女
	IF TCVAR:履歴_EXP_処女喪失
		PRINT 男を知らない
	;濡れ
	ELSEIF TCVAR:キャラ潤滑
		PRINT 濡れそぼった
	ENDIF
	;小柄体型
	IF TALENT:小柄体型
		PRINT 幼いワレメを
	ELSE
		PRINT 秘裂を
	ENDIF
	PRINTL 貫いた……
ENDIF
;陥落済み
IF MARK:陥落刻印
	PRINTFORML %CALLNAME%は%CALLNAME:PLAYER%に足を絡め腰を押し付けてきた……
ENDIF

WAIT


;=================================================
;   sub COM_FUNC_GET_TYPE_Xxxx
;=================================================
;   取得:コマンド種別
;-------------------------------------------------
; Output:
;  RESULT				コマンド種別
;-------------------------------------------------
@COM_FUNC_GET_TYPE_X80
RETURN [[COM種別:Ｖ性交系]]

