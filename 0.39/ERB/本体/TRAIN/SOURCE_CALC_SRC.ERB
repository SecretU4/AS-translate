;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Module		:SOURCE_CALC_SRC.ERB
;	Facility	:SOURCE関連処理(ソース計算)。
;
;	Licence		:ライセンスフリー。
;
;	Modification Data:
;
;	Edit	Date			Author					Reason
;	001		20XX/XX/XX		----					新規作成
;
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#

;===============================================================================
;  ◇記載関数一覧(記載順)
;
;		Module Name						Explanation
;
; *- XXXX
;
;	・ SRC_CALC_SENSE_MAIN				SRC計算:感覚(主処理)
;	・ SRC_CALC_SENSE_DATA				SRC計算:感覚(データ)
;	・ SRC_CALC_MAIN					SRC計算:主処理
;	・ SRC_CALC_DETAIL					SRC計算:詳細
;
; *- SRC補正
;
;	・ SRC_REVISION_TALENT_感度			SRC補正:感度
;	・ SRC_REVISION_ABL_感覚			SRC補正:ABL(感覚)
;	・ SRC_REVISION_ABL_篭絡			SRC補正:ABL(篭絡)
;	・ SRC_REVISION_EXP_部位経験		SRC補正:EXP(部位経験)
;	・ SRC_REVISION_PALAM_欲情			SRC補正:PALAM(欲情)
;	・ SRC_REVISION_PALAM_潤滑			SRC補正:PALAM(潤滑)
;	・ SRC_REVISION_PALAM_恥情			SRC補正:PALAM(恥情)
;	・ SRC_REVISION_ABL_魅了技能		SRC補正:ABL(魅了技能)
;	・ SRC_REVISION_ABL_欲望			SRC補正:ABL(欲望)
;	・ SRC_REVISION_ABL_技巧			SRC補正:ABL(技巧)
;	・ SRC_REVISION_ABL_従順			SRC補正:ABL(従順)
;	・ SRC_REVISION_ABL_親密			SRC補正:ABL(親密)
;	・ SRC_REVISION_相殺				SRC補正:相殺
;	・ SRC_REVISION_性行動				SRC補正:性行動
;
; *- SRC共通
;
;	・ SRC_COMMON_肉体接触系			SRC共通:肉体接触系
;	・ SRC_COMMON_征服系				SRC共通:征服系
;	・ SRC_COMMON_部位快感				SRC共通:部位快感
;
; *- SOURCE反映(個別)
;
;	・ SRC_REFLECT_快Ｃ					SRC反映:快Ｃ
;	・ SRC_REFLECT_快Ｖ					SRC反映:快Ｖ
;	・ SRC_REFLECT_快Ａ					SRC反映:快Ａ
;	・ SRC_REFLECT_快Ｂ					SRC反映:快Ｂ
;	・ SRC_REFLECT_液体					SRC反映:液体
;	・ SRC_REFLECT_接触					SRC反映:接触
;	・ SRC_REFLECT_性行為				SRC反映:性行為
;	・ SRC_REFLECT_性奉仕				SRC反映:性奉仕
;	・ SRC_REFLECT_苦痛					SRC反映:苦痛
;	・ SRC_REFLECT_露出					SRC反映:露出
;	・ SRC_REFLECT_征服					SRC反映:征服
;	・ SRC_REFLECT_純愛					SRC反映:純愛
;	・ SRC_REFLECT_習得					SRC反映:習得
;	・ SRC_REFLECT_絶頂					SRC反映:絶頂
;	・ SRC_REFLECT_精液					SRC反映:精液
;	・ SRC_REFLECT_子宮精液				SRC反映:子宮精液
;	・ SRC_REFLECT_歓楽					SRC反映:歓楽
;	・ SRC_REFLECT_生活					SRC反映:生活
;
; *- SOURCE反映(その他)
;
;	・ SRC_CUP_EXTRA					SRC反映:CUP(特殊)
;	・ SRC_EXP							SRC反映:経験値
;	・ SRC_DOWNBASE						SRC反映:DOWNBASE
;
;===============================================================================

;-------------------------------------------------------------------------------
;   XXXX
;-------------------------------------------------------------------------------

;=================================================
;   sub SRC_CALC_SENSE_MAIN
;=================================================
;   SRC計算:感覚(主処理)
;-------------------------------------------------
;   先に快○○系を集計する(絶頂判定が先行処理の為)
;-------------------------------------------------
@SRC_CALC_SENSE_MAIN
#DIM L_I
#DIM L_J
#DIM L_T
#DIMS L_STR

FOR L_I, 0, DEFTR_PL_NUM

	L_T = GTR_PL:L_I

	;CHK
	SIF L_T < 0
		BREAK

	CALL SRC_CALC_SENSE_DATA(L_T, "快Ｃ")
	CALL SRC_CALC_SENSE_DATA(L_T, "快Ｖ")
	CALL SRC_CALC_SENSE_DATA(L_T, "快Ａ")
	CALL SRC_CALC_SENSE_DATA(L_T, "快Ｂ")

NEXT

;=================================================
;   sub SRC_CALC_SENSE_DATA
;=================================================
;   SRC計算:感覚(データ)
;-------------------------------------------------
; Input:
;  ARG:0				キャラ
;  ARGS					ソース名
;-------------------------------------------------
@SRC_CALC_SENSE_DATA(ARG:0, ARGS)
#DIM L_I
#DIM L_T
#DIMS L_STR
L_I = GETNUM(SOURCE, ARGS)
L_T = ARG
L_STR = %SOURCENAME:L_I%

SIF !SOURCE:L_T:L_STR
	RETURN

SELECTCASE L_T
	;主人は全部2倍
	CASE MASTER
		SOURCE:L_T:L_STR *= 2
	;主人以外は一部調整
	CASEELSE
		IF MARK:陥落刻印
			SIF L_STR == "快Ｖ"
				TIMES SOURCE:L_T:L_STR, 1.50
		ENDIF
ENDSELECT

;追加で上昇(150%)
SOURCE:L_T:L_STR = C_TIMES(SOURCE:L_T:L_STR, 150)

;- SRC反映:xxx
CALLFORM SRC_REFLECT_%L_STR%(L_T)

;同一コマンド時、半減
SIF TFLAG:同一コマンド実行フラグ && (L_T == TFLAG:メインターゲット)
	CUP:L_T:L_STR /= 2

;計算:絶頂加算値
TCVAR:L_T:絶頂加算値 += CUP:L_T:L_STR


;=================================================
;   sub SRC_CALC_MAIN
;=================================================
;   SRC計算:主処理
;-------------------------------------------------
;   全PLAYERの全SOURCEを計算する
;-------------------------------------------------
@SRC_CALC_MAIN
#DIM L_I
#DIM L_T

;-----------------------------
;-- 全ソースを先に計算
;-----------------------------

FOR L_I, 0, DEFTR_PL_NUM

	L_T = GTR_PL:L_I

	;CHK:END
	SIF L_T < 0
		BREAK

	;- SRC計算:詳細
	CALL SRC_CALC_DETAIL(L_T)

	;同一コマンド時、半減
	IF TFLAG:同一コマンド実行フラグ && (L_T == TARGET)
		CUP:L_T:欲情 /= 2
		CUP:L_T:習得 /= 2
		CUP:L_T:恥情 /= 2
		CUP:L_T:好意 /= 2
		CUP:L_T:屈服 /= 2
	ENDIF
NEXT

;- あなたは消費無し
DOWNBASE:MASTER:体力=0

;-----------------------------
;-- 全ソース計算後、特殊計算処理
;-----------------------------

FOR L_I, 0, DEFTR_PL_NUM

	L_T = GTR_PL:L_I

	;CHK:END
	SIF L_T < 0
		BREAK

	;- SRC反映:CUP(特殊)
	CALL SRC_CUP_EXTRA(L_T)

	;- SRC反映:経験値
	CALL SRC_EXP(L_T)

	;- SRC反映:DOWNBASE
	CALL SRC_DOWNBASE(L_T)

	;- 行動フラグを立てる(ソース変動)
	TCVAR:L_T:行動フラグ_ソース変動 = 1

	;- 各種フラグ立て
	TCVAR:L_T:キャラ欲情 = ( (TR_GETPALAMLV("欲情", L_T) >= 5) ? 1 # 0 )
	TCVAR:L_T:ペニス勃起 = ( (TR_GETPALAMLV("欲情", L_T) >= 3) ? 1 # 0 )
	TCVAR:L_T:キャラ潤滑 = ( (TR_GETPALAMLV("潤滑", L_T) >= 5) ? 1 # 0 )

NEXT

;=================================================
;   sub SRC_CALC_DETAIL
;=================================================
;   SRC計算:詳細
;-------------------------------------------------
;   既に一部ソースが先行計算されている為、
;   当関数では計算されていないソースを処理する。
;-------------------------------------------------
; Input:
;  ARG:0				対象者
;-------------------------------------------------
@SRC_CALC_DETAIL(ARG=-1)
#DIM L_I
#DIMS L_STR

FOR L_I, 0, VARSIZE("SOURCE")

	; CHK:値
	SIF !SOURCE:ARG:L_I
		CONTINUE

	L_STR = %SOURCENAME:L_I%

	;先行計算済みは除外
	SIF GROUPMATCH(L_STR, "快Ｃ", "快Ｖ", "快Ａ", "快Ｂ")
		CONTINUE

	;- SRC反映:xxx
	CALLFORM SRC_REFLECT_%L_STR%(ARG)

NEXT

;-------------------------------------------------------------------------------
;   SRC補正
;-------------------------------------------------------------------------------

;=================================================
;   sub SRC_REVISION_TALENT_感度
;=================================================
;   SRC補正:感度
;-------------------------------------------------
;   範囲: 50%/100%/150%
;-------------------------------------------------
; Input:
;  ARG:0				対象者
;  ARG:1				対象値
;  ARGS					対象感度
; Output:
;  RETURNF				倍率(%)
;-------------------------------------------------
@SRC_REVISION_TALENT_感度(ARG, ARG:1, ARGS)
#FUNCTION
#DIM L_TMP
#DIMS L_STR

L_STR = %SUBSTRING(ARGS,0,2)%

L_TMP = 0
SIF TALENT:ARG:@"%L_STR%敏感"
	L_TMP = 1
SIF TALENT:ARG:@"%L_STR%鈍感"
	L_TMP = -1

SELECTCASE L_TMP
	CASE IS < 0
		L_TMP = 50
	CASE IS > 0
		L_TMP = 150
	CASEELSE
		L_TMP = 100
ENDSELECT

RETURNF (L_TMP * ARG:1 / 100)

;=================================================
;   sub SRC_REVISION_ABL_感覚
;=================================================
;   SRC補正:ABL(感覚)
;-------------------------------------------------
;   範囲: 10%〜250%
;-------------------------------------------------
; Input:
;  ARG					対象感覚
; Output:
;  RETURNF				倍率(%)
;-------------------------------------------------
@SRC_REVISION_ABL_感覚(ARG)
#FUNCTION
#DIM L_TMP

SELECTCASE ARG
;-------------------------------------------------
	CASE 0
		L_TMP = 10
	CASE 1
		L_TMP = 30
	CASE 2
		L_TMP = 50
	CASE 3
		L_TMP = 150
	CASE 4 TO 8
		L_TMP = 150 + (10 * ARG)
	CASEELSE
		L_TMP = 250
;-------------------------------------------------
;	CASE 0
;		L_TMP = 10
;	CASE 1
;		L_TMP = 30
;	CASE 2
;		L_TMP = 50
;	CASE 3
;		L_TMP = 150
;	CASE 4 TO 8
;		L_TMP = 50 + (50 * ARG)
;	CASEELSE
;		L_TMP = 500
;-------------------------------------------------
;	CASE 0
;		L_TMP = 5
;	CASE 1
;		L_TMP = 10
;	CASE 2
;		L_TMP = 30
;	CASE 5
;		L_TMP = 70
;	CASE 4
;		L_TMP = 150
;	CASE 5 TO 8
;		L_TMP = 50 + (50 * ARG)
;	CASEELSE
;		L_TMP = 500
;-------------------------------------------------
ENDSELECT


RETURNF L_TMP

;=================================================
;   sub SRC_REVISION_ABL_篭絡
;=================================================
;   SRC補正:ABL(篭絡)
;-------------------------------------------------
;   範囲: 20%〜200%
;-------------------------------------------------
; Input:
;  ARG:0				対象キャラ
; Output:
;  RETURNF				倍率(%)
;-------------------------------------------------
@SRC_REVISION_ABL_篭絡(ARG)
#FUNCTION
#DIM L_TMP

SELECTCASE ABL:ARG:篭絡
	CASE 0 TO 8
		L_TMP = 20 + (20 * ABL:ARG:篭絡)
	CASEELSE
		L_TMP = 200
ENDSELECT

RETURNF L_TMP

;=================================================
;   sub SRC_REVISION_EXP_部位経験
;=================================================
;   SRC補正:EXP(部位経験)
;-------------------------------------------------
;   範囲: 30%〜150%
;-------------------------------------------------
; Input:
;  ARG					対象部位経験
; Output:
;  RETURNF				倍率(%)
;-------------------------------------------------
@SRC_REVISION_EXP_部位経験(ARG)
#FUNCTION
#DIM L_TMP

;--経験をみる
SELECTCASE ARG
	CASE IS < EXPLV:1
		L_TMP = 030
	CASE IS < EXPLV:2
		L_TMP = 060
	CASE IS < EXPLV:3
		L_TMP = 090
	CASE IS < EXPLV:4
		L_TMP = 120
	CASE IS < EXPLV:5
		L_TMP = 130
	CASEELSE
		L_TMP = 150
ENDSELECT

RETURNF L_TMP

;=================================================
;   sub SRC_REVISION_PALAM_欲情
;=================================================
;   SRC補正:PALAM(欲情)
;-------------------------------------------------
;   範囲: 20%〜100% (通常)
;   範囲: 10%〜60% (低)
;-------------------------------------------------
; Input:
;  ARG:0				対象キャラ
;  ARG:9				処理区分(1:通常 2:補正低)
; Output:
;  RETURNF				倍率(%)
;-------------------------------------------------
@SRC_REVISION_PALAM_欲情(ARG,ARG:9 = 1)
#FUNCTION
#DIM L_TMP
#DIM L_VAL

L_VAL = PALAM:ARG:欲情
;--PALAM:欲情をみる

IF ARG:9 == 2
;補正:低
	SELECTCASE L_VAL
		CASE IS < PALAMLV:1
			L_TMP = 010
		CASE IS < PALAMLV:2
			L_TMP = 020
		CASE IS < PALAMLV:4
			L_TMP = 040
		CASE IS < PALAMLV:6
			L_TMP = 060
		CASEELSE
			L_TMP = 080
	ENDSELECT
ELSE
;補正:通常
	SELECTCASE L_VAL
		CASE IS < PALAMLV:1
			L_TMP = 020
		CASE IS < PALAMLV:2
			L_TMP = 030
		CASE IS < PALAMLV:4
			L_TMP = 050
		CASE IS < PALAMLV:6
			L_TMP = 070
		CASEELSE
			L_TMP = 100
	ENDSELECT
ENDIF

RETURNF L_TMP

;=================================================
;   sub SRC_REVISION_PALAM_潤滑
;=================================================
;   SRC補正:PALAM(潤滑)
;-------------------------------------------------
;   範囲: 10%〜140%
;-------------------------------------------------
; Input:
;  ARG:0				対象キャラ
; Output:
;  RETURNF				倍率(%)
;-------------------------------------------------
@SRC_REVISION_PALAM_潤滑(ARG)
#FUNCTION
#DIM L_TMP
#DIM L_VAL

L_VAL = PALAM:ARG:潤滑
;--PALAM:欲情をみる

SELECTCASE L_VAL
	CASE IS < PALAMLV:1
		L_TMP = 010
	CASE IS < PALAMLV:2
		L_TMP = 040
	CASE IS < PALAMLV:4
		L_TMP = 060
	CASE IS < PALAMLV:6
		L_TMP = 100
	CASEELSE
		L_TMP = 140
ENDSELECT

RETURNF L_TMP


;=================================================
;   sub SRC_REVISION_PALAM_恥情
;=================================================
;   SRC補正:PALAM(恥情)
;-------------------------------------------------
;   範囲:100%〜60%
;-------------------------------------------------
; Input:
;  ARG:0				対象キャラ
; Output:
;  RETURNF				対象値
;-------------------------------------------------
@SRC_REVISION_PALAM_恥情(ARG)
#FUNCTION
#DIM L_TMP
#DIM L_VAL

;PALAM:恥情をみる
L_VAL = TR_GETPALAMLV("恥情", ARG)

;恥ずかしさを感じれば感じるほどあまり恥ずかしく無くなる
SELECTCASE L_VAL
	CASE 0 TO 4
		L_TMP = 100 - (010 * L_VAL)
	CASEELSE
		L_TMP = 060
ENDSELECT

RETURNF L_TMP


;=================================================
;   sub SRC_REVISION_ABL_魅了技能
;=================================================
;   SRC補正:ABL(魅了技能)
;-------------------------------------------------
;   範囲: 10%〜150%
;-------------------------------------------------
; Input:
;  ARG:0				対象キャラ
; Output:
;  RETURNF				対象値
;-------------------------------------------------
@SRC_REVISION_ABL_魅了技能(ARG)
#FUNCTION
#DIM L_TMP
#DIM L_VAL

L_VAL = ABL:ARG:魅了技能

SELECTCASE L_VAL
	CASE 0 TO 8
		L_TMP = 10 +(15 * L_VAL)
	CASEELSE
		L_TMP = 150
ENDSELECT

RETURNF L_TMP

;=================================================
;   sub SRC_REVISION_ABL_欲望
;=================================================
;   SRC補正:ABL(欲望)
;-------------------------------------------------
;   範囲: 20%〜200%
;-------------------------------------------------
; Input:
;  ARG:0				対象キャラ
; Output:
;  RETURNF				対象値
;-------------------------------------------------
@SRC_REVISION_ABL_欲望(ARG)
#FUNCTION
#DIM L_TMP
#DIM L_VAL

L_VAL = ABL:ARG:欲望

SELECTCASE L_VAL
	CASE 0 TO 8
		L_TMP = 20 +(20 * L_VAL)
	CASEELSE
		L_TMP = 200
ENDSELECT

RETURNF L_TMP

;=================================================
;   sub SRC_REVISION_ABL_技巧
;=================================================
;   SRC補正:ABL(技巧)
;-------------------------------------------------
;   範囲: 20%〜200%
;-------------------------------------------------
; Input:
;  ARG:0				対象キャラ
; Output:
;  RETURNF				対象値
;-------------------------------------------------
@SRC_REVISION_ABL_技巧(ARG)
#FUNCTION
#DIM L_TMP
#DIM L_VAL

L_VAL = ABL:ARG:技巧

SELECTCASE L_VAL
	CASE 0 TO 8
		L_TMP = 20 +(20 * L_VAL)
	CASEELSE
		L_TMP = 200
ENDSELECT

RETURNF L_TMP


;=================================================
;   sub SRC_REVISION_ABL_従順
;=================================================
;   SRC補正:ABL(従順)
;-------------------------------------------------
;   範囲: 20%〜200%
;-------------------------------------------------
; Input:
;  ARG:0				対象キャラ
; Output:
;  RETURNF				対象値
;-------------------------------------------------
@SRC_REVISION_ABL_従順(ARG)
#FUNCTION
#DIM L_TMP
#DIM L_VAL

L_VAL = ABL:ARG:従順

SELECTCASE L_VAL
	CASE 0 TO 8
		L_TMP = 20 +(20 * L_VAL)
	CASEELSE
		L_TMP = 200
ENDSELECT

RETURNF L_TMP

;=================================================
;   sub SRC_REVISION_ABL_親密
;=================================================
;   SRC補正:ABL(親密)
;-------------------------------------------------
;   範囲: 20%〜200%
;-------------------------------------------------
; Input:
;  ARG:0				対象キャラ
; Output:
;  RETURNF				対象値
;-------------------------------------------------
@SRC_REVISION_ABL_親密(ARG)
#FUNCTION
#DIM L_TMP
#DIM L_VAL

L_VAL = ABL:ARG:親密

SELECTCASE L_VAL
	CASE 0 TO 8
		L_TMP = 20 +(20 * L_VAL)
	CASEELSE
		L_TMP = 200
ENDSELECT

RETURNF L_TMP


;=================================================
;   sub SRC_REVISION_相殺
;=================================================
;   SRC補正:相殺
;-------------------------------------------------
;   マイナスパラメータの相殺に使われる
;   範囲:130%〜0%
;-------------------------------------------------
; Input:
;  ARG:0				対象値
;  ARG:1				対象キャラ
;  ARG:9				処理タイプ(0:通常 1:性関連)
; Output:
;  RETURNF				対象値
;-------------------------------------------------
@SRC_REVISION_相殺(ARG,ARG:1,ARG:9 = 0)
#FUNCTION
#DIM L_P
#DIM L_T
#DIM L_VAL
#DIM L_SUM
#DIM L_TMP
#DIM L_倍率

L_P = MASTER
L_T = ARG:1
L_VAL = ARG

;-----------------------------
;- ポイント計算
;-----------------------------
L_SUM = 0

;- ABLで加算(範囲:0〜9 * 個数)
L_SUM += ABL:L_T:親密
L_SUM += ABL:L_T:従順
L_SUM += ABL:L_T:欲望
L_SUM += ABL:L_P:魅了技能

;ムードレベル
L_SUM += VAR_MOOD_LV(L_T)

;- 陥落状態で更に加算(基礎値5 * 個数)
SIF MARK:L_T:淫乱刻印
	L_SUM += 5
SIF MARK:L_T:陥落刻印
	L_SUM += 5

;- 絶頂強度で更に加算(基礎値3 * 範囲:0〜3)
L_SUM += 3 * TCVAR:L_T:絶頂強度

IF !TCVAR:L_T:絶頂強度
	;- 反発刻印で減算(基礎値5 * 範囲:0〜3)
	L_SUM -= 5 * MARK:L_T:反発刻印
ENDIF

;最低は-20
L_SUM = MAX(L_SUM, -20)

;-----------------------------
;- 倍率計算
;-----------------------------

;- ()内に0になる必要ポイント数をセット(例:20の場合は、上記計算で20ポイントあれば完全に相殺)

L_TMP=0
SELECTCASE ARG:9
	;性関連
	CASE 1
		L_TMP = 100 / (20)
	CASEELSE
		L_TMP = 100 / (10)
ENDSELECT

;- 計算
L_倍率 = MAX(100 - (L_TMP * L_SUM) , 0)


;-----------------------------
;- 相殺
;-----------------------------
L_VAL = (L_VAL * L_倍率) / 100

;- 特殊減算
L_VAL -= 10 * (ABL:L_T:欲望 + ABL:L_T:従順 + ABL:L_T:奉仕精神)
L_VAL = MAX(L_VAL, 0)

RETURNF L_VAL

;=================================================
;   sub SRC_REVISION_性行動
;=================================================
;   SRC補正:性行動
;-------------------------------------------------
;   性行動に関係するマイナスパラメータの変動に使われる
;   範囲:?
;-------------------------------------------------
; Input:
;  ARG:0				対象値
;  ARG:1				対象キャラ
; Output:
;  RETURNF				対象値
;-------------------------------------------------
@SRC_REVISION_性行動(ARG,ARG:1)
#DIM L_TMP
#DIM L_VAL
#DIM L_BASE
#FUNCTION

L_TMP = 0
L_VAL = ARG

;- 貞操度
L_BASE=20
SIF TALENT:(ARG:1):貞操観念
	L_TMP += L_BASE
SIF TALENT:(ARG:1):貞操無頓着
	L_TMP -= L_BASE

;- 性的興味
L_BASE=20
SIF TALENT:(ARG:1):好奇心
	L_TMP -= L_BASE
SIF TALENT:(ARG:1):保守的
	L_TMP += L_BASE

;- 計算
L_VAL = C_TIMES(L_VAL, 100 + L_TMP)


RETURNF L_VAL


;-------------------------------------------------------------------------------
;   SRC共通
;-------------------------------------------------------------------------------

;=================================================
;   sub SRC_COMMON_肉体接触系
;=================================================
;   SRC共通:肉体接触系
;-------------------------------------------------
;   肉体接触系の共通処理を纏める。
;   下記に対して入力値を基に反映する
;   ◆ 欲情・好意・屈服・不快
;-------------------------------------------------
; Input:
;  ARG:0				対象者
;  ARG:5				入力値
;  ARG:9				オリジナル値(SOURCE)
;  ARG:3				区分(0:通常 1:奉仕)
;-------------------------------------------------
@SRC_COMMON_肉体接触系(ARG,ARG:5 = 0,ARG:9 = 0,ARG:3 = 0)
#DIM L_基本倍率
#DIM L_倍率
#DIM L_SRC
#DIM L_OLD
#DIM L_P

L_P = PLAYER
L_SRC = ARG:5

SIF !L_SRC
	RETURN

;- 退避(オリジナルSOURCE)
L_OLD = ARG:9

;-----------------------------
;-- 共通倍率
;-----------------------------

;- 各種SRC補正
L_基本倍率 = 0
L_基本倍率 += SRC_REVISION_PALAM_欲情(ARG)
L_基本倍率 += SRC_REVISION_ABL_欲望(ARG)
L_基本倍率 += SRC_REVISION_ABL_魅了技能(L_P)

;- 計算
L_SRC = C_TIMES(L_SRC, L_基本倍率)


;-----------------------------
;-- CUP取得:欲情
;-----------------------------
L_倍率 = 100
L_倍率 += SRC_REVISION_ABL_親密(ARG)
CUP:ARG:欲情 += C_TIMES(L_SRC / 2, L_倍率)


;-----------------------------
;-- CUP取得:好意
;-----------------------------
CUP:ARG:好意 += C_TIMES(L_SRC, L_倍率)


;-----------------------------
;-- CUP取得:屈服
;-----------------------------
L_倍率 = 100
L_倍率 += SRC_REVISION_ABL_従順(ARG)
SIF ARG:3
	L_倍率 *= 3
CUP:ARG:屈服 += C_TIMES(L_SRC, L_倍率)


;-----------------------------
;-- CUP取得:不快
;-----------------------------
L_OLD = SRC_REVISION_相殺(L_OLD,ARG)
L_OLD = SRC_REVISION_性行動(L_OLD,ARG)

CUP:ARG:不快 += L_OLD


;=================================================
;   sub SRC_COMMON_征服系
;=================================================
;   SRC共通:征服系
;-------------------------------------------------
;   征服系の共通処理を纏める。
;   下記に対して入力値を基に反映する
;   ◆ 対象者:屈服・欲情・好意
;   ◆ 実行者:誘惑
;-------------------------------------------------
; Input:
;  ARG:0				対象者
;  ARG:5				入力値
;  ARG:3				区分(0:通常 1:純愛)
;-------------------------------------------------
@SRC_COMMON_征服系(ARG,ARG:5 = 0,ARG:3 = 0)
#DIM L_基本倍率
#DIM L_倍率
#DIM L_SRC
#DIM L_TMP
#DIM L_P

L_P = PLAYER
L_SRC = ARG:5

SIF !L_SRC
	RETURN

;-----------------------------
;-- CUP取得:屈服
;-----------------------------
L_倍率 = 100
L_倍率 += SRC_REVISION_ABL_従順(ARG)
CUP:ARG:屈服 += C_TIMES(L_SRC, L_倍率)

;-----------------------------
;-- CUP取得:欲情
;-----------------------------
L_倍率 = 100
L_倍率 += SRC_REVISION_PALAM_欲情(ARG)
L_TMP = C_TIMES(L_SRC, L_倍率)
CUP:ARG:欲情 += L_TMP

;-----------------------------
;-- CUP取得:好意
;-----------------------------
L_倍率 = 100
L_倍率 += SRC_REVISION_ABL_親密(ARG)
L_TMP = C_TIMES(L_SRC, L_倍率)
SIF ARG:3
	L_TMP *= 2
CUP:ARG:好意 += L_TMP

;-----------------------------
;-- CUP取得:誘惑(相手)
;-----------------------------
CUP:L_P:誘惑 += L_TMP


;=================================================
;   sub SRC_COMMON_部位快感
;=================================================
;   SRC共通:部位快感
;-------------------------------------------------
;   範囲:???%〜???%
;-------------------------------------------------
; Input:
;  ARG:0				対象者
; Output:
;  RETURNF				倍率(%)
;-------------------------------------------------
@SRC_COMMON_部位快感(ARG)
#FUNCTION
#DIM L_TMP

L_TMP = 10 * MARK:ARG:淫乱刻印

;陥落前のみ有効
IF !MARK:ARG:陥落刻印

	;自制心
	SIF TALENT:ARG:自制心
		L_TMP -= 10

ENDIF


RETURNF L_TMP


;-------------------------------------------------------------------------------
;   SRC反映(個別)
;-------------------------------------------------------------------------------

;=================================================
;   sub SRC_REFLECT_快Ｃ
;=================================================
;   SRC反映:快Ｃ
;-------------------------------------------------
; Input:
;  ARG:0				対象者
;-------------------------------------------------
@SRC_REFLECT_快Ｃ(ARG)
#DIM L_基本倍率
#DIM L_倍率
#DIM L_SRC

L_SRC = SOURCE:ARG:快Ｃ

;-----------------------------
;-- SOURCE更新
;-----------------------------

;- 各種SRC補正
L_基本倍率 = 0
L_基本倍率 += SRC_REVISION_ABL_感覚(ABL:ARG:Ｃ感覚)
L_基本倍率 += SRC_REVISION_EXP_部位経験(EXP:ARG:Ｃ経験)
L_基本倍率 += SRC_COMMON_部位快感(ARG)
L_基本倍率 = SRC_REVISION_TALENT_感度(ARG, L_基本倍率, "Ｃ感度")

;- 計算
L_SRC = C_TIMES(L_SRC, L_基本倍率)

;- 固定加算
L_SRC += TCVAR:ARG:固定加算_快Ｃ

;更新
SOURCE:ARG:快Ｃ = L_SRC

;-----------------------------
;-- CUP取得:快Ｃ
;-----------------------------
L_倍率 = 100
L_倍率 += SRC_REVISION_PALAM_欲情(ARG)
CUP:ARG:快Ｃ += C_TIMES(L_SRC, L_倍率)

;-----------------------------
;-- CUP取得:潤滑
;-----------------------------
CUP:ARG:潤滑 += C_TIMES(L_SRC / 2, L_倍率)

;-----------------------------
;-- CUP取得:欲情
;-----------------------------
L_倍率 = 100
L_倍率 += SRC_REVISION_ABL_篭絡(ARG)
CUP:ARG:欲情 += C_TIMES(L_SRC, L_倍率)


;=================================================
;   sub SRC_REFLECT_快Ｖ
;=================================================
;   SRC反映:快Ｖ
;-------------------------------------------------
; Input:
;  ARG:0				対象者
;-------------------------------------------------
@SRC_REFLECT_快Ｖ(ARG)
#DIM L_基本倍率
#DIM L_倍率
#DIM L_SRC

L_SRC = SOURCE:ARG:快Ｖ

;-----------------------------
;-- SOURCE更新
;-----------------------------

;- 各種SRC補正
L_基本倍率 = 0
L_基本倍率 += SRC_REVISION_ABL_感覚(ABL:ARG:Ｖ感覚)
L_基本倍率 += SRC_REVISION_EXP_部位経験(EXP:ARG:Ｖ経験)
L_基本倍率 += SRC_COMMON_部位快感(ARG)
L_基本倍率 = SRC_REVISION_TALENT_感度(ARG, L_基本倍率, "Ｖ感度")

L_基本倍率 = C_TIMES(L_基本倍率, SRC_REVISION_PALAM_潤滑(ARG))

;- 計算
L_SRC = C_TIMES(L_SRC, L_基本倍率)

;- 固定加算
L_SRC += TCVAR:ARG:固定加算_快Ｖ

;更新
SOURCE:ARG:快Ｖ = L_SRC

;-----------------------------
;-- CUP取得:快Ｖ
;-----------------------------
L_倍率 = 100
L_倍率 += SRC_REVISION_PALAM_欲情(ARG)
CUP:ARG:快Ｖ += C_TIMES(L_SRC, L_倍率)

;-----------------------------
;-- CUP取得:潤滑
;-----------------------------
CUP:ARG:潤滑 += C_TIMES(L_SRC / 2, L_倍率)

;-----------------------------
;-- CUP取得:欲情
;-----------------------------
L_倍率 = 100
L_倍率 += SRC_REVISION_ABL_篭絡(ARG) * 2
CUP:ARG:欲情 += C_TIMES(L_SRC, L_倍率)


;=================================================
;   sub SRC_REFLECT_快Ａ
;=================================================
;   SRC反映:快Ａ
;-------------------------------------------------
; Input:
;  ARG:0				対象者
;-------------------------------------------------
@SRC_REFLECT_快Ａ(ARG)
#DIM L_基本倍率
#DIM L_倍率
#DIM L_SRC

L_SRC = SOURCE:ARG:快Ａ

;-----------------------------
;-- SOURCE更新
;-----------------------------

;- 各種SRC補正
L_基本倍率 = 0
L_基本倍率 += SRC_REVISION_ABL_感覚(ABL:ARG:Ａ感覚)
L_基本倍率 += SRC_REVISION_EXP_部位経験(EXP:ARG:Ａ経験)
L_基本倍率 += SRC_COMMON_部位快感(ARG)
L_基本倍率 = SRC_REVISION_TALENT_感度(ARG, L_基本倍率, "Ａ感度")

L_基本倍率 = C_TIMES(L_基本倍率, SRC_REVISION_PALAM_潤滑(ARG))

;- 計算
L_SRC = C_TIMES(L_SRC, L_基本倍率)

;- 固定加算
L_SRC += TCVAR:ARG:固定加算_快Ａ

;更新
SOURCE:ARG:快Ａ = L_SRC

;-----------------------------
;-- CUP取得:快Ａ
;-----------------------------
L_倍率 = 100
L_倍率 += SRC_REVISION_PALAM_欲情(ARG,2)
CUP:ARG:快Ａ += C_TIMES(L_SRC, L_倍率)


;-----------------------------
;-- CUP取得:潤滑
;-----------------------------
CUP:ARG:潤滑 += C_TIMES(L_SRC / 4, L_倍率)


;-----------------------------
;-- CUP取得:欲情
;-----------------------------
L_倍率 = 100
L_倍率 += SRC_REVISION_ABL_篭絡(ARG) * 2
CUP:ARG:欲情 += C_TIMES(L_SRC, L_倍率)


;=================================================
;   sub SRC_REFLECT_快Ｂ
;=================================================
;   SRC反映:快Ｂ
;-------------------------------------------------
; Input:
;  ARG:0				対象者
;-------------------------------------------------
@SRC_REFLECT_快Ｂ(ARG)
#DIM L_基本倍率
#DIM L_倍率
#DIM L_SRC

L_SRC = SOURCE:ARG:快Ｂ

;-----------------------------
;-- SOURCE更新
;-----------------------------

;- 各種SRC補正
L_基本倍率 = 0
L_基本倍率 += SRC_REVISION_ABL_感覚(ABL:ARG:Ｂ感覚)
L_基本倍率 += SRC_REVISION_EXP_部位経験(EXP:ARG:Ｂ経験)
L_基本倍率 += SRC_COMMON_部位快感(ARG)
L_基本倍率 = SRC_REVISION_TALENT_感度(ARG, L_基本倍率, "Ｂ感度")

;- 計算
L_SRC = C_TIMES(L_SRC, L_基本倍率)

;- 固定加算
L_SRC += TCVAR:ARG:固定加算_快Ｂ

;更新
SOURCE:ARG:快Ｂ = L_SRC

;-----------------------------
;-- CUP取得:快Ｂ
;-----------------------------
L_倍率 = 100
L_倍率 += SRC_REVISION_PALAM_欲情(ARG)
CUP:ARG:快Ｂ += C_TIMES(L_SRC, L_倍率)


;-----------------------------
;-- CUP取得:潤滑
;-----------------------------
CUP:ARG:潤滑 += C_TIMES(L_SRC / 2, L_倍率)


;-----------------------------
;-- CUP取得:欲情
;-----------------------------
L_倍率 = 100
L_倍率 += SRC_REVISION_ABL_篭絡(ARG)
CUP:ARG:欲情 += C_TIMES(L_SRC, L_倍率)


;=================================================
;   sub SRC_REFLECT_液体
;=================================================
;   SRC反映:液体
;-------------------------------------------------
; Input:
;  ARG:0				対象者
;-------------------------------------------------
@SRC_REFLECT_液体(ARG)
#DIM L_SRC

L_SRC = SOURCE:ARG:液体

;-----------------------------
;-- CUP取得:潤滑
;-----------------------------
CUP:ARG:潤滑 += L_SRC



;=================================================
;   sub SRC_REFLECT_接触
;=================================================
;   SRC反映:接触
;-------------------------------------------------
; Input:
;  ARG:0				対象者
;-------------------------------------------------
@SRC_REFLECT_接触(ARG)
#DIM L_基本倍率
#DIM L_倍率
#DIM L_SRC
#DIM L_OLD
#DIM L_P

L_P = PLAYER
L_SRC = SOURCE:ARG:接触

;- 退避(オリジナルSOURCE)
L_OLD = L_SRC

;-----------------------------
;-- SOURCE更新
;-----------------------------

;- 各種SRC補正
L_基本倍率 = 0
L_基本倍率 += SRC_REVISION_ABL_親密(ARG)

;- 計算
L_SRC = C_TIMES(L_SRC, L_基本倍率)

;更新
SOURCE:ARG:接触 = L_SRC

;-----------------------------
;-- CUP取得:xxx(肉体接触系)
;-----------------------------

;- SRC共通:肉体接触系
CALL SRC_COMMON_肉体接触系(ARG,L_SRC,L_OLD)

;-----------------------------
;-- CUP取得:誘惑 (実行者)
;-----------------------------

CUP:L_P:誘惑 += L_SRC


;=================================================
;   sub SRC_REFLECT_性行為
;=================================================
;   SRC反映:性行為
;-------------------------------------------------
; Input:
;  ARG:0				対象者
;-------------------------------------------------
@SRC_REFLECT_性行為(ARG)
#DIM L_基本倍率
#DIM L_倍率
#DIM L_SRC
#DIM L_OLD
#DIM L_P
#DIM L_TMP

L_P = PLAYER
L_SRC = SOURCE:ARG:性行為

;- 退避(オリジナルSOURCE)
L_OLD = L_SRC

;-----------------------------
;-- SOURCE更新
;-----------------------------

;- 各種SRC補正
L_基本倍率 = 0
L_基本倍率 += SRC_REVISION_ABL_親密(ARG)
L_基本倍率 += SRC_REVISION_ABL_技巧(L_P)

;- 計算
L_SRC = C_TIMES(L_SRC, L_基本倍率)

;更新
SOURCE:ARG:性行為 = L_SRC

;-----------------------------
;-- CUP取得:xxx(肉体接触系)
;-----------------------------

;- SRC共通:肉体接触系
CALL SRC_COMMON_肉体接触系(ARG,L_SRC,L_OLD)


;-----------------------------
;-- CUP取得:習得
;-----------------------------
L_倍率 = 0
L_倍率 += SRC_REVISION_ABL_欲望(ARG)
L_TMP = C_TIMES(L_SRC, L_倍率)
CUP:ARG:習得 += L_TMP

;-----------------------------
;-- CUP取得:誘惑 (実行者)
;-----------------------------
CUP:L_P:誘惑 += (L_SRC + L_TMP) / 2

;-----------------------------
;-- CUP取得:習得 (実行者)
;-----------------------------
CUP:L_P:習得 += L_SRC

;-----------------------------
;-- CUP取得:欲情 (実行者)
;-----------------------------
CUP:L_P:欲情 += L_SRC


;=================================================
;   sub SRC_REFLECT_性奉仕
;=================================================
;   SRC反映:性奉仕
;-------------------------------------------------
; Input:
;  ARG:0				対象者
;-------------------------------------------------
@SRC_REFLECT_性奉仕(ARG)
#LOCALSIZE 10
#DIM L_基本倍率
#DIM L_倍率
#DIM L_SRC
#DIM L_OLD
#DIM L_P
VARSET LOCAL

L_P = PLAYER
L_SRC = SOURCE:ARG:性奉仕

;- 退避(オリジナルSOURCE)
L_OLD = L_SRC

;-----------------------------
;-- SOURCE更新
;-----------------------------

;- 各種SRC補正
L_基本倍率 = 0
L_基本倍率 += SRC_REVISION_ABL_親密(ARG)
L_基本倍率 += SRC_REVISION_ABL_技巧(ARG)

;小悪魔
SIF TALENT:ARG:小悪魔
	L_基本倍率 += 20

;- 計算
L_SRC = C_TIMES(L_SRC, L_基本倍率)

;更新
SOURCE:ARG:性奉仕 = L_SRC

;-----------------------------
;-- CUP取得:xxx(肉体接触系)
;-----------------------------

;- SRC共通:肉体接触系
CALL SRC_COMMON_肉体接触系(ARG,L_SRC,L_OLD,1)


;-----------------------------
;-- CUP取得:誘惑
;-----------------------------
L_倍率 = 100
L_倍率 += SRC_REVISION_ABL_魅了技能(ARG)
LOCAL:1 = C_TIMES(L_SRC, L_倍率)
CUP:ARG:誘惑 += LOCAL:1


;-----------------------------
;-- CUP取得:習得
;-----------------------------
L_倍率 = 100
LOCAL:2 = C_TIMES(L_SRC, L_倍率)
CUP:ARG:習得 += LOCAL:2

;-----------------------------
;-- CUP取得:誘惑 (実行者)
;-----------------------------
CUP:L_P:誘惑 += (L_SRC + LOCAL:2) / 2

;-----------------------------
;-- CUP取得:習得 (実行者)
;-----------------------------
CUP:L_P:習得 += L_SRC / 2

;-----------------------------
;-- CUP取得:欲情(実行者)
;-----------------------------
CUP:L_P:欲情 += LOCAL:1

;-----------------------------
;-- CUP取得:反感
;-----------------------------
L_OLD = SRC_REVISION_相殺(L_OLD,ARG,1)
L_OLD = SRC_REVISION_性行動(L_OLD,ARG)
CUP:ARG:反感 += L_OLD


;=================================================
;   sub SRC_REFLECT_苦痛
;=================================================
;   SRC反映:苦痛
;-------------------------------------------------
; Input:
;  ARG:0				対象者
;-------------------------------------------------
@SRC_REFLECT_苦痛(ARG)
#LOCALSIZE 10
#DIM L_基本倍率
#DIM L_倍率
#DIM L_SRC
#DIM L_OLD
#DIM L_P
VARSET LOCAL

L_P = PLAYER
L_SRC = SOURCE:ARG:苦痛

;- 退避(オリジナルSOURCE)
L_OLD = L_SRC

;-----------------------------
;-- SOURCE更新
;-----------------------------
;無し

;-----------------------------
;-- CUP取得:苦痛
;-----------------------------
CUP:ARG:苦痛 += L_SRC

;-----------------------------
;-- CUP取得:欲情
;-----------------------------

LOCAL:1 = L_SRC

;CHK:マゾっ気
SELECTCASE ABL:ARG:マゾっ気
	CASE 0 TO 9
		LOCAL:1 = C_TIMES(LOCAL:1, (020 * ABL:ARG:マゾっ気))
	CASEELSE
		TIMES LOCAL:1 , 3.00
ENDSELECT

;CHK:調教者のサド
SIF TALENT:L_P:サド
	LOCAL:1 = C_TIMES(LOCAL:1, 100 + (050 * TALENT:L_P:サド))

CUP:ARG:欲情 += LOCAL:1


;-----------------------------
;-- CUP取得:恐怖
;-----------------------------

LOCAL:2 = L_SRC / 20

;CHK:マゾっ気
SELECTCASE ABL:ARG:マゾっ気
	CASE 0 TO 8
		LOCAL:2 = C_TIMES(LOCAL:2, 100 - (012 * ABL:ARG:マゾっ気))
	CASEELSE
		LOCAL:2 = 0
ENDSELECT

CUP:ARG:恐怖 += LOCAL:2


;-----------------------------
;-- CUP取得:屈服
;-----------------------------

LOCAL:3 = L_SRC + LOCAL:2

CUP:ARG:屈服 += LOCAL:3

;-----------------------------
;-- CUP取得:反感
;-----------------------------

;- SRC補正:相殺
L_OLD = SRC_REVISION_相殺(L_OLD,ARG)

;CHK:マゾっ気
SELECTCASE ABL:ARG:マゾっ気
	CASE 0 TO 2
		L_OLD = C_TIMES(L_OLD, 200 - (050 * ABL:ARG:マゾっ気))
	CASEELSE
		L_OLD = C_TIMES(L_OLD, 100 - (012 * ABL:ARG:マゾっ気))
ENDSELECT

CUP:ARG:反感 += L_OLD



;=================================================
;   sub SRC_REFLECT_露出
;=================================================
;   SRC反映:露出
;-------------------------------------------------
; Input:
;  ARG:0				対象者
;-------------------------------------------------
@SRC_REFLECT_露出(ARG)
#LOCALSIZE 10
#DIM L_基本倍率
#DIM L_倍率
#DIM L_SRC
#DIM L_OLD
#DIM L_P
#DIM L_TMP
VARSET LOCAL

L_P = PLAYER
L_SRC = SOURCE:ARG:露出

;- 退避(オリジナルSOURCE)
L_OLD = L_SRC

;-----------------------------
;-- SOURCE更新
;-----------------------------

;羞恥心
L_TMP = 050
SIF TALENT:ARG:恥薄い
	L_SRC = C_TIMES(L_SRC, 100 - L_TMP)
SIF TALENT:ARG:恥じらい
	L_SRC = C_TIMES(L_SRC, 100 + L_TMP)

;- 各種SRC補正
L_基本倍率 = 100
L_基本倍率 += SRC_REVISION_ABL_魅了技能(ARG)

;- 計算
L_SRC = C_TIMES(L_SRC, L_基本倍率)

;更新
SOURCE:ARG:露出 = L_SRC

;-----------------------------
;-- CUP取得:欲情
;-----------------------------

LOCAL:1 = L_SRC

;CHK:露出癖
SELECTCASE ABL:ARG:露出癖
	CASE 0 TO 8
		LOCAL:1 = C_TIMES(LOCAL:1, (020 * ABL:ARG:露出癖))
	CASEELSE
		TIMES LOCAL:1 , 2.00
ENDSELECT

CUP:ARG:欲情 += LOCAL:1

;-----------------------------
;-- CUP取得:誘惑
;-----------------------------
L_倍率 = 100
L_倍率 += SRC_REVISION_PALAM_欲情(L_P)
LOCAL:2 = C_TIMES(LOCAL:1, L_倍率)
CUP:ARG:誘惑 += LOCAL:2

;-----------------------------
;-- CUP取得:恥情
;-----------------------------
L_倍率 = 0
L_倍率 += SRC_REVISION_PALAM_恥情(ARG)
LOCAL:3 = C_TIMES(L_SRC, L_倍率)
CUP:ARG:恥情 += LOCAL:3

;-----------------------------
;-- CUP取得:屈服
;-----------------------------
LOCAL:4 = L_SRC + LOCAL:3
L_倍率 = 100
L_倍率 += SRC_REVISION_ABL_親密(ARG)

LOCAL:4 = C_TIMES(LOCAL:4, L_倍率)
CUP:ARG:屈服 += LOCAL:4


;-----------------------------
;-- CUP取得:反感
;-----------------------------
L_OLD = SRC_REVISION_相殺(L_OLD,ARG)
L_OLD = SRC_REVISION_性行動(L_OLD,ARG)
CUP:ARG:反感 += L_OLD


;=================================================
;   sub SRC_REFLECT_征服
;=================================================
;   SRC反映:征服
;-------------------------------------------------
; Input:
;  ARG:0				対象者
;-------------------------------------------------
@SRC_REFLECT_征服(ARG)
#DIM L_基本倍率
#DIM L_倍率
#DIM L_SRC
#DIM L_OLD
#DIM L_TMP
#DIM L_P

L_P = PLAYER
L_SRC = SOURCE:ARG:征服

;- 退避(オリジナルSOURCE)
L_OLD = L_SRC

;-----------------------------
;-- SOURCE更新
;-----------------------------

;- 各種SRC補正
L_基本倍率 = 100
L_基本倍率 += SRC_REVISION_ABL_魅了技能(L_P)

;- 計算
L_SRC = C_TIMES(L_SRC, L_基本倍率)

;更新
SOURCE:ARG:征服 = L_SRC

;-----------------------------
;-- CUP取得:xxx(征服系)
;-----------------------------

;- SRC共通:征服系
CALL SRC_COMMON_征服系(ARG,L_SRC)

;-----------------------------
;-- CUP取得:抑鬱
;-----------------------------
L_倍率 = 100
L_倍率 += SRC_REVISION_PALAM_欲情(ARG)
L_TMP = C_TIMES(L_SRC, L_倍率)
L_TMP = SRC_REVISION_相殺(L_TMP,ARG)
CUP:ARG:抑鬱 += L_TMP

;-----------------------------
;-- CUP取得:反感
;-----------------------------
L_TMP = SRC_REVISION_相殺(L_OLD,ARG)
L_TMP = SRC_REVISION_性行動(L_TMP,ARG)
CUP:ARG:反感 += L_TMP


;=================================================
;   sub SRC_REFLECT_純愛
;=================================================
;   SRC反映:純愛
;-------------------------------------------------
; Input:
;  ARG:0				対象者
;-------------------------------------------------
@SRC_REFLECT_純愛(ARG)
#DIM L_基本倍率
#DIM L_SRC
#DIM L_P

L_P = PLAYER
L_SRC = SOURCE:ARG:純愛

;-----------------------------
;-- SOURCE更新
;-----------------------------

;- 各種SRC補正
L_基本倍率 = 100
L_基本倍率 += SRC_REVISION_ABL_魅了技能(L_P)
L_基本倍率 += SRC_REVISION_ABL_従順(ARG)

;- 計算
L_SRC = C_TIMES(L_SRC, L_基本倍率)

;更新
SOURCE:ARG:純愛 = L_SRC

;-----------------------------
;-- CUP取得:xxx(征服系)
;-----------------------------

;- SRC共通:征服系
CALL SRC_COMMON_征服系(ARG,L_SRC,1)


;=================================================
;   sub SRC_REFLECT_習得
;=================================================
;   SRC反映:絶頂
;-------------------------------------------------
; Input:
;  ARG:0				対象者
;-------------------------------------------------
@SRC_REFLECT_習得(ARG)
#DIM L_基本倍率
#DIM L_倍率
#DIM L_SRC
#DIM L_TMP
#DIM L_P

L_P = PLAYER
L_SRC = SOURCE:ARG:習得

;-----------------------------
;-- SOURCE更新
;-----------------------------
;特に無し

;-----------------------------
;-- CUP取得:屈服
;-----------------------------

CUP:ARG:習得 += L_SRC




;=================================================
;   sub SRC_REFLECT_絶頂
;=================================================
;   SRC反映:絶頂
;-------------------------------------------------
; Input:
;  ARG:0				対象者
;-------------------------------------------------
@SRC_REFLECT_絶頂(ARG)
#DIM L_基本倍率
#DIM L_倍率
#DIM L_SRC
#DIM L_TMP
#DIM L_P

L_P = PLAYER
L_SRC = SOURCE:ARG:絶頂

;-----------------------------
;-- SOURCE更新
;-----------------------------

;- 各種SRC補正
L_基本倍率 = 100
L_基本倍率 += SRC_REVISION_ABL_魅了技能(L_P) / 6
L_基本倍率 += SRC_REVISION_ABL_欲望(ARG)

;- 計算
L_SRC = C_TIMES(L_SRC, L_基本倍率)

;更新
SOURCE:ARG:絶頂 = L_SRC

;-----------------------------
;-- CUP取得:屈服
;-----------------------------
CUP:ARG:屈服 += L_SRC


;-----------------------------
;-- CUP取得:欲情
;-----------------------------
L_倍率 = 100
L_倍率 += SRC_REVISION_PALAM_欲情(ARG)
L_TMP = C_TIMES(L_SRC, L_倍率)
CUP:ARG:欲情 += L_TMP


;-----------------------------
;-- CUP取得:恥情
;-----------------------------
L_倍率 = 0
L_倍率 += SRC_REVISION_PALAM_恥情(ARG)
L_TMP = C_TIMES(L_SRC, L_倍率)
CUP:ARG:恥情 += L_TMP


;-----------------------------
;-- CUP取得:抑鬱
;-----------------------------
L_TMP = L_SRC / 10
L_TMP = SRC_REVISION_相殺(L_TMP,ARG,1)
CUP:ARG:抑鬱 += L_TMP


;=================================================
;   sub SRC_REFLECT_精液
;=================================================
;   SRC反映:精液
;-------------------------------------------------
; Input:
;  ARG:0				対象者
;-------------------------------------------------
@SRC_REFLECT_精液(ARG)
#DIM L_基本倍率
#DIM L_倍率
#DIM L_SRC
#DIM L_TMP
#DIM L_P
#DIM L_ADD
#DIM L_VAL

L_P = PLAYER
L_SRC = SOURCE:ARG:精液
L_ADD=0
L_VAL=1

;-----------------------------
;-- SOURCE更新
;-----------------------------
L_基本倍率 = 100
L_基本倍率 += SRC_REVISION_ABL_魅了技能(L_P)
L_基本倍率 += SRC_REVISION_ABL_篭絡(ARG)
L_SRC = C_TIMES(L_SRC, L_基本倍率)

;更新
SOURCE:ARG:精液 = L_SRC

IF TCVAR:ARG:被射精部位 == [[口上:主人Ｖ射精]]
	CFLAG:ARG:子宮精液貯蔵量       += L_SRC
	CFLAG:ARG:子宮精液貯蔵量合計 += L_SRC
	SOURCE:ARG:子宮精液 = L_SRC
	SOURCE:ARG:精液 = 0
	;陥落後の場合、効果追加
	IF MARK:ARG:陥落刻印
;		L_ADD=L_SRC
	;陥落前は効果減少
	ELSE
		TIMES L_SRC, 0.50
	ENDIF
ENDIF

IF MARK:ARG:陥落刻印
	L_VAL = 3
ELSEIF MARK:ARG:隷属刻印
	L_VAL = 2
ENDIF


;-----------------------------
;-- CUP取得:屈服
;-----------------------------
CUP:ARG:屈服 += (L_SRC + L_ADD) * L_VAL


;-----------------------------
;-- CUP取得:好意
;-----------------------------
SIF MARK:ARG:陥落刻印
	CUP:ARG:好意 += (L_SRC + L_ADD) * 3

;-----------------------------
;-- CUP取得:欲情
;-----------------------------
L_倍率 = 100
L_倍率 += SRC_REVISION_PALAM_欲情(ARG)
L_TMP = C_TIMES(L_SRC, L_倍率)
CUP:ARG:欲情 += L_TMP


;-----------------------------
;-- CUP取得:恥情
;-----------------------------
L_倍率 = 0
L_倍率 += SRC_REVISION_PALAM_恥情(ARG)
L_TMP = C_TIMES(L_SRC, L_倍率)

CUP:ARG:恥情 += L_TMP


;-----------------------------
;-- CUP取得:抑鬱
;-----------------------------
L_TMP = L_SRC / 10
L_TMP = SRC_REVISION_相殺(L_TMP,ARG,1)
CUP:ARG:抑鬱 += L_TMP


;=================================================
;   sub SRC_REFLECT_子宮精液
;=================================================
;   SRC反映:子宮精液
;-------------------------------------------------
;   ダミー関数。
;-------------------------------------------------
; Input:
;  ARG:0				対象者
;-------------------------------------------------
@SRC_REFLECT_子宮精液(ARG)
;ダミー



;=================================================
;   sub SRC_REFLECT_歓楽
;=================================================
;   SRC反映:歓楽
;-------------------------------------------------
; Input:
;  ARG:0				対象者
;-------------------------------------------------
@SRC_REFLECT_歓楽(ARG)
#DIM L_基本倍率
#DIM L_倍率
#DIM L_SRC
#DIM L_TMP
#DIM L_P

L_P = PLAYER
L_SRC = SOURCE:ARG:歓楽

;-----------------------------
;-- SOURCE更新
;-----------------------------
L_基本倍率 = 100
;L_基本倍率 += (010 * ABL:L_P:話術)
L_SRC = C_TIMES(L_SRC, L_基本倍率)

;更新
SOURCE:ARG:歓楽 = L_SRC

;-----------------------------
;-- CUP取得:好意
;-----------------------------
L_倍率 = 100
L_倍率 += SRC_REVISION_ABL_親密(ARG)
L_倍率 += SRC_REVISION_ABL_魅了技能(L_P)
CUP:ARG:好意 += C_TIMES(L_SRC, L_倍率)

;-----------------------------
;-- CUP取得:欲情
;-----------------------------

L_TMP = ABL:L_P:魅了技能 + ABL:ARG:従順

;- 性別が違う or [実行者の魅了技能 + 対象者の従順]の合計が4以上
IF (VAR_HETEROSEX(ARG,L_P) == 0) || (L_TMP >= 4)

	L_倍率 = 0
	L_倍率 += (010 * L_TMP)
	CUP:ARG:欲情 += C_TIMES(L_SRC, L_倍率)

ENDIF

;-----------------------------
;-- CUP取得:誘惑(相手)
;-----------------------------
L_倍率 = 100
L_倍率 += SRC_REVISION_PALAM_欲情(ARG)
L_倍率 += SRC_REVISION_ABL_魅了技能(L_P)
CUP:L_P:誘惑 += C_TIMES(L_SRC, L_倍率) / 3


;=================================================
;   sub SRC_REFLECT_生活
;=================================================
;   SRC反映:生活
;-------------------------------------------------
; Input:
;  ARG:0				対象者
;-------------------------------------------------
@SRC_REFLECT_生活(ARG)
#DIM L_SRC
#DIM L_P

L_P = PLAYER
L_SRC = SOURCE:ARG:生活

;-----------------------------
;-- SOURCE更新
;-----------------------------
;なし

;-----------------------------
;-- CUP取得:生活
;-----------------------------
CUP:ARG:生活 += L_SRC
CUP:L_P:生活 += L_SRC


;-------------------------------------------------------------------------------
;   SRC反映(その他)
;-------------------------------------------------------------------------------

;=================================================
;   sub SRC_CUP_EXTRA
;=================================================
;   SRC反映:CUP(特殊)
;-------------------------------------------------
;   CUPを纏めて調整する
;-------------------------------------------------
; Input:
;  ARG:0				対象者
;-------------------------------------------------
@SRC_CUP_EXTRA(ARG)
#DIM L_T
#DIM L_TMP
#DIM L_BASE
#DIMS L_STR

;- 初期化
L_T = ARG
L_STR =

;-------------------------------------------------
;- 反映:個人PALAM
;-------------------------------------------------

;-----------------------------
;- 計算:欲情
;-----------------------------

L_TMP = TCVAR:L_T:絶頂加算値

CUP:L_T:欲情 += (L_TMP / 2)

SIF TCVAR:L_T:絶頂強度
	CUP:L_T:欲情 += (TCVAR:L_T:絶頂強度 * 300)

;-----------------------------
;- 計算:潤滑
;-----------------------------

;- 女の場合
SIF VAR_SEX(L_T)
	CUP:L_T:潤滑 += (CUP:L_T:欲情 / 2) + (L_TMP / 2)

;-----------------------------
;- 補正:その他
;-----------------------------


;-- 陥落素質
;陥落
L_TMP = MARK:L_T:陥落刻印
SELECTCASE L_TMP
	CASE IS > 0
		TIMES CUP:L_T:抑鬱 , 0.50
ENDSELECT

;-- MASTERの場合
IF L_T == MASTER
	;- マイナスPALAMは0
	CUP:L_T:抑鬱 = 0
ENDIF


;-------------------------------------------------
;- 反映:対人PALAM
;-------------------------------------------------

;-----------------------------
;- 事前チェック
;-----------------------------

;- 対象者がMASTERの場合
IF (L_T == MASTER)
	;- 対人PALAM無し
	CUP:L_T:好意 = 0
	CUP:L_T:屈服 = 0
	CUP:L_T:恐怖 = 0
	CUP:L_T:反感 = 0
	CUP:L_T:不快 = 0
	RETURN
ENDIF

;-----------------------------
;- 補正:刻印
;-----------------------------

;陥落
L_TMP = MARK:L_T:陥落刻印
SELECTCASE L_TMP
	CASE IS > 0
		CUP:L_T:反感 = C_TIMES(CUP:L_T:反感, 100 - (020 * L_TMP))
		CUP:L_T:不快 = C_TIMES(CUP:L_T:不快, 100 - (020 * L_TMP))
	CASE 0
		TIMES CUP:L_T:反感 , 1.20
		TIMES CUP:L_T:不快 , 1.20
ENDSELECT

;-----------------------------
;- 補正:素質
;-----------------------------
;合意
IF TALENT:L_T:合意
	TIMES CUP:L_T:反感 , 0.50
	TIMES CUP:L_T:不快 , 0.50
ENDIF
;調教済み
IF TALENT:L_T:調教済み
	TIMES CUP:L_T:反感 , 0.50
	TIMES CUP:L_T:不快 , 0.50
ENDIF
;非合意
IF TALENT:L_T:非合意 && TALENT:L_T:調教済み == 0
	TIMES CUP:L_T:反感 , 700
	TIMES CUP:L_T:不快 , 700
ENDIF



;プライド
IF TALENT:L_T:プライド高い
	TIMES CUP:L_T:反感 , 2.00
	TIMES CUP:L_T:不快 , 2.00
ELSEIF TALENT:L_T:プライド低い
	TIMES CUP:L_T:反感 , 0.50
	TIMES CUP:L_T:不快 , 0.50
ENDIF

;幼稚
IF TALENT:L_T:幼稚
	TIMES CUP:L_T:反感 , 0.75
	TIMES CUP:L_T:不快 , 0.75
	TIMES CUP:L_T:好意 , 1.25
ENDIF

;性的興味(保守的/好奇心)
L_BASE=020
SIF TALENT:L_T:保守的
	CUP:L_T:習得 = C_TIMES(CUP:L_T:習得, 100 - L_BASE)
SIF TALENT:L_T:好奇心
	CUP:L_T:習得 = C_TIMES(CUP:L_T:習得, 100 + L_BASE)

;習得速度(保守的/好奇心)
SIF TALENT:L_T:習得遅い
	TIMES CUP:L_T:習得 , 0.8
SIF TALENT:L_T:習得早い
	TIMES CUP:L_T:習得 , 1.2

;習得速度(アホの娘/天才)
SIF TALENT:L_T:アホの娘
	TIMES CUP:L_T:習得 , 0.5
SIF TALENT:L_T:天才
	TIMES CUP:L_T:習得 , 1.5


;態度(素直)
IF TALENT:L_T:素直
	TIMES CUP:L_T:好意 , 1.20
	TIMES CUP:L_T:屈服 , 1.20
ENDIF

;応答(大人しい)
IF TALENT:L_T:大人しい
	TIMES CUP:L_T:反感 , 0.80
	TIMES CUP:L_T:不快 , 0.80
ENDIF

;社交性(控え目/目立ちたがり)
L_BASE=025
SIF TALENT:L_T:控え目
	CUP:L_T:恥情 = C_TIMES(CUP:L_T:恥情, 100 - L_BASE)
SIF TALENT:L_T:目立ちたがり
	CUP:L_T:恥情 = C_TIMES(CUP:L_T:恥情, 100 + L_BASE)

;思考(楽観的)
IF TALENT:L_T:楽観的 > 0
	TIMES CUP:L_T:反感 , 0.80
	TIMES CUP:L_T:不快 , 0.80
ENDIF

;男好き
IF TALENT:L_T:男好き && !VAR_SEX(PLAYER)
	TIMES CUP:L_T:好意 , 1.20
	TIMES CUP:L_T:屈服 , 1.20
ENDIF
;同性愛
IF TALENT:L_T:同性愛 && VAR_SEX(PLAYER)
	TIMES CUP:L_T:好意 , 1.20
	TIMES CUP:L_T:屈服 , 1.20
ENDIF

;惚れっぽい
IF TALENT:L_T:惚れっぽい 
	TIMES CUP:L_T:好意 , 2
	TIMES CUP:L_T:屈服 , 2
ENDIF

;即堕ち
IF TALENT:L_T:即堕ち
	TIMES CUP:L_T:好意 , 3
	TIMES CUP:L_T:屈服 , 3
ENDIF

;警戒心
IF TALENT:L_T:警戒心 
	TIMES CUP:L_T:好意 , 0.80
	TIMES CUP:L_T:屈服 , 0.80
ENDIF

;人間不信
IF TALENT:L_T:人間不信
	TIMES CUP:L_T:好意 , 0.50
	TIMES CUP:L_T:屈服 , 0.50
ENDIF

;レイプ願望
IF TALENT:L_T:レイプ願望
	TIMES CUP:L_T:反感 , 0.60
	TIMES CUP:L_T:恐怖 , 0.60
	TIMES CUP:L_T:屈服 , 1.2
ENDIF


;陥落前のみ有効
IF !MARK:L_T:陥落刻印

	;無関心
	IF TALENT:L_T:無関心
		;個人PALAM
		TIMES CUP:L_T:欲情 , 0.80
		;対人PALAM
		TIMES CUP:L_T:好意 , 0.80
		TIMES CUP:L_T:屈服 , 0.80
		TIMES CUP:L_T:恐怖 , 0.80
		TIMES CUP:L_T:反感 , 0.80
		TIMES CUP:L_T:不快 , 0.80
	ENDIF

	;態度(反抗的)
	IF TALENT:L_T:反抗的
		TIMES CUP:L_T:好意 , 0.80
		TIMES CUP:L_T:屈服 , 0.80
	ENDIF

	;応答(生意気)
	IF TALENT:L_T:生意気
		TIMES CUP:L_T:好意 , 0.80
		TIMES CUP:L_T:屈服 , 0.80
	ENDIF

	;男嫌い/女嫌い
	IF (TALENT:L_T:男嫌い && !VAR_SEX(PLAYER)) || (TALENT:L_T:女嫌い && VAR_SEX(PLAYER))
		TIMES CUP:L_T:好意 , 0.80
		TIMES CUP:L_T:屈服 , 0.80
	ENDIF


	;両刀以外
	IF !TALENT:L_T:両刀
		;性別が同じ
		IF VAR_SEX(L_T) == VAR_SEX(PLAYER)
			TIMES CUP:L_T:好意 , 0.90
			TIMES CUP:L_T:屈服 , 0.90
		ENDIF
	ENDIF

	;抵抗
	IF TALENT:L_T:抵抗
		TIMES CUP:L_T:欲情 , 0.80
		TIMES CUP:L_T:反感 , 1.20
	ENDIF

	;抑圧
	IF TALENT:L_T:抑圧
		TIMES CUP:L_T:欲情 , 0.90
		TIMES CUP:L_T:抑鬱 , 1.10
	ENDIF

	;献身的
	IF TALENT:L_T:献身的
		TIMES CUP:L_T:好意 , 1.20
		TIMES CUP:L_T:屈服 , 1.20
	ENDIF

	;思考(悲観的)
	IF TALENT:L_T:悲観的
		TIMES CUP:L_T:抑鬱 , 1.10
	ENDIF
	;白百合姫
	IF TALENT:L_T:白百合姫
		TIMES CUP:L_T:好意 , 0.50
		TIMES CUP:L_T:屈服 , 0.50
	ENDIF


ENDIF

;陥落後のみ有効
IF MARK:L_T:陥落刻印
	;ツンデレ
	SIF TALENT:L_T:ツンデレ
		TIMES CUP:L_T:好意 , 1.25
;	;ヤンデレ
;	SIF TALENT:L_T:ヤンデレ
;		TIMES CUP:L_T:好意 , 2.00
ENDIF

;一線越えない
IF TALENT:L_T:一線越えない
	;陥落してたら上昇/未陥落なら減少
	L_TMP = MARK:L_T:陥落刻印

	;個人PALAM
	CUP:L_T:欲情 = C_TIMES(CUP:L_T:欲情, 80 + (040 * L_TMP))
	;対人PALAM
	CUP:L_T:好意 = C_TIMES(CUP:L_T:好意, 80 + (040 * L_TMP))
	CUP:L_T:屈服 = C_TIMES(CUP:L_T:屈服, 80 + (040 * L_TMP))
ENDIF

;貞操度(貞操観念)
IF TALENT:L_T:貞操観念
	;陥落してたら上昇/未陥落なら減少
	L_TMP = MARK:L_T:陥落刻印

	IF L_TMP
		;個人PALAM
		CUP:L_T:欲情 = C_TIMES(CUP:L_T:欲情, 80 + (040 * L_TMP))
		;対人PALAM
		CUP:L_T:好意 = C_TIMES(CUP:L_T:好意, 80 + (040 * L_TMP))
		CUP:L_T:屈服 = C_TIMES(CUP:L_T:屈服, 80 + (040 * L_TMP))
	ELSE
		TIMES CUP:L_T:抑鬱 , 1.20
		TIMES CUP:L_T:反感 , 1.20
		TIMES CUP:L_T:不快 , 1.20
	ENDIF

;貞操度(貞操無頓着)
ELSEIF TALENT:L_T:貞操無頓着

	TIMES CUP:L_T:抑鬱 , 0.80
	TIMES CUP:L_T:反感 , 0.80
	TIMES CUP:L_T:不快 , 0.80

ENDIF

IF TALENT:L_T:ＮＴＬ 
	TIMES CUP:L_T:反感 , 0.5
	TIMES CUP:L_T:不快 , 0.5
	TIMES CUP:L_T:好意 , 2.00
	TIMES CUP:L_T:屈服 , 2.00
ENDIF


;-----------------------------
;- 補正:刻印
;-----------------------------



;-------------------------------------------------
;- 反映:バランス調整
;-------------------------------------------------
;  キャラ情報ではなく、システム的な意味でのバランス調整をここで行う
;-------------------------------------------------
TIMES CUP:L_T:欲情 , 0.25
TIMES CUP:L_T:好意 , 0.50
TIMES CUP:L_T:屈服 , 0.50

;- 淫乱刻印無しの場合は欲情・好意・屈服低下 (75%)
IF !MARK:L_T:淫乱刻印
	TIMES CUP:L_T:欲情 , 0.75
	TIMES CUP:L_T:好意 , 0.75
	TIMES CUP:L_T:屈服 , 0.75
ENDIF

;- 隷属刻印無しの場合は好意・屈服低下 (75%)
IF !MARK:L_T:隷属刻印
	TIMES CUP:L_T:好意 , 0.75
	TIMES CUP:L_T:屈服 , 0.75
ENDIF

;- 暫定で陥落刻印無しの場合は好意が300以上を超えにくくする
IF !MARK:L_T:陥落刻印
	SELECTCASE CUP:L_T:好意
		CASE IS < 300
		CASE IS < 500
			TIMES CUP:L_T:好意 , 0.75
		CASE IS < 1000
			TIMES CUP:L_T:好意 , 0.5
		CASEELSE
			TIMES CUP:L_T:好意 , 0.4
	ENDSELECT
;- 暫定で陥落刻印有りの場合は好意、屈服が5000以上を超えにくくする
ELSE
	SELECTCASE CUP:L_T:好意
		CASE IS < 3000
		CASE IS < 5000
			TIMES CUP:L_T:好意 , 0.75
		CASE IS < 10000
			TIMES CUP:L_T:好意 , 0.5
		CASE IS < 15000
			TIMES CUP:L_T:好意 , 0.4
		CASEELSE
			TIMES CUP:L_T:好意 , 0.3
	ENDSELECT
	SELECTCASE CUP:L_T:屈服
		CASE IS < 3000
		CASE IS < 5000
			TIMES CUP:L_T:屈服 , 0.75
		CASE IS < 10000
			TIMES CUP:L_T:屈服 , 0.5
		CASE IS < 15000
			TIMES CUP:L_T:屈服 , 0.4
		CASEELSE
			TIMES CUP:L_T:屈服 , 0.3
	ENDSELECT
ENDIF

;主人以外
IF L_T != MASTER
	;性交有りの場合
	IF TEQUIP:L_T:ポイント性交
		;陥落度合いで効果減少
		IF MARK:L_T:陥落刻印
			;スルー
		ELSEIF MARK:L_T:隷属刻印
			TIMES CUP:L_T:好意 , 0.60
			TIMES CUP:L_T:屈服 , 0.60
		ELSE
			TIMES CUP:L_T:好意 , 0.25
			TIMES CUP:L_T:屈服 , 0.25
		ENDIF
	ENDIF
ENDIF

;TIMES CUP:L_T:好意 , 0.50
;TIMES CUP:L_T:屈服 , 0.50


;=================================================
;   sub SRC_EXP
;=================================================
;   SRC反映:経験値
;-------------------------------------------------
; Input:
;  ARG:0				対象者
;-------------------------------------------------
@SRC_EXP(ARG)
#DIM L_TMP

SIF SOURCE:ARG:快Ｃ
	GTR_EXPUP:ARG:DB_EXPUP_POS(@"Ｃ経験") += 1

SIF SOURCE:ARG:快Ｖ
	GTR_EXPUP:ARG:DB_EXPUP_POS(@"Ｖ経験") += 1

SIF SOURCE:ARG:快Ａ
	GTR_EXPUP:ARG:DB_EXPUP_POS(@"Ａ経験") += 1

SIF SOURCE:ARG:快Ｂ
	GTR_EXPUP:ARG:DB_EXPUP_POS(@"Ｂ経験") += 1

IF CUP:ARG:苦痛 && (MARK:ARG:隷属刻印 || ABL:ARG:マゾっ気 || TALENT:ARG:マゾ)
	L_TMP=0
	SELECTCASE CUP:ARG:苦痛
		CASE IS >= 3000
			L_TMP = 4
		CASE IS >= 2000
			L_TMP = 3
		CASE IS >= 1000
			L_TMP = 2
		CASEELSE
			L_TMP = 1
	ENDSELECT
	L_TMP += ABL:ARG:マゾっ気
	GTR_EXPUP:ARG:DB_EXPUP_POS(@"苦痛快楽経験") += L_TMP
ENDIF




;=================================================
;   sub SRC_DOWNBASE
;=================================================
;   SRC反映:DOWNBASE
;-------------------------------------------------
; Input:
;  ARG:0				対象者
;-------------------------------------------------
@SRC_DOWNBASE(ARG)
BASE:ARG:体力 = LIMIT((BASE:ARG:体力 + DOWNBASE:ARG:体力), 0, MAXBASE:ARG:体力)

SIF BASE:ARG:体力 <= 50
	CFLAG:ARG:調教疲労フラグ = 1

