;-------------------------------------------------
;外界調査
;-------------------------------------------------
@RPG_MAIN
#DIM L_INPUT
#DIMS DYNAMIC OP_RELOAD     ;初回以外のページ更新とリスト更新を抑制する
Flag:ターン進行深度 = 0
Flag:ターン戦闘回数 = 0
FLAG:ターン経験値 = 0
Flag:調査イベント番号 = 0

$INPUT_LOOP
CLEARLINE LINECOUNT
DRAWLINE
PRINTFORM 【調査先:
IF Flag:調査区域 == 90
	PRINTFORM Lv{Flag:日替わり区画難易度}:ある区画】(最深部:{Flag:日替わり区画深度限界})
ELSEIF Flag:調査区域 == 50
	PRINTFORM LvXX:砂塵の都】
ELSEIF Flag:調査区域 == 5
	PRINTFORM Lv5:完全侵食区域[現代迷宮]】
ELSEIF Flag:調査区域 == 4
	PRINTFORM Lv4:狂気の廃都】
ELSEIF Flag:調査区域 == 3
	PRINTFORM Lv3:危険区域】
ELSEIF Flag:調査区域 == 2
	PRINTFORM Lv2:堕ちた都市】
ELSEIF Flag:調査区域 == 1
	PRINTFORM Lv1:無法区域】
ELSE
	PRINTFORM Lv0:都市境界線】
ENDIF
PRINTFORML (現在深度:{Flag:調査深度})
DRAWLINE
IF Flag:ターン調査済み == 1
	PRINTFORML このターンは調査済みです 
ELSEIF  Flag:都市_防衛 != 0
	PRINTFORML 防衛戦時は調査出来ません
ELSE
	PRINTFORML [0] 調査開始
ENDIF
PRINTFORML [1] 調査区域選択
PRINTFORML 
PRINTFORML [5] 部隊配置
PRINTFORM [2] 武装改良　　
PRINTFORM [10] アーティファクト　　
PRINTFORML [20] クラフト
PRINTFORM [3] 技能強化　　
PRINTFORML [21] アイテム使用
PRINTFORML [4] アイテム確認
PRINTFORML 
PRINTFORML [999] やめる
INPUT
L_INPUT = RESULT
SELECTCASE L_INPUT
CASE 0
	CALL RPG_EXPLORER_SELECT
CASE 1
	CALL RPG_STAGESELECT
CASE 2
	CALL RPG_CHARA_SELECT("RPG_武器強化")
CASE 3
	CALL RPG_CHARA_SELECT("RPG_技能成長")
CASE 4
	CALL RPG_ITEM
CASE 5
	CALL RPG_CHARA_SELECT("RPG_随伴配置")
CASE 10
	CALL RPG_ARTIFACT
CASE 20
	CALL RPG_CRAFT
CASE 21
	CALL RPG_CHARA_SELECT("RPG_強化剤投与")
CASE 999
	CALL SYSTEM_MENU
CASEELSE
ENDSELECT

GOTO INPUT_LOOP




@RPG_CHARA_SELECT(MODE_NOW = "RPG_武器強化")
#DIMS         MODE_NOW
#DIM          MODE_ID
#DIM          L_INPUT
#DIMS DYNAMIC OP_RELOAD     ;初回以外のページ更新とリスト更新を抑制する
{
#DIMS CONST   MODE_NAME = 
	"RPG_武器強化", 
	"RPG_技能成長", 
	"RPG_強化剤投与", 
	"RPG_超人薬投与", 
	"RPG_随伴配置", 
	"RPG_後方配置", 
	"RPG_回収配置", 
	"RPG_探索配置"
}
{
#DIMS CONST   MEMO_STR = 
	"武器強化の対象者を選択して下さい。", 
	"技能成長の対象者を選択して下さい。", 
	"強化剤投与の対象者を選択して下さい。", 
	"超人薬投与の対象者を選択して下さい。", 
	"随伴部隊に配置する人物を選択して下さい。", 
	"後方部隊に配置する人物を選択して下さい。", 
	"回収部隊に配置する人物を選択して下さい。", 
	"探索部隊に配置する人物を選択して下さい。"
}



MODE_ID = FINDELEMENT(MODE_NAME, MODE_NOW)
SELECTCASE MODE_ID
CASE -1
	MODE_NOW '= "RPG_武器強化"
	RESTART
ENDSELECT
CLEARLINE LINECOUNT
CALL LIST_CHARA_STATUS_MAIN(0, OP_RELOAD, MODE_NOW, MEMO_STR:MODE_ID)
L_INPUT = RESULT
OP_RELOAD '= "「ページ更新無し」「リスト更新無し」"
SELECTCASE L_INPUT
CASE 0
	RETURN
CASE 1 TO CHARANUM - 1
	SELECTCASE MODE_NOW
	CASE "RPG_武器強化"
		CALL RPG_WEAPON_MENU(L_INPUT)
	CASE "RPG_技能成長"
		CALL RPG_SKILLSET(L_INPUT)
	CASE "RPG_強化剤投与", "RPG_超人薬投与"
		CALL RPG_DOPING(L_INPUT, MODE_NOW)
	CASE "RPG_随伴配置", "RPG_後方配置", "RPG_回収配置", "RPG_探索配置"
		CALL RPG_SUPPORT_MENU(L_INPUT, MODE_NOW)
	ENDSELECT
CASE 1000 TO 1007
	MODE_NOW '= MODE_NAME:(L_INPUT % 1000)
CASEELSE
ENDSELECT
RESTART













