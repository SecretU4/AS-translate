;-------------------------------------------------
;戦闘関係
;-------------------------------------------------
@RPG_BATTLE(ARG)
#DIM L_INPUT
#DIM L_I
#DIM B_B

ARG = Flag:調査キャラ

Flag:戦闘_攻勢 = 0

L_I = ARG

B_B = 0


CALL RPG_BATTLE_E 
SIF Flag:敵_レア != 0 || Flag:都市_防衛 != 0
	B_B = 1
CLEARLINE LINECOUNT
IF B_B == 1
	SETBGCOLOR 90,10,10
	PRINTFORML  ―――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――
	PRINTFORML  ―――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――
ENDIF
IF Flag:敵_レア == 100
	PRINTFORML 　　　　　　　　　　　　　　　　　　　　　　警告LvXX　　　外神襲来　　　　　　　　
ELSEIF Flag:敵_レア != 0 && Flag:調査区域 == 90 && (Flag:調査深度 >= Flag:日替わり区画深度限界)
	PRINTFORML 　　　　　　　　　　　　　　　　　　　　　　最深部到達　本作戦最終工程開始　　　　　　　　
ELSEIF Flag:敵_レア != 0
	PRINTFORML 　　　　　　　　　　　　　　　　　　　　　　警戒Lv5　認知外敵性存在襲来　　　　　　　　
ELSEIF Flag:都市_防衛 != 0
	PRINTFORML 　　　　　　　　　　　　　　　　　　　　　都市防衛戦　　認知外敵性存在迎撃　　　　　　　　
ENDIF
IF B_B == 1
	PRINTFORML  ―――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――
	PRINTFORML  ―――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――
	PRINTFORML
	PRINTFORML
	PRINTFORML
	PRINTFORML
	PRINTFORML
	PRINTFORML
	PRINTFORMW
ENDIF


CLEARLINE LINECOUNT
DRAWLINE
PRINTFORML %SAVESTR:敵_名前%が現れた！。
DRAWLINE
PRINTFORML
PRINTFORML
PRINTFORMW
IF CFlag:L_I:住人_SPD >= RAND:100
;ファーストアタック
	SETCOLOR 0,240,240
	PRINTFORML 【――――FASTATACK!!――――】
	RESETCOLOR
	PRINTFORML %CALLNAME:L_I%は敵が動き出す前に攻撃を放った！
	CALL RPG_BATTLE_ATTACK_P(ARG)
ENDIF




$INPUT_LOOP
SIF Flag:都市_防衛 != 0
	FLAG:都市_防衛ターン -= 1
CLEARLINE LINECOUNT
DRAWLINE
PRINTFORM 【%CALLNAME:L_I%】
IF CFlag:L_I:住人_現在気力 <= CFlag:L_I:住人_最大気力-((CFlag:L_I:住人_最大気力*7)/10)
	SETCOLOR 220,220,0
	PRINTFORML 【疲労】
	RESETCOLOR
ELSE 
	PRINTFORML 
ENDIF
PRINTFORML 【HP:{CFlag:L_I:住人_現在体力}/{CFlag:L_I:住人_最大体力}】【SP:{CFlag:L_I:住人_現在気力}/{CFlag:L_I:住人_最大気力}】
PRINTFORML 【GUTS:{CFlag:L_I:住人_GUTS}】【BRAVE:{CFlag:L_I:住人_BRAVE}】
PRINTFORML 【SAN:{CFlag:L_I:住人_SAN}】
PRINTFORML 【BRE:{CFlag:L_I:住人_現在BRE}/{CFlag:L_I:住人_最大BRE}】
SIF Flag:戦闘_攻勢 > 0
	PRINTFORML 【攻勢(残り{Flag:戦闘_攻勢}ターン)】
DRAWLINE
PRINTFORML 【%SAVESTR:敵_名前%】
PRINTFORM 【状態:
IF Flag:敵_現在体力 <= Flag:敵_最大体力-((Flag:敵_最大体力*9)/10)
	PRINTFORM 瀕死
ELSEIF Flag:敵_現在体力 <= Flag:敵_最大体力-((Flag:敵_最大体力*6)/10)
	PRINTFORM 重症
ELSEIF Flag:敵_現在体力 <= Flag:敵_最大体力-((Flag:敵_最大体力*4)/10)
	PRINTFORM 重傷
ELSEIF Flag:敵_現在体力 <= Flag:敵_最大体力-((Flag:敵_最大体力*2)/10)
	PRINTFORM 軽傷
ELSE 
	PRINTFORM 無傷
ENDIF
PRINTFORML 】

DRAWLINE
SIF Flag:自動戦闘 == 1
	GOTO BATTLE_START
PRINTFORM [1]  - ターン経過(SP-1)　
PRINTFORM [2]  - 自動戦闘
PRINTFORML 
PRINTFORM [10] - 攻勢指示(SP-10)(与ダメージ2倍　5ターン)
PRINTFORML 
PRINTFORML 
IF Flag:依頼_依頼開始 == 1
	PRINTFORML 依頼中は逃げられません
ELSEIF Flag:調査区域 == 90 && (Flag:調査深度 >= Flag:日替わり区画深度限界)
	PRINTFORML ボス戦は逃げられません
ELSEIF Flag:都市_防衛 != 0
	PRINTFORML 防衛中は逃げられません
ELSE 
	PRINTFORML [0] - 逃げる
ENDIF
$INPUT_LOOP22
INPUT
L_INPUT = RESULT
SIF Flag:戦闘_攻勢 > 0
	Flag:戦闘_攻勢 -= 1
SELECTCASE L_INPUT
CASE 1
	$BATTLE_START
	IF CFlag:L_I:住人_SPD >= Flag:敵_SPD
		CALL RPG_BATTLE_ATTACK_P(ARG)
		CALL RPG_BATTLE_ATTACK_E(ARG)
	ELSE 
		CALL RPG_BATTLE_ATTACK_E(ARG)
		CALL RPG_BATTLE_ATTACK_P(ARG)
	ENDIF
	DRAWLINE
	IF CFlag:L_I:住人_BRAVE >= RAND:100
	;ブレイブアタック
		SETCOLOR 0,240,240
		PRINTFORML 【――――BRAVEATACK!!――――】
		RESETCOLOR
		PRINTFORML %CALLNAME:L_I%は再度攻撃を繰り出した！
		CALL RPG_BATTLE_ATTACK_P(ARG)
	ENDIF
	CFlag:L_I:住人_現在気力 -= 1
	GOTO INPUT_LOOP
CASE 2
	Flag:自動戦闘 = 1
	GOTO BATTLE_START
CASE 10
	IF CFlag:L_I:住人_現在気力 >= 10
		Flag:戦闘_攻勢 = 5
		CFlag:L_I:住人_現在気力 -= 10
		PRINTFORMW 【――――攻勢指示発令!!――――】
	ELSE
		PRINTFORMW 【――――SPが不足しています――――】
	ENDIF
		GOTO INPUT_LOOP
CASE 0
	IF Flag:依頼_依頼開始 == 1
		GOTO INPUT_LOOP22
	ELSEIF Flag:都市_防衛 != 0
		GOTO INPUT_LOOP22
	ELSEIF RAND:(CFlag:L_I:住人_SPD) >= RAND:(Flag:敵_SPD)
		CALL RPG_START(ARG)
	ELSE
		SETCOLOR 250,40,40
		PRINTFORML 【――――逃げられなかった！――――】
		RESETCOLOR
		CALL RPG_BATTLE_ATTACK_E(ARG)
		GOTO INPUT_LOOP
	ENDIF
CASEELSE
	GOTO INPUT_LOOP22
ENDSELECT



;-------------------------------------------------
;攻撃処理 味方側
;-------------------------------------------------
@RPG_BATTLE_ATTACK_P(ARG)
#DIM L_INPUT
#DIM L_I
#DIM L_D
#DIM L_W
#DIM L_DL
#DIM E_A

L_INPUT = 0
L_W = 0
L_DL = 0
L_D = 0
E_A = 0
L_I = ARG

$EXTRA_LOOP

IF 70 >= RAND:100
;メイン武装
	L_W = 0
ELSE
;サブ武装
	L_W = 10
ENDIF
DRAWLINE
PRINTFORML 【%CALLNAME:L_I%のターン】【%CSTR:L_I:(4000+L_W)%:{CFlag:L_I:(6551+L_W)}D{CFlag:L_I:(6552+L_W)}+{CFlag:L_I:(6553+L_W)}】
PRINTFORMW %CALLNAME:L_I%は%CSTR:L_I:(4000+L_W)%で攻撃。
IF 100+(CFlag:L_I:住人_HIT)+(FLAG:遺物_HIT合計値)-Flag:敵_SPD >= RAND:100
;命中
	;ヒット数
	L_DL = CFlag:L_I:(6551+L_W)
	$DL_LOOP
		;基本ダメージ&アーティファクト補正
		L_D += RAND:(CFlag:L_I:(6552+L_W))+1+RAND:((FLAG:遺物_ATK合計値)+1)
		;ダメージボーナス&アーティファクト補正
		L_D += CFlag:L_I:(6553+L_W)+((1+RAND:((FLAG:遺物_ATK合計値)+1))/5)
		L_DL -= 1
		SIF L_DL > 0
			GOTO DL_LOOP
	L_D += (L_D*((CFlag:L_I:住人_ATK)))/100
	
	IF CFlag:L_I:(6558+L_W) >= 10
	;必殺
		SETCOLOR 0,240,240
		PRINTFORML 【――――SPECIALATACK!!――――】
		RESETCOLOR
		PRINTFORML %CALLNAME:L_I%は渾身の一撃を繰り出した！！。
		L_D += L_D/2
	ENDIF
	SIF CFlag:L_I:(6550+L_W) == Flag:敵_弱点
		L_D += L_D/2
	SIF CFlag:L_I:(6550+L_W) == Flag:敵_耐性
		L_D -= L_D/2
	SIF Flag:戦闘_攻勢 > 0
		L_D = L_D*2
	L_D -= (Flag:敵_DEF/3)+RAND:((Flag:敵_DEF/3)+1)+RAND:((Flag:敵_DEF/3)+1)
	SIF L_D <= 0
	 L_D = (RAND:3)+1
	PRINTFORML
	PRINTFORMW %SAVESTR:敵_名前%に{L_D}ダメージを与えた。
	Flag:敵_現在体力 -= L_D
	SIF FLAG:都市_防衛 != 0
		FLAG:都市_防衛ダメージ += L_D
ELSE 
;命中せず
	PRINTFORMW %SAVESTR:敵_名前%は攻撃を回避した。
ENDIF



IF FLAG:支援_随伴合計ATK != 0 && RAND:((FLAG:支援_随伴合計SPD/3)+1) >= (RAND:100)+1
	;随伴攻撃
		SETCOLOR 240,140,240
		PRINTFORML 【――――SUPPORTATACK!!――――】
		RESETCOLOR
		PRINTFORML 随伴隊員たちのよる援護攻撃。
		L_D = 0
		L_D += FLAG:支援_随伴合計ATK/5
		L_D -= (Flag:敵_DEF/2)+RAND:(Flag:敵_DEF/2)
		SIF L_D <= 0
		 L_D = 1
		PRINTFORML
		PRINTFORMW %SAVESTR:敵_名前%に{L_D}ダメージを与えた。
		Flag:敵_現在体力 -= L_D
		SIF FLAG:都市_防衛 != 0
			FLAG:都市_防衛ダメージ += L_D
ENDIF



IF FLAG:都市_防衛ターン <= 0 && FLAG:都市_防衛 != 0
	JUMP CITY_RID_B_END
ENDIF



IF Flag:敵_現在体力 <= 0
	PRINTFORML
	PRINTFORMW %SAVESTR:敵_名前%を倒した。
	CFlag:L_I:住人_経験値 += Flag:敵_取得経験値
	FLAG:ターン経験値 += Flag:敵_取得経験値
	CFlag:L_I:住人_勝利回数 += 1
	SIF CFlag:L_I:住人_勝利回数 >= 50 && TALENT:L_I:「イェーガー」 == 0
		TALENT:L_I:「イェーガー」 = 1
	Flag:ターン戦闘回数 += 1
	Flag:自動戦闘 = 0
	
    IF Flag:依頼_依頼開始 == 1
		JUMP MISSION_CLEAR_B
	ELSEIF Flag:EP追撃方法 == 1
		JUMP EP_COUNTER_CLEAR
	ENDIF

	IF CFLAG:L_I:住人_技能_医療 >= RAND:100
		SETCOLOR 0,240,240
		PRINTFORMW 【――――%CALLNAME:L_I%は傷ついた身体を治療した――――】
		RESETCOLOR
		CFlag:L_I:住人_現在体力 += CFlag:L_I:住人_最大体力/7
		SIF CFlag:L_I:住人_現在体力 >= CFlag:L_I:住人_最大体力 
			CFlag:L_I:住人_現在体力 = CFlag:L_I:住人_最大体力
	ENDIF
	IF CFLAG:L_I:住人_技能_解体 >= RAND:100
		SETCOLOR 0,240,240
		PRINTFORML 【――――%CALLNAME:L_I%は倒した敵を解体した――――】
		IF Flag:敵_取得経験値 > 50
			Flag:I骸 += 1
			PRINTFORMW 【――――I骸を手に入れた――――】
		ELSEIF Flag:敵_取得経験値 > 25
			Flag:I供物 += 1
			PRINTFORMW 【――――I供物を手に入れた――――】
		ELSE
			Flag:I血液 += 1
			PRINTFORMW 【――――I血液を手に入れた――――】
		ENDIF
		RESETCOLOR
	ENDIF
	SIF RAND:((Flag:調査深度)+1) >=  20+RAND:100
		CALL RPG_ARTIFACT_GET(ARG)
	IF Flag:調査区域 == 90 && (Flag:調査深度 >= Flag:日替わり区画深度限界)
		RESETBGCOLOR
		PRINTFORML ≪区画踏破おめでとうございます、「外敵」が根城にしていたフロアにアーティファクト反応を検知≫
		PRINTFORMW ≪せっかくですし回収していきましょう、良いものが見つかるといいですね≫
		CALL RPG_ARTIFACT_GET(ARG)
		CALL RPG_ARTIFACT_GET(ARG)
		CALL RPG_ARTIFACT_GET(ARG)
		Flag:調査深度 = 0
		Flag:ターン調査済み = 1
			CALL RPG_END
		JUMP RPG_MAIN
	ENDIF
	JUMP RPG_START(ARG)
ENDIF


IF CFlag:L_I:(6558+L_W) >= 20 && (CFlag:L_I:(6558+L_W)-E_A) > RAND:100
;連続攻撃
	SETCOLOR 0,240,240
	PRINTFORML 【――――EXTRAATACK!!――――】
	RESETCOLOR
	PRINTFORML %CALLNAME:L_I%は続けざまに再度攻撃を仕掛けた！。
	E_A += 20
	GOTO EXTRA_LOOP
ENDIF


;-------------------------------------------------
;攻撃処理 敵側
;-------------------------------------------------
@RPG_BATTLE_ATTACK_E(ARG)
#DIM L_INPUT
#DIM L_I
#DIM L_D
#DIM L_W
#DIM L_DL
#DIM M_N

M_N = 0
L_INPUT = 0
L_W = 0
L_DL = 0
L_D = 0

L_I = ARG
DRAWLINE
IF 70 >= RAND:100
;メイン武装
	L_W = 0
ELSE
;サブ武装
	L_W = 30
ENDIF
PRINTFORML 【%SAVESTR:敵_名前%のターン】【{Flag:(9031+L_W)}D{Flag:(9032+L_W)}+{Flag:(9033+L_W)}】
PRINTFORMW %SAVESTR:敵_名前%は%CALLNAME:L_I%に攻撃。
IF 100+Flag:敵_HIT-CFlag:L_I:住人_SPD-FLAG:遺物_SPD合計値 >= RAND:100
;命中
	L_DL = Flag:(9031+L_W)
	$DL_LOOP
	L_D += RAND:(Flag:(9032+L_W))+1
	L_D += Flag:(9033+L_W)
	L_DL -= 1
	SIF L_DL > 0
		GOTO DL_LOOP
	L_D += (L_D*((Flag:敵_ATK)))/100
	
	IF RAND:((CFlag:L_I:住人_HIT)*5+1+FLAG:遺物_HIT合計値)-RAND:((Flag:敵_HIT)*5+1) >= RAND:100
	;パリィ
		SETCOLOR 0,240,240
		PRINTFORML 【――――PARRY!!――――】
		RESETCOLOR
		PRINTFORML %CALLNAME:L_I%は%SAVESTR:敵_名前%の攻撃を受け流して威力を落とした。
		L_D -= L_D/2
	ENDIF
	IF FLAG:支援_随伴合計DEF != 0 && FLAG:支援_随伴合計HIT != 0 && RAND:((FLAG:支援_随伴合計SPD/3)+1) >= (RAND:100)+1 && L_D > 0
	;援護防御
		SETCOLOR 240,140,240
		PRINTFORML 【――――SUPPORTGUARD!!――――】
		RESETCOLOR
		PRINTFORML 随伴隊員は%CALLNAME:L_I%を庇うように前に立ち、威力を軽減した。
		L_D /= 2 + FLAG:支援_随伴合計DEF / 300 + FLAG:支援_随伴合計HIT / 300
	ENDIF
	
	SIF CFlag:L_I:住人_現在気力 <= CFlag:L_I:住人_最大気力-((CFlag:L_I:住人_最大気力*7)/10)
		L_D += L_D/2
	L_D -= (CFlag:L_I:住人_DEF/3)+RAND:(CFlag:L_I:住人_DEF/3)+RAND:(CFlag:L_I:住人_DEF/3)+RAND:((FLAG:遺物_DEF合計値/2)+1)
	SIF L_D <= 0
	 L_D = (RAND:3)+1
	PRINTFORML
	SETCOLOR 250,50,50
	PRINTFORMW %CALLNAME:L_I%は{L_D}ダメージを受けた。
	RESETCOLOR
	PRINTFORML 
	CFlag:L_I:住人_現在体力 -= L_D
ELSE 
;命中せず
	PRINTFORMW %CALLNAME:L_I%は攻撃を回避した。
ENDIF




IF CFlag:L_I:住人_現在体力 <= 0
	CFlag:L_I:住人_現在体力 = 1
	PRINTFORML
	SETCOLOR 250,50,50
	PRINTFORMW %WORDS_RPG("DEFEATED_CITY", L_I)%
	RESETCOLOR
	Flag:敵_レア = 0
	Flag:自動戦闘 = 0
	IF FLAG:都市_防衛 != 0
		FLAG:都市_防衛蓄積ダメージ += FLAG:都市_防衛ダメージ
		FLAG:ターン終了フラグ = 1
		JUMP SYSTEM_MENU
	ENDIF
	RESETBGCOLOR
	IF Flag:依頼_依頼開始 == 1
		REPEAT 50
			Flag:(800+M_N) = 0
			Flag:(850+M_N) = 0
			M_N += 1
		REND
		;VARSET FLAG:0, 0, 800, 850
		;VARSET FLAG:0, 0, 850, 900
		;849と900はリセットされない？
		JUMP MISSION_MISS
	ELSEIF Flag:EP追撃方法 == 1
		JUMP EP_COUNTER_MISS
	ENDIF

	JUMP SYSTEM_MENU
ENDIF







