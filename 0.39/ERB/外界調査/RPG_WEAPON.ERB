;-------------------------------------------------
;武装改良
;-------------------------------------------------
@RPG_WEAPON_MENU(L_I)
#DIM L_I
#DIM L_INPUT
CLEARLINE LINECOUNT
DRAWLINE
CALL C_PICTURE_RD(L_I)
DRAWLINE
PRINTFORML 【経験値:{CFlag:L_I:住人_経験値}】【資金:{MONEY}】
DRAWLINE
PRINTFORML [0] 武装強化(経験値・資金消費)
PRINTFORML [1] タイプ変更(要初期化)
PRINTFORML [2] 武装名変更
PRINTFORML 
PRINTFORML [999] 戻る
INPUT
L_INPUT = RESULT
SELECTCASE L_INPUT
CASE 0
	CALL RPG_WEAPON_CUSTOM(L_I)
	SIF RESULT
		RETURN
CASE 1
	CALL RPG_WEAPON_CHANGE(L_I)
	SIF RESULT
		RETURN
CASE 2
	CALL RPG_WEAPON_NAMING(L_I)
	SIF RESULT
		RETURN
CASE 999
	RETURN
CASEELSE
ENDSELECT
RESTART



;-------------------------------------------------
;武装強化
;-------------------------------------------------
;かなり変則的な設計になってしまったがコレは有りなのだろうか
@RPG_WEAPON_CUSTOM(L_I)
#DIM         L_I
#DIM         L_INPUT
#DIM DYNAMIC PAGE
#DIM DYNAMIC WEAPON_SLOT
#DIM DYNAMIC CUSTOM_MENU
#DIM DYNAMIC COST_MONEY
#DIM DYNAMIC COST_EXP
#DIM CONST   PAGE_武器スロット選択 = 0
#DIM CONST   PAGE_強化項目選択     = 1
#DIM CONST   PAGE_武器強化実行     = 2
CLEARLINE LINECOUNT
DRAWLINE
CALL C_PICTURE_RD(L_I)
DRAWLINE
PRINTFORML 【経験値:{CFlag:L_I:住人_経験値}】【資金:{MONEY}】
DRAWLINE
SELECTCASE PAGE
CASE PAGE_武器スロット選択
	PRINTFORML 強化したい武器を選択してください。
	DRAWLINE
	PRINTFORML [0]  メイン武器
	PRINTFORML [10] 　サブ武器
	PRINTFORML 
	PRINTFORMLC [888] 武装改良の選択に戻る
	PRINTFORMLC [999] キャラ選択に戻る
	INPUT
	L_INPUT = RESULT
	SELECTCASE L_INPUT
	CASE 0, 10
		WEAPON_SLOT = L_INPUT
		PAGE = PAGE_強化項目選択
	CASE 888
		RETURN
	CASE 999
		RETURN -1
	CASEELSE
	ENDSELECT
	RESTART
CASE PAGE_強化項目選択
	IF !WEAPON_SLOT
		PRINTFORML ＊主武器の強化中＊
	ELSE
		PRINTFORML ＊副武器の強化中＊
	ENDIF
	PRINTFORML 強化したい箇所を選択してください。
	DRAWLINE
	PRINTFORML [3] ダメージボーナス(現在{CFlag:L_I:(6553 + WEAPON_SLOT)})
	PRINTFORML [2] 最大ダメージ　　(現在{CFlag:L_I:(6552 + WEAPON_SLOT)})
	PRINTFORML [1] 攻撃回数　　　　(現在{CFlag:L_I:(6551 + WEAPON_SLOT)})
	PRINTFORML 
	PRINTFORMLC [777] 別の武器を強化
	PRINTFORMLC [888] 武装改良の選択に戻る
	PRINTFORMLC [999] キャラ選択に戻る
	INPUT
	CUSTOM_MENU = RESULT
	SELECTCASE CUSTOM_MENU
	CASE 1, 2, 3
		PAGE = PAGE_武器強化実行
	CASE 777
		IF !WEAPON_SLOT
			WEAPON_SLOT = 10
		ELSE
			WEAPON_SLOT = 0
		ENDIF
	CASE 888
		RETURN
	CASE 999
		RETURN -1
	CASEELSE
	ENDSELECT
	RESTART
CASE PAGE_武器強化実行
	IF !WEAPON_SLOT
		PRINTFORML ＊主武器の強化中＊
	ELSE
		PRINTFORML ＊副武器の強化中＊
	ENDIF
	CALLF CALC_COST_WEAPON_CUSTOM(L_I, WEAPON_SLOT, CUSTOM_MENU, COST_MONEY, COST_EXP)
	IF MONEY < COST_MONEY
		PRINTFORMW 資金が不足しています。(必要:{COST_MONEY})
		PAGE --
		RESTART
	ELSEIF CFlag:L_I:住人_経験値 < COST_EXP
		PRINTFORMW 経験値が不足しています。(必要:{COST_EXP})
		PAGE --
		RESTART
	ENDIF
	DRAWLINE
	PRINTFORML 経験値{COST_EXP}と資金{COST_MONEY}を消費しますがよろしいですか？。
	DRAWLINE
	PRINTFORML [2] 連続して強化
	PRINTFORML [1] はい
	PRINTFORML 
	PRINTFORML [0] いいえ
	INPUT
	L_INPUT = RESULT
	SELECTCASE L_INPUT
	CASE 2, 1
		MONEY -= COST_MONEY
		CFlag:L_I:住人_経験値 -= COST_EXP
		CFlag:L_I:(6550+ WEAPON_SLOT + CUSTOM_MENU) ++
		CFlag:L_I:(6558+ WEAPON_SLOT) ++
		PRINTFORML %CSTR:L_I:(4000 + WEAPON_SLOT)%を改造しました。
		PRINTFORML
		SIF L_INPUT == 1
			PAGE = PAGE_武器スロット選択
		RESTART
	CASEELSE
	ENDSELECT
	PAGE = PAGE_強化項目選択
	RESTART
ENDSELECT



;-------------------------------------------------
;強化コストの算出
;-------------------------------------------------
@CALC_COST_WEAPON_CUSTOM(L_I, WEAPON_SLOT, CUSTOM_MENU, COST_MONEY, COST_EXP)
#FUNCTION
#DIM       L_I
#DIM       WEAPON_SLOT
#DIM       WEAPON_LV
#DIM       BASIC_COST
#DIM       CUSTOM_MENU
#DIM REF   COST_MONEY
#DIM REF   COST_EXP
#DIM CONST CUSTOM_ダイス数強化 = 1
#DIM CONST CUSTOM_ダイス面強化 = 2
#DIM CONST CUSTOM_固定値強化   = 3
WEAPON_LV  = CFlag:L_I:(6558 + WEAPON_SLOT)
BASIC_COST = WEAPON_LV * WEAPON_LV
SELECTCASE CUSTOM_MENU
CASE CUSTOM_ダイス数強化
	COST_MONEY = BASIC_COST * 5000
	COST_EXP   = BASIC_COST * 3
CASE CUSTOM_ダイス面強化
	COST_MONEY = BASIC_COST * 500
	COST_EXP   = BASIC_COST * 2
CASE CUSTOM_固定値強化
	COST_MONEY = BASIC_COST * 3500
	COST_EXP   = BASIC_COST * 3
ENDSELECT



;-------------------------------------------------
;タイプ変更
;-------------------------------------------------
@RPG_WEAPON_CHANGE(L_I)
#DIM          L_I
#DIM          L_INPUT
#DIM  DYNAMIC PAGE
#DIM  DYNAMIC WEAPON_SLOT
#DIM  DYNAMIC WEAPON_TYPE
#DIM  CONST   PAGE_武器スロット選択 = 0
#DIM  CONST   PAGE_武器タイプ選択   = 1
#DIM  CONST   PAGE_武器変更実行     = 2
CLEARLINE LINECOUNT
DRAWLINE
CALL C_PICTURE_RD(L_I)
DRAWLINE
SELECTCASE PAGE
CASE PAGE_武器スロット選択
	PRINTFORML タイプを変更したい武装を選択してください。
	DRAWLINE
	PRINTFORML [0]  メイン武器
	PRINTFORML [10] 　サブ武器
	PRINTFORML 
	PRINTFORMLC [888] 武装改良の選択に戻る
	PRINTFORMLC [999] キャラ選択に戻る
	INPUT
	L_INPUT = RESULT
	SELECTCASE L_INPUT
	CASE 0, 10
		WEAPON_SLOT = L_INPUT
		PAGE = PAGE_武器タイプ選択
	CASE 888
		RETURN
	CASE 999
		RETURN -1
	CASEELSE
	ENDSELECT
	RESTART
CASE PAGE_武器タイプ選択
	IF !WEAPON_SLOT
		PRINTFORML ＊主武器の変更中＊
	ELSE
		PRINTFORML ＊副武器の変更中＊
	ENDIF
	PRINTFORML タイプを選択してください。
	DRAWLINE
	FOR WEAPON_TYPE, 0, 5
		PRINTFORML [{WEAPON_TYPE}] - %INFO_STR_HEISOU(WEAPON_TYPE)%
	NEXT
	PRINTFORML 
	PRINTFORMLC [777] 別の武器を変更
	PRINTFORMLC [888] 武装改良の選択に戻る
	PRINTFORMLC [999] キャラ選択に戻る
	INPUT
	L_INPUT = RESULT
	SELECTCASE L_INPUT
	CASE 0 TO 4
		WEAPON_TYPE = L_INPUT
		IF CFlag:L_I:(6550 + WEAPON_SLOT) == WEAPON_TYPE
			PRINTFORMW 選択している武器は「%INFO_STR_HEISOU(WEAPON_TYPE)%」の武器です
		ELSE
			PAGE = PAGE_武器変更実行
		ENDIF
	CASE 777
		IF !WEAPON_SLOT
			WEAPON_SLOT = 10
		ELSE
			WEAPON_SLOT = 0
		ENDIF
	CASE 888
		RETURN
	CASE 999
		RETURN -1
	CASEELSE
	ENDSELECT
	RESTART
CASE PAGE_武器変更実行
	IF !WEAPON_SLOT
		PRINTFORML ＊主武器を「%INFO_STR_HEISOU(WEAPON_TYPE)%」に変更中＊
	ELSE
		PRINTFORML ＊副武器を「%INFO_STR_HEISOU(WEAPON_TYPE)%」に変更中＊
	ENDIF
	DRAWLINE
	PRINTFORML タイプを変更すると武装が初期化されますがよろしいですか？。
	DRAWLINE
	PRINTFORML [1] はい
	PRINTFORML [0] いいえ
	INPUT
	L_INPUT = RESULT
	SELECTCASE L_INPUT
	CASE 1
		CFlag:L_I:(6550 + WEAPON_SLOT) = WEAPON_TYPE
		IF WEAPON_SLOT == 0
			CFlag:L_I:(6551 + WEAPON_SLOT) = 2
			CFlag:L_I:(6552 + WEAPON_SLOT) = 3
			CFlag:L_I:(6553 + WEAPON_SLOT) = 2
		ELSE
			CFlag:L_I:(6551 + WEAPON_SLOT) = 1
			CFlag:L_I:(6552 + WEAPON_SLOT) = 3
			CFlag:L_I:(6553 + WEAPON_SLOT) = 1
		ENDIF
		CFlag:L_I:(6558 + WEAPON_SLOT) = 1
		PRINTFORMW %CSTR:L_I:(4000 + WEAPON_SLOT)%のタイプを%INFO_STR_HEISOU(WEAPON_TYPE)%に変更しました。
		PAGE = PAGE_武器スロット選択
	CASEELSE
		PAGE = PAGE_武器タイプ選択
	ENDSELECT
	RESTART
ENDSELECT



;-------------------------------------------------
;武装名変更
;-------------------------------------------------
@RPG_WEAPON_NAMING(L_I)
#DIM          L_I
#DIM          L_INPUT
#DIM  DYNAMIC PAGE
#DIM  DYNAMIC WEAPON_SLOT
#DIM  CONST   PAGE_武器スロット選択 = 0
#DIM  CONST   PAGE_武器名入力       = 1
CLEARLINE LINECOUNT
DRAWLINE
CALL C_PICTURE_RD(L_I)
DRAWLINE
SELECTCASE PAGE
CASE PAGE_武器スロット選択
	PRINTFORML 武装名を変更したい武装を選択してください。
	DRAWLINE
	PRINTFORML [0]  メイン武器
	PRINTFORML [10] 　サブ武器
	PRINTFORML 
	PRINTFORMLC [888] 武装改良の選択に戻る
	PRINTFORMLC [999] キャラ選択に戻る
	INPUT
	L_INPUT = RESULT
	SELECTCASE L_INPUT
	CASE 0, 10
		WEAPON_SLOT = L_INPUT
		PAGE = PAGE_武器名入力
	CASE 888
		RETURN
	CASE 999
		RETURN -1
	CASEELSE
	ENDSELECT
	RESTART
CASE PAGE_武器名入力
	IF !WEAPON_SLOT
		PRINTFORML ＊主武器の変更中＊
	ELSE
		PRINTFORML ＊副武器の変更中＊
	ENDIF
	PRINTFORML 新しい武装名を入力してください
	DRAWLINE
	INPUTS
	SELECTCASE RESULTS
	CASE ""
		PRINTFORMW 変更しませんでした。
	CASEELSE
		CSTR:L_I:(4000 + WEAPON_SLOT) = %RESULTS%
		PRINTFORMW 武装名を「%RESULTS%」に変更しました。
	ENDSELECT
	RETURN
ENDSELECT
