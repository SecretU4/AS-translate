;-------------------------------------------------
;発見
;-------------------------------------------------
@RPG_DISCOVERY(ARG)
#DIM L_INPUT
#DIM L_I
#DIM L_S
#DIM L_AG
#DIM L_DG1
#DIM L_DG2
#DIM L_G
#DIM I_G

L_G = RAND:20
L_DG1 = RAND:6
L_DG2 = RAND:6
L_I = ARG
L_S = 0
L_AG = 0

IF Flag:フロア_品質 >= RAND:100
	IF Flag:フロア_品質 >= RAND:100
		;高品質
		I_G = (RAND:6)+20
	ELSE 
		;中品質
		I_G = (RAND:6)+10
	ENDIF
ELSE 
	;低品質
	I_G = (RAND:6)
ENDIF

CLEARLINE LINECOUNT
DRAWLINE
PRINTFORML 【―――――――――発見!!―――――――――】
DRAWLINE
IF FLAG:フロア_特殊調査 == 3
	PRINTFORML ふと気になるものを見つけ良く調べてみると、古風で硬そうな施錠がなされた箱を見つける。
	PRINTFORML 簡単な構造ではあるが、逆に鍵を開ける以外にまともに開ける方法はなうようだ。
	PRINTFORML 【鍵開け】の技能があれば開けることが出来るかもしれない。
	PRINTFORML ないなら鍵を破壊するしかないが、中身を傷つけてしまうかもしれない…
ELSEIF FLAG:フロア_特殊調査 == 2
	PRINTFORML とある施設に侵入し各部屋を調べていると機械的にロックされている扉を見つける。
	PRINTFORML まだ機能してはいるが開けるには【コンピュータ】の技能に長けていないと難しいだろう。
	PRINTFORML 面倒なら力技で開けてしまうのも手だが、壊した反動で室内が滅茶苦茶になってしまう可能性がある…
	PRINTFORML 
ELSEIF FLAG:フロア_特殊調査 == 1
	PRINTFORML 区画を調査していると壊れた大型輸送機を発見する、ぱっと見まだ破損して間もないようだ。
	PRINTFORML しかし中を調べようとしたところ、損壊による緊急ロックが掛かっているらしく簡単には開かなかった。
	PRINTFORML これを調べるには【機械修理】の技能が必要だろう、得意分野なら優先したい選択ではある。
	PRINTFORML もしくは無理矢理開けてしまうか、だがその場合は確率も低く獲得できる物も少なくなるかもしれない…
ENDIF
DRAWLINE

PRINTFORML 
PRINTFORML [0] 技能を使用
PRINTFORML [1] 無理矢理解決
PRINTFORML 
$INPUT_LOOP
INPUT
DRAWLINE
L_INPUT = RESULT
SELECTCASE L_INPUT
CASE 0
	IF FLAG:フロア_特殊調査 == 3
		SIF CFlag:L_I:住人_技能_鍵開け >=  RAND:100
			L_S = 1
	ELSEIF FLAG:フロア_特殊調査 == 2
		SIF CFlag:L_I:住人_技能_コンピュータ >=  RAND:100
			L_S = 1
	ELSEIF FLAG:フロア_特殊調査 == 1
		SIF CFlag:L_I:住人_技能_機械修理 >=  RAND:100
			L_S = 1
	ENDIF
	IF L_S == 1
		SETCOLOR 50,250,250
		PRINTFORML 【―――――――――成功―――――――――】
		RESETCOLOR
		PRINTFORML %CALLNAME:L_I%の巧みな技能により、問題は解決することが出来たようだ。
		PRINTFORML 続いて更なる調査をしてみたところ、使えるものをいくつか発見できた。
		PRINTFORML ホクホクしつつそれらを回収し、回収部隊に搬送を頼んだ…
		PRINTFORML
		L_AG = RAND:4
		IF L_AG == 3
			PRINTFORML 【アイテム入手】%FlagNAME:(2100+I_G)%を3個入手した
			FLAG:(2100+I_G) += 3
		ELSEIF L_AG == 2
			PRINTFORML 【ゲノム入手】%FlagNAME:(2500+L_G)%を2個入手した
			Flag:(2500+L_G) += 2
		ELSEIF L_AG == 1
			PRINTFORML 【強化剤入手】%FlagNAME:(2800+L_DG1)%を1個入手した
			PRINTFORML 【強化剤入手】%FlagNAME:(2800+L_DG2)%を1個入手した
			Flag:(2800+L_DG1) += 1
			Flag:(2800+L_DG2) += 1
		ELSE
			PRINTFORML 【超人薬入手】%FlagNAME:(2800+L_DG1)%を1個入手した
			Flag:(2810+L_DG1) += 1
		ENDIF
	ELSE
		SETCOLOR 250,50,50
		PRINTFORML 【―――――――――失敗―――――――――】
		RESETCOLOR
		PRINTFORML いろいろ試行錯誤してみたものの失敗してしまったようだ…
		PRINTFORML もうこれを解決することはできないだろう。
		PRINTFORML やや落胆しつつ%CALLNAME:L_I%が区画調査を再開した…
	ENDIF
CASE 1
	SIF 30 >=  RAND:100
			L_S = 1
	IF L_S == 1
		SETCOLOR 50,250,250
		PRINTFORML 【―――――――――成功―――――――――】
		RESETCOLOR
		PRINTFORML なんとか対象だけに打撃が行くように攻撃を加えると、運よく上手くいったようだ。
		PRINTFORML だが少々資材にダメージが行ったようで幾つかはダメになっている。
		PRINTFORML まあ仕方ないと妥協しつつそれらを回収し、回収部隊に搬送を頼んだ…
		PRINTFORML
		L_AG = RAND:3
		IF L_AG == 2
			PRINTFORML 【アイテム入手】%FlagNAME:(2100+I_G)%を1個入手した
			FLAG:(2100+I_G) += 1
		ELSEIF L_AG == 1
			PRINTFORML 【ゲノム入手】%FlagNAME:(2500+L_G)%を1個入手した
			Flag:(2500+L_G) += 1
		ELSE
			PRINTFORML 【強化剤入手】%FlagNAME:(2800+L_DG2)%を1個入手した
			Flag:(2800+L_DG2) += 1
		ENDIF
	ELSE
		SETCOLOR 250,50,50
		PRINTFORML 【―――――――――失敗―――――――――】
		RESETCOLOR
		PRINTFORML 力加減を誤って派手に破壊してしまった…。
		PRINTFORML これでは調べることは出来ないだろう。
		PRINTFORML やや落胆しつつ%CALLNAME:L_I%が区画調査を再開した…
	ENDIF
CASEELSE
	GOTO INPUT_LOOP
ENDSELECT

PRINTFORML 
PRINTFORMW 

Flag:フロア_特殊調査 = 0



























